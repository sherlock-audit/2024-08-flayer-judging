Skinny Pear Crane

High

# `FlayerTokenMigration` uses fixed rates to swap from `$NFTX/$FLOOR` to `$Flayer` that creates arbitrage opportunities

## Summary
Issue High: `FlayerTokenMigration` uses fixed rates to swap from `$NFTX/$FLOOR` to `$Flayer` that creates arbitrage opportunities

## Vulnerability Detail

`FlayerTokenMigration` uses fixed rates to swap from `$NFTX/$FLOOR` to `$Flayer`. A user burns `100e18` `NFTX` to get `1234e18` `Flayer` with fixed `nftxRatio` and burns `100e18` `FLOOR` to get `123e18` `Flayer` with fixed `floorRatio`. The price of `NFTX` is $12.17 in `UniswapV3` at Sep-10-2024 04:50:59 AM UTC. While in the floorV2 protocol, a user deposit `1e18` `WETH` can mint `1000e18` `FLOOR`, which by converting the price of `FLOOR` is $2.34 at Sep-10-2024 04:50:59 AM UTC. Thus, there exists arbitrage opportunities. A user can mint `Flayer` by burning `NFTX` at a lower cost than a user who mint `Flayer` by burning `FLOOR`.


[FlayerTokenMigration](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/migration/FlayerTokenMigration.sol#L37-L38)
```solidity
    uint public constant nftxRatio = 12.34 ether;
    uint public constant floorRatio = 1.23 ether;
```
[FlayerTokenMigration](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/migration/FlayerTokenMigration.sol#L64-L104)
```solidity
    function swap(address _recipient, bool _burnNftx, bool _burnFloor) external whenNotPaused {
        // If no tokens are selected to be burnt, then we revert early
        if (!_burnNftx && !_burnFloor) revert NoTokensSelected();

        // Store the total amount of $FLAYER token that will be minted to
        // the recipient.
        uint totalFlayerAmount;

        // Define our balance variables as we use them in the event regardless
        uint nftxBalance;
        uint floorBalance;

        // Check if we are burning $NFTX
        if (_burnNftx) {
            nftxBalance = nftx.balanceOf(msg.sender);
            nftx.transferFrom(msg.sender, 0x000000000000000000000000000000000000dEaD, nftxBalance);

            unchecked {
                totalFlayerAmount += nftxBalance * nftxRatio / 1 ether;
            }
        }

        // Check if we are burning $FLOOR
        if (_burnFloor) {
            floorBalance = floor.balanceOf(msg.sender);
            floor.burnFrom(msg.sender, floorBalance);

            unchecked {
                totalFlayerAmount += floorBalance * floorRatio / 1 ether;
            }
        }

        // If we have build up an amount of $FLAYER to send, then mint it and
        // send it to the recipient.
        if (totalFlayerAmount > 0) {
            flayer.transfer(_recipient, totalFlayerAmount);
        }

        // Notify listeners that we have performed a swap
        emit TokensSwapped(nftxBalance, floorBalance, totalFlayerAmount);
    }
```



## Proof of Concept


1.The price of NFTX in uniswapV3 can be viewed in the following link.
https://coinmarketcap.com/dexscan/ethereum/0x930b2c8ff1de619d4d6594da0ba03fdeda09a672/.

2.In the floorV2 protocol, a user deposit `1e18` `WETH` can mint `1000e18` `FLOOR`.
https://etherscan.io/address/0x89336e8a7e6fb2a0ad0803e940958718c5bf44be#code
```solidity
    function claim(address _to, uint _amount) external {
        // Ensure that we have sufficient FLOOR allocation to claim against
        require(_allocation[msg.sender] >= _amount, 'Insufficient allocation');

        // We ensure that the amount is not 0, and that `_amount % 1e3` equals zero as
        // otherwise it could be exploited to acquire a non-zero amount of FLOOR tokens
        // without transferring any WETH tokens due to the way the 1-to-1000 ratio between
        // the tokens is enforced.
        require(_amount != 0, 'Invalid amount');
        require(_amount % 1e3 == 0, 'Invalid amount');

        // Transfer the WETH to the {Treasury}. This will need to have already been
        // approved by the sender.
        WETH.safeTransferFrom(msg.sender, address(treasury), _amount / 1e3);

        // Reduce the allocation amount from the user. This has already been sanitized
        unchecked {
            _allocation[msg.sender] -= _amount;
        }

        // Transfer our FLOOR to the defined recipient
        FLOOR.mint(_to, _amount);
    }
```


## Impact

A user can mint `$Flayer` by burning $NFTX at a lower cost than a user who mint `$Flayer` by burning $FLOOR.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/migration/FlayerTokenMigration.sol#L37-L38
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/migration/FlayerTokenMigration.sol#L64-L104

## Tool used
Manual Review

## Recommendation

Do not use fixed rates to swap from `$NFTX/$FLOOR` to `$Flayer`.