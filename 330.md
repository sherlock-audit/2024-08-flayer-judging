Shiny Mint Lion

High

# Due to the delay in converting token1 fees into token0 (WETH) fees in beforeSwap(), an attacker can execute a sandwich attack to gain risk-free profits.


## Summary
Due to the delay in converting token1 fees into token0 (WETH) fees in beforeSwap(), an attacker can execute a sandwich attack to gain risk-free profits.
## Vulnerability Detail
```javascript
 /**
     * Before a swap is made, we pull in the dynamic pool fee that we have set to ensure it is
     * applied to the tx.
     *
@>>     * We also see if we have any token1 fee tokens that we can use to fill the swap before it
     * hits the Uniswap pool. This prevents the pool from being affected and reduced gas costs.
     * This also allows us to benefit from the Uniswap routing infrastructure.
     *
     * This frontruns UniSwap to sell undesired token amounts from our fees into desired tokens
     * ahead of our fee distribution. This acts as a partial orderbook to remove impact against
     * our pool.
     *
     * @param sender The initial msg.sender for the swap call
     * @param key The key for the pool
     * @param params The parameters for the swap
     * @param hookData Arbitrary data handed into the PoolManager by the swapper to be be passed on to the hook
     *
     * @return selector_ The function selector for the hook
     * @return beforeSwapDelta_ The hook's delta in specified and unspecified currencies. Positive: the hook is owed/took currency, negative: the hook owes/sent currency
     * @return swapFee_ The percentage fee applied to our swap
     */
    function beforeSwap(address sender, PoolKey calldata key, IPoolManager.SwapParams memory params, bytes calldata hookData) public override onlyByPoolManager returns (bytes4 selector_, BeforeSwapDelta beforeSwapDelta_, uint24 swapFee_) {
        PoolId poolId = key.toId();

        // Ensure our dynamic fees are set to the correct amount and mark it with the override flag
        swapFee_ = getFee(poolId, sender) | LPFeeLibrary.OVERRIDE_FEE_FLAG;

        // Load our PoolFees as storage as we will manipulate them later if we trigger
@>>        ClaimableFees storage pendingPoolFees = _poolFees[poolId];
        PoolParams memory poolParams = _poolParams[poolId];

        // We want to check if our token0 is the eth equivalent, or if it has swapped to token1
        bool trigger = poolParams.currencyFlipped ? !params.zeroForOne : params.zeroForOne;
@>>        if (trigger && pendingPoolFees.amount1 != 0) {
            // Set up our internal logic variables
            uint ethIn;
            uint tokenOut;

            // Get the current price for our pool
            (uint160 sqrtPriceX96,,,) = poolManager.getSlot0(poolId);

            // Since we have a positive amountSpecified, we can determine the maximum
            // amount that we can transact from our pool fees. We do this by taking the
            // max value of either the pool fees or the amount specified to swap for.
            if (params.amountSpecified >= 0) {
                uint amountSpecified = (uint(params.amountSpecified) > pendingPoolFees.amount1) ? pendingPoolFees.amount1 : uint(params.amountSpecified);

                // Capture the amount of desired token required at the current pool state to
                // purchase the amount of token speicified, capped by the pool fees available. We
                // don't apply a fee for this as it benefits the ecosystem and essentially performs
                // a free swap benefitting both parties.
                (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
                    sqrtPriceCurrentX96: sqrtPriceX96,
                    sqrtPriceTargetX96: params.sqrtPriceLimitX96,
                    liquidity: poolManager.getLiquidity(poolId),
                    amountRemaining: int(amountSpecified),
                    feePips: 0
                });

                // Update our hook delta to reduce the upcoming swap amount to show that we have
                // already spent some of the ETH and received some of the underlying ERC20.
                beforeSwapDelta_ = toBeforeSwapDelta(-tokenOut.toInt128(), ethIn.toInt128());
            }
            // As we have a negative amountSpecified, this means that we are spending any amount
            // of token to get a specific amount of undesired token.
            else {
                (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
                    sqrtPriceCurrentX96: sqrtPriceX96,
                    sqrtPriceTargetX96: params.sqrtPriceLimitX96,
                    liquidity: poolManager.getLiquidity(poolId),
                    amountRemaining: int(pendingPoolFees.amount1),
                    feePips: 0
                });

                // If we cannot fulfill the full amount of the internal orderbook, then we want
                // to avoid using any of it, as implementing proper support for exact input swaps
                // is significantly difficult when we want to restrict them by the output token
                // we have available.
                if (tokenOut <= uint(-params.amountSpecified)) {
                    // Update our hook delta to reduce the upcoming swap amount to show that we have
                    // already spent some of the ETH and received some of the underlying ERC20.
                    // Specified = exact input (ETH)
                    // Unspecified = token1
                    beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
                } else {
                    ethIn = tokenOut = 0;
                }
            }

            // Reduce the amount of fees that have been extracted from the pool and converted
            // into ETH fees.
            if (ethIn != 0 || tokenOut != 0) {
@>>                pendingPoolFees.amount0 += ethIn;
@>>                pendingPoolFees.amount1 -= tokenOut;

                // Transfer the tokens to our PoolManager, which will later swap them to our user
                if (poolParams.currencyFlipped) {
                    poolManager.take(key.currency1, address(this), ethIn);
                    _pushTokens(key.currency0, tokenOut);
                } else {
                    poolManager.take(key.currency0, address(this), ethIn);
                    _pushTokens(key.currency1, tokenOut);
                }

                // Capture the swap cost that we captured from our drip
                emit PoolFeesSwapped(poolParams.collection, params.zeroForOne, ethIn, tokenOut);
            }
        }

        // Set our return selector
        selector_ = IHooks.beforeSwap.selector;
    }
```
If the fee is in token1, it will not be directly distributed to LP holders. Instead, it will wait until someone needs to buy token1, at which point token1 will first be swapped into token0. The distribution will only occur the next time _distributeFees() is triggered.
```javascript
    function _distributeFees(PoolKey memory _poolKey) internal {
        // If the pool is not initialized, we prevent this from raising an exception and bricking hooks
        PoolId poolId = _poolKey.toId();
        PoolParams memory poolParams = _poolParams[poolId];

        if (!poolParams.initialized) {
            return;
        }

        // Get the amount of the native token available to donate
@>>        uint donateAmount = _poolFees[poolId].amount0;

        // Ensure that the collection has sufficient fees available
        if (donateAmount < donateThresholdMin) {
            return;
        }

        // Reduce our available fees
        _poolFees[poolId].amount0 = 0;

        // Split the donation amount between beneficiary and LP
        (uint poolFee, uint beneficiaryFee) = feeSplit(donateAmount);

        // Make our donation to the pool, with the beneficiary amount remaining in the
        // contract ready to be claimed.
        if (poolFee > 0) {
            // Determine whether the currency is flipped to determine which is the donation side
@>>            (uint amount0, uint amount1) = poolParams.currencyFlipped ? (uint(0), poolFee) : (poolFee, uint(0));
@>>            BalanceDelta delta = poolManager.donate(_poolKey, amount0, amount1, '');

            // Check the native delta amounts that we need to transfer from the contract
            if (delta.amount0() < 0) {
                _pushTokens(_poolKey.currency0, uint128(-delta.amount0()));
            }

            if (delta.amount1() < 0) {
                _pushTokens(_poolKey.currency1, uint128(-delta.amount1()));
            }

            emit PoolFeesDistributed(poolParams.collection, poolFee, 0);
        }

        // Check if we have beneficiary fees to distribute
        if (beneficiaryFee != 0) {
            // If our beneficiary is a Flayer pool, then we make a direct call
            if (beneficiaryIsPool) {
                // As we don't want to make a transfer call, we just extrapolate
                // the required logic from the `depositFees` function.
                _poolFees[_poolKeys[beneficiary].toId()].amount0 += beneficiaryFee;
                emit PoolFeesReceived(beneficiary, beneficiaryFee, 0);
            }
            // Otherwise, we can just update the escrow allocation
            else {
                beneficiaryFees[beneficiary] += beneficiaryFee;
                emit BeneficiaryFeesReceived(beneficiary, beneficiaryFee);
            }
        }
    }

```
From _distributeFees(), we can also see that only token0 (WETH) is distributed to LP holders. token1 accumulates until the next time a user needs token1, at which point it is traded into token0 and added to amount0.

However, the majority of our fees are earned through token1, such as in the Listings contract with functions like fillListings(), cancelListings(), modifyListings(), and _resolveListingTax(), among others.
```javascript

 function fillListings(FillListingsParams calldata params) public nonReentrant lockerNotPaused {
     //--------skip---------
        uint fillFee = _tload(FILL_FEE);
        if (fillFee != 0) {
            _collectionToken.approve(address(locker.implementation()), fillFee);
@>>            locker.implementation().depositFees(collection, 0, fillFee);
            assembly { tstore(FILL_FEE, 0) }
        }

    }


  function cancelListings(address _collection, uint[] memory _tokenIds, bool _payTaxWithEscrow) public lockerNotPaused {
      //------skip--------
        // Give some partial fees to the LP
        if (fees != 0) {
            collectionToken.approve(address(locker.implementation()), fees);
@>>            locker.implementation().depositFees(_collection, 0, fees);
        }

    }
```
Additionally, this includes the ProtectedListings contractâ€™s liquidateProtectedListing() function, among others.
```javascript
  function liquidateProtectedListing(address _collection, uint _tokenId) public lockerNotPaused listingExists(_collection, _tokenId) {
        //----skip------
        // Send the remaining tokens to {Locker} implementation as fees
        uint remainingCollateral = (1 ether - listing.tokenTaken - KEEPER_REWARD) * 10 ** denomination;
        if (remainingCollateral > 0) {
            IBaseImplementation implementation = locker.implementation();
            collectionToken.approve(address(implementation), remainingCollateral);
@>>            implementation.depositFees(_collection, 0, remainingCollateral);
        }

    }
```
Attack scenario:

	1.	The attacker notices a large accumulation of token1 fees.
	2.	The attacker monitors for a swap transaction where token1 is being exchanged for token0.
	3.	The attacker frontruns this swap transaction by providing a large amount of token0-token1 liquidity to the pool.
	4.	After the swap is completed, the attacker removes the liquidity they just added. The attacker can then collect the corresponding portion of the fees in token0.

Because during the liquidity removal callback, the distribution of token0 fees is triggered.
```javascript
function beforeRemoveLiquidity(address sender, PoolKey calldata key, IPoolManager.ModifyLiquidityParams calldata params, bytes calldata hookData) public override onlyByPoolManager returns (bytes4 selector_) {
        // Distribute fees to our LPs
        _distributeFees(key);

        // Set our return selector
        selector_ = IHooks.beforeRemoveLiquidity.selector;
    }
```

Since this attack method carries no risk, the attacker can borrow large amounts of capital to capture the majority of the fees and repeat the attack multiple times.

## Impact
he attacker steals the majority of the fees through a sandwich attack, resulting in losses for the LP holders.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L308
## Tool used

Manual Review

## Recommendation
Consider directly distributing the token1 fees to LP holders.