Wobbly Neon Hyena

High

# Wrong comparison value used in `UniswapImplementation::beforeSwap` forcing swaps to revert

### Summary

When swapping ETH equivalent tokens to collection tokens, or vice versa, they pass through UniswapImplementation which is added as the hook for the Uniswap pool. beforeSwap front runs the swap on Uniswap and tries to fulfill it from the accumulated fees. When doing exact in (amountSpecified < 0), the hook does an internal swap using:
```solidity
(, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
    sqrtPriceCurrentX96: sqrtPriceX96,
    sqrtPriceTargetX96: params.sqrtPriceLimitX96,
    liquidity: poolManager.getLiquidity(poolId),
    amountRemaining: int(pendingPoolFees.amount1),
    feePips: 0
});

// If we cannot fulfill the full amount of the internal orderbook, then we want
// to avoid using any of it, as implementing proper support for exact input swaps
// is significantly difficult when we want to restrict them by the output token
// we have available.
if (tokenOut <= uint(-params.amountSpecified)) {
    // Update our hook delta to reduce the upcoming swap amount to show that we have
    // already spent some of the ETH and received some of the underlying ERC20.
    // Specified = exact input (ETH)
    // Unspecified = token1
    beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
} else {
    ethIn = tokenOut = 0;
}
```
As shown from the code comments, the internal swap is skipped in case the full swap can't be fulfilled internally, this is done by comparing `params.amountSpecified` and `tokenOut`.

However, this check is misleading, as `params.amountSpecified` is the ETH amount (exact In & WETH -> Token) and `tokenOut` is the collection token amount, so in most cases the result of this check is wrong.

This forces the swap to revert, as skipping logic won't work as anticipated.

### Root Cause

In `UniswapImplementation::beforeSwap`, when computing the internal swap values, `params.amountSpecified` (which is ETH in this case) is being compared with `tokenOut` (which represents the collection token), [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L547).

### Impact

Swapping ETH equivalent tokens to collection tokens, using "exact In" will sometimes revert.

### PoC

Add the following test in `flayer/test/UniswapImplementation.t.sol`:

```solidity
function test_WrongComparisonValue() public withLiquidity withTokens {
    uniswapImplementation.setAmmFee(5_000);
    uniswapImplementation.setAmmBeneficiary(BENEFICIARY);

    bool flipped = false;
    PoolKey memory poolKey = _poolKey(flipped);
    CollectionToken token = flipped ? flippedToken : unflippedToken;
    ERC721Mock nft = flipped ? flippedErc : unflippedErc;

    // Simulate Fees
    uint256 fees = 10 ether;
    deal(address(token), address(this), fees);
    token.approve(address(uniswapImplementation), type(uint).max);
    uniswapImplementation.depositFees(address(nft), 0, fees);

    uint amountSpecified = 15 ether;

    vm.expectRevert();
    poolSwap.swap(
        poolKey,
        IPoolManager.SwapParams({
            zeroForOne: true,
            amountSpecified: -int(amountSpecified),
            sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
        }),
        PoolSwapTest.TestSettings({
            takeClaims: false,
            settleUsingBurn: false
        }),
        ""
    );
}
```



### Mitigation

In `UniswapImplementation::beforeSwap`, when comparing `params.amountSpecified`, which represents ETH, use `ethIn` instead of `tokenOut`, which represents the collection token.

```diff
function beforeSwap(address sender, PoolKey calldata key, IPoolManager.SwapParams memory params, bytes calldata hookData) public override onlyByPoolManager returns (bytes4 selector_, BeforeSwapDelta beforeSwapDelta_, uint24 swapFee_) {
    ...

    if (trigger && pendingPoolFees.amount1 != 0) {
        ...

        if (params.amountSpecified >= 0) {
            ...
        }
        // As we have a negative amountSpecified, this means that we are spending any amount
        // of token to get a specific amount of undesired token.
        else {
            ...

            // If we cannot fulfill the full amount of the internal orderbook, then we want
            // to avoid using any of it, as implementing proper support for exact input swaps
            // is significantly difficult when we want to restrict them by the output token
            // we have available.
-           if (tokenOut <= uint(-params.amountSpecified)) {
+           if (ethIn <= uint(-params.amountSpecified)) {
                // Update our hook delta to reduce the upcoming swap amount to show that we have
                // already spent some of the ETH and received some of the underlying ERC20.
                // Specified = exact input (ETH)
                // Unspecified = token1
                beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
            } else {
                ethIn = tokenOut = 0;
            }
        }

        // Reduce the amount of fees that have been extracted from the pool and converted
        // into ETH fees.
        if (ethIn != 0 || tokenOut != 0) {
            ...
        }
    }

    // Set our return selector
    selector_ = IHooks.beforeSwap.selector;
}
```