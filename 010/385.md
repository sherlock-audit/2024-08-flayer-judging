Narrow Fiery Rabbit

High

# Invalid validation of the `UniswapImplementation::beforeSwap` hook when exact in swap

## Lines of code
<https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L547>

## Vulnerability Detail
### Description
Flayer implements a novel internal swap mechanism that aims to prevent slippage within the pool whilst converting pool rewards from an unwanted token, the CollectionToken (CT), into a wanted token, an ETH equivalent token (EET) If the collection have any token1 fee tokens that can use to fill the swap before it hits the Uniswap pool. 

When exact in swap (L532 - 556), the internal swap will be fullfill the full amount of `pendingPoolFees.amount1`, If it cannot fulfill the full amount of the internal orderbook, it avoids using any of it.

This functionality has a flaw in exact-input swaps where `tokenOut` (L535) equals to the `pendingPoolFees.amount1`. The validation (L547) fails because it compares `tokenOut`(the CollectionToken output amount) to `params.amountSpecified`(the ETH equivalent token input amount). Consequently, the internal swap isn't used, even though the `pendingPoolFees.amount1` could fulfill the swap without hitting the Uniswap pool.

```solidity
File: UniswapImplementation.sol
490:     function beforeSwap(address sender, PoolKey calldata key, IPoolManager.SwapParams memory params, bytes calldata hookData) public override onlyByPoolManager returns (bytes4 selector_, BeforeSwapDelta beforeSwapDelta_, uint24 swapFee_) {
491:         PoolId poolId = key.toId();
492: 
493:         // Ensure our dynamic fees are set to the correct amount and mark it with the override flag
494:         swapFee_ = getFee(poolId, sender) | LPFeeLibrary.OVERRIDE_FEE_FLAG;
495: 
496:         // Load our PoolFees as storage as we will manipulate them later if we trigger
497:         ClaimableFees storage pendingPoolFees = _poolFees[poolId];
498:         PoolParams memory poolParams = _poolParams[poolId];
499: 
500:         // We want to check if our token0 is the eth equivalent, or if it has swapped to token1
501:         bool trigger = poolParams.currencyFlipped ? !params.zeroForOne : params.zeroForOne;
502:         if (trigger && pendingPoolFees.amount1 != 0) {
---
513:             if (params.amountSpecified >= 0) {
514:                 uint amountSpecified = (uint(params.amountSpecified) > pendingPoolFees.amount1) ? pendingPoolFees.amount1 : uint(params.amountSpecified);
515: 
516:                 // Capture the amount of desired token required at the current pool state to
517:                 // purchase the amount of token speicified, capped by the pool fees available. We
518:                 // don't apply a fee for this as it benefits the ecosystem and essentially performs
519:                 // a free swap benefitting both parties.
520:                 (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
521:                     sqrtPriceCurrentX96: sqrtPriceX96,
522:                     sqrtPriceTargetX96: params.sqrtPriceLimitX96,
523:                     liquidity: poolManager.getLiquidity(poolId),
524:                     amountRemaining: int(amountSpecified),
525:                     feePips: 0
526:                 });
527: 
528:                 // Update our hook delta to reduce the upcoming swap amount to show that we have
529:                 // already spent some of the ETH and received some of the underlying ERC20.
530:                 beforeSwapDelta_ = toBeforeSwapDelta(-tokenOut.toInt128(), ethIn.toInt128());
531:             }
532:             // As we have a negative amountSpecified, this means that we are spending any amount
533:             // of token to get a specific amount of undesired token.
534:             else {
535:                 (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
536:                     sqrtPriceCurrentX96: sqrtPriceX96,
537:                     sqrtPriceTargetX96: params.sqrtPriceLimitX96,
538:                     liquidity: poolManager.getLiquidity(poolId),
539:                     amountRemaining: int(pendingPoolFees.amount1),
540:                     feePips: 0
541:                 });
542: 
543:                 // If we cannot fulfill the full amount of the internal orderbook, then we want
544:                 // to avoid using any of it, as implementing proper support for exact input swaps
545:                 // is significantly difficult when we want to restrict them by the output token
546:                 // we have available.
547:                 if (tokenOut <= uint(-params.amountSpecified)) {
548:                     // Update our hook delta to reduce the upcoming swap amount to show that we have
549:                     // already spent some of the ETH and received some of the underlying ERC20.
550:                     // Specified = exact input (ETH)
551:                     // Unspecified = token1
552:                     beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
553:                 } else {
554:                     ethIn = tokenOut = 0;
555:                 }
556:             }
---
580:     }

```

## Impact
The internal swap isn't used, even though the `pendingPoolFees.amount1` could fulfill the swap without hitting the Uniswap pool which breaks the intended functionality for internal swap.

## Proof of Concept
### Coded proof of concept
<details>
<summary>Toggle</summary>

Add the following test inside `test/Locker.t.sol` and run with 
```bash
forge test --match-path test/Locker.t.sol --match-test test_CanSwapWithIntermediaryToken1PoolFeeSwapExactInputWhenOutputEqToPoolFee
```

The test will fail because the event `BaseImplementation.PoolFeesSwapped` is not properly emitted with the internal swap amount, and the `fees.amount` after swap not be zero.

```solidity
      function test_CanSwapWithIntermediaryToken1PoolFeeSwapExactInputWhenOutputEqToPoolFee() public {
        // Add liquidity to the pool to allow for swaps
        _addLiquidityToPool(address(erc721b), 10 ether, int(10 ether), false);

        // Reference our collection token
        ICollectionToken token = locker.collectionToken(address(erc721b));

        // Confirm our starting balance of the pool
        uint poolStartEth = 12.071067811865475244 ether;
        uint poolTokenStart = 24.142135623730950488 ether;
        _assertNativeBalance(address(poolManager), poolStartEth, 'Invalid starting poolManager ETH balance');
        assertEq(token.balanceOf(address(poolManager)), poolTokenStart, 'Invalid starting poolManager token balance');

        // Add 2 tokens to the pool fees
        deal(address(token), address(this), 2 ether);
        token.approve(address(uniswapImplementation), 2 ether);
        uniswapImplementation.depositFees(address(erc721b), 0, 2 ether);

        // Confirm that the fees are ready
        IBaseImplementation.ClaimableFees memory fees = uniswapImplementation.poolFees(address(erc721b));
        assertEq(fees.amount0, 0, 'Incorrect starting pool ETH fees');
        assertEq(fees.amount1, 2 ether, 'Incorrect starting pool token1 fees');

        uint internalSwapEthInput = 1.090325523878396331 ether;
        uint internalSwapTokenOutput = 2 ether;
        // Get our user's starting balances
        _dealExactNativeToken(address(this), internalSwapEthInput);
        _approveNativeToken(address(this), address(poolSwap), type(uint).max);

        _assertNativeBalance(address(this), internalSwapEthInput, 'Invalid starting user ETH balance');
        assertEq(token.balanceOf(address(this)), 0, 'Invalid starting user token balance');

        PoolKey memory poolKey = abi.decode(uniswapImplementation.getCollectionPoolKey(address(erc721b)), (PoolKey));

        // Confirm that the pool fee tokens have been swapped to ETH
        vm.expectEmit();
        emit BaseImplementation.PoolFeesSwapped(0xa0Cb889707d426A7A386870A03bc70d1b0697598, false, internalSwapEthInput, internalSwapTokenOutput);

        // Make a swap that spends 1.090325523878396331  ETH to acquire as much underlying token as possible
        poolSwap.swap(
            poolKey,
            IPoolManager.SwapParams({
                zeroForOne: _poolKeyZeroForOne(poolKey),
                amountSpecified: -1.090325523878396331 ether,
                sqrtPriceLimitX96: TickMath.MAX_SQRT_PRICE - 1
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );

        // Confirm that the pool fee tokens are subsequently distributed
        fees = uniswapImplementation.poolFees(address(erc721b));
        assertEq(fees.amount0, 0, 'Incorrect closing pool ETH fees');
        assertEq(fees.amount1, 0, 'Incorrect closing pool token1 fees');

        // Determine the amount that Uniswap provides us for the remaining ETH amount
        uint uniswapSwapTokenOutput = 0 ether;

        // Confirm that the user has received their total expected tokens
        _assertNativeBalance(address(this), 0, 'Invalid closing user ETH balance');
        assertEq(token.balanceOf(address(this)), internalSwapTokenOutput + uniswapSwapTokenOutput, 'Invalid closing user token balance');

        // Confirm that the pool has gained the expected 3 ETH and reduced the token holding
        _assertNativeBalance(address(poolManager), poolStartEth + internalSwapEthInput, 'Invalid closing poolManager ETH balance');
        assertEq(token.balanceOf(address(poolManager)), poolTokenStart - uniswapSwapTokenOutput, 'Invalid closing poolManager token balance');
    }
```
</details>

## Tool used

Manual Review

## Recommendation
Update the condition to `uint(-params.amountSpecified) >= ethIn`. This ensures that the `params.amountSpecified`(exact input) is higher than the `ethIn` required for `tokenOut`, allowing the use of `pendingPoolFees.amount1` for the internal swap.

```diff
    else {
        (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
            sqrtPriceCurrentX96: sqrtPriceX96,
            sqrtPriceTargetX96: params.sqrtPriceLimitX96,
            liquidity: poolManager.getLiquidity(poolId),
            amountRemaining: int(pendingPoolFees.amount1),
            feePips: 0
        });

        // If we cannot fulfill the full amount of the internal orderbook, then we want
        // to avoid using any of it, as implementing proper support for exact input swaps
        // is significantly difficult when we want to restrict them by the output token
        // we have available.
-       if (tokenOut <= uint(-params.amountSpecified)) {
+       if (uint(-params.amountSpecified) >= ethIn) {
            // Update our hook delta to reduce the upcoming swap amount to show that we have
            // already spent some of the ETH and received some of the underlying ERC20.
            // Specified = exact input (ETH)
            // Unspecified = token1
            beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
        } else {
            ethIn = tokenOut = 0;
        }
    }
```
