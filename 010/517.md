Large Mauve Parrot

Medium

# `UniswapImplementation::beforeSwap()` might revert when swapping native tokens to collection tokens

### Summary

[UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L547) performs a wrong check which can lead to swaps from native tokens to collection tokens reverting.

### Root Cause

[UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L547) swaps the internally accumulated collection token fees into native tokens when:

1. The hook accumulated at least `1` wei of fees in collection tokens
2. An user is swapping native tokens for collection tokens

When the swap is performed by specifing the exact amount of native tokens to pay (ie. `amountSpecified < 0`) the protocol should allow the internal swap only when the amount of native tokens being paid is enough to convert all of the accumulated collection token fees. The protocol however does this incorrectly, as the code checks the `amountSpecified` against `tokenOut` instead of `ethIn`:

```solidity
if (params.amountSpecified >= 0) {
    ...
else {
    (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
        sqrtPriceCurrentX96: sqrtPriceX96,
        sqrtPriceTargetX96: params.sqrtPriceLimitX96,
        liquidity: poolManager.getLiquidity(poolId),
        amountRemaining: int(pendingPoolFees.amount1),
        feePips: 0
    });

@>  if (tokenOut <= uint(-params.amountSpecified)) {
        // Update our hook delta to reduce the upcoming swap amount to show that we have
        // already spent some of the ETH and received some of the underlying ERC20.
        // Specified = exact input (ETH)
        // Unspecified = token1
        beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
    } else {
        ethIn = tokenOut = 0;
    }
}
```

This results in the [UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490) reverting in the situations explained in internal pre-conditions below.

### Internal pre-conditions

1. User is swapping native tokens for collection tokens
2. The hook accumulated at least `1` wei of collection tokens in fees
3. `tokenOut` is lower than `uint(-params.amountSpecified)` and `ethIn` is bigger than `uint(-params.amountSpecified)`

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

All swaps that follow the internal pre-conditions will revert.

### PoC

To copy-paste in `UniswapImplementation.t.sol`:
```solidity
function test_swapFails() public {
    address alice = makeAddr("alice");
    address bob = makeAddr("bob");

    ERC721Mock erc721 = new ERC721Mock();
    CollectionToken ctoken = CollectionToken(locker.createCollection(address(erc721), 'ERC721', 'ERC', 0));




    //### APPROVALS
    //-> Alice approvals
    vm.startPrank(alice);
    erc721.setApprovalForAll(address(locker), true);
    ctoken.approve(address(poolSwap), type(uint256).max);
    ctoken.approve(address(uniswapImplementation), type(uint256).max);
    vm.stopPrank();
    _approveNativeToken(alice, address(locker), type(uint).max);
    _approveNativeToken(alice, address(poolManager), type(uint).max);
    _approveNativeToken(alice, address(poolSwap), type(uint).max);

    //-> Bob approvals
    vm.startPrank(bob);
    erc721.setApprovalForAll(address(locker), true);
    ctoken.approve(address(uniswapImplementation), type(uint256).max);
    ctoken.approve(address(poolSwap), type(uint256).max);
    vm.stopPrank();
    _approveNativeToken(bob, address(locker), type(uint).max);
    _approveNativeToken(bob, address(poolManager), type(uint).max);
    _approveNativeToken(bob, address(poolSwap), type(uint).max);

    // uniswapImplementation.setAmmFee(1000);
    // uniswapImplementation.setAmmBeneficiary(BENEFICIARY);



    //### MINT NFTs
    //-> Mint 10 tokens to Alice
    uint[] memory _tokenIds = new uint[](10);
    for (uint i; i < 10; ++i) {
        erc721.mint(alice, i);
        _tokenIds[i] = i;
    }

    //-> Mint an extra token to Alice
    uint[] memory _tokenIdToDepositAlice = new uint[](1);
    erc721.mint(alice, 10);
    _tokenIdToDepositAlice[0] = 10;


    //### [ALICE] COLLECTION INITIALIZATION + LIQUIDITY PROVISION
    //-> alice initializes a collection and adds liquidity: 1e19 NATIVE + 1e19 CTOKEN
    uint256 initialNativeLiquidity = 1e19;
    _dealNativeToken(alice, initialNativeLiquidity);
    vm.startPrank(alice);
    locker.initializeCollection(address(erc721), initialNativeLiquidity, _tokenIds, 0, SQRT_PRICE_1_1);
    vm.stopPrank();



    //### [ALICE] ADDING CTOKEN FEES TO HOOK
    //-> alice deposits an NFT to get 1e18 CTOKEN and then deposits 1e18 CTOKENS as fees in the UniswapImplementation hook
    vm.startPrank(alice);
    locker.deposit(address(erc721), _tokenIdToDepositAlice, alice);
    uniswapImplementation.depositFees(address(erc721), 0, 1e18);
    vm.stopPrank();



    //### [BOB] SWAP FAILS 
    _dealNativeToken(bob, 1e18);

    //-> bob swaps `1e18` NATIVE tokens for CTOKENS but the swap fails
    vm.startPrank(bob);
    poolSwap.swap(
        PoolKey({
            currency0: Currency.wrap(address(ctoken)),
            currency1: Currency.wrap(address(WETH)),
            fee: LPFeeLibrary.DYNAMIC_FEE_FLAG,
            tickSpacing: 60,
            hooks: IHooks(address(uniswapImplementation))
        }),
        IPoolManager.SwapParams({
            zeroForOne: false,
            amountSpecified: -int(1e18),
            sqrtPriceLimitX96: (TickMath.MAX_SQRT_PRICE - 1)
        }),
        PoolSwapTest.TestSettings({
            takeClaims: false,
            settleUsingBurn: false
        }),
        ''
    );
}
```


### Mitigation

In [UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L547) check against `ethIn` instead of `tokenOut`:

```solidity
@>  if (ethIn <= uint(-params.amountSpecified)) {
        // Update our hook delta to reduce the upcoming swap amount to show that we have
        // already spent some of the ETH and received some of the underlying ERC20.
        // Specified = exact input (ETH)
        // Unspecified = token1
        beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
    } else {
        ethIn = tokenOut = 0;
    }
```