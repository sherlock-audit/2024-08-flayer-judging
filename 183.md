Happy Wintergreen Kookaburra

High

# The `Locker.sol` contract did not initialize the owner while using the solady ownable contract

## Summary
The contract `Locker.sol` does not initialize an owner during deployment, which means the deployer does not automatically become the owner. Instead, ownership must be established through the `_initializeOwner` call as a constructor or initializer, ensuring that ownership and control can be assigned or changed at `transferOwnership`.

## Vulnerability Detail

- The OpenZeppelin Ownable contract includes a constructor that sets the initial owner to an address provided at deployment but that is not the same as the solady ownable.sol as there you have to initialize an owner
- https://github.com/Vectorized/solady/blob/main/src/auth/Ownable.sol#L87
```solidity
    /// @dev Initializes the owner directly without authorization guard.
    /// This function must be called upon initialization,
    /// regardless of whether the contract is upgradeable or not.
    /// This is to enable generalization to both regular and upgradeable contracts,
    /// and to save gas in case the initial owner is not the caller.
    /// For performance reasons, this function will not check if there
    /// is an existing owner.
    function _initializeOwner(address newOwner) internal virtual {
```
- The solady ownable does not set the initial owner to msg.sender (the deployer) directly in the constructor, because the contract can handle cases where ownership is assigned to a different address without incurring extra gas costs that might arise from unnecessary operations if the deployer is not the intended owner, therefore, it recommends users who are going to use the contract to always initialize the owner themselves

## Impact
- The deployer won't be able to call the functions with `onlyOwner` modifiers, such as updating the `setListingsContract`, `CollectionShutdown`, and `TaxCalculator` functions, setting the `Locker implementation`, or `pausing` the protocol

## Code Snippet
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L459-L510

<details>

<summary>Snippet</summary>

```solidity
    /**
     * Allows a new {Listings} contract to be set.
     *
     * @param _listings The new contract address
     */
    function setListingsContract(address _listings) public onlyOwner {
        if (_listings == address(0)) revert ZeroAddress();
        listings = IListings(_listings);
        emit ListingsContractUpdated(_listings);
    }

    /**
     * Allows a {ICollectionShutdown} contract to be set. This will be the only contract
     * that will be able call the `sunsetCollection` function, allowing it to remove the
     * functionality of a {CollectionToken}.
     *
     * @param _collectionShutdown The new contract address
     */
    function setCollectionShutdownContract(address payable _collectionShutdown) public onlyOwner {
        if (_collectionShutdown == address(0)) revert ZeroAddress();
        collectionShutdown = ICollectionShutdown(_collectionShutdown);
        emit CollectionShutdownContractUpdated(_collectionShutdown);
    }

    /**
     * Allows a {ITaxCalculator} contract to be set. This will be the contract that
     * will determine all tax for liquid, dutch and protected listings.
     *
     * @param _taxCalculator The new contract address
     */
    function setTaxCalculator(address _taxCalculator) public onlyOwner {
        if (_taxCalculator == address(0)) revert ZeroAddress();
        taxCalculator = ITaxCalculator(_taxCalculator);
        emit TaxCalculatorContractUpdated(_taxCalculator);
    }

    /**
     * Allows the contract owner to set the implementation used by the {Locker}.
     *
     * @dev This can only be set once to a non-zero address.
     *
     * @param _implementation The new `IBaseImplementation` address
     */
    function setImplementation(address _implementation) public onlyOwner {
        if (address(implementation) != address(0)) revert CannotChangeImplementation();
        implementation = IBaseImplementation(_implementation);
    }

    /**
     * Allows the contract owner to pause all {Locker} related activity, essentially preventing
     * any activity in the protocol.
     *
     * @param _paused If we are pausing (true) or unpausing (false) the protocol
     */
    function pause(bool _paused) public onlyOwner {
        (_paused) ? _pause() : _unpause();
    }
```
</details>

## Tool used

Manual Review

## Recommendation
- Initialize the owner at the constructor as a deployer 
```diff
    constructor (address _tokenImplementation, address _lockerManager) {
        // Ensure that we don't provide zero addresses
        if (_tokenImplementation == address(0)) revert InvalidTokenImplementation(); //n if the tokeImp is zero address revert

        // Set our base ERC20 token implementation
        tokenImplementation = _tokenImplementation; //n  the token implementation gets set into the provided implementation

        // Reference our Locker Manager
        lockerManager = ILockerManager(_lockerManager); //n the address of the interface is set to the provided one

+       // Assign our contract owner
+       _initializeOwner(msg.sender);

        }
```