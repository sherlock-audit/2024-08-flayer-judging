Wobbly Neon Hyena

Medium

# Invalid comparison in `CollectionShutdown::cancel`, blocking users from canceling the shutdown process for collections whose denomination is > 0

### Summary

When collections are created in the `Locker` contract, users can add denominations for the created ERC20, these are extra decimals to the token, representing some kind of meme coins. This denomination is "imaginary" decimals that are used whenever using these tokens within the protocol, i.e. all balances and supply are e18.

On the other hand, when canceling the shutdown process of a collection using `CollectionShutdown::cancel`, the protocol checks if the supply of the collection tokens is greater than 4 tokens (4e18), however, it denominates the threshold with the denomination decimals and compares it with the bare supply, which is 18 decimals:
```solidity
// Check if the total supply has surpassed an amount of the initial required
// total supply. This would indicate that a collection has grown since the
// initial shutdown was triggered and could result in an unsuspected liquidation.
if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
    revert InsufficientTotalSupplyToCancel();
}
```

This leads to inaccurate results, blocking users from canceling the shutdown process for collections with a high total supply.

### Root Cause

In `CollectionShutdown:: cancel`, total supply (which is not denominated) is being compared with 4e18 ** denomination, always leading to inaccurate results for collections with denomination >0, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L390-L405).

### Impact

Users can't cancel the shutdown process for active/decent collections.

### PoC

Add the following test in `flayer/test/utils/CollectionShutdown.t.sol`:

```solidity
function test_cancel_WrongDenominationComparison() public {
    ERC721Mock NFT = new ERC721Mock();
    ICollectionToken NFTcollectionToken;

    // denomination = 5
    locker.createCollection(address(NFT), "Test Collection", "TEST", 5);
    NFTcollectionToken = locker.collectionToken(address(NFT));

    // Simulate the minting of 3 tokens
    vm.prank(address(locker));
    NFTcollectionToken.mint(address(this), 3 ether);

    // Total supply is less than MAX_SHUTDOWN_TOKENS (4 ether)
    assertLt(
        NFTcollectionToken.totalSupply(),
        collectionShutdown.MAX_SHUTDOWN_TOKENS()
    );

    // Start the shutdown process, doesn't revert
    NFTcollectionToken.approve(address(collectionShutdown), type(uint).max);
    collectionShutdown.start(address(NFT));

    // Shutdown process started
    assertGt(
        collectionShutdown.collectionParams(address(NFT)).shutdownVotes,
        0
    );

    // Simulate the minting of 2 more tokens
    vm.prank(address(locker));
    NFTcollectionToken.mint(address(this), 2 ether);

    // Total supply is greater than MAX_SHUTDOWN_TOKENS (4 ether)
    assertGt(
        NFTcollectionToken.totalSupply(),
        collectionShutdown.MAX_SHUTDOWN_TOKENS()
    );

    // Can't cancel the shutdown process
    vm.expectRevert(
        ICollectionShutdown.InsufficientTotalSupplyToCancel.selector
    );
    collectionShutdown.cancel(address(NFT));
}
```

### Mitigation

Remove the denomination from the comparison in `CollectionShutdown::cancel`:

```diff
function cancel(address _collection) public whenNotPaused {
    // Ensure that the vote count has reached quorum
    CollectionShutdownParams memory params = _collectionParams[_collection];
    if (!params.canExecute) revert ShutdownNotReachedQuorum();

    // Check if the total supply has surpassed an amount of the initial required
    // total supply. This would indicate that a collection has grown since the
    // initial shutdown was triggered and could result in an unsuspected liquidation.
-   if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
+   if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS) {
        revert InsufficientTotalSupplyToCancel();
    }

    // Remove our execution flag
    delete _collectionParams[_collection];
    emit CollectionShutdownCancelled(_collection);
}
```