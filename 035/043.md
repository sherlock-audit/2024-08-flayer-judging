Wobbly Neon Hyena

Medium

# Invalid comparison in `CollectionShutdown::start`, allowing users to start the shutdown process for collections whose denomination is > 0

### Summary

When collections are created in the `Locker` contract, users can add denominations for the created ERC20, these are extra decimals to the token, representing some kind of meme coins. This denomination is "imaginary" decimals that are used whenever using these tokens within the protocol, i.e. all balances and supply are e18.

On the other hand, when starting the shutdown process of a collection using `CollectionShutdown::start`, the protocol checks if the supply of the collection tokens is greater than 4 tokens (4e18), however, it denominates the threshold with the denomination decimals and compares it with the bare supply, which is 18 decimals:
```solidity
if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();
```

This leads to inaccurate results, allowing users to start the shutdown process for collections with high total supply.

### Root Cause

In `CollectionShutdown::start`, total supply (which is not denominated) is being compared with 4e18 ** denomination, always leading to inaccurate results for collections with denomination >0, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157).

### Impact

Users can start the shutdown process for active/decent collections.

### PoC

Add the following test in `flayer/test/utils/CollectionShutdown.t.sol`:

```solidity
function test_WrongDenominationComparison() public {
    ERC721Mock NFT = new ERC721Mock();
    ICollectionToken NFTcollectionToken;

    // denomination = 5
    locker.createCollection(address(NFT), "Test Collection", "TEST", 5);
    NFTcollectionToken = locker.collectionToken(address(NFT));

    // Simulate the minting of 100 tokens
    vm.prank(address(locker));
    NFTcollectionToken.mint(address(this), 100 ether);

    // Total supply is greater than MAX_SHUTDOWN_TOKENS (4 ether)
    assertGt(
        NFTcollectionToken.totalSupply(),
        collectionShutdown.MAX_SHUTDOWN_TOKENS()
    );

    // Start the shutdown process, doesn't revert
    NFTcollectionToken.approve(address(collectionShutdown), type(uint).max);
    collectionShutdown.start(address(NFT));

    // Shutdown process started
    assertGt(
        collectionShutdown.collectionParams(address(NFT)).shutdownVotes,
        0
    );
}
```

### Mitigation

Remove the denomination from the comparison in `CollectionShutdown::start`:
```diff
function start(address _collection) public whenNotPaused {
    // Confirm that this collection is not prevented from being shutdown
    if (shutdownPrevented[_collection]) revert ShutdownPrevented();

    // Ensure that a shutdown process is not already actioned
    CollectionShutdownParams memory params = _collectionParams[_collection];
    if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();

    // Get the total number of tokens still in circulation, specifying a maximum number
    // of tokens that can be present in a "dormant" collection.
    params.collectionToken = locker.collectionToken(_collection);
    uint totalSupply = params.collectionToken.totalSupply();
-   if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();
+   if (totalSupply > MAX_SHUTDOWN_TOKENS) revert TooManyItems();

    // Set our quorum vote requirement
    params.quorumVotes = uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT);

    // Notify that we are processing a shutdown
    emit CollectionShutdownStarted(_collection);

    // Cast our vote from the user
    _collectionParams[_collection] = _vote(_collection, params);
}
```