Clean Snowy Mustang

High

# Incorrect denominator is used for calculating AMM fee

## Summary
Incorrect denominator is used for calculating AMM fee.

## Vulnerability Detail
UniswapImplementation owner can call [setAmmFee()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L803) to set an overwritting AMM fee rate.

[UniswapImplementation.sol#L803-L810](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L803-L810):
```solidity
    function setAmmFee(uint24 _ammFee) public onlyOwner {
        // Ensure that the AMM fee is a valid amount
@>      _ammFee.validate();

        // Set our pool fee overwrite value
        ammFee = _ammFee;
        emit AMMFeeSet(_ammFee);
    }
```
The AMM fee rate is expected to conform to Uniswap V4 requirements, hence [validate()](https://github.com/Uniswap/v4-core/blob/18b223cab19dc778d9d287a82d29fee3e99162b0/src/libraries/LPFeeLibrary.sol#L43) is called to ensure the AMM fee rate is a valid amount.

[LPFeeLibrary.sol#L43-L45](https://github.com/Uniswap/v4-core/blob/18b223cab19dc778d9d287a82d29fee3e99162b0/src/libraries/LPFeeLibrary.sol#L43-L45):
```solidity
    function validate(uint24 self) internal pure {
        if (!self.isValid()) LPFeeTooLarge.selector.revertWith(self);
    }
```

[LPFeeLibrary.sol#L37-L39](https://github.com/Uniswap/v4-core/blob/18b223cab19dc778d9d287a82d29fee3e99162b0/src/libraries/LPFeeLibrary.sol#L37-L39):
```solidity
    function isValid(uint24 self) internal pure returns (bool) {
        return self <= MAX_LP_FEE;
    }
```

[LPFeeLibrary.sol#L24-L25](https://github.com/Uniswap/v4-core/blob/18b223cab19dc778d9d287a82d29fee3e99162b0/src/libraries/LPFeeLibrary.sol#L24-L25):
```solidity
    /// @notice the lp fee is represented in hundredths of a bip, so the max is 100%
    uint24 public constant MAX_LP_FEE = 1000000;
```

As we can see from above, the AMM fee is expected to be represented in hundredths of a bip, and the range is [0, 1000000]. 

However, when calculates AMM fee in [afterswap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L594), the denominator is **100_000** instead of **1_000_000**:

[UniswapImplementation.sol#L606-L607](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L606-L607):
```solidity
            // Calculate our fee amount
            uint feeAmount = uint128(swapAmount) * ammFee / 100_000;
```

This means the AMM fee will be **10** times larger than expected, and if AMM fee rate is set to be larger than **100_000**, the AMM fee will be more than the swap amount, and transaction will eventually revert in PoolManager.

## Impact

1. **10** time larger AMM fee is charged and trader suffers a loss;
2. Swap transaction will revert if AMM fee rate is larger than **100_000**.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L607

## Tool used

Manual Review

## Recommendation

When calculates the AMM fee amount, the denominator should be 1_000_000 (`MAX_LP_FEE`).

[UniswapImplementation.sol#L607](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L607):
```diff
-            uint feeAmount = uint128(swapAmount) * ammFee / 100_000;
+            uint feeAmount = uint128(swapAmount) * ammFee / 1_000_000;
```