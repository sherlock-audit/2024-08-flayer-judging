Raspy Raspberry Tapir

Medium

# `ERC721Bridgable` is not EIP-721 compliant

### Summary

According to the [README](https://github.com/sherlock-audit/2024-08-flayer/blob/main/README.md#moongate-4):
>  The Bridged721 should be strictly  compliant with EIP-721 and EIP-2981

**1. `ownerOf` is required to throw for invalid NFTs, but it doesn't**

[EIP-721](https://eips.ethereum.org/EIPS/eip-721) states the following about `ownerOf`:

```solidity
/// @notice Find the owner of an NFT
/// @dev NFTs assigned to zero address are considered invalid, and queries
///  about them do throw.
/// @param _tokenId The identifier for an NFT
/// @return The address of the owner of the NFT
function ownerOf(uint256 _tokenId) external view returns (address);
```

But we see that [ERC721Bridgable::ownerOf](https://github.com/sherlock-audit/2024-08-flayer/blob/main/moongate/src/libs/ERC721Bridgable.sol#L108-L110) doesn't throw for invalid NFTs, but returns a zero address:

```solidity
function ownerOf(uint id) public view override returns (address owner_) {
    owner_ = _ownerOf[id];
}
```

It's worth contrasting this with the implementation of `ownerOf` in e.g. [OZ's ERC721Upgradeable](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/668ea63dc629f2e3ebf27fc4c7caa21766314511/contracts/token/ERC721/ERC721Upgradeable.sol#L87-L89):

```solidity
function ownerOf(uint256 tokenId) public view virtual returns (address) {
    return _requireOwned(tokenId);
}

function _requireOwned(uint256 tokenId) internal view returns (address) {
    address owner = _ownerOf(tokenId);
    if (owner == address(0)) {
        revert ERC721NonexistentToken(tokenId);
    }
    return owner;
}
```

**2. `tokenURI` is required to throw for invalid NFTs, but it doesn't**

[EIP-721](https://eips.ethereum.org/EIPS/eip-721) states the following about `tokenURI`:
```solidity
/// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
/// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
///  3986. The URI may point to a JSON file that conforms to the "ERC721
///  Metadata JSON Schema".
function tokenURI(uint256 _tokenId) external view returns (string);
```

But we see that [ERC721Bridgable::tokenURI](https://github.com/sherlock-audit/2024-08-flayer/blob/main/moongate/src/libs/ERC721Bridgable.sol#L95-L97) doesn't throw for invalid NFTs, but returns an empty string:

```solidity
function tokenURI(uint id) public view override returns (string memory) {
    return uriForToken[id];
}
```

It's worth contrasting this with the implementation of `tokenURI` in e.g. [OZ's ERC721Upgradeable](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/668ea63dc629f2e3ebf27fc4c7caa21766314511/contracts/token/ERC721/ERC721Upgradeable.sol#L110-L115):

```solidity
function tokenURI(uint256 tokenId) public view virtual returns (string memory) {
    _requireOwned(tokenId);

    string memory baseURI = _baseURI();
    return bytes(baseURI).length > 0 ? string.concat(baseURI, tokenId.toString()) : "";
}
```

### Impact

Protocols integrating with `ERC721Bridgable` may work incorrectly.

### Mitigation

Throw for non-existing NFTs, as per the specification.