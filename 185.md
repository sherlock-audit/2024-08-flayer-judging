Lone Coconut Cat

Medium

# Relisting Then Cancelling A Liquidation Auction Results In Losses On Subsequent Deposits

## Summary

Relisting a token undergoing dutch auction risks erroneously persisting an orphaned `isLiquidation` state flags on the token, resulting in loss of due harberger taxes and refunds when the token is relisted.

## Vulnerability Detail

In the following sequence, we protect, liquidate, relist and finally cancel a listing for an underlying token.

Once the sequence is finished, all value is conserved and the underlying token is back in the hands of the original owner - however we have managed to erroneously persist an `isLiquidation` flag against the underlying token, even though it is no longer undergoing liquidation.

### ProtectedListings.t.sol

First, add the following test sequence to `ProtectedListings.t.sol`:

```solidity
function testSherlock_CreateOrphanedFlag() external returns (uint256[] memory tokenIds) {

    // 0. Assume a flashloan address:
    address flashloan = address(0xf1a510a9);
    uint256 numberOfFlashloanTokens = 10;
    vm.startPrank(flashloan);
    {
        tokenIds = new uint256[](numberOfFlashloanTokens);
        for (uint256 i = 0; i < numberOfFlashloanTokens; i++) erc721a.mint(flashloan, tokenIds[i] = 10_000 + i);
        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(address(erc721a), tokenIds);
    }
    vm.stopPrank();

    assertEq(locker.collectionToken(address(erc721a)).balanceOf(flashloan), 10000000000000000000) /* Flashloan Initial Balance */;

    // 1. `deadbeef` mints and protects their token:
    address deadbeef = address(0xdeadbeef);
    uint256 deadbeefTokenId = 100;
    vm.startPrank(deadbeef);
        erc721a.setApprovalForAll(address(protectedListings), true);
        erc721a.mint(deadbeef, deadbeefTokenId);
        tokenIds = new uint256[](1);
        tokenIds[0] = deadbeefTokenId;
        IProtectedListings.ProtectedListing memory listing = IProtectedListings.ProtectedListing({
            owner: payable(deadbeef),
            tokenTaken: 0.95 ether,
            checkpoint: 0
        });
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: tokenIds,
                listing: listing
            })
        });
    vm.stopPrank();
    assertEq(locker.collectionToken(address(erc721a)).balanceOf(deadbeef), 950000000000000000) /* Deadbeef Balance After Deposit */;

    // 2. Block is mined:
    vm.roll(block.number + 1);
    vm.warp(block.timestamp + 2);

    // 3. Asset is now unhealthy.
    assert(protectedListings.getProtectedListingHealth(address(erc721a), deadbeefTokenId) < 0) /* unhealthy */;

    // 4. Start a liquidation:
    vm.startPrank(deadbeef);
        protectedListings.liquidateProtectedListing(address(erc721a), deadbeefTokenId);
    vm.stopPrank();
    assertEq(locker.collectionToken(address(erc721a)).balanceOf(deadbeef), 1000000000000000000) /* Deadbeef Liquidates Own Listing */;

    // 5. `deadbeefOtherAccount` flash loans the required capital:
    address deadbeefOtherAccount = address(0xdeadbeef + 1);
    uint256 flashLoanAmount = 3000000000000000000 + 90000000000000000;
    vm.startPrank(flashloan);
        locker.collectionToken(address(erc721a)).transfer(deadbeefOtherAccount, flashLoanAmount);
    vm.stopPrank();

    assertEq(locker.collectionToken(address(erc721a)).balanceOf(flashloan), 6910000000000000000) /* Flash Loan Balance Reduced */;

    // 6. Relist, masquerading as a different account:
    vm.startPrank(deadbeefOtherAccount);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint256).max) /* Max Approve Listings */;

        tokenIds = new uint256[](1);
        tokenIds[0] = deadbeefTokenId;
        listings.relist(
            IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: tokenIds,
                listing: IListings.Listing({
                    owner: payable(deadbeef),
                    created: uint40(block.timestamp),
                    duration: 7 days /* convert back into a liquid listing */,
                    floorMultiple: 400
                })
            }),
            false
        );
    vm.stopPrank();

    // 7. Now the listing is no longer in dutch auction, cancel it:
    vm.startPrank(deadbeef);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint256).max);
        listings.cancelListings(address(erc721a), tokenIds, false);

        // Repay the flash loan for deadbeef:
        locker.collectionToken(address(erc721a)).transfer(flashloan, flashLoanAmount);

        // Assert that value has been conserved (nothing has been lost):
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(deadbeef), 0);
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(deadbeefOtherAccount), 0);
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(flashloan), numberOfFlashloanTokens * 1 ether);

        // Deadbeef is back in possession of the token:
        assertEq(erc721a.ownerOf(deadbeefTokenId), deadbeef);

    vm.stopPrank();

}
```

Then add the following `console.log`s to `Listings.sol` to the end of [`cancelListings`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L414C14-L414C28):

```diff
// Create our checkpoint as utilisation rates will change
protectedListings.createCheckpoint(_collection); /// @custom:hunter now we can call this arbitrarily since tehre are no throws
+
+ for (uint256 i = 0; i < _tokenIds.length; i++) {
+    console.log("After cancellation, is still a liquidation?", _isLiquidation[_collection][_tokenIds[i]]);
+ }
+
emit ListingsCancelled(_collection, _tokenIds);
```

And finally run using:

```shell
forge test --match-test "testSherlock_CreateOrphanedFlag" -vv
```

```shell
Ran 1 test for test/ProtectedListings.t.sol:ProtectedListingsTest
[PASS] testSherlock_CreateOrphanedFlag() (gas: 2248654)
Logs:
  After cancellation, is still a liquidation? true

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.49ms (2.00ms CPU time)
```

This confirms that it is possible to persist an `_isLiquidation` flag against a token which is no longer undergoing liquidation.

## Impact

An orphaned `_isLiquidation` flag persisted against a token can result in losses for a users and the protocol if it were to be deposited back into the system.

This is because refunds for harberger taxes are only paid for the case where a token is specifically not marked as `_isLiquidation`:

```solidity
// Check if there is collateral on the listing, as this we bypass fees and refunds
if (!_isLiquidation[_collection][_tokenId]) { /// @audit only pay refunds for tokens which aren't marked as liquidations
    // Find the amount of prepaid tax from current timestamp to prepaid timestamp
    // and refund unused gas to the user.
    (uint fee, uint refund) = _resolveListingTax(_listings[_collection][_tokenId], _collection, false);
    emit ListingFeeCaptured(_collection, _tokenId, fee);

    assembly {
        tstore(FILL_FEE, add(tload(FILL_FEE), fee))
        tstore(FILL_REFUND, add(tload(FILL_REFUND), refund))
    }
} else {
    delete _isLiquidation[_collection][_tokenId];
}
```

```solidity
// We can process a tax refund for the existing listing if it isn't a liquidation
if (!_isLiquidation[_collection][_tokenId]) {
    (uint _fees,) = _resolveListingTax(oldListing, _collection, true); /// @audit only realize refunds and fees if not a liquidation
    if (_fees != 0) {
        emit ListingFeeCaptured(_collection, _tokenId, _fees);
    }
}
```

This materializes as losses for both the user and the protocol.

## Code Snippet

## Tool used

[**Shaheen Vision**](https://x.com/0x_Shaheen/status/1722664258142650806)

## Recommendation

In our proof of concept, we break the protocol invariant that a [dutch auction cannot be modified](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L311C13-L312C98) by first converting it back into a liquid listing via a call to [`relist`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625C14-L625C20).

Since dutch auctions should not be modifiable, we recommend **rejecting attempts to relist them**, as this is a form of modification.

In addition, ensure that the `_isLiquidation` flag is cleared when cancelling a listing.
