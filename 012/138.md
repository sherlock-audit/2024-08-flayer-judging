Clean Snowy Mustang

Medium

# Old beneficiary / AMM beneficiary won't be able to claim fees if the current beneficiary is a pool

## Summary
Old beneficiary / AMM beneficiary won't be able to claim fees if the current beneficiary is a pool.

## Vulnerability Detail

UniswapImplementation [beneficiary](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L52) receives a share of the fees collected, and [claim()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L164) can be called to send the collected fees to the beneficiary.

Beneficiary can be updated by calling [setBeneficiary()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L211), as the comment says, the old beneficiary will still have access to claim any fees that were generated whilst they were set.
[BaseImplementation.sol#L203-L211](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L203-L211):
```solidity
    /**
     * Allows our beneficiary address to be updated, changing the address that will
     * be allocated fees moving forward. The old beneficiary will still have access
     * to `claim` any fees that were generated whilst they were set.
     *
     * @param _beneficiary The new fee beneficiary
     * @param _isPool If the beneficiary is a Flayer pool
     */
    function setBeneficiary(address _beneficiary, bool _isPool) public onlyOwner {
```
However, `claim()` will revert if the current beneficiary is a pool, hence the old beneficiary cannot claim any fees.
[BaseImplementation.sol#L171](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L171):
```solidity
        if (beneficiaryIsPool) revert BeneficiaryPoolCannotClaim();
```

Please run the PoC in UniswapImplementation.t.sol to verify:
```solidity
    function testAudit_OldBeneficiaryCannotClaim() public {
        address alice = makeAddr("Alice");
        deal(address(WETH), alice, 1 ether);

        address oldBeneficiary = makeAddr("oldBeneficiary");

        // Set beneficiary
        uniswapImplementation.setBeneficiary(oldBeneficiary, false);

        // Deposit fees
        vm.startPrank(alice);
        WETH.approve(address(uniswapImplementation), 1 ether);
        uniswapImplementation.depositFees(address(flippedErc), 1 ether, 0);
        vm.stopPrank();

        // Set to beneficiary royalt to 100% (no poolFee)
        uniswapImplementation.setBeneficiaryRoyalty(100_0);
        
        // call `beforeAddLiquidity()` to distribute fees
        vm.prank(address(uniswapImplementation.poolManager()));
        uniswapImplementation.beforeAddLiquidity({
            sender: address(0),
            key: _poolKey(true),
            params: IPoolManager.ModifyLiquidityParams({
                tickLower: -887220,
                tickUpper: 887220,
                liquidityDelta: 0,
                salt: ''
            }),
            hookData: ''
        });

        // Update beneficiary to pool
        uniswapImplementation.setBeneficiary(address(flippedErc), true);

        // Old beneficiary has 1 ether fees to claim
        assertEq(uniswapImplementation.beneficiaryFees(oldBeneficiary), 1 ether);
        // Old beneficiary won't be able to claim
        vm.expectRevert(IBaseImplementation.BeneficiaryPoolCannotClaim.selector);
        uniswapImplementation.claim(oldBeneficiary);
    }
```

## Impact

Old beneficiary / AMM beneficiary has no access to claim any fees generated whilst they were set.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol#L171

## Tool used

Manual Review

## Recommendation

`beneficiaryIsPool` only indicates if the current beneficiary is a pool, protocol should store old beneficiaries info to tell if one is a pool, and should allow a old beneficiary to claim collected fees if they are not a pool.