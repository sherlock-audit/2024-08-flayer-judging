Lone Coconut Cat

Medium

# Beneficiaries Cannot Access Rewards When The Current Beneficiary Is A Pool

## Summary

When the current beneficiary of the [`BaseImplementation`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol) is a pool, previous beneficiaries are locked out from their rewards.

## Vulnerability Detail

The [`BaseImplementation`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/BaseImplementation.sol) is designed in a way that should not prevent previous beneficiaries from claiming their rewards.

Check the [setter](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/BaseImplementation.sol#L211C14-L211C28) documentation:

```solidity
/**
 * Allows our beneficiary address to be updated, changing the address that will
 * be allocated fees moving forward. The old beneficiary will still have access
 * to `claim` any fees that were generated whilst they were set.
 *
 * @param _beneficiary The new fee beneficiary
 * @param _isPool If the beneficiary is a Flayer pool
 */
function setBeneficiary(address _beneficiary, bool _isPool) public onlyOwner {
    beneficiary = _beneficiary;
    beneficiaryIsPool = _isPool;

    // If we are setting the beneficiary to be a Flayer pool, then we want to
    // run some additional logic to confirm that this is a valid pool by checking
    // if we can match it to a corresponding {CollectionToken}.
    if (_isPool && address(locker.collectionToken(_beneficiary)) == address(0)) {
        revert BeneficiaryIsNotPool();
    }

    emit BeneficiaryUpdated(_beneficiary, _isPool);
}
```

Therefore, previous beneficiaries should still be able to access any of their old accrued fees.

Beneficiaries claim their due rewards via the [`claim(address)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/BaseImplementation.sol#L164C14-L164C41) function:

```solidity
function claim(address _beneficiary) public nonReentrant {
    // Ensure that the beneficiary has an amount available to claim. We don't revert
    // at this point as it could open an external protocol to DoS.
    uint amount = beneficiaryFees[_beneficiary];
    if (amount == 0) return;

    // We cannot make a direct claim if the beneficiary is a pool
    if (beneficiaryIsPool) revert BeneficiaryPoolCannotClaim(); /// @audit reverts_if_current_beneficiary_is_a_pool

    // Reduce the amount of fees allocated to the `beneficiary` for the token. This
    // helps to prevent reentrancy attacks.
    beneficiaryFees[_beneficiary] = 0;

    // Claim ETH equivalent available to the beneficiary
    IERC20(nativeToken).transfer(_beneficiary, amount);
    emit BeneficiaryFeesClaimed(_beneficiary, amount);
}
```

Consequently, when [`beneficiaryIsPool`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/BaseImplementation.sol#L53C19-L53C36) is `true`, then **no beneficiaries will be able to claim their rewards**.

The root cause of this issue is that the `beneficiaryIsPool` flag should only apply to the _current_ beneficiary, however it is being incorrectly used to gate access to rewards to other beneficiaries who weren't themselves pools.

## Impact

A beneficary's rightful access to due rewards is rejected.

## Code Snippet

```solidity
function claim(address _beneficiary) public nonReentrant {
    // Ensure that the beneficiary has an amount available to claim. We don't revert
    // at this point as it could open an external protocol to DoS.
    uint amount = beneficiaryFees[_beneficiary];
    if (amount == 0) return;

    // We cannot make a direct claim if the beneficiary is a pool
    if (beneficiaryIsPool) revert BeneficiaryPoolCannotClaim();

    // Reduce the amount of fees allocated to the `beneficiary` for the token. This
    // helps to prevent reentrancy attacks.
    beneficiaryFees[_beneficiary] = 0;

    // Claim ETH equivalent available to the beneficiary
    IERC20(nativeToken).transfer(_beneficiary, amount);
    emit BeneficiaryFeesClaimed(_beneficiary, amount);
}
```

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/BaseImplementation.sol#L155C5-L180C6

## Tool used

Manual Review

## Recommendation

The [`beneficiaryIsPool`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/BaseImplementation.sol#L53C19-L53C36) flag should be recorded against a specific beneficiary:

```solidity
mapping (address beneficiary => bool isPool) _beneficiaryIsPool;
```
