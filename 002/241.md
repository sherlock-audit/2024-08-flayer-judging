Happy Wintergreen Kookaburra

High

# The `initializeCollection` is using a wrong Refund Logic causing the function to send zero Refunds

## Summary
The contract transfers the entire ETH amount to the implementation contract, which then sends only the required portion to the pool, leaving the excess tokens stuck in the implementation

## Vulnerability Detail
The Function `initializeCollection` does not perform a proper check to determine the exact amount of ETH required for liquidity in the pool during the `initializeCollection` process. Instead, it sends the entire amount of ETH provided by the user to the implementation contract, assuming it will handle the allocation. The issue arises because the implementation contract only uses the required amount for pool creation, but the remaining ETH (or excess tokens) is not refunded

## Impact
Over time, the failure to calculate the required ETH for liquidity in the `initializeCollection` function will result in users overpaying, leading to losses. This issue will increase as more users interact with the contract, causing a accumulation of excess ETH that gets stuck in the implementation contract, reducing user funds and damaging trust in the protocol

## Code Snippet
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L367-L399
```solidity
    function initializeCollection(address _collection, uint _eth, uint[] calldata _tokenIds, uint _tokenSlippage, uint160 _sqrtPriceX96) public virtual whenNotPaused collectionExists(_collection) {
        // Ensure the collection is not already initialised
        if (collectionInitialized[_collection]) revert CollectionAlreadyInitialized();

        // Ensure that the minimum threshold of collection tokens have been provided
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength < MINIMUM_TOKEN_IDS) revert InsufficientTokenIds();

        // cache
        IBaseImplementation _implementation = implementation;
        IERC20 nativeToken = IERC20(_implementation.nativeToken());

        // Convert the tokens into ERC20's which will return at a rate of 1:1
        deposit(_collection, _tokenIds, address(_implementation));

        // Send the native ETH equivalent token into the implementation
        uint startBalance = nativeToken.balanceOf(address(this));
        nativeToken.transferFrom(msg.sender, address(_implementation), _eth);

        // Make our internal call to our implementation
        uint tokens = _tokenIdsLength * 1 ether * 10 ** _collectionToken[_collection].denomination();
        _implementation.initializeCollection(_collection, _eth, tokens, _tokenSlippage, _sqrtPriceX96);

        // Map our collection as initialized
        collectionInitialized[_collection] = true;
        emit CollectionInitialized(_collection, _implementation.getCollectionPoolKey(_collection), _tokenIds, _sqrtPriceX96, msg.sender);

        // Refund any unused relative token to the user
        nativeToken.transfer(
            msg.sender,
            startBalance - nativeToken.balanceOf(address(this))
        );
    }
```

## PoC
- Put the PoC inside the `Locker.t.sol`

<details>

<summary>POC</summary>

```solidity
    function test_PoC2() public {
        // the collection we will be interacting with
        address _collection = address(erc721a);

        // Create the Collection and Collection Token
        locker.createCollection(_collection, 'Test Collection', 'TEST', 0);

        uint tokenOffset = 100000;

        // Mint enough tokens to initialize successfully
        uint tokenIdsLength = locker.MINIMUM_TOKEN_IDS(); // 10 tokens
        uint[] memory _tokenIds = new uint[](tokenIdsLength);
        for (uint i; i < tokenIdsLength; ++i) {
            _tokenIds[i] = tokenOffset + i;
            ERC721Mock(_collection).mint(address(this), tokenOffset + i); //mint to this address
        }

        console.log("This address Native Token Starting Balance:", WETH.balanceOf(address(this)));
        // Approve our {Locker} to transfer the tokens
        ERC721Mock(_collection).setApprovalForAll(address(locker), true);

        // required Amount is 5 ether, send the (amount - 5 ether) back if its more than 5 ether
        vm.expectRevert(); // Not enough Liquidity (as 5 ether is required)
        locker.initializeCollection(_collection, 4 ether, _tokenIds, 0, SQRT_PRICE_1_2);
        // correct liquidity plus extra amount
        locker.initializeCollection(_collection, 10 ether, _tokenIds, 0, SQRT_PRICE_1_2);

        // Collection is marked as initialized but no eth was returned
        assertTrue(locker.collectionInitialized(_collection));
        console.log("This address Native Token Balance After:", WETH.balanceOf(address(this)));
        console.log("The address of the Implementation: ", address(uniswapImplementation)); // to know the address of the implementation
    }
```

</details>

- Output
```solidity
  This address Native Token Starting Balance: 100000000000000000000
  This address Native Token Balance After: 90000000000000000000
  The address of the Implementation:  0x57D88D547641a626eC40242196F69754b25D2FCC // The address that will receive the eth
```
- Traces
```solidity
 ├─ [6188] 0xA4AD4f68d0b91CFD19687c881e50f3A00242828c::transfer(0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f, 5000000000000000000 [5e18])
  │   ├─ emit Transfer(from: 0x57D88D547641a626eC40242196F69754b25D2FCC, to: 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f, value: 5000000000000000000 [5e18]) 
// this is what the implementation is going to send (The required eth that was required when creating the liquidity position)
```

## Tool used

Manual Review

## Recommendation
Before transferring any ETH, the contract should calculate the exact amount required to initialize the pool and Transfer Only the Required Amount
