Large Mauve Parrot

Medium

# `initializeCollection()` doesn't refund native tokens

### Summary

_No response_

### Root Cause

When initializing a new collection and providing the first liquidity the function [Locker::initializeCollection()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L395-L398) attempts to refund left-over native tokens to the caller at the end of execution:
```solidity
...
nativeToken.transfer(
    msg.sender,
    startBalance - nativeToken.balanceOf(address(this))
);
...
```

this doesn't work because native tokens are never held in the locker, they are held in the `UniswapImplementation` hook instead.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Excess native tokens are not refunded and are stuck in the `UniswapImplementation` hook.

### PoC

<details>
  <summary>To copy-paste in `UniswapImplementation.t.sol`:</summary>

```solidity
    function test_nativeTokensNotRefunded() public {
        address alice = makeAddr("alice");

        ERC721Mock erc721 = new ERC721Mock();
        CollectionToken ctoken = CollectionToken(locker.createCollection(address(erc721), 'ERC721', 'ERC', 0));


        //### APPROVALS
        //-> Alice approvals
        vm.startPrank(alice);
        erc721.setApprovalForAll(address(locker), true);
        ctoken.approve(address(poolSwap), type(uint256).max);
        ctoken.approve(address(uniswapImplementation), type(uint256).max);
        vm.stopPrank();
        _approveNativeToken(alice, address(locker), type(uint).max);
        _approveNativeToken(alice, address(poolManager), type(uint).max);
        _approveNativeToken(alice, address(poolSwap), type(uint).max);


        //-> Mint 10 tokens to Alice
        uint[] memory _tokenIds = new uint[](10);
        for (uint i; i < 10; ++i) {
            erc721.mint(alice, i);
            _tokenIds[i] = i;
        }

        uint256 nativeInHookBefore = WETH.balanceOf(address(uniswapImplementation));

        //-> alice initializes a collection and adds liquidity: 1e19+1e18 NATIVE + 1e19 CTOKEN
        uint256 initialNativeLiquidity = 1e19 + 1e18;
        _dealNativeToken(alice, initialNativeLiquidity);
        vm.startPrank(alice);
        locker.initializeCollection(address(erc721), initialNativeLiquidity, _tokenIds, 0, SQRT_PRICE_1_1);
        vm.stopPrank();

        //-> 1e18 native tokens are stuck
        uint256 nativeInHookAfter = WETH.balanceOf(address(uniswapImplementation));
        uint256 stuckNative = nativeInHookAfter - nativeInHookBefore;
        assertEq(stuckNative, 1e18);
    }
```
</details>

### Mitigation

In [UniswapImplementation::_unlockCallback()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L376C14-L376C22) send back the remaining native tokens to the locker, which will then send it back to the caller.