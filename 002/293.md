Melodic Pickle Goose

High

# UniswapImplementation's `beforeInitialize()` hook will always revert

### Summary

The **UniswapImplementation**'s `beforeInitialize()` hook will always revert, DOSing the Uniswap pool creation for any collection.


### Root Cause

Instead of returning `IHooks.beforeInitialize.selector`, the `beforeInitialize()` function in the **UniswapImplementation** contract will always revert:
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L453
```solidity
    function beforeInitialize(address /* sender */, PoolKey memory /* key */, uint160 /* sqrtPriceX96 */, bytes calldata /* hookData */) public view override onlyByPoolManager returns (bytes4) {
→       revert CannotBeInitializedDirectly();
    }
```


### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

No need for attack, the contract will itself cause an improper revert DOSing a major functionality around a collection in the protocol.


### Impact

The **UniswapImplementation** contract will **not** be able to create a Uniswap pool by itself and thus accumulated CollectionToken fees for a given collection will be stuck irretrievable in the contract indefinitely. The more collections on Flayer, the more CT tokens stuck, accumulating a huge amount of CT tokens that are worth something.

### PoC

Create a new file **TestBeforeInitialize.sol** under **flayer/src/contracts/implementation** and paste the following content inside:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.22;

import {BaseHook} from "@uniswap-periphery/base/hooks/BaseHook.sol";
import {console} from 'forge-std/console.sol';

import {Hooks} from "@uniswap/v4-core/src/libraries/Hooks.sol";
import {IHooks} from "@uniswap/v4-core/src/interfaces/IHooks.sol";
import {IPoolManager} from "@uniswap/v4-core/src/interfaces/IPoolManager.sol";
import {PoolKey} from "@uniswap/v4-core/src/types/PoolKey.sol";

contract TestBeforeInitialize is BaseHook {
    address private manager;

    constructor (address _poolManager) BaseHook(IPoolManager(_poolManager)) {
      manager = msg.sender;
    }

    function beforeInitialize(
      address /* sender */,
      PoolKey memory /* key */,
      uint160 /* sqrtPriceX96 */,
      bytes calldata /* hookData */
    ) public override onlyByPoolManager view returns (bytes4) {
        revert CannotBeInitializedDirectly();
    }

    function afterInitialize(
        address /* sender */,
        PoolKey calldata /* key */,
        uint160 /* sqrtPriceX96 */,
        int24 /* tick */,
        bytes calldata /* hookData */
    ) external view override returns (bytes4) {
      return IHooks.afterInitialize.selector;
    }

    function getHookPermissions() public pure override returns (Hooks.Permissions memory) {
        return Hooks.Permissions({
            beforeInitialize: true,
            afterInitialize: true,
            beforeAddLiquidity: true,
            afterAddLiquidity: true,
            beforeRemoveLiquidity: true,
            afterRemoveLiquidity: true,
            beforeSwap: true,
            afterSwap: true,
            beforeDonate: true,
            afterDonate: true,
            beforeSwapReturnDelta: true,
            afterSwapReturnDelta: true,
            afterAddLiquidityReturnDelta: true,
            afterRemoveLiquidityReturnDelta: true
        });
    }

    error CallerNotPoolManager();
    error CannotBeInitializedDirectly();
}
```
This is a minimal copy of the **UniswapImplementation** contract with identical `beforeInitialize()` hook.

Now add the following test case to the **flayer/test/UniswapImplementation.t.sol** file:
```solidity

    function testCallingRevertingHookShouldRevert() public {
        address payable ALL_HOOKS = payable(0x0000000000000000000000000000000000003fFF);

        // Deploy our test contract to the address for which all hooks are enabled.
        deployCodeTo("TestBeforeInitialize.sol", abi.encode(address(poolManager)), ALL_HOOKS);

        PoolKey memory key =
            PoolKey(Currency.wrap(address(1)), Currency.wrap(address(2)), 0, 60, IHooks(ALL_HOOKS));

        // @audit Fails with "CannotBeInitializedDirectly()" when it should just return the
        // `IHooks.beforeInitialize.selector` and continue.
        vm.expectRevert();
        poolManager.initialize(key, 4295128740, '');
    }
```
And run it using `forge test --match-test testCallingRevertingHookShouldRevert`. The test will pass.

```shell
[⠊] Compiling...
[⠆] Compiling 2 files with Solc 0.8.26
[⠰] Solc 0.8.26 finished in 8.46s
Compiler run successful with warnings:
Warning (2018): Function state mutability can be restricted to pure
  --> src/contracts/implementation/TestBeforeInitialize.sol:28:5:
   |
28 |     function afterInitialize(
   |     ^ (Relevant source part starts here and spans across multiple lines).


Ran 1 test for test/UniswapImplementation.t.sol:UniswapImplementationTest
[PASS] testCallingRevertingHookShouldRevert() (gas: 44678)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.99ms (546.33µs CPU time)

Ran 1 test suite in 147.22ms (4.99ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

If you replace the `revert` with `return IHooks.beforeInitialize.selector` in the `beforeInitialize()` function of **TestBeforeInitialize**, the `vm.expectRevert()` assertion will now start failing.


### Mitigation

```diff
diff --git a/flayer/src/contracts/implementation/UniswapImplementation.sol b/flayer/src/contracts/implementation/UniswapImplementation.sol
index c898b32..7afffde 100644
--- a/flayer/src/contracts/implementation/UniswapImplementation.sol
+++ b/flayer/src/contracts/implementation/UniswapImplementation.sol
@@ -450,7 +450,7 @@ contract UniswapImplementation is BaseImplementation, BaseHook {
      * to call initialize, we want to prevent this from hitting.
      */
     function beforeInitialize(address /* sender */, PoolKey memory /* key */, uint160 /* sqrtPriceX96 */, bytes calldata /* hookData */) public view override onlyByPoolManager returns (bytes4) {
-        revert CannotBeInitializedDirectly();
+        return IHooks.beforeInitialize.selector;
     }
 
     /**
```
