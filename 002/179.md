Tiny Plastic Hyena

High

# Users who initialize a collection will not be refunded excess WETH sent during initialization

### Summary

Faulty WETH accounting/transfers in Locker::initializeCollection() will cause the loss of funds for users initializing a collection.

### Root Cause

Locker::initializeCollection() [transfers WETH from the user to the Implementation contract](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L384). The Locker is supposed to [return excess WETH from the pool initialization to the user](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L394-L398), but it never gets back the excess. In the Implementation contract, [initializeCollection()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L205-L240) and the subsequently called [_unlockCallback()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L376-L420) spend the WETH received initializing the pool, but never send the excess back to the Locker contract or user.

```solidity
    function initializeCollection(address _collection, uint _eth, uint[] calldata _tokenIds, uint _tokenSlippage, uint160 _sqrtPriceX96) public virtual whenNotPaused collectionExists(_collection) {
        ...
        // Send the native ETH equivalent token into the implementation
        uint startBalance = nativeToken.balanceOf(address(this));
@>      nativeToken.transferFrom(msg.sender, address(_implementation), _eth); // @audit weth sent out

        ...

        // Refund any unused relative token to the user
@>      nativeToken.transfer( //@audit excess weth supposed to be sent back, but never is, this will always transfer 0
            msg.sender,
            startBalance - nativeToken.balanceOf(address(this))
        );
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Any time a user initializes a collection, any excess WETH not needed to initialize the pool will be stuck in the Implementation contract. It is stated in the contract that excess WETH will be refunded, as the initial pool balances are decided by the sqrtPricex96 argument and pulled from the contract. However in practice, this function leads to lost funds.

### PoC

Please paste the following into Locker.t.sol:
```solidity
function test_InitializerIsNotRefundedExcessEth() public {
        // implementation contract
        address imp = address(locker.implementation());
        
        // user who initializes the collection
        address depositor = makeAddr("depositor");
        vm.startPrank(depositor);

        ERC721Mock astroidDogs = new ERC721Mock();
        // Approve some of the ERC721Mock collections in our {Listings}
        locker.createCollection(address(astroidDogs), 'Astroid Dogs', 'ADOG', 0);
        address adog = address(locker.collectionToken(address(astroidDogs)));

        // mint the depositor enough dogs and eth, approve locker to spend
        uint[] memory tokenIds = new uint[](10);
        for (uint i = 0; i < 10; ++i) {
            astroidDogs.mint(depositor, i);
            tokenIds[i] = i;
            astroidDogs.approve(address(locker), i);
            
        }
        deal(address(WETH), depositor, 15e18);
        WETH.approve(address(locker), 15e18);

        // get balances of user and all contracts before initialization
        uint256 preDep = WETH.balanceOf(depositor);
        uint256 preLoc = WETH.balanceOf(address(locker));
        uint256 prePm = WETH.balanceOf(address(poolManager));
        uint256 preImp = WETH.balanceOf(imp);
        
        // initialize collection with a 1:1 ratio of NFTs to fTokens set by price, but sending 5 additional WETH
        //                                                      slippage setting and squrtPrice 1:1 ratio
        locker.initializeCollection(address(astroidDogs), 15e18, tokenIds, 1, 79228162514264337593543950336);

        // get balances after the initialization
        uint256 postDep = WETH.balanceOf(depositor);
        uint256 postLoc = WETH.balanceOf(address(locker));
        uint256 postPm = WETH.balanceOf(address(poolManager));
        uint256 postImp = WETH.balanceOf(imp);

        // all funds have been spent on behalf of user (initial balance 15 weth)
        assertEq(postDep, 0);
        // all funds that flowed through the Locker contract are gone
        assertEq(preLoc, postLoc);
        // the PoolManager has 10 more Weth than it started with (1:1 ratio of WETH to NFTs deposited)
        assertEq(prePm + 10 ether, postPm);
        // the excess 5 weth that should have been refunded at the end of initializeCollection is stuck in the implementation contract
        assertEq(preImp + 5 ether, postImp);
    }
```

### Mitigation

Have the Implementation contract send unused WETH back to the Locker contract after the pool has been initialized or alternatively move all WETH accounting/refunding into the implementation contract.