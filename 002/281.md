Mythical Gauze Lizard

High

# The user does not get the remaining funds back when executing `initializeCollection()` in `Locker.sol` due to incorrect implementation.

### Summary

The user calls [`initializeCollection()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L367) of `Locker.sol` to initialize `collection`. This function implements the logic to return the remaining funds, but it does not work properly. As a result, the user does not get the remaining funds back.

### Root Cause

incorrect logic in `initializeCollection()` of `Locker.sol`


### Internal pre-conditions

_No response_

### External pre-conditions

User sends more `nativeToken`s than needs.

### Attack Path

The user calls `initializeCollection()`. Now `startBalance` is 0. Then remaining fund is not transfered from `implementation` contract to `Locker` contract.
So `nativeToken.balanceOf(address(this)` is also 0.

### Impact

The user does not get the remaining funds back when executing `initializeCollection()` in `Locker.sol` due to incorrect implementation.

### PoC

The user calls `initializeCollection()` of `Locker.sol` to initialize `collection`.
```solidity
    function initializeCollection(address _collection, uint _eth, uint[] calldata _tokenIds, uint _tokenSlippage, uint160 _sqrtPriceX96) public virtual whenNotPaused collectionExists(_collection) {
        // Ensure the collection is not already initialised
        if (collectionInitialized[_collection]) revert CollectionAlreadyInitialized();

        // Ensure that the minimum threshold of collection tokens have been provided
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength < MINIMUM_TOKEN_IDS) revert InsufficientTokenIds();

        // cache
        IBaseImplementation _implementation = implementation;
        IERC20 nativeToken = IERC20(_implementation.nativeToken());

        // Convert the tokens into ERC20's which will return at a rate of 1:1
        deposit(_collection, _tokenIds, address(_implementation));

        // Send the native ETH equivalent token into the implementation
@       uint startBalance = nativeToken.balanceOf(address(this));
        nativeToken.transferFrom(msg.sender, address(_implementation), _eth);

        // Make our internal call to our implementation
        uint tokens = _tokenIdsLength * 1 ether * 10 ** _collectionToken[_collection].denomination();
        _implementation.initializeCollection(_collection, _eth, tokens, _tokenSlippage, _sqrtPriceX96);

        // Map our collection as initialized
        collectionInitialized[_collection] = true;
        emit CollectionInitialized(_collection, _implementation.getCollectionPoolKey(_collection), _tokenIds, _sqrtPriceX96, msg.sender);

        // Refund any unused relative token to the user
@       nativeToken.transfer(
@          msg.sender,
@          startBalance - nativeToken.balanceOf(address(this))
@       );
    }
```
You can see the logic to return the remaining funds. But `startBalance` is always 0. Also `nativeToken.balanceOf(address(this)` is allways 0 because the `nativeToken` is not transferred from `_implementation` contract to this contract.
```solidity
    function initializeCollection(address _collection, uint _amount0, uint _amount1, uint _amount1Slippage, uint160 _sqrtPriceX96) public override {
        // Ensure that only our {Locker} can call initialize
        if (msg.sender != address(locker)) revert CallerIsNotLocker();

        // Ensure that the PoolKey is not empty
        PoolKey memory poolKey = _poolKeys[_collection];
        if (poolKey.tickSpacing == 0) revert UnknownCollection();

        // Initialise our pool
        poolManager.initialize(poolKey, _sqrtPriceX96, '');

        // After our contract is initialized, we mark our pool as initialized and emit
        // our first state update to notify the UX of current prices, etc.
        PoolId id = poolKey.toId();
        _emitPoolStateUpdate(id);

        // Load our pool parameters and update the initialized flag
        PoolParams storage poolParams = _poolParams[id];
        poolParams.initialized = true;

        // Obtain the UV4 lock for the pool to pull in liquidity
        poolManager.unlock(
            abi.encode(CallbackData({
                poolKey: poolKey,
                liquidityDelta: LiquidityAmounts.getLiquidityForAmounts({
                    sqrtPriceX96: _sqrtPriceX96,
                    sqrtPriceAX96: TICK_SQRT_PRICEAX96,
                    sqrtPriceBX96: TICK_SQRT_PRICEBX96,
                    amount0: poolParams.currencyFlipped ? _amount1 : _amount0,
                    amount1: poolParams.currencyFlipped ? _amount0 : _amount1
                }),
                liquidityTokens: _amount1,
                liquidityTokenSlippage: _amount1Slippage
            })
        ));
    }
```


### Mitigation

The function to return remaining funds must be added in accordance with the protocol.