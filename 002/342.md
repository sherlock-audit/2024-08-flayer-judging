Melodic Pickle Goose

High

# Locker's `initializeCollection()` can be DOSed by deploying the Uniswap pool in advance for the same pool key

### Summary

Because the **UniswapImplementation** contract's `initializeCollection()` function is not idempotent with regards to the initialization of the Uniswap pool it can easily be DOSed by deploying the Uniswap pool separately before Flayer deploys it and thus prevent the hook-up between the Flayer collection and the Uniswap pool for it. Which itself will generally break a lot of the interactions with the collection in Flayer, primarily the creation of listings for NFTs of that collection and the execution of the hooks themselves.

### Root Cause

In order to initialize a Uniswap pool for a CollectionToken, the collection must first be registered at which point the PoolKey for that collection pool will be known:

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L161-L187
```solidity
    function registerCollection(address _collection, ICollectionToken _collectionToken) public override {
        // Ensure that only our {Locker} can call register
        if (msg.sender != address(locker)) revert CallerIsNotLocker();

        // Check if our pool currency is flipped
        bool currencyFlipped = nativeToken > address(_collectionToken);

        // Create our Uniswap pool and store the pool key
→       PoolKey memory poolKey = PoolKey({
            currency0: Currency.wrap(!currencyFlipped ? nativeToken : address(_collectionToken)),
            currency1: Currency.wrap(currencyFlipped ? nativeToken : address(_collectionToken)),
            fee: LPFeeLibrary.DYNAMIC_FEE_FLAG,
            tickSpacing: POOL_TICK_SPACING,
            hooks: IHooks(address(this))
        });

        // Store our {PoolKey} mapping against the collection
        _poolKeys[_collection] = poolKey;

        // Store our pool parameters
        _poolParams[poolKey.toId()] = PoolParams({
            collection: _collection,
            poolFee: 0,
            initialized: false,
            currencyFlipped: currencyFlipped
        });
    }
```

An attacker can then use this to front-run the call to `initializeCollection()` by calling Uniswap's **PoolManager** directly to initialize the pool themselves with the exact same PoolKey. After this, calls to **Locker**#`initializeCollection()` will revert on Uniswap's side because the pool has already been initialized (`sqrtPriceX96` in `slot0` is **not** 0) and will prevent Flayer from being able to hook up with the pool.

https://github.com/Uniswap/v4-core/blob/18b223cab19dc778d9d287a82d29fee3e99162b0/src/PoolManager.sol#L115-L144
```solidity
    function initialize(PoolKey memory key, uint160 sqrtPriceX96, bytes calldata hookData)
        external
        noDelegateCall
        returns (int24 tick)
    {
        // ... omitted for brewity
        
        PoolId id = key.toId();
        uint24 protocolFee = _fetchProtocolFee(key);

→       tick = _pools[id].initialize(sqrtPriceX96, protocolFee, lpFee);

        // ...
    }
```

https://github.com/Uniswap/v4-core/blob/main/src/libraries/Pool.sol#L97-L107
```solidity
    function initialize(State storage self, uint160 sqrtPriceX96, uint24 protocolFee, uint24 lpFee)
        internal
        returns (int24 tick)
    {
→       if (self.slot0.sqrtPriceX96() != 0) PoolAlreadyInitialized.selector.revertWith();

        tick = TickMath.getTickAtSqrtPrice(sqrtPriceX96);

        self.slot0 = Slot0.wrap(bytes32(0)).setSqrtPriceX96(sqrtPriceX96).setTick(tick).setProtocolFee(protocolFee)
            .setLpFee(lpFee);
    }
```
(As we can see when the pool is initialized, `sqrtPriceX96` is set in `slot0` and consecutive calls will fail because of the check on L101).

This is possible because the creation of a collection token and the Uniswap pool initialization are not happening in a single TX. It is the only variable in the PoolKey that an attacker needs to know in order to initialize the Uniswap pool and also possible because after a collection is created in Flayer it's not immediately initialized (only registered).

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L299-L330
```solidity
    function createCollection(address _collection, string calldata _name, string calldata _symbol, uint _denomination) public whenNotPaused returns (address) {
        // Ensure that our denomination is a valid value
        if (_denomination > MAX_TOKEN_DENOMINATION) revert InvalidDenomination();

        // Ensure the collection does not already have a listing token
        if (address(_collectionToken[_collection]) != address(0)) revert CollectionAlreadyExists();

        // Validate if a contract does not appear to be a valid ERC721
        if (!IERC721(_collection).supportsInterface(0x80ac58cd)) revert InvalidERC721();

        // Deploy our new ERC20 token using Clone. We use the impending ID
        // to clone in a deterministic fashion.
        ICollectionToken collectionToken_ = ICollectionToken(
            LibClone.cloneDeterministic(tokenImplementation, bytes32(_collectionCount))
        );
        _collectionToken[_collection] = collectionToken_;

        // Initialise the token with variables
        collectionToken_.initialize(_name, _symbol, _denomination);

        // Registers our collection against our implementation
        implementation.registerCollection({
            _collection: _collection,
            _collectionToken: collectionToken_
        });

        // Increment our vault counter
        unchecked { ++_collectionCount; }

        emit CollectionCreated(_collection, address(collectionToken_), _name, _symbol, _denomination, msg.sender);
        return address(collectionToken_);
    }
```


### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

1. Attacker comes in and calls **Locker**#`createCollection()` for whichever collection address they'd like.
2. The collection is registered in the **Locker** and in the **UniswapImplementation** contract but the Uniswap pool for it is **NOT** yet initialized.
3. In the same transaction (or whenever they like but before anyone calls **UniswapImplementation**#`initializeCollection()`) the Attacker calls Uniswap's **PoolManager**#`initialize` for the already known PoolKey for the collection in question.
4. From this point on, calls to **Locker**#`initializeCollection()` will fail because of a check on Uniswap's side that ensures the pool's `sqrtPriceX96` in `slot0` is zero and reverts if its **not**. The collection will be impossible to be marked as initialized in the Locker's `collectionInitialized` mapping, DOSing a handful of functionalities for the collection in Flayer.


### Impact

As the **Locker**#`createCollection()` method is **NOT** permissioned, anybody can come in and create a collection in Flayer for any NFT collection and right after that initialize externally a Uniswap pool for it in the same transaction. As a result it will **NOT** be possible to interact with that collection in Flayer since there are checks if the collection is initialized in the **Locker** when creating either a regular listing or a protected listing.

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L130-L166
```solidity
    function createListings(CreateListing[] calldata _createListings) public nonReentrant lockerNotPaused {
        // Loop variables
        uint taxRequired;
        uint tokensIdsLength;
        uint tokensReceived;

        // Loop over the unique listing structures
        for (uint i; i < _createListings.length; ++i) {
            // Store our listing for cheaper access
            CreateListing calldata listing = _createListings[i];

            // Ensure our listing will be valid
→           _validateCreateListing(listing);
            
            // ...
        }
    }
```

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L262-L293
```solidity
    function _validateCreateListing(CreateListing calldata _listing) private view {
        // Ensure that our collection exists and is initialised
→       if (!locker.collectionInitialized(_listing.collection)) revert CollectionNotInitialized();

        // ...
    }
```

The same stands true for the **ProtectedListings** contract and the creation of listings there:

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L117-L156
```solidity
    function createListings(CreateListing[] calldata _createListings) public nonReentrant lockerNotPaused {
        // Loop variables
        uint checkpointIndex;
        bytes32 checkpointKey;
        uint tokensIdsLength;
        uint tokensReceived;

        // Loop over the unique listing structures
        for (uint i; i < _createListings.length; ++i) {
            // Store our listing for cheaper access
            CreateListing calldata listing = _createListings[i];

            // Ensure our listing will be valid
→           _validateCreateListing(listing);

            // ...
        }
    }
```

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L218-L231
```solidity
    function _validateCreateListing(CreateListing calldata _listing) internal view {
        // Ensure that our collection exists and is initialised
→       if (!locker.collectionInitialized(_listing.collection)) revert CollectionNotInitialized();

        // ...
    }
```

Also, **UniswapImplementation**#`_distributeFees()` will return early and will not be donating the fees to the Uniswap pool (even though such fees will be impossible to accumulate due to the inability to create any type of listings for that collection), however the impact is there caused by the reverting `initializeCollection()` call that marks the pool as initialized in the **UniswapImplementation** contract as well.

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L308-L365
```solidity
    function _distributeFees(PoolKey memory _poolKey) internal {
        // If the pool is not initialized, we prevent this from raising an exception and bricking hooks
        PoolId poolId = _poolKey.toId();
        PoolParams memory poolParams = _poolParams[poolId];

→       if (!poolParams.initialized) {
            return;
        }

        // ...
    }
```

This itself will affect the `afterSwap`, `beforeRemoveLiquidity` and `beforeAddLiquidity` hooks for that collection's pool as they are the only callers of the `_distributeFees()` method.


### PoC

See **Attack Path**.


### Mitigation

Wrap the call to Uniswap's **PoolManager** in a `try-catch` block so it's idempotent and doesn't revert if a Uniswap pool is already deployed for that CollectionToken and `nativeToken` pair (and fee, tick spacing and hook contract address).
