Shiny Mint Lion

Medium

# The tokens (collectionTokens and WETH) used for initializeCollection() to create a liquidity position are permanently locked in Uniswap V4.

## Summary
The tokens (collectionTokens and WETH) used for initializeCollection() to create a liquidity position are permanently locked in Uniswap V4.
## Vulnerability Detail
```javascript
function initializeCollection(address _collection, uint _eth, uint[] calldata _tokenIds, uint _tokenSlippage, uint160 _sqrtPriceX96) public virtual whenNotPaused collectionExists(_collection) {
        // Ensure the collection is not already initialised
        if (collectionInitialized[_collection]) revert CollectionAlreadyInitialized();

        // Ensure that the minimum threshold of collection tokens have been provided
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength < MINIMUM_TOKEN_IDS) revert InsufficientTokenIds();

        // cache
        IBaseImplementation _implementation = implementation;
        IERC20 nativeToken = IERC20(_implementation.nativeToken());

        // Convert the tokens into ERC20's which will return at a rate of 1:1
@>>        deposit(_collection, _tokenIds, address(_implementation));

        // Send the native ETH equivalent token into the implementation
        uint startBalance = nativeToken.balanceOf(address(this));
@>>        nativeToken.transferFrom(msg.sender, address(_implementation), _eth);

        // Make our internal call to our implementation
        uint tokens = _tokenIdsLength * 1 ether * 10 ** _collectionToken[_collection].denomination();
@>>        _implementation.initializeCollection(_collection, _eth, tokens, _tokenSlippage, _sqrtPriceX96);

        // Map our collection as initialized
        collectionInitialized[_collection] = true;
        emit CollectionInitialized(_collection, _implementation.getCollectionPoolKey(_collection), _tokenIds, _sqrtPriceX96, msg.sender);

        // Refund any unused relative token to the user
        nativeToken.transfer(
            msg.sender,
            startBalance - nativeToken.balanceOf(address(this))
        );
    }
```
When the user calls Locker::initializeCollection(), it internally calls UniswapImplementation::initializeCollection(), which then calls poolManager::modifyLiquidity(). At the same time, tokens are transferred from the user to the UniswapImplementation contract, and then to the poolManager contract of Uniswap V4.
```javascript
 function modifyLiquidity(
        PoolKey memory key,
        IPoolManager.ModifyLiquidityParams memory params,
        bytes calldata hookData
    ) external onlyWhenUnlocked noDelegateCall returns (BalanceDelta callerDelta, BalanceDelta feesAccrued) {
        PoolId id = key.toId();
        Pool.State storage pool = _getPool(id);
        pool.checkPoolInitialized();

        key.hooks.beforeModifyLiquidity(key, params, hookData);

        BalanceDelta principalDelta;
        (principalDelta, feesAccrued) = pool.modifyLiquidity(
            Pool.ModifyLiquidityParams({
@>>                owner: msg.sender,
                tickLower: params.tickLower,
                tickUpper: params.tickUpper,
                liquidityDelta: params.liquidityDelta.toInt128(),
                tickSpacing: key.tickSpacing,
                salt: params.salt
            })
        );

        // fee delta and principal delta are both accrued to the caller
        callerDelta = principalDelta + feesAccrued;

        // event is emitted before the afterModifyLiquidity call to ensure events are always emitted in order
        emit ModifyLiquidity(id, msg.sender, params.tickLower, params.tickUpper, params.liquidityDelta, params.salt);

        BalanceDelta hookDelta;
        (callerDelta, hookDelta) = key.hooks.afterModifyLiquidity(key, params, callerDelta, hookData);

        // if the hook doesnt have the flag to be able to return deltas, hookDelta will always be 0
        if (hookDelta != BalanceDeltaLibrary.ZERO_DELTA) _accountPoolBalanceDelta(key, hookDelta, address(key.hooks));

        _accountPoolBalanceDelta(key, callerDelta, msg.sender);
    }
```
In the poolManager contract of Uniswap V4, the owner of the liquidity pool is msg.sender, which is the UniswapImplementation. Therefore, no one except UniswapImplementation can directly withdraw liquidity from the pool. However, UniswapImplementation does not have any function allowing users to withdraw their liquidity tokens. Even if the NFT collection is shut down through the CollectionShutdown contract, these liquidity tokens still cannot be retrieved.
## Impact
The tokens used to provide the initial liquidity cannot be retrieved, resulting in a financial loss for the users.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L367

## Tool used

Manual Review

## Recommendation
Provide a function in UniswapImplementation that allows users to withdraw their liquidity tokens.