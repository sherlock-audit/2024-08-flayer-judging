Uneven Burlap Dalmatian

Medium

# Unused ```nativeToken```s are not transferred back to the Pool initializer after the ```Locker::initializeCollection()``` and they stay on ```UniswapV4Implementation```.

### Summary

The ```nativeToken```s that didn't be used during the collection initializing and initial liquidity provision in ```Locker::initializeCollection()``` and ```UniswapV4Implementation::initializeCollection()``` are not transferred back to whoever made the initial deposit.

### Root Cause

The implementation of the transfer back of ```nativeToken```(probably ```WETH```) is not correctly since it assumes that the unused ```WETH``` will return to the ```Locker``` after the ```UniswapV4Implementation::initializeCollection()``` while this is not the case. We can see the vulnerable code here :
```solidity
    function initializeCollection(address _collection, uint _eth, uint[] calldata _tokenIds, uint _tokenSlippage, uint160 _sqrtPriceX96) public virtual whenNotPaused collectionExists(_collection) {
        // ...

        // Send the native ETH equivalent token into the implementation
        uint startBalance = nativeToken.balanceOf(address(this));
@>        nativeToken.transferFrom(msg.sender, address(_implementation), _eth);

        // Make our internal call to our implementation
        uint tokens = _tokenIdsLength * 1 ether * 10 ** _collectionToken[_collection].denomination();
        _implementation.initializeCollection(_collection, _eth, tokens, _tokenSlippage, _sqrtPriceX96);

        // ...

        // Refund any unused relative token to the user
@>        nativeToken.transfer(
            msg.sender,
            startBalance - nativeToken.balanceOf(address(this))
        );
    }
```
[Link to code](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L367)

```Locker::initalizeCollection()``` assumes incorrectly that at the end of ```_implementation.initializeCollection``` call, the ```nativeToken``` that didn't deposited in the Pool will be returned back to the contract so they can be returned to the ```msg.sender```. However, as we can see in the ```UniswapV4Implementation::initializeCollection()```, this is not the case and the ```nativeToken``` which didn't deposit, just stay in the Hook. We can see the ```UniswapV4Implementation::_unlockCallback()``` that does never return the funds :
```solidity
    function _unlockCallback(bytes calldata _data) internal override returns (bytes memory) {
        // ...

        // Check the native delta amounts that we need to transfer from the contract
        if (delta.amount0() < 0) {
            _pushTokens(params.poolKey.currency0, uint128(-delta.amount0()));
        }

        // Check our ERC20 donation
        if (delta.amount1() < 0) {
            _pushTokens(params.poolKey.currency1, uint128(-delta.amount1()));
        }

        // If we have an expected amount of tokens being provided as liquidity, then we
        // need to ensure that this exact amount is sent. There may be some dust that is
        // lost during rounding and for this reason we need to set a small slippage
        // tolerance on the checked amount.
        if (params.liquidityTokens != 0) {
            uint128 deltaAbs = _poolParams[params.poolKey.toId()].currencyFlipped ? uint128(-delta.amount0()) : uint128(-delta.amount1());
            if (params.liquidityTokenSlippage < params.liquidityTokens - deltaAbs) {
                revert IncorrectTokenLiquidity(
                    deltaAbs,
                    params.liquidityTokenSlippage,
                    params.liquidityTokens
                );
            }
        }

        // We return our `BalanceDelta` response from the donate call
        return abi.encode(delta);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L376C1-L420C6)

### Internal pre-conditions
1. The initial LP to initialize a collection and deposit some NFTs of this collection together with ```nativeToken``` to the Uniswap V4 Pool so to initialize liquidity as well.

### External pre-conditions
1. Some of these ```WETH``` provided to not be used for the liquidity provision.

### Attack Path
1. Initial LP call ```Locker::registerCollection()```, so to register collection.
2. Initial LP call ```Locker::initializeCollection()```, so to initialize collection and pool with ```CollectionToken``` and ```WETH``` liquidity.

### Impact
The impact of this vulnerability is that the initial Liquidity Provider of a collection (the ```msg.sender of the ```Locker::initializeCollection()```) loses funds that were supposed to be returned to him after the initialization. This means that he, basically, overpaying for his initial liquidity provision and the unused ```WETH``` that belong to him stay in the ```UniswapV4Implementation``` contract.

### PoC
No PoC needed.

### Mitigation
Make sure that the unused ```WETH``` are returned to the ```Locker``` contract, so it will be able to be returned to the ```msg.sender``` as well.