Crazy Chiffon Spider

Medium

# Precision loss occurs in taxCalculation after floorMultiplier kink that can lead to accrued losses for the Flayer protocol

## Summary
Precision loss occurs in `calculateTax()` in `TaxCalculator.sol` after the `floorMultiplier` is > `FLOOR_MULTIPLE_KINK `which could lead to accrued losses for the protocol.

## Vulnerability Detail
The main cause in [calculateTax()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/TaxCalculator.sol#L35-L44) leading to this issue is the division by `2` which occurs in the following scenario:
```solidity
        if (_floorMultiple > FLOOR_MULTIPLE_KINK) {
@>>         _floorMultiple = FLOOR_MULTIPLE_KINK + ((_floorMultiple - FLOOR_MULTIPLE_KINK) / 2);
        }
```
`FLOOR_MULTIPLE_KINK` is hardcoded to `200`. Replacing the values in the formula, we conclude that:
`200 + ((202 - 200) / 2) = 201` will produce the same result as when the `floorMultiplier` is 203 
`200 + ((203 - 200) / 2) = 201`, since we round down.

This is enough loss of precision to result in decent amount of loss.

`TaxCalculator.calculateTax()` is used in `getListingTaxRequired()`, which is crucial as it defines the fees that will be taken out from the user when he creates a new liquid listing for his NFT. 

This incentivizes users to put odd numbers for their `floorMultiple`, as it would save them fees and provide them additional profit, as they could sell an NFT for a little higher price.
```solidity
    function getListingTaxRequired(Listing memory _listing, address _collection)
     public view returns (uint taxRequired_) {
@>>     taxRequired_ = locker.taxCalculator().calculateTax(_collection, _listing.floorMultiple, _listing.duration);
        taxRequired_ *= 10 ** locker.collectionToken(_collection).denomination();
    }
```    

Let's do an **estimation of losses**.
Tax in `TaxCalculator.calculateTax()` is calculated as follows:
```solidity
        taxRequired_ = (_floorMultiple ** 2 * 1e12 * _duration) / 7 days;
```
The `_floorMultiple`, as previously discussed has the precision loss.

For 10 days ~ 864000 seconds.
With 202 vs 203 floor multiple scenario:

`201 ** 2 1e12 * 864000 / 7 * 86400 = 57715714285714285`

To conclude the loss we should compare the jump from `202` to `204` `floorMultiplier` and divide it by `2`, as we want the median:

`floorMultiplier` is `204`, so `floorMultiple` is `202`
`202 ** 2 1e12 * 864000 / 7 * 86400 = 58291428571428571`

`(58291428571428571 - 57715714285714285) / 2 = 287857142857143`

As a % of the total fee this is ~ `0.5%` or 6000$ for every $1.2M in fees.

### Coded PoC
Add to `Listings.t.sol` and use `forge test --match-test test_listingTax -vvv`
```solidity
    function test_listingTax() public {
        //===========Setup===========
        listings.setProtectedListings(address(protectedListings));
        ICollectionToken token = locker.collectionToken(address(erc721a));
        assertEq(token.denomination(), 0);

        address _owner = address(0x77);
        //===========Setup===========

        Listings.Listing memory listing = IListings.Listing({
            owner: payable(_owner),
            created: uint40(block.timestamp),
            duration: 50 days,
            floorMultiple: 202
        });

        Listings.Listing memory listingOdd = IListings.Listing({
            owner: payable(_owner),
            created: uint40(block.timestamp),
            duration: 50 days,
            floorMultiple: 203
        });

        Listings.Listing memory listingNext = IListings.Listing({
            owner: payable(_owner),
            created: uint40(block.timestamp),
            duration: 50 days,
            floorMultiple: 204
        });

        // Save listing tax
        uint listingTax = listings.getListingTaxRequired(listing, address(erc721a));
        uint listingTaxOdd = listings.getListingTaxRequired(listingOdd, address(erc721a));

        assertEq(listingTax, listingTaxOdd);

        //===Calculate loss of fees for 50 days and 202/203/204 floor multiple===

        //We use listingTaxNext to find the tax value between 202 and 204, then dividing it by 2 we can see how much tax is 1 unit of floorMultiple worth.
        uint listingTaxNext = listings.getListingTaxRequired(listingNext, address(erc721a));
        uint taxLost = (listingTaxNext - listingTax) / 2;

        //Verify total tax fee:
        assertEq(listingTaxNext, 291457142857142857);

        //This would be our loss:
        assertEq(taxLost, 1439285714285714);
        // 1439285714285714 / 291457142857142857 = 0.0049318
        // This means that we are losing ~ 0.5% of the tax fee.
    }
```    
## Impact
This leads to loss of fees totaling to approximately ~ 0.5%.

## Tool used

Manual Review

## Recommendation
Increase the precision of floorMultiplier by a factor of 2 decimals, this should be enough to reduce the fees to negligible amounts.