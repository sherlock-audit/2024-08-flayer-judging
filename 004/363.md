Skinny Coconut Parrot

High

# Modifying the listing price will result in an overcharge of tax.

### Summary

When modifying the listing price, the tax is overcharged, as the duration of the listing is not updated, and the tax of the elapsed duration will be charged again at the new price.

### Root Cause

When modifying the listing price only, the `listing.floorMultiple` will be set to the new price (`Listings.sol:351`), and the `listing.created` and `listing.duration` remain unchanged. Then the new tax is calculated according to the duration and the new price  (`Listings.sol:355`). Since the duration remains unchanged, the taxes for the elapsed period of `block.timestamp - listing.created` will be recalculated at the new price. This leads to user to pay more taxes than he should.
```solidity
// Function: Listings.sol#modifyListings()

            if (params.floorMultiple != listing.floorMultiple) {
                // If we are creating a listing, and not performing an instant liquidation (which
                // would be done via `deposit`), then we need to ensure that the `floorMultiple` is
                // greater than 1.
                if (params.floorMultiple <= MIN_FLOOR_MULTIPLE) revert FloorMultipleMustBeAbove100(params.floorMultiple);
                if (params.floorMultiple > MAX_FLOOR_MULTIPLE) revert FloorMultipleExceedsMax(params.floorMultiple, MAX_FLOOR_MULTIPLE);

                emit ListingFloorMultipleUpdated(_collection, params.tokenId, listing.floorMultiple, params.floorMultiple);

351:@>          listing.floorMultiple = params.floorMultiple;
            }

            // Get the amount of tax required for the newly extended listing
355:@>      taxRequired_ += getListingTaxRequired(listing, _collection);
```
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L342-L355

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

_No response_

### PoC

1. We assume some user intend to create listings for 2 NFTs.
2. When listing NFT-1, the user sets the `duration` to 10 days, and `floorMultiple` to 600. We say that the user receives `n1` collection tokens.
3. When listing NFT-2, the user sets the same `duration` (10 days), but the `floorMultiple` is **halved** (300). After some time, the user modifies the price of the listing of NFT-2 to 600. We say the user receives another `n2` collection tokens.
4. Under the same `duration`, the larger the `floorMultiple`, the larger the tax and the fewer collection tokens the user receives. So `n1` should be smaller than `n2`, but it is not.
```solidity
    function test_UpdateListingPrice() public {
        uint32 _duration = 10 days;
        uint16 _floorMultiple = 600;

        // Flatten our token balance before processing for ease of calculation
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), address(this), 0 ether);
        
        erc721a.mint(address(this), 1);
        erc721a.mint(address(this), 2);
        assertEq(erc721a.ownerOf(1), address(this));
        assertEq(erc721a.ownerOf(2), address(this));
        erc721a.setApprovalForAll(address(listings), true);
        
        uint[] memory _tokenIds = new uint[](1);
        uint256 balance0 = token.balanceOf(address(this)); // save original balance

        // create listing for token 1
        _tokenIds[0] = 1;
        IListings.CreateListing[] memory _listings1 = new IListings.CreateListing[](1);
        _listings1[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIds,
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: _duration,                 // _duration
                floorMultiple: _floorMultiple        // _floorMultiple
            })
        });
        listings.createListings(_listings1);
        uint256 balance1 = token.balanceOf(address(this)); // balance after first listing
        console.log("For listing 1, user receives %d", balance1 - balance0);

        // create listing for token 2
        _tokenIds[0] = 2;
        IListings.CreateListing[] memory _listings2 = new IListings.CreateListing[](1);
        _listings2[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIds,
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: _duration,                 // _duration
                floorMultiple: _floorMultiple / 2    // 1/2 _floorMultiple
            })
        });
        listings.createListings(_listings2);

        vm.warp(block.timestamp + 1 days);
        // Approve our {CollectionToken} to be used by the {Listing} contract
        token.approve(address(listings), type(uint).max);

        IListings.ModifyListing[] memory params = new IListings.ModifyListing[](1);
        params[0] = IListings.ModifyListing({
            tokenId: 2,
            duration: 0,                     // do not modify duration
            floorMultiple: _floorMultiple    // modify price to _floorMultiple
        });
        listings.modifyListings(address(erc721a), params, false);
        uint256 balance2 = token.balanceOf(address(this)); // balance after first listing
        console.log("For listing 2, user receives %d", balance2 - balance1);

        assertGt(balance1 - balance0, balance2 - balance1); // n1 > n2
    }
```

Add the above test case to `Listings.t.sol`, and test it with `forge test -vv --match-test=test_UpdateListingPrice`, the reuslt is shown as:
```solidity
Ran 1 test for test/Listings.t.sol:ListingsTest
[PASS] test_UpdateListingPrice() (gas: 896173)
Logs:
  For listing 1, user receives 771428571428571429
  For listing 2, user receives 762500000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.06ms (1.17ms CPU time)

Ran 1 test suite in 11.64ms (7.06ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

When modifing only the listing price, do not forget to update the `listing.created` and `listing.duration`.
```solidity
            if (params.floorMultiple != listing.floorMultiple) {
                // If we are creating a listing, and not performing an instant liquidation (which
                // would be done via `deposit`), then we need to ensure that the `floorMultiple` is
                // greater than 1.
                if (params.floorMultiple <= MIN_FLOOR_MULTIPLE) revert FloorMultipleMustBeAbove100(params.floorMultiple);
                if (params.floorMultiple > MAX_FLOOR_MULTIPLE) revert FloorMultipleExceedsMax(params.floorMultiple, MAX_FLOOR_MULTIPLE);

                emit ListingFloorMultipleUpdated(_collection, params.tokenId, listing.floorMultiple, params.floorMultiple);

                listing.floorMultiple = params.floorMultiple;
                if (params.duration == 0) {
+                   uint32 timeElapsed = uint32(block.timestamp - listing.created);
+                   listing.created = uint40(block.timestamp);
+                   listing.duration = listing.duration - timeElapsed;
                }
            }
```