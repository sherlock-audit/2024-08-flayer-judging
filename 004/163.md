Stable Chili Ferret

High

# Underflow in `Listings.sol#cancelListing()` function

### Summary

Underflow may occur because the `_refund` returned by the `Listings.sol#_resolveListingTax()` function may be greater than 1 ether.


### Root Cause

The root cause is the calculation result of `1 Ether - refund` in the `Listings.sol#cancelListing()` function.


### Internal pre-conditions

- `refund` is bigger than 1 ether.

### External pre-conditions

_No response_

### Attack Path

`_refund` is calculated by `duration` and `floorMultiple` in the [`Listings.sol#_resolveListingTax()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L918-L956) function, so it is likely to be larger than 1 Ether.

In this case, the transaction may be reverted due to underflow at #L451.

### Impact

Users may not be able to cancel their Listings in a timely manner, which may result in unexpected losses to collateral.


### PoC

```solidity
    function test_CannotCancelMultipleListings() public {
        // Provide us with some base tokens that we can use to tax later on
        uint startBalance = 1 ether;
        deal(address(locker.collectionToken(address(erc721a))), address(this), startBalance * 10000);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        uint[] memory tokenIds = new uint[](4);
        for (uint i; i < tokenIds.length; ++i) {
            tokenIds[i] = i;
            erc721a.mint(address(this), i);
        }
        erc721a.setApprovalForAll(address(listings), true);

        // Set up multiple listings
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 120
            })
        });

        // Create our listings
        listings.createListings(_listings);

        vm.warp(block.timestamp + (VALID_LIQUID_DURATION / 2));

        IListings.ModifyListing[] memory params = new IListings.ModifyListing[](4);
        params[0] = IListings.ModifyListing(0, 21 days, 1000);
        params[1] = IListings.ModifyListing(1, 21 days, 1000);
        params[2] = IListings.ModifyListing(2, 21 days, 1000);
        params[3] = IListings.ModifyListing(3, 21 days, 1000);

        listings.modifyListings(address(erc721a), params, true);

        // Cancel our listings
        listings.cancelListings(address(erc721a), tokenIds, false);
    }
```

Result:
```solidity
Ran 1 test suite in 10.33ms (8.79ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Listings.t.sol:ListingsTest
[FAIL. Reason: panic: arithmetic underflow or overflow (0x11)] test_CannotCancelMultipleListings() (gas: 1814250)
```


### Mitigation

It is recommended to modify the `Listings.sol#cancelListing()` function as follows:
```solidity
    function cancelListings(address _collection, uint[] memory _tokenIds, bool _payTaxWithEscrow) public lockerNotPaused {
        uint fees;
        uint refund;

        for (uint i; i < _tokenIds.length; ++i) {
            uint _tokenId = _tokenIds[i];

            // Read the listing in a single read
            Listing memory listing = _listings[_collection][_tokenId];

            // Ensure the caller is the owner of the listing
            if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

            // We cannot allow a dutch listing to be cancelled. This will also check that a liquid listing has not
            // expired, as it will instantly change to a dutch listing type.
            Enums.ListingType listingType = getListingType(listing);
            if (listingType != Enums.ListingType.LIQUID) revert CannotCancelListingType();

            // Find the amount of prepaid tax from current timestamp to prepaid timestamp
            // and refund unused gas to the user.
            (uint _fees, uint _refund) = _resolveListingTax(listing, _collection, false);
            emit ListingFeeCaptured(_collection, _tokenId, _fees);

            fees += _fees;
            refund += _refund;

            // Delete the listing objects
            delete _listings[_collection][_tokenId];

            // Transfer the listing ERC721 back to the user
            locker.withdrawToken(_collection, _tokenId, msg.sender);
        }

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Burn the ERC20 token that would have been given to the user when it was initially created
---     uint requiredAmount = ((1 ether * _tokenIds.length) * 10 ** collectionToken.denomination()) - refund;
+++     uint requiredAmount = getListingTaxRequired(listing, _collection) - refund;
        payTaxWithEscrow(address(collectionToken), requiredAmount, _payTaxWithEscrow);
        collectionToken.burn(requiredAmount + refund);

        // Give some partial fees to the LP
        if (fees != 0) {
            collectionToken.approve(address(locker.implementation()), fees);
            locker.implementation().depositFees(_collection, 0, fees);
        }

        // Remove our listing type
        unchecked {
            listingCount[_collection] -= _tokenIds.length;
        }

        // Create our checkpoint as utilisation rates will change
        protectedListings.createCheckpoint(_collection);

        emit ListingsCancelled(_collection, _tokenIds);
    }
```