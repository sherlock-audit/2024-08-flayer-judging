Shiny Mint Lion

High

# There is a calculation error inside the modifyListings() function.

## Summary
There is a calculation error inside the modifyListings() function, causing users to overpay tax.
## Vulnerability Detail
```javascript
function modifyListings(address _collection, ModifyListing[] calldata _modifyListings, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused returns (uint taxRequired_, uint refund_) {
        uint fees;

        for (uint i; i < _modifyListings.length; ++i) {
            // Store the listing
            ModifyListing memory params = _modifyListings[i];
            Listing storage listing = _listings[_collection][params.tokenId];

            // We can only modify liquid listings
            if (getListingType(listing) != Enums.ListingType.LIQUID) revert InvalidListingType();

            // Ensure the caller is the owner of the listing
            if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

            // Check if we have no changes, as we can continue our loop early
@>>             if (params.duration == 0 && params.floorMultiple == listing.floorMultiple) {
                continue;
            }

            // Collect tax on the existing listing
@>>            (uint _fees, uint _refund) = _resolveListingTax(listing, _collection, false);
            emit ListingFeeCaptured(_collection, params.tokenId, _fees);

            fees += _fees;
            refund_ += _refund;

            // Check if we are altering the duration of the listing
@>>         if (params.duration != 0) {
                // Ensure that the requested duration falls within our listing range
                if (params.duration < MIN_LIQUID_DURATION) revert ListingDurationBelowMin(params.duration, MIN_LIQUID_DURATION);
                if (params.duration > MAX_LIQUID_DURATION) revert ListingDurationExceedsMax(params.duration, MAX_LIQUID_DURATION);

                emit ListingExtended(_collection, params.tokenId, listing.duration, params.duration);

                listing.created = uint40(block.timestamp);
                listing.duration = params.duration;
            }

            // Check if the floor multiple price has been updated
            if (params.floorMultiple != listing.floorMultiple) {
                // If we are creating a listing, and not performing an instant liquidation (which
                // would be done via `deposit`), then we need to ensure that the `floorMultiple` is
                // greater than 1.
                if (params.floorMultiple <= MIN_FLOOR_MULTIPLE) revert FloorMultipleMustBeAbove100(params.floorMultiple);
                if (params.floorMultiple > MAX_FLOOR_MULTIPLE) revert FloorMultipleExceedsMax(params.floorMultiple, MAX_FLOOR_MULTIPLE);

                emit ListingFloorMultipleUpdated(_collection, params.tokenId, listing.floorMultiple, params.floorMultiple);

                listing.floorMultiple = params.floorMultiple;
            }

            // Get the amount of tax required for the newly extended listing
@>>            taxRequired_ += getListingTaxRequired(listing, _collection);
        }

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // If our tax refund does not cover the full amount of tax required, then we will need to make an
        // additional tax payment.
        if (taxRequired_ > refund_) {
            unchecked {
                payTaxWithEscrow(address(collectionToken), taxRequired_ - refund_, _payTaxWithEscrow);
            }
            refund_ = 0;
        } else {
            unchecked {
                refund_ -= taxRequired_;
            }
        }

        // Check if we have fees to be paid from the listings
        if (fees != 0) {
            collectionToken.approve(address(locker.implementation()), fees);
            locker.implementation().depositFees(_collection, 0, fees);
        }

        // If there is tax to refund after paying the new tax, then allocate it to the user via escrow
        if (refund_ != 0) {
            _deposit(msg.sender, address(collectionToken), refund_);
        }
    }
```
In the modifyListings() function, when params.duration == 0, it indicates that the duration of the listing does not need to be changed and should remain the same.
```javascript
            // Collect tax on the existing listing
@>>            (uint _fees, uint _refund) = _resolveListingTax(listing, _collection, false);
            emit ListingFeeCaptured(_collection, params.tokenId, _fees);
```
These lines of code within the function calculate the tax that has already been incurred.

```javascript
            // Get the amount of tax required for the newly extended listing
@>>            taxRequired_ += getListingTaxRequired(listing, _collection);

```
The line of code in the function calculates the amount of tax required for the newly extended listing. However, since some time has already passed, the duration has changed.

The issue arises in the scenario where params.duration == 0. Although time has passed and the actual duration has shortened, the listing.duration remains unchanged. This causes taxRequired_ to still be calculated based on the original listing.duration, resulting in an inflated taxRequired.

Even more problematic is that when calling cancelListings() to cancel the listing or fillListings() to complete the listing, both internally call _resolveListingTax() to calculate the tax. This leads to an overcalculation of the user’s tax, causing the user to overpay.
```javascript
function _resolveListingTax(Listing memory _listing, address _collection, bool _action) private returns (uint fees_, uint refund_) {
       //skip---------
        // Get the amount of tax to be refunded. If the listing has already ended
        // then no refund will be offered.
        if (block.timestamp < _listing.created + _listing.duration) {
@>>            refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
        }

        // Send paid tax fees to the {FeeCollector}
        unchecked {
            fees_ = (taxPaid > refund_) ? taxPaid - refund_ : 0;
        }

       //--------------skip-------------
    }
```
This is because _resolveListingTax() always calculates based on the original listing.created and listing.duration. When params.duration == 0, the listing.created and listing.duration values also need to be updated.

#### poc
Let’s assume a listing has a floorMultiple of 200 and a duration of 30 days.

(1) After 10 days have passed, the user adjusts the floorMultiple to 400, and params.duration is set to 0, meaning the duration should not be changed. In this case, taxRequired is still calculated using the original listing.duration (30 days), which causes the taxRequired to be inflated.

(2) Now, assume another 10 days pass, and the user cancels the listing. The refund is calculated by _resolveListingTax() as taxPaid * (30 - 20) / 30, but it should actually be taxPaid * (20 - 10) / 20. As a result, the refund is reduced, and the fees increase.
## Impact
When the user calls modifyListings(), both the taxRequired and the final fees paid are increased.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L303

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L918
## Tool used

Manual Review

## Recommendation
```diff
function modifyListings(address _collection, ModifyListing[] calldata _modifyListings, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused returns (uint taxRequired_, uint refund_) {
        uint fees;

        for (uint i; i < _modifyListings.length; ++i) {
            // Store the listing
            ModifyListing memory params = _modifyListings[i];
            Listing storage listing = _listings[_collection][params.tokenId];

            // We can only modify liquid listings
            if (getListingType(listing) != Enums.ListingType.LIQUID) revert InvalidListingType();

            // Ensure the caller is the owner of the listing
            if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

            // Check if we have no changes, as we can continue our loop early
-             if (params.duration == 0 && params.floorMultiple == listing.floorMultiple) {
+             if (params.duration == listing.duration && params.floorMultiple == listing.floorMultiple) {
                continue;
            }

            // Collect tax on the existing listing
            (uint _fees, uint _refund) = _resolveListingTax(listing, _collection, false);
            emit ListingFeeCaptured(_collection, params.tokenId, _fees);

            fees += _fees;
            refund_ += _refund;

            // Check if we are altering the duration of the listing
-         if (params.duration != 0) {
                // Ensure that the requested duration falls within our listing range
                if (params.duration < MIN_LIQUID_DURATION) revert ListingDurationBelowMin(params.duration, MIN_LIQUID_DURATION);
                if (params.duration > MAX_LIQUID_DURATION) revert ListingDurationExceedsMax(params.duration, MAX_LIQUID_DURATION);

                emit ListingExtended(_collection, params.tokenId, listing.duration, params.duration);

                listing.created = uint40(block.timestamp);
                listing.duration = params.duration;
-            }

            // Check if the floor multiple price has been updated
            if (params.floorMultiple != listing.floorMultiple) {
                // If we are creating a listing, and not performing an instant liquidation (which
                // would be done via `deposit`), then we need to ensure that the `floorMultiple` is
                // greater than 1.
                if (params.floorMultiple <= MIN_FLOOR_MULTIPLE) revert FloorMultipleMustBeAbove100(params.floorMultiple);
                if (params.floorMultiple > MAX_FLOOR_MULTIPLE) revert FloorMultipleExceedsMax(params.floorMultiple, MAX_FLOOR_MULTIPLE);

                emit ListingFloorMultipleUpdated(_collection, params.tokenId, listing.floorMultiple, params.floorMultiple);

                listing.floorMultiple = params.floorMultiple;
            }

            // Get the amount of tax required for the newly extended listing
            taxRequired_ += getListingTaxRequired(listing, _collection);
        }

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // If our tax refund does not cover the full amount of tax required, then we will need to make an
        // additional tax payment.
        if (taxRequired_ > refund_) {
            unchecked {
                payTaxWithEscrow(address(collectionToken), taxRequired_ - refund_, _payTaxWithEscrow);
            }
            refund_ = 0;
        } else {
            unchecked {
                refund_ -= taxRequired_;
            }
        }

        // Check if we have fees to be paid from the listings
        if (fees != 0) {
            collectionToken.approve(address(locker.implementation()), fees);
            locker.implementation().depositFees(_collection, 0, fees);
        }

        // If there is tax to refund after paying the new tax, then allocate it to the user via escrow
        if (refund_ != 0) {
            _deposit(msg.sender, address(collectionToken), refund_);
        }
    }
```