Clean Snowy Mustang

Medium

# Listings cannot be created even if both floorMultiple and duration are in range

## Summary
Listings cannot be created even if both floorMultiple and duration are in range.

## Vulnerability Detail
When a user creates listings, protocol will check if `listing.floorMultiple` and `listing.duration` are in range.

[Listings.sol#L275-L276](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L275-L276):
```solidity
        if (listing.floorMultiple <= MIN_FLOOR_MULTIPLE) revert FloorMultipleMustBeAbove100(listing.floorMultiple);
        if (listing.floorMultiple > MAX_FLOOR_MULTIPLE) revert FloorMultipleExceedsMax(listing.floorMultiple, MAX_FLOOR_MULTIPLE);
```

[Listings.sol#L279-L292](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L279-L292):
```solidity
        Enums.ListingType listingType = getListingType(_listing.listing);
        if (listingType == Enums.ListingType.DUTCH) {
            // Ensure that the requested duration falls within our listing range
            if (listing.duration < MIN_DUTCH_DURATION) revert ListingDurationBelowMin(listing.duration, MIN_DUTCH_DURATION);
            if (listing.duration > MAX_DUTCH_DURATION) revert ListingDurationExceedsMax(listing.duration, MAX_DUTCH_DURATION);

        } else if (listingType == Enums.ListingType.LIQUID) {
            // Ensure that the requested duration falls within our listing range
            if (listing.duration < MIN_LIQUID_DURATION) revert ListingDurationBelowMin(listing.duration, MIN_LIQUID_DURATION);
            if (listing.duration > MAX_LIQUID_DURATION) revert ListingDurationExceedsMax(listing.duration, MAX_LIQUID_DURATION);

        } else {
            revert InvalidListingType();
        }
```

The limitations are as below:
> MIN_FLOOR_MULTIPLE: 100
> MAX_FLOOR_MULTIPLE: 10_00
> MIN_LIQUID_DURATION: 7 days
> MAX_LIQUID_DURATION: 180 days
> MIN_DUTCH_DURATION: 1 days
> MAX_DUTCH_DURATION: 7 days - 1

Therefore, it is expected that user can create a listing with both `floorMulitple` and `duration` are in range, for example, `floorMulitple` is 5_00 and `duration` is 60 days.

However, it is not always the case. In addition to receive one collection token per ERC721 item they deposited, user need to prepay tax for the listing. The tax is calculated as below:

[TaxCalculator.sol#L35-L44](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L35-L44):
```solidity
    function calculateTax(address _collection, uint _floorMultiple, uint _duration) public pure returns (uint taxRequired_) {
        // If we have a high floor multiplier, then we want to soften the increase
        // after a set amount to promote grail listings.
        if (_floorMultiple > FLOOR_MULTIPLE_KINK) {
            _floorMultiple = FLOOR_MULTIPLE_KINK + ((_floorMultiple - FLOOR_MULTIPLE_KINK) / 2);
        }

        // Calculate the tax required per second
        taxRequired_ = (_floorMultiple ** 2 * 1e12 * _duration) / 7 days;
    }
```

To listing one ERC721 item for 60 days with 5x floor multiplier,  the tax required is $(200 + (500 - 200) / 2) ** 2 * 1e12 * 60 days / 7 days = 1.05 ether$, then when calculating the actual token user can receive, the transaction will revert due to that the required tax is larger than token user can initially receive:

[Listings.sol#L150)](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L150):
```solidity
            if (taxRequired > tokensReceived) revert InsufficientTax(tokensReceived, taxRequired);
```

Please run the PoC in Listings.t.sol:
```solidity
    function testAudit_CannotCreateListingsEvenIfInRange() public {
        address alice = makeAddr("Alice");

        uint256 tokenId = 123;
        erc721a.mint(alice, tokenId);

        Listings.Listing memory listing = IListings.Listing({
            owner: payable(alice),
            created: uint40(block.timestamp),
            duration: 60 days,
            floorMultiple: 5_00
        });

        vm.startPrank(alice);
        erc721a.approve(address(listings), tokenId);
        IListings.CreateListing[] memory createListings = new IListings.CreateListing[](1);
        createListings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(tokenId),
            listing: listing
        });
        vm.expectRevert(abi.encodeWithSelector(IListings.InsufficientTax.selector, 1000000000000000000, 1050000000000000000));
        listings.createListings(createListings);
        vm.stopPrank();
    }
```

## Impact

Listings cannot be created with reasonable arguments.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L43

## Tool used

Manual Review

## Recommendation

Refactor tax calculation formula or limit maximum tax within 1 ether. 