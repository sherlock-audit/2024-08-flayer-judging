Uneven Burlap Dalmatian

High

# No slippage protection on ```Listings::fillListings()``` can cause ```buyer``` being front-runned by price change from ```lister``` and causing him to overpay.

### Summary

Owner of a ```listing``` can front-run the ```Listings::fillListings()``` call of buyer and change the price of the ```listing``` causing him to overpay without having any way to be protected from that.

### Root Cause

Buyer of a token can give his ```collectionTokens``` and buy the token by calling ```Listings::fillListings()``` and get the token the specified price. Let's see the ```Listings::fillListings()``` :
```solidity
    function fillListings(FillListingsParams calldata params) public nonReentrant lockerNotPaused {
        // ...

        // Iterate over owners
        for (uint ownerIndex; ownerIndex < params.tokenIdsOut.length; ++ownerIndex) {
            // Iterate over the owner tokens. If the owner has no tokens, just skip
            // to the next owner in the loop.
            uint ownerIndexTokens = params.tokenIdsOut[ownerIndex].length;
            if (ownerIndexTokens == 0) {
                continue;
            }

            // Reset our owner for the group as the first owner in the iteration
            owner = _listings[collection][params.tokenIdsOut[ownerIndex][0]].owner;

            for (uint i; i < ownerIndexTokens; ++i) {
                uint tokenId = params.tokenIdsOut[ownerIndex][i];

                // If this is not the first listing, then we want to validate that the owner
                // matches the first of the group.
                if (i != 0 && _listings[collection][tokenId].owner != owner) {
                    revert InvalidOwner();
                }

                // Action our listing fill
                _fillListing(collection, address(_collectionToken), tokenId);
            }

            // If there is ERC20 left to be claimed, then deposit this into the escrow
            ownerReceives = _tload(FILL_PRICE) - (ownerIndexTokens * 1 ether * 10 ** _collectionToken.denomination());
            if (ownerReceives != 0) {
                _deposit(owner, address(_collectionToken), ownerReceives);
                totalPrice += ownerReceives;
            }

            refundAmount = _tload(FILL_REFUND);
            if (refundAmount != 0) {
                _deposit(owner, address(_collectionToken), refundAmount);
                assembly { tstore(FILL_REFUND, 0) }
            }

            // Reset the price back to zero
            assembly { tstore(FILL_PRICE, 0) }

            totalBurn += ownerIndexTokens;
        }

        // Transfer enough tokens from the user to cover the `_deposit` calls made during
        // our fill loop.
@>        _collectionToken.transferFrom(msg.sender, address(this), totalPrice);

       // ...
    }

   function _fillListing(address _collection, address _collectionToken, uint _tokenId) private {
        // Get our listing information
@>        (bool isAvailable, uint price) = getListingPrice(_collection, _tokenId);

        // ...
    }

    function getListingPrice(address _collection, uint _tokenId) public view returns (bool isAvailable_, uint price_) {
        // ...

        // Get our collection token's base price, accurate to the token's denomination
        price_ = 1 ether * 10 ** locker.collectionToken(_collection).denomination();

        // If we don't have a listing object against the token ID, then we just consider
        // it to be a Floor level asset.
        if (listing.owner == address(0)) {
            return (true, price_);
        }

        // Determine the listing price based on the floor multiple. If this is a dutch
        // listing then further calculations will be applied later.
@>        uint totalPrice = (price_ * uint(listing.floorMultiple)) / MIN_FLOOR_MULTIPLE;

        // ...

        // ...

        // By this point, we just show the listing value as it should be for sale
@>        return (true, totalPrice);
    }

```
[Link to code](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L766C1-L772C6)

As we can see, the ```listing``` price is determined from the ```listing.floorMultiple```. However, ```Listings::fillListings()``` lacks the support for slippage protection (user to specify max amount of ```collectionTokens``` he is willing to trade for the specific token) and this allows the owner of the ```listing``` to modify the ```floorMultiple``` of the ```listing``` just before the buy.

### Internal pre-conditions

1. User A has create a ```listing``` for his token using ```Listings::createListings()``` specifying ```floorMultiple``` and ```duration```.

### External pre-conditions

1. Buyer wants to exchange his ```collectionTokens``` so to buy the token that User A listed.

### Attack Path

1. Seller lists his token on ```Listings``` contract.
2. Buyer calls ```Listings::fillListings()``` so to buy it.
3. Seller front-runs this call by modifying the ```floorMultiple``` calling ```Listings::modifyListings()```.
4. Buyer overpays for the token unexpectedly.

### Impact

The impact of this vulnerability is critical since any malicious lister can cause unexpected (and as high as the balance of the buyer) loss by having him overpaying for a token. Since there is no protection against this, we can expect this phenomenon to be happening a lot on high volume and victims accumulate lot of losses of funds due to this. In any case, it goes without saying that in a protocol like this slippage protection is a **must**.

### PoC

No PoC needed.

### Mitigation

To mitigate this vulnerability successfully, add a parameter on every NFT/```collectionToken``` exchange that will serve as the ```collectionTokensMax``` protection.