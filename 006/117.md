Raspy Raspberry Tapir

High

# Frequency-dependent `TaxCalculator.sol::calculateCompoundedFactor` leads to interest loss either for users or for protocol

### Summary

[TaxCalculator.sol::calculateCompoundedFactor](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L90) uses discrete formula for calculating the compounded factor. Combined with the wrong divisor (`1000` instead of `10_000`, as I outline in another finding), when collection's utilization factor is moderately high (e.g. 90%), and the operations with the collection happen relatively frequently (e.g. every day), this leads to charging users excessive interest rates: for this example, `3200%` more than expected. 

If the divisor is fixed to the correct value (`10_000`), then the effects from using the discrete formula are less severe, but still quite substantial: namely for a 100% collection utilization, depending of the frequency, either the user will be charged up to $e-2 \approx$ `71%`  more interest per year compared to the non-compound interest, or the protocol will receive up to $1- 1/(e-1) \approx$ `42%` less interest per year compared to the compound interest.

It's worth noting that `calculateCompoundedFactor` is called whenever a new checkpoint is created for the collection, which happens e.g. when _any user_ [creates a listing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L162), [cancels a listing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L467), or [fills a listing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L603). **This is neither enough to reach the desired precision, nor is it gas efficient.**

Depending on the frequency of collection operations, either:

- If non-compound interest is expected, but the frequency is high, then the users will be charged up to `71%` more interest than they expect;
- If compound interest is expected, but the frequency is low, then the protocol will receive up to `42%` less interest than it expects.

### Root Cause

[TaxCalculator.sol::calculateCompoundedFactor](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L90) employs the following formula: 

```solidity
compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 1000 * _timePeriod)) / 1e18;
```

As I explain in another finding, the divisor `1000` is incorrect, and has to be fixed to `10_000`. Provided this is fixed, the resulting formula is the correct discrete formula for calculating the compounded interest. The problem is that the formula will give vastly different results depending on the frequency of operations which have nothing to do with the user who holds the protected listing.

### Internal pre-conditions

Varying frequency of collection operations.

### External pre-conditions

none

### Attack Path

No attack is necessary. The interest rates will be wrongly calculated in most cases.

### Impact

Either users are charged up to `71%` more interest than they expect, or the protocol receives up to `42%` less interest than it expects.

### PoC

Drop this test to [TaxCalculator.t.sol](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/test/TaxCalculator.t.sol#L86), and execute with `forge test --match-test test_WrongInterestCalculation`

```solidity
   // This test uses the unmodified source code, with the wrong divisor of 1000
   function test_WrongInterestCalculation() public view {
        // We fix collection utilization to 90%
        uint utilization = 0.9 ether;

        // New checkpoints with the updated compoundedFactor are created 
        // and stored whenever there is activity wrt. the collection

        // The expected interest multiplier after 1 year
        uint expectedFactor = 
            taxCalculator.calculateCompoundedFactor(1 ether, utilization, 365 days);

        // The resulting interest multiplier if some activity happens every day
        uint compoundedFactor = 1 ether;
        for (uint time = 0; time < 365 days; time += 1 days) {
            compoundedFactor = 
                taxCalculator.calculateCompoundedFactor(compoundedFactor, utilization, 1 days);
        }

        // The user loss due to the activity which doesn't concern them is 3200%
        assertApproxEqRel(
            33 ether * expectedFactor / 1 ether,
            compoundedFactor,
            0.01 ether);
   }
```

### Mitigation

**Variant 1**: If non-compound interest is desired, apply this diff:

```diff
diff --git a/flayer/src/contracts/TaxCalculator.sol b/flayer/src/contracts/TaxCalculator.sol
index 915c0ff..4031aba 100644
--- a/flayer/src/contracts/TaxCalculator.sol
+++ b/flayer/src/contracts/TaxCalculator.sol
@@ -87,7 +87,7 @@ contract TaxCalculator is ITaxCalculator {
         uint perSecondRate = (interestRate * 1e18) / (365 * 24 * 60 * 60);
 
         // Calculate new compounded factor
-        compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 1000 * _timePeriod)) / 1e18;
+        compoundedFactor_ = _previousCompoundedFactor + (perSecondRate * _timePeriod / 1000);
     }
 
     /**
```

_Note: in the above the divisor is still unfixed, as this belongs to a different finding._

**Variant 2**: If compound interest is desired, employ either periodic per-second compounding, or continuous compounding with exponentiation. Any of these approaches are precise enough and much more gas efficient than the current one, but require substantial refactoring.