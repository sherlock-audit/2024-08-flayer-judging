Striped Boysenberry Fox

High

# Incorrect Calculation of Compound Factor in `TaxCalculator`

## Summary

The interest rate in the tax calculator is designed to have two decimal places, meaning 10_000 equals 100%. However, when calculating the compound factor, the rate is divided by 1000, which may result in overestimating the compound factor.

## Vulnerability Detail

According to the [comment](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L46-L58) and the [implementation](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L59-L71) of `TaxCalculator::calculateProtectedInterest()` function, the decimal places of interest rate should be 2.

```solidity
    /**
     ... ...
@>   * @dev The interest rate is returned to 2 decimal places (200 = 2%)
     ... ...
     */
    function calculateProtectedInterest(uint _utilizationRate) public pure returns (uint interestRate_) {
        // If we haven't reached our kink, then we can just return the base fee
        if (_utilizationRate <= UTILIZATION_KINK) {
            // Calculate percentage increase for input range 0 to 0.8 ether (2% to 8%)
            interestRate_ = 200 + (_utilizationRate * 600) / UTILIZATION_KINK;
        }
        // If we have passed our kink value, then we need to calculate our additional fee
        else {
            // Convert value in the range 0.8 to 1 to the respective percentage between 8% and
@>          // 100% and make it accurate to 2 decimal places.
            interestRate_ = (((_utilizationRate - UTILIZATION_KINK) * (100 - 8)) / (1 ether - UTILIZATION_KINK) + 8) * 100;
        }
    }
```

In the [`calculateCompoundFactor()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L80-L91) function, the calculated annual interest rate is divided by total seconds per year to calculate the rate per second.

```solidity
    uint perSecondRate = (interestRate * 1e18) / (365 * 24 * 60 * 60);
```

After then, the compound factor is newly calculated from the previous compound factor, the interest rate per second and the period seconds.

```solidity
    compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 1000 * _timePeriod)) / 1e18;
```

Here is a problem with this line:

If `interestRate` is `100%(=1_00_00)` and `_timePeriod` is 365 days, then `(perSecondRate / 1000 * _timePeriod)` should be `1e18`. But because of wrong divisor(1000), `(perSecondRate / 1000 * _timePeriod)` becomes around `1e19`.

Therefore new compound factor around 11 times comparing to the previous one.

### Proof-Of-Concept

Adds a new test case in `TaxCalculator.t.sol`:

```solidity
    function test_IncorrectCompoundFactor() public {
        // Utilization rate is 1e18. Thus annual interest rate becomes 10000.
        // Then, `perSecondRate` becomes 317,097,919,837,645.
        // Thus new compound factor should be `
        //   1e18 * (1e18 + (317,097,919,837,645 / 1000 * 365 days)) / 1e18 = 10999999999979632000(>1e19)`
        assertEq(taxCalculator.calculateCompoundedFactor(1e18, 1e18, 365 days), 10_999_999_999_979_632_000);
    }
```

Here are successful logs after running the test:

```bash
$ forge test --match-test test_IncorrectCompoundFactor -vv
[⠒] Compiling...
[⠢] Compiling 1 files with Solc 0.8.26
[⠆] Solc 0.8.26 finished in 6.07s
Compiler run successful!

Ran 1 test for test/TaxCalculator.t.sol:TaxCalculatorTest
[PASS] test_IncorrectCompoundFactor() (gas: 10114)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 788.50µs (97.80µs CPU time)

Ran 1 test suite in 7.37ms (788.50µs CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

Highly over-valued compound factors will make compound amounts significantly higher in the `TaxCalculator::compound()` function which are further used in calculating unlocking price.

Thus the unlocking price gets much higher than expected, and will make protected NFTs unhealthy and instantly liquidable.

## Code Snippet

[TaxCalculator.sol#L90](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L90)

## Tool used

Manual Review

## Recommendation

Should replace `1000` to `10000`:

```diff
    function calculateCompoundedFactor(uint _previousCompoundedFactor, uint _utilizationRate, uint _timePeriod) public view returns (uint compoundedFactor_) {
        ... ...
-       compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 1000 * _timePeriod)) / 1e18;
+       compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 10000 * _timePeriod)) / 1e18;
    }
```
