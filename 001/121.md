Lone Coconut Cat

High

# Relisting Can Be Used To Steal Funds

## Summary

Unlike creating a listing or a liquidation listing, when [`relist`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625C14-L625C20)ing a token, the `created` timestamp of the user's [`CreateListing`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/interfaces/IListings.sol#L72C12-L72C25) argument is not validated.

This can enable users to steal funds from the protocol, and subvert harberger taxes.

## Vulnerability Detail

The [`Listings`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol) contract allows three ways to create a new `Listing`:

1. [`createListings`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L130C14-L130C28)
2. [`createLiquidationListing`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L178C14-L178C38)
3. [`relist`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625C14-L625C20)

Notice that each of these functions accept a [`CreateListing`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/interfaces/IListings.sol#L72C12-L72C25) struct, which has the following format:

```solidity
struct CreateListing {
  address collection;
  uint[] tokenIds;
  Listing listing; /// @audit contains a `Listing` struct
}
```

```solidity
struct Listing {
  address payable owner;
  uint40 created; /// @audit allows caller to specify the `created` date of the listing
  uint32 duration;
  uint16 floorMultiple;
}
```

Normally, although an external caller may indeed specify an arbitrary `created` date on the nested `Listing` struct, this has no effect because the `CreateListing` object is usually passed through [`_mapListings`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L242C14-L242C26):

```solidity
function _mapListings(CreateListing calldata _createListing, uint _tokenIds) private returns (uint tokensReceived_) {
    // Loop through our tokens
    for (uint i; i < _tokenIds; ++i) {
        // Create our initial listing and update the timestamp of the listing creation to now
        _listings[_createListing.collection][_createListing.tokenIds[i]] = Listing({
            owner: _createListing.listing.owner,
            created: uint40(block.timestamp), /// @audit safely overwrites the caller's created timestamp
            duration: _createListing.listing.duration,
            floorMultiple: _createListing.listing.floorMultiple
        });
    }

    // Our user will always receive one ERC20 per ERC721
    tokensReceived_ = _tokenIds * 1 ether;
}
```

This safely prevents the caller from using a malicious timestamp.

> [!NOTE]
>
> [`_validateCreateListing`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L262C14-L262C36) **does not** validate the `created` field.

However, these protections are only in place for [`createListings`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L130C14-L130C28) and [`createLiquidationListing`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L178C14-L178C38). [[1]](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L146C30-L146C42) [[2]](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L202C9-L202C21)

Invocations to [`relist`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625C14-L625C20) allow the caller to persist a malicious `created` field directly to contract storage, as the field goes unvalidated and unmodified:

```solidity
// Validate our new listing
_validateCreateListing(_listing);

// Store our listing into our Listing mappings
_listings[_collection][_tokenId] = listing; /// @audit listing is saved directly to storage without validating the timestamp

// Pay our required taxes
payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);
```

Consequently, by relisting to move the `created` field closer to the `block.timestamp`, a user can receive undue refunds:

```solidity
// Get the amount of tax to be refunded. If the listing has already ended
// then no refund will be offered.
if (block.timestamp < _listing.created + _listing.duration) {
    /// @audit Increasing listing.created can secure a greater refund for listings which did not sell.
    refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
}
```

Likewise, a user can subvert harberger taxes.

By using another EOA, a user can relist their `Listing` at a `creation` date far in the future (i.e. `type(uint40`.max) [to prevent their underlying token from being purchased](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L855C9-L859C10):

```solidity
// This is an edge case, but protects against potential future logic. If the
// listing starts in the future, then we can't sell the listing.
if (listing.created > block.timestamp) { /// @audit users can maliciously activate this condition
    return (isAvailable_, totalPrice);
}
```

This will eventually result in a depeg event:

> **2.1 Harberger Fees**
>
> Liquid Listings would not work without an incentive to ensure that the seller is setting a fair price. Without such an incentive, users could list their items at extortionate prices with no intention to be filled whilst still accessing a freshly minted ƒ token.
> **This would result in the ƒ token depegging from the market as it could no longer be used to claim a fairly priced item.**


## Impact

Stolen due refunds from the protocol.

## Code Snippet

```solidity
// Validate our new listing
_validateCreateListing(_listing);

// Store our listing into our Listing mappings
_listings[_collection][_tokenId] = listing;

// Pay our required taxes
payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);
```

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L661C9-L669C1

## Tool used

Manual Review

## Recommendation

Validate the `created` timestamp when relisting.