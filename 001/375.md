Happy Wintergreen Kookaburra

High

# The `collectionShutdown` Contract Allows Normal Voting After Execution, Preventing Users from Receiving Compensation for Burned Tokens and NFTs sold

## Summary
In the `collectionShutdown` contract, a bug allows users to call the `vote` function even after the `execute` function has been called. This leads to a scenario where the collection can be canceled after execution, preventing users from claiming their rewards or compensation for burned tokens and sold NFTs

## Vulnerability Detail
The vulnerability arises from the ability to call `vote` after the execute function has already been invoked. This allows a user, after the collection is executed, to manipulate the system by re-enabling `canExecute` through voting. Consequently, the `cancel` function can be called, which deletes critical parameters like `sweeperPool`, leading to failed claims and effectively locking users out of their rewards. 
## Impact
- Users lose their Funds that they were expecting
### How it works
1. 2 Users who own 50 % call `start` and `vote` with both of them having (1e24 x 2) with `4e24` Collection Tokens total supply
2. The `(shutdownVotes >= params.quorumVotes)` executes and sets the `canExecute = true`
3. Bob who owns an NFT comes and deposits then mints collection tokens (Increasing the supply to `5e24` Collection Tokens)
4. The owner calls `execute` updating the new `newQuorum` (so Bob can be able to claim as he has supply), Deleting the Collection address and setting `canExecute = false` (`cancel` cannot be called)
4. Bob comes and calls `vote` (As he had minted the Tokens), setting `canExecute = true` (`cancel` can now be called)
5. Bob is mad that the collection he once bought was useless, he calls `cancel` which only checks (`canExecute = true`(Pass ✅) and Supply have increased (Pass ✅))
6. `Cancel` Function now deletes the `_collectionParams[_collection]` which will cause `params.sweeperPool` to be address(0) and is checked when you call `claim` (reverts if its address(0))
7. Now the `claim` function cannot be called
8. Overall Users cannot claim and the reason for that was because of the `canExecute = true` executed at `vote` by Bob


## Code Snippet
1. `Start` Function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-#L157
<details>
<summary>Start</summary>

```solidity
    function start(address _collection) public whenNotPaused {
        // Confirm that this collection is not prevented from being shutdown
        if (shutdownPrevented[_collection]) revert ShutdownPrevented();

        // Ensure that a shutdown process is not already actioned
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();

        // Get the total number of tokens still in circulation, specifying a maximum number
        // of tokens that can be present in a "dormant" collection.
        params.collectionToken = locker.collectionToken(_collection);
        uint totalSupply = params.collectionToken.totalSupply();
        if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();

        // Set our quorum vote requirement
        params.quorumVotes = uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT);

        // Notify that we are processing a shutdown
        emit CollectionShutdownStarted(_collection);

        // Cast our vote from the user
        _collectionParams[_collection] = _vote(_collection, params);
    }
```
</details>

2. `vote` Function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L175-L214
<details>
<summary>Vote</summary>

```solidity
    function vote(address _collection) public nonReentrant whenNotPaused {
        // Ensure that we are within the shutdown window
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.quorumVotes == 0) revert ShutdownProccessNotStarted();

        _collectionParams[_collection] = _vote(_collection, params);
    }

    /**
     * Processes the logic for casting a vote.
     *
     * @param _collection The collection address
     * @param params The collection shutdown parameters
     *
     * @return The updated shutdown parameters
     */
    function _vote(address _collection, CollectionShutdownParams memory params) internal returns (CollectionShutdownParams memory) {
        // Take tokens from the user and hold them in this escrow contract
        uint userVotes = params.collectionToken.balanceOf(msg.sender);
        if (userVotes == 0) revert UserHoldsNoTokens();

        // Pull our tokens in from the user
        params.collectionToken.transferFrom(msg.sender, address(this), userVotes);

        // Register the amount of votes sent as a whole, and store them against the user
        params.shutdownVotes += uint96(userVotes);

        // Register the amount of votes for the collection against the user
        unchecked { shutdownVoters[_collection][msg.sender] += userVotes; }

        emit CollectionShutdownVote(_collection, msg.sender, userVotes);

        // If we can execute, then we need to fire another event
        if (!params.canExecute && params.shutdownVotes >= params.quorumVotes) {
            params.canExecute = true;
            emit CollectionShutdownQuorumReached(_collection);
        }

        return params;
    }
```
</details>

3. `execute` Function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231-L275
<details>
<summary>Execute</summary>

```solidity
    function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Ensure we have specified token IDs
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength == 0) revert NoNFTsSupplied();

        // Check that no listings currently exist
        if (_hasListings(_collection)) revert ListingsExist();

        // Refresh total supply here to ensure that any assets that were added during
        // the shutdown process can also claim their share.
        uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
        if (params.quorumVotes != newQuorum) {
            params.quorumVotes = uint88(newQuorum);
        }

        // Lockdown the collection to prevent any new interaction
        locker.sunsetCollection(_collection);

        // Iterate over our token IDs and transfer them to this contract
        IERC721 collection = IERC721(_collection);
        for (uint i; i < _tokenIdsLength; ++i) {
            locker.withdrawToken(_collection, _tokenIds[i], address(this));
        }

        // Approve sudoswap pair factory to use our NFTs
        collection.setApprovalForAll(address(pairFactory), true);

        // Map our collection to a newly created pair
        address pool = _createSudoswapPool(collection, _tokenIds);

        // Set the token IDs that have been sent to our sweeper pool
        params.sweeperPoolTokenIds = _tokenIds;
        sweeperPoolCollection[pool] = _collection;

        // Update our collection parameters with the pool
        params.sweeperPool = pool;

        // Prevent the collection from being executed again
        params.canExecute = false;
        emit CollectionShutdownExecuted(_collection, pool, _tokenIds);
    }
```
</details>

4. `Claim` and `Claim&Vote` Function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231-L275
<details>
<summary>Claim</summary>

```solidity
    function claim(address _collection, address payable _claimant) public nonReentrant whenNotPaused {
        // Ensure our user has tokens to claim
        uint claimableVotes = shutdownVoters[_collection][_claimant];
        if (claimableVotes == 0) revert NoTokensAvailableToClaim();

        // Ensure that we have moved token IDs to the pool
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.sweeperPool == address(0)) revert ShutdownNotExecuted();

        // Ensure that all NFTs have sold from our Sudoswap pool
        if (!collectionLiquidationComplete(_collection)) revert NotAllTokensSold();

        // We can now delete our sweeper pool tokenIds
        if (params.sweeperPoolTokenIds.length != 0) {
            delete _collectionParams[_collection].sweeperPoolTokenIds;
        }

        // Burn the tokens from our supply
        params.collectionToken.burn(claimableVotes);

        // Set our available tokens to claim to zero
        delete shutdownVoters[_collection][_claimant];

        // Get the number of votes from the claimant and the total supply and determine from that the percentage
        // of the available funds that they are able to claim.
        uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
        (bool sent,) = _claimant.call{value: amount}('');
        if (!sent) revert FailedToClaim();

        emit CollectionShutdownClaim(_collection, _claimant, claimableVotes, amount);
    }

    /**
     * Users that missed the initial voting window can still claim, but it is more gas efficient to use this
     * combined function.
     *
     * @param _collection The collection address
     */
    function voteAndClaim(address _collection) public whenNotPaused {
        // Ensure that we have moved token IDs to the pool
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.sweeperPool == address(0)) revert ShutdownNotExecuted();

        // Ensure that all NFTs have sold from our Sudoswap pool
        if (!collectionLiquidationComplete(_collection)) revert NotAllTokensSold();

        // Take tokens from the user and hold them in this escrow contract
        uint userVotes = params.collectionToken.balanceOf(msg.sender);
        if (userVotes == 0) revert UserHoldsNoTokens();
        params.collectionToken.burnFrom(msg.sender, userVotes);

        // We can now delete our sweeper pool tokenIds
        if (params.sweeperPoolTokenIds.length != 0) {
            delete _collectionParams[_collection].sweeperPoolTokenIds;
        }

        // Get the number of votes from the claimant and the total supply and determine from that the percentage
        // of the available funds that they are able to claim.
        uint amount = params.availableClaim * userVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
        (bool sent,) = payable(msg.sender).call{value: amount}('');
        if (!sent) revert FailedToClaim();

        emit CollectionShutdownClaim(_collection, msg.sender, userVotes, amount);
    }
```
</details>

5. `Cancel` Function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L390-L405
<details>
<summary>Cancel</summary>

```solidity
    function cancel(address _collection) public whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Check if the total supply has surpassed an amount of the initial required
        // total supply. This would indicate that a collection has grown since the
        // initial shutdown was triggered and could result in an unsuspected liquidation.
        if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
            revert InsufficientTotalSupplyToCancel();
        }

        // Remove our execution flag
        delete _collectionParams[_collection];
        emit CollectionShutdownCancelled(_collection);
    }
```
</details>


## Tool used
Manual Review

## Recommendation
Users should only call the `voteAndClaim` after the `execute` call has been made, so restrict the `vote` function from being called after the `execute` call has been completed