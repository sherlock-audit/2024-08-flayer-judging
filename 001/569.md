Genuine Slate Sloth

High

# Users can't claim ETH after the collection has been shut down and fully liquidated due to formula for calculating the ETH amount to claim

## Summary
In the `CollectionShutdown` contract, users cannot claim ETH after the collection has been shut down and fully liquidated due to formula for calculating the ETH amount to claim.

## Vulnerability Detail
In the `CollectionShutdown::claim` and `CollectionShutdown::voteAndClaim` functions, the formula for calculating the ETH amount that each user can claim is: `params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT)`. Since the data type of `params.quorumVotes` is `uint88`, the expression `params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT` = `params.quorumVotes *  2` can overflow if `params.quorumVotes` >= 2^87. To satisfy this condition, the total supply of `CollectionToken` only need to be greater than `2 ether * 10^9`. As a result, the `CollectionShutdown::claim` and `CollectionShutdown::voteAndClaim` functions would revert due to overflow.

[CollectionShutdown::claim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L310) function:

```Solidity
function claim(address _collection, address payable _claimant) public nonReentrant whenNotPaused {
    ...
=>  uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
    (bool sent,) = _claimant.call{value: amount}('');
    if (!sent) revert FailedToClaim();
    ...
}
```

## Impact
Users can't claim ETH after the collection has been shut down and fully liquidated.

## Proof of Concept
Copy this test case into the `CollectionShutdown.t.sol` file:

```Solidity
function test_overflowQuorumVotes() external {
    locker.createCollection(address(erc721c), 'Test Collection', 'TEST', 9);
    locker.setInitialized(address(erc721c), true);

    ICollectionToken collectionErc20Token = locker.collectionToken(address(erc721c));
    uint256 erc20Denomination = collectionErc20Token.denomination();

    // Mint erc20 token for users
    address user1 = makeAddr('user1');
    address user2 = makeAddr('user2');
    uint256 mintAmount = 1 ether * 10 ** erc20Denomination;
    vm.startPrank(address(locker));
    collectionErc20Token.mint(user1, mintAmount);
    collectionErc20Token.mint(user2, mintAmount);
    vm.stopPrank();
    
    uint256 totalSupply = collectionErc20Token.totalSupply();
    assertEq(totalSupply, 2 ether * 10** erc20Denomination);
    
    // Initiate the process to shut down the collection
    vm.startPrank(user1);
    collectionErc20Token.approve(address(collectionShutdown), mintAmount);
    collectionShutdown.start(address(erc721c));
    vm.stopPrank();

    // Check shutdown params
    ICollectionShutdown.CollectionShutdownParams memory shutdownParams = collectionShutdown.collectionParams(address(erc721c));
    console.log('Total supply:         ', totalSupply);
    console.log('Actual quorumVotes:   ', shutdownParams.quorumVotes);

    // More importantly, it will prevent users from claiming ETH after the collection has been shut down and fully liquidated
    uint256 availableToClaim = 5 ether;
    // Formula for calculating the ETH amount to claim: availableClaim * claimableVotes / (quorumVotes * 100 / 50)
    uint256 user1ClaimableVotes = collectionShutdown.shutdownVoters(address(erc721c), user1);
    uint256 user1ClaimAmount = (availableToClaim * user1ClaimableVotes) / (shutdownParams.quorumVotes * 100 / 50);
    console.log('Amount of ETH available to claim:   ', availableToClaim);
    console.log('Amount of ETH that User1 can claim: ', user1ClaimAmount);
    assertLt(availableToClaim, user1ClaimAmount);
}
```

Then run `forge test --mt test_overflowQuorumVotes -vv`, and you will see the following result:

![image](https://github.com/user-attachments/assets/10b5a3d6-31fb-4ee2-8ffe-2eee05dbf571)

## Code Snippet
- [CollectionShutdown::claim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L310) function
- [CollectionShutdown::voteAndClaim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L343-L345) function

## Tool used

Manual Review

## Recommendation
Update the formula:

```diff
function claim(address _collection, address payable _claimant) public nonReentrant whenNotPaused {
    ...
-  uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
+  uint amount = params.availableClaim * claimableVotes / (uint256(params.quorumVotes) * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
    (bool sent,) = _claimant.call{value: amount}('');
    if (!sent) revert FailedToClaim();
    ...
}

function voteAndclaim(address _collection, address payable _claimant) public whenNotPaused {
    ...
-  uint amount = params.availableClaim * userVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
+  uint amount = params.availableClaim * userVotes / (uint256(params.quorumVotes) * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
    (bool sent,) = _claimant.call{value: amount}('');
    if (!sent) revert FailedToClaim();
    ...
}
```