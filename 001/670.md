Unique Inky Puppy

Medium

# Collections that were previously shutdown `cannot` be shutdown again.

## Summary
When a collection that has been previously shutdown becomes illiquid, this collection can never be removed as again as vital state variables used during shutdown are not properly erased,
leading to the pool becoming unusable.

## Vulnerability Detail
In Flayer, When a collection is illiquid and we have a disperate number of tokens spread across multiple users, a pool has the potential to become unusable. For this reason, we use this `CollectionShutdown.sol` as a method of winding down the collection and dispersing the ETH value of the remaining assets to the dust token holders.
The process begins with [start()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157) this function begins the shutdown process, holders of the collection tokens vote using [_vote()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L191-L214) once quorum is reached the contract owner uses [execute()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231) to complete the shutdown process, `execute()` sells the remaining NFTs and distribute the value to all the remaining holders.

[start()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157) 
```solidity
    function start(address _collection) public whenNotPaused {
        // Confirm that this collection is not prevented from being shutdown
        if (shutdownPrevented[_collection]) revert ShutdownPrevented();


        // Ensure that a shutdown process is not already actioned
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();


        // Get the total number of tokens still in circulation, specifying a maximum number
        // of tokens that can be present in a "dormant" collection.
        params.collectionToken = locker.collectionToken(_collection);
        uint totalSupply = params.collectionToken.totalSupply();
        if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();


        // Set our quorum vote requirement
        params.quorumVotes = uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT);


        // Notify that we are processing a shutdown
        emit CollectionShutdownStarted(_collection);


        // Cast our vote from the user
        _collectionParams[_collection] = _vote(_collection, params);
    }
```

[locker::createCollection()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L2990) is used to added new collection to the protocol, this function can be called by anyone to add any verified NFT to the protocol, this two functions above constitute a circle of addition of relevant and removal of irrelevant tokens throughout the lifespan of the protocol. Therefore it is fair to assume that previously shutdown collections can be added again once relevant.
```solidity
    function createCollection(address _collection, string calldata _name, string calldata _symbol, uint _denomination) public whenNotPaused returns (address) {
        // Ensure that our denomination is a valid value
        if (_denomination > MAX_TOKEN_DENOMINATION) revert InvalidDenomination();


        // Ensure the collection does not already have a listing token
        if (address(_collectionToken[_collection]) != address(0)) revert CollectionAlreadyExists();


        // Validate if a contract does not appear to be a valid ERC721
        if (!IERC721(_collection).supportsInterface(0x80ac58cd)) revert InvalidERC721();


        // Deploy our new ERC20 token using Clone. We use the impending ID
        // to clone in a deterministic fashion.
        ICollectionToken collectionToken_ = ICollectionToken(
            LibClone.cloneDeterministic(tokenImplementation, bytes32(_collectionCount))
        );
        _collectionToken[_collection] = collectionToken_;


        // Initialise the token with variables
        collectionToken_.initialize(_name, _symbol, _denomination);


        // Registers our collection against our implementation
        implementation.registerCollection({
            _collection: _collection,
            _collectionToken: collectionToken_
        });


        // Increment our vault counter
        unchecked { ++_collectionCount; }


        emit CollectionCreated(_collection, address(collectionToken_), _name, _symbol, _denomination, msg.sender);
        return address(collectionToken_);
    }
```

The `issue` here is that after the removal of an illiquid collection the variable [CollectionShutdownParams.shutdownVotes](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/interfaces/utils/ICollectionShutdown.sol#L43-L44) of the collection is not reset back to zero and as `CollectionShutdown.sol` uses only the collection address to reference each collection, the `re-added` collection cannot be removed again as [start()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157) will always revert [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L141).

```solidity
        if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();
```


## Impact
Re-added collections that become illiquid cannot be removed, potentially making the pool unusable and trapping users' dust amounts.


## Tool used
Manual Review

## Recommendation
`CollectionShutdown.sol` claim functions should reset the `CollectionShutdownParams` values after all the ETH under the collection has been claimed to ensure that re-added collections can be removed effectively.