Mythical Gauze Lizard

Medium

# In `createListings` function of `ProtectedListings.sol`, checkpoint is computed differently than intended by the protocol.

### Summary

In protocol, they use _createCheckpoint whenever they alter the number of collection assets held as listings or protected listings. This allows them to determine the utilisation rate when calculating the protected tax. In `createListings` of `ProtectedListings.sol`, checkpointIndex will ensure that all created protected listings will hold the same checkpointIndex registered to them. But when `collectionCheckpoints[_collection].length == 0`, it works differently.

### Root Cause

incorrect logic in [`createListings()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L117) of `ProtectedListings.sol`

### Internal pre-conditions

collectionCheckpoints[_collection].length == 0

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The user does not get the remaining funds back when executing `initializeCollection()` in `Locker.sol` due to incorrect implementation.

### PoC

The user calls `createListings()` of `ProtectedListings.sol` to create listings.
```solidity
    function createListings(CreateListing[] calldata _createListings) public nonReentrant lockerNotPaused {
        // Loop variables
        uint checkpointIndex;
        bytes32 checkpointKey;
        uint tokensIdsLength;
        uint tokensReceived;

        // Loop over the unique listing structures
        for (uint i; i < _createListings.length; ++i) {
            // Store our listing for cheaper access
            CreateListing calldata listing = _createListings[i];

            // Ensure our listing will be valid
            _validateCreateListing(listing);

            // Update our checkpoint for the collection if it has not been done yet for
            // the listing collection.
            checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection));
            assembly { checkpointIndex := tload(checkpointKey) }
            if (checkpointIndex == 0) {
                checkpointIndex = _createCheckpoint(listing.collection);
                assembly { tstore(checkpointKey, checkpointIndex) }
            }

            // Map our listings
            tokensIdsLength = listing.tokenIds.length;
            tokensReceived = _mapListings(listing, tokensIdsLength, checkpointIndex) * 10 ** locker.collectionToken(listing.collection).denomination();

            // Register our listing type
            unchecked {
                listingCount[listing.collection] += tokensIdsLength;
            }

            // Deposit the tokens into the locker and distribute ERC20 to user
            _depositNftsAndReceiveTokens(listing, tokensReceived);

            // Event fire
            emit ListingsCreated(listing.collection, listing.tokenIds, listing.listing, tokensReceived, msg.sender);
        }
    }
```
As you can see, `createListings()` calls `_createCheckpoint()` when `checkpointIndex == 0`. Next let's look at `_createCheckpoint()`.
```solidity
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        // Determine the index that will be created
        index_ = collectionCheckpoints[_collection].length;

        // Register the checkpoint that has been created
        emit CheckpointCreated(_collection, index_);

        // If this is our first checkpoint, then our logic will be different as we won't have
        // a previous checkpoint to compare against and we don't want to underflow the index.
        if (index_ == 0) {
            // Calculate the current interest rate based on utilization
            (, uint _utilizationRate) = utilizationRate(_collection);

            // We don't have a previous checkpoint to calculate against, so we initiate our
            // first checkpoint with base data.
            collectionCheckpoints[_collection].push(
                Checkpoint({
                    compoundedFactor: locker.taxCalculator().calculateCompoundedFactor({
                        _previousCompoundedFactor: 1e18,
                        _utilizationRate: _utilizationRate,
                        _timePeriod: 0
                    }),
                    timestamp: block.timestamp
                })
            );

            return index_;
        }

        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
            return index_;
        }

        // Store the new (current) checkpoint
        collectionCheckpoints[_collection].push(checkpoint);
    }
```
As you can see, when `index_ == 0`, `_createCheckpoint()` return 0. So checkpointIndex is 0.
Then another user assumes that he callls `createListings()` again. At this time because of checkpointIndex is 0, `_createCheckpoint()` will be called again. This is not the protocol's intended behavior.


### Mitigation

This should be considered if checkpointIndex is 0.