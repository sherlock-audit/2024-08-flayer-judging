Tiny Plastic Hyena

Medium

# Multiple interactions with ProtectedListings in the same block bricks listings

### Summary

In ProtectedListings.sol, _createCheckpoint() returns the index to a checkpoint that doesn't exist if it is called twice in the same block. createListings() relies on this return value to set the checkpoint of the listing, leading to listings that cannot be interacted with until another checkPoint is set in another block. This can lead to the denial of service of listings which a user should be able to interact with, as well as faulty accounting for protocol fees.

### Root Cause

_createCheckpoint only pushes a new Checkpoint onto collectionCheckpoints [if the timestamp is not the same](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L562C14-L567). Otherwise it just modifies the current Checkpoint. However, it [always returns the index as if it had pushed a new checkpoint](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L532). This causes the listings made on blocks with multiple interactions have an index that points to a checkpoint that doesn't exist.

```solidity
function _createCheckpoint(address _collection) internal returns (uint index_) {
        // Determine the index that will be created
@>      index_ = collectionCheckpoints[_collection].length; // @audit index is set at beginning as though a new one will be pushed onto the array
        ...
        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
@>      if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
@>          return index_; // @audit modifies existing checkpoint, but returns index that doesn't exist!
        }

        // Store the new (current) checkpoint
        collectionCheckpoints[_collection].push(checkpoint);
    }
```

```solidity
    function createListings(CreateListing[] calldata _createListings) public nonReentrant lockerNotPaused {
            ...
            // Update our checkpoint for the collection if it has not been done yet for
            // the listing collection.
            checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection));
            assembly { checkpointIndex := tload(checkpointKey) }
            if (checkpointIndex == 0) {
@>              checkpointIndex = _createCheckpoint(listing.collection); // @audit faulty return value of _createCheckpoint used to set index of new listing
                assembly { tstore(checkpointKey, checkpointIndex) }
            }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Any function in ProtectedListings that calls _createCheckpoint() is called
2. As long as it happens in the same block, any following createListings() call will have a nonexistent checkpoint

### Impact

This can cause a temporary inability to interact with the listing in any way. Because all interactions that change the state of a listing must access its checkpoint, any such functions will revert for trying to access an array item that doesn't exist.
Additionally, because the checkpoint is set to one in the future, this will lead to a loss of fees for the protocol because the clock doesn't start ticking until the next checkpoint is set.

### PoC

Please copy and paste the following into Listings.t.sol:
```solidity
    function test_UnreachableCheckpoint() public {
        address user = makeAddr("user");

        // Mint a mock erc to the user
        erc721a.mint(user, 5);
        erc721a.mint(user, 6);
        erc721a.mint(user, 7);

        // The user creates a protected listing for all NFTs in same block
        // Please note - it does NOT have to be a listing to trigger this effect
        // anything that interacts with _createCheckpoint() can mess up a listing in the same block
        vm.startPrank(user);        

        erc721a.setApprovalForAll(address(protectedListings), true);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(5),
                listing: IProtectedListings.ProtectedListing({
                    owner: payable(user),
                    tokenTaken: 0.5 ether,
                    checkpoint: 0
                })
            })
        });

        erc721a.setApprovalForAll(address(protectedListings), true);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(6),
                listing: IProtectedListings.ProtectedListing({
                    owner: payable(user),
                    tokenTaken: 0.5 ether,
                    checkpoint: 0
                })
            })
        });

        erc721a.setApprovalForAll(address(protectedListings), true);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(7),
                listing: IProtectedListings.ProtectedListing({
                    owner: payable(user),
                    tokenTaken: 0.5 ether,
                    checkpoint: 0
                })
            })
        });

        // check the checkpoints of the listings - first is 0, second two are 1
        assertEq(protectedListings.listings(address(erc721a), 5).checkpoint, 0);
        assertEq(protectedListings.listings(address(erc721a), 6).checkpoint, 1);
        assertEq(protectedListings.listings(address(erc721a), 7).checkpoint, 1);


        // first checkpoint is fine
        protectedListings.collectionCheckpoints(address(erc721a), 0);
        // second one reverts because it was never set, the first was just overwritten
        vm.expectRevert();
        protectedListings.collectionCheckpoints(address(erc721a), 1);

        // this bricks any functionallity on the second two listings because they are set to index 1 and 2
        // but only index 0 exists

        // the first nft deposited functions
        protectedListings.unlockPrice(address(erc721a), 5);

        // the second and third are bricked until a new checkpoint is created by someone
        vm.expectRevert();
        protectedListings.unlockPrice(address(erc721a), 6);
    }
```

### Mitigation

Consider subtracting 1 from index_ when returning in line [566](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L566). This would solve the logic issue, the CheckpointCreated event emission should also be altered so as not to publish false data.