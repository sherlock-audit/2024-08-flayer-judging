Shiny Mint Lion

Medium

# Malicious users can exploit createListings() and liquidateProtectedListing() functions in the ProtectedListings contract to replace Listings::createListings() in order to evade paying the tax fee.”

## Summary
Malicious users can exploit createListings() and liquidateProtectedListing() functions in the ProtectedListings contract to replace Listings::createListings() in order to evade paying the tax fee.”
## Vulnerability Detail
```javascript
  function liquidateProtectedListing(address _collection, uint _tokenId) public lockerNotPaused listingExists(_collection, _tokenId) {
        // Ensure that the protected listing has run out of collateral
        int collateral = getProtectedListingHealth(_collection, _tokenId);
        if (collateral >= 0) revert ListingStillHasCollateral();

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);
        uint denomination = collectionToken.denomination();

        // Keeper gets 0.05 as a reward for triggering the liquidation
        collectionToken.transfer(msg.sender, KEEPER_REWARD * 10 ** denomination);

        // Create a base listing
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;

        // Load our {ProtectedListing} for subsequent reads
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Create our liquidation {Listing} belonging to the original owner. Since we
        // have already collected our `KEEPER_REWARD`, we don't need to highlight them
        // in any way against the new listing.
@>>        _listings.createLiquidationListing(
            IListings.CreateListing({
                collection: _collection,
                tokenIds: tokenIds,
                listing: IListings.Listing({
                    owner: listing.owner,
                    created: uint40(block.timestamp),
                    duration: 4 days,
                    floorMultiple: 400
                })
            })
        );

        // Send the remaining tokens to {Locker} implementation as fees
        uint remainingCollateral = (1 ether - listing.tokenTaken - KEEPER_REWARD) * 10 ** denomination;
        if (remainingCollateral > 0) {
            IBaseImplementation implementation = locker.implementation();
            collectionToken.approve(address(implementation), remainingCollateral);
            implementation.depositFees(_collection, 0, remainingCollateral);
        }

        // Reduce the number of protected listings that we have registered
        unchecked {
            --listingCount[_collection];
        }

        // Delete our protected listing
        delete _protectedListings[_collection][_tokenId];

        // Update our checkpoint to reflect that listings have been removed
        _createCheckpoint(_collection);

        emit ProtectedListingLiquidated(_collection, _tokenId, msg.sender);
    }
```
The root cause is that the liquidateProtectedListing function internally calls _listings.createLiquidationListing(), and in createLiquidationListing(), no tax is required to be paid.
```javascript
 function createLiquidationListing(CreateListing calldata _createListing) public nonReentrant lockerNotPaused {
        // We can only call this from our {ProtectedListings} contract
        if (msg.sender != address(protectedListings)) revert CallerIsNotProtectedListings();

        // Map our Listing struct as it is referenced a few times moving forward
        Listing calldata listing = _createListing.listing;

        /// Ensure our listing will be valid
        if (listing.floorMultiple != LIQUIDATION_LISTING_FLOOR_MULTIPLE) {
            revert InvalidFloorMultiple(listing.floorMultiple, LIQUIDATION_LISTING_FLOOR_MULTIPLE);
        }

        if (listing.duration != LIQUIDATION_LISTING_DURATION) {
            revert InvalidLiquidationListingDuration(listing.duration, LIQUIDATION_LISTING_DURATION);
        }

        // Flag our listing as a liquidation
@>>        _isLiquidation[_createListing.collection][_createListing.tokenIds[0]] = true;

        // Our token will already be in the {Locker}, so we can just confirm ownership. This
        // saves us 2 transfer calls.
        if (IERC721(_createListing.collection).ownerOf(_createListing.tokenIds[0]) != address(locker)) revert LockerIsNotTokenHolder();

        // Map our listing
@>>        _mapListings(_createListing, 1);

        // Increment our listings count
        unchecked { listingCount[_createListing.collection] += 1; }

        emit ListingsCreated(_createListing.collection, _createListing.tokenIds, listing, Enums.ListingType.DUTCH, 0, 0, msg.sender);
    }

```
And since _isLiquidation is marked as true, fillListings() or createListings() also do not require any fees to be paid.

Attack scenario and steps:

	1.	The malicious user uses one NFT to call ProtectedListings::createListings() and obtains the maximum amount of 0.95e18 collectionToken.
	2.	The malicious user, using another account, calls liquidateProtectedListing() in the next block to liquidate the NFT and obtains 0.05e18 collectionToken.
	3.	The NFT is now listed in Listings without needing to pay any tax.

Remaining limitations:

	1.	The user pays a small amount of interest for the one-block duration.
	2.	The final listing has a duration of 4 days and a price of 400, which is suitable for most NFTs.
 


## Impact
The protocol lost the tax fee collection.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L429

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L178
## Tool used

Manual Review

## Recommendation
Consider applying a tax fee to the liquidation listing of NFTs.