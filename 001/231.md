Precise Lava Starfish

Medium

# Incorrect index return in _createCheckpoint

## Summary
The return index is incorrect when `checkpoint.timestamp` equals `collectionCheckpoints[_collection][index_ - 1].timestamp`.

## Vulnerability Detail
In Flayer, we use checkpoint system to track listing's timestamp and compound factor. The latest valid index should be `collectionCheckpoints[_collection].length - 1`.
When current timestamp equals last checkpoint's timestamp, we will not add one new checkpoint, just update the compound factor. The problem is that we return `index_`, and `index_` should be expected to be the next index. 
This will cause one listing's checkpoint will be recorded with one incorrect index. When we try to unlock this token, this operation may be reverted because the array is out of bond.
```solidity
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        // Determine the index that will be created
        index_ = collectionCheckpoints[_collection].length;
        ...
        if (index_ == 0) {
            ...
            return index_;
        }
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
            // @audit incorrect index here, should be index_ - 1
@>          return index_;
        }
        collectionCheckpoints[_collection].push(checkpoint);
    }

    function _mapListings(CreateListing memory _createListing, uint _tokenIds, uint _checkpointIndex) internal returns (uint tokensReceived_) {
        // Loop through our tokens
        for (uint i; i < _tokenIds; ++i) {
            // Update our request with the current checkpoint and store the listing
@>            _createListing.listing.checkpoint = _checkpointIndex;
            _protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;

            // Increase the number of tokens received by the amount requested
            tokensReceived_ += _createListing.listing.tokenTaken;

            emit ListingDebtAdjusted(_createListing.collection, _createListing.tokenIds[i], int(uint(_createListing.listing.tokenTaken)));
        }
    }
```

```solidity
    function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
        // Get the information relating to the protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Calculate the final amount using the compounded factors and principle amount
        unlockPrice_ = locker.taxCalculator().compound({
            _principle: listing.tokenTaken, // similar borrow ?
@>            _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
            _currentCheckpoint: _currentCheckpoint(_collection)
        });
    }
```

### Poc
```solidity
    function test_Poc_reserve() public {
        // Mint one ERC721 token
        address alice = vm.addr(1);
        erc721a.mint(alice, 0);
        // List
        vm.startPrank(alice);
        erc721a.approve(address(listings), 0);
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = 0;
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 120
            })
        });

        listings.createListings(_listings);
        // Reserve
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), alice, 10 ether);
        token.approve(address(listings), 10 ether);

        listings.reserve({
            _collection: address(erc721a),
            _tokenId: 0,
            _collateral: 0.3 ether
        });
        // Unlock the reserve
        token.approve(address(protectedListings), 10 ether);
        protectedListings.unlockProtectedListing(address(erc721a), 0, true);
    }
```
This test case will be reverted because of out of bound.

## Impact
We will record the wrong checkpoint for one listing. If the wrong checkpoint is still not available, we cannot unlock this tokens.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L530-L571

## Tool used

Manual Review

## Recommendation
Return `index_ - 1` if current.timestamp equals last checkpoint's timestamp.