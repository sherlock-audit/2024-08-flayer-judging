Wonderful Cinnabar Llama

High

# Protected listing fees will be calculated incorrectly.

## Summary
Protected listing fee is calculated based on utilization rate which depends on protected listing count and total supply of collection tokens. Therefore checkpoints for protected listings must be created before updating protected listing count or total supply of collection tokens. However, there are several functions which breaks this requirement.

## Vulnerability Detail
Protected listing fee is calculated based on utilization rate which depends on protected listing count and total supply of collection tokens as follows.
```solidity
    function utilizationRate(address _collection) public view virtual returns (uint listingsOfType_, uint utilizationRate_) {
        // Get the count of active listings of the specified listing type
        listingsOfType_ = listingCount[_collection];

        // If we have listings of this type then we need to calculate the percentage, otherwise
        // we will just return a zero percent value.
        if (listingsOfType_ != 0) {
            ICollectionToken collectionToken = locker.collectionToken(_collection);

            // If we have no totalSupply, then we have a zero percent utilization
            uint totalSupply = collectionToken.totalSupply();
            if (totalSupply != 0) {
                utilizationRate_ = (listingsOfType_ * 1e36 * 10 ** collectionToken.denomination()) / totalSupply;
            }
        }
    }
```
Checkpoints for protected listings must be created before updating protected listing count or total supply of collection tokens. If not, new count or total supply will be applied for the period before they are set. However, there are several functions which breaks this requirement.

For instance, the following `ProtectedListings::unlockProtectedListing()` create checkpoint in `L325` after it change the total supply in `L305` and `L308` and change listing count in `L311`.
```solidity
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
        // Ensure this is a protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Ensure the caller owns the listing
        if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

        // Ensure that the protected listing has run out of collateral
        int collateral = getProtectedListingHealth(_collection, _tokenId);
        if (collateral < 0) revert InsufficientCollateral();

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);
        uint denomination = collectionToken.denomination();
        uint96 tokenTaken = _protectedListings[_collection][_tokenId].tokenTaken;

        // Repay the loaned amount, plus a fee from lock duration
        uint fee = unlockPrice(_collection, _tokenId) * 10 ** denomination;
305     collectionToken.burnFrom(msg.sender, fee);

        // We need to burn the amount that was paid into the Listings contract
308     collectionToken.burn((1 ether - tokenTaken) * 10 ** denomination);

        // Remove our listing type
311     unchecked { --listingCount[_collection]; }

        // Delete the listing objects
        delete _protectedListings[_collection][_tokenId];

        // Transfer the listing ERC721 back to the user
        if (_withdraw) {
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else {
            canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }

        // Update our checkpoint to reflect that listings have been removed
325     _createCheckpoint(_collection);

        // Emit an event
        emit ListingUnlocked(_collection, _tokenId, fee);
    }
```
The same problems exist in `ProtectedListings::liquidateProtectedListing()`, `Listings::createListings()`, `Listings::cancelListings()` and `Listings::fillListings()`.

## Impact
Protected listing fees will be calculated incorrectly.
Loss of users' or protocol's funds.

## Code Snippet
- [Listings::createListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L130-L166)
- [Listings::cancelListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L414-L470)
- [Listings::fillListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L528-L607)
- [ProtectedListings::unlockProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287-L329)
- [ProtectedListings::liquidateProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L429-L484)

## Tool used

Manual Review

## Recommendation
Create checkpoints before changing protected listing count or total supply of collection tokens.