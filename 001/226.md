Shiny Mint Lion

High

# There is a logical error inside the ProtectedListings::adjustPosition() function, which could lead to manipulation of users’ interest.

## Summary
There is a logical error inside the ProtectedListings::adjustPosition() function, which could lead to manipulation of users’ interest.
## Vulnerability Detail
```javascript
     function adjustPosition(address _collection, uint _tokenId, int _amount) public lockerNotPaused {
        // Ensure we don't have a zero value amount
        if (_amount == 0) revert NoPositionAdjustment();

        // Load our protected listing
        ProtectedListing memory protectedListing = _protectedListings[_collection][_tokenId];

        // Make sure caller is owner
        if (protectedListing.owner != msg.sender) revert CallerIsNotOwner(protectedListing.owner);

        // Get the current debt of the position
        int debt = getProtectedListingHealth(_collection, _tokenId);

        // Calculate the absolute value of our amount
        uint absAmount = uint(_amount < 0 ? -_amount : _amount);

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Check if we are decreasing debt
        if (_amount < 0) {
            // The user should not be fully repaying the debt in this way. For this scenario,
            // the owner would instead use the `unlockProtectedListing` function.
            if (debt + int(absAmount) >= int(MAX_PROTECTED_TOKEN_AMOUNT)) revert IncorrectFunctionUse();

            // Take tokens from the caller
            collectionToken.transferFrom(
                msg.sender,
                address(this),
                absAmount * 10 ** collectionToken.denomination()
            );

            // Update the struct to reflect the new tokenTaken, protecting from overflow
@>>            _protectedListings[_collection][_tokenId].tokenTaken -= uint96(absAmount);
        }
        // Otherwise, the user is increasing their debt to take more token
        else {
            // Ensure that the user is not claiming more than the remaining collateral
            if (_amount > debt) revert InsufficientCollateral();

            // Release the token to the caller
            collectionToken.transfer(
                msg.sender,
                absAmount * 10 ** collectionToken.denomination()
            );

            // Update the struct to reflect the new tokenTaken, protecting from overflow
@>>            _protectedListings[_collection][_tokenId].tokenTaken += uint96(absAmount);
        }

        emit ListingDebtAdjusted(_collection, _tokenId, _amount);
    }

```
We can see that when adjusting the position, the principal (_protectedListings[_collection][_tokenId].tokenTaken) is directly adjusted, leading to an incorrect interest calculation on the absAmount portion during the interest computation.

```javascript
  function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
        // Get the information relating to the protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Calculate the final amount using the compounded factors and principle amount
@>>        unlockPrice_ = locker.taxCalculator().compound({
@>>            _principle: listing.tokenTaken,
            _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
            _currentCheckpoint: _currentCheckpoint(_collection)
        });
    }
```

```javascript
 function compound(
        uint _principle,
        IProtectedListings.Checkpoint memory _initialCheckpoint,
        IProtectedListings.Checkpoint memory _currentCheckpoint
    ) public pure returns (uint compoundAmount_) {
        // If the initial checkpoint timestamp is >= the current checkpoint then we just
        // return the initial principle value.
        if (_initialCheckpoint.timestamp >= _currentCheckpoint.timestamp) {
            return _principle;
        }

        uint compoundedFactor = _currentCheckpoint.compoundedFactor * 1e18 / _initialCheckpoint.compoundedFactor;
@>>        compoundAmount_ = _principle * compoundedFactor / 1e18;
    }
```
Because the final calculation of the total compoundAmount_ (which includes principal and interest) is done by multiplying the principal by a coefficient (greater than 1), this leads to issues in certain scenarios.

Example scenarios:

	1.	Reducing the position
If the position is reduced by 50, then only 50 * compoundedFactor / 1e18 needs to be calculated, which incorrectly results in no interest being applied to the reduced portion.
If the position is reduced by 100, making listing.tokenTaken = 0, then all interest becomes 0 as well, meaning no interest needs to be paid.

	2.	Increasing the position
If the position is increased by 50, then the calculation becomes 150 * compoundedFactor / 1e18, meaning the newly added 50 is incorrectly compounded as if it had accrued 1 month’s interest.


## Impact
The protocol or the user’s funds are subject to losses.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L607

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L106
## Tool used

Manual Review

## Recommendation
When adjusting the principal of a position, the effect on the accrued interest must be taken into account.