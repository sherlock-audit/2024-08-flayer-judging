Fancy Emerald Lark

High

# Users can manipulate the `unlock price` and their `ProtectedListingHealth`

## Summary
Users can manipulate the `unlock price` and its `ProtectedListingHealth` by creating listings on the same timestamp of the latest checkpoint update. The more gap between their listing creation and next checkpoint update time, the more manipulation of their listing health they can do.

Root cause: returning index instead of index -1 on one of the create checkpoint cases.
See the recommendation section for faster understanding.

## Vulnerability Detail
A checkpoint is created to keep track of the utilization rate depending upon the collection token supply and the number of listings count. So, `ProtectedListings._createCheckpoint` is used to create them, and `_createCheckpoint` is used on the following functions, `ProtectedListings.liquidateProtectedListing`, `ProtectedListings.unlockProtectedListing`, `ProtectedListings.createListings`,  &&&`Listings.cancelListings`, `Listings.fillListings`, `Listings.createListings` via `ProtectedListings.createCheckpoint` external call.

And the handling of 3 cases is done in `_createCheckpoint`, 
 1. first time when index == 0 on line 546 and new checkpoint is pushed.
 2. when the timestamp of the previous checkpoint and current checkpoint is same, so that only `compoundedFactor` is updated and no checkpoint is pushed, but next index is returned on line 570 which is supposed to be index - 1.
 3. when its a different timestamp, so a new checkpoint is pushed.


**Issue flow:**
1. at t = 50 , a checkpoint at index 99 is created by someone's `unlockProtectedListing` call, and compounding factor is 2e18
2. At same timestamp, may be different block/same, someone calls `createListings`, but here the checkpoint index returned is 100 and compound factor is updated to 2.1e18 and suer's listing holds the checkpoint index as 100 on line 181 below.
3. Then for 2 days the checkpoint is not at all updated, the length is only at 100, but the last available index is 99. And now someone update the checkpoint by any possible calls, so now there's 101 length and last available index is 100 and the compounding factor is 2.5e18 now.
4. Now that user who got allotted index 100 on create listing will point by saying that his listing was created at this chekpoint index 100  where compounding factor is 2.5e18, but actually the index should be 2 days before at 99 index (100 length) where compounding factor is 2.1e18.

So, the unlock price of teh user's listing is manipulated by (2.5e18 - 2.1e18 compounding factor), which in turn manipulates the `getProtectedListingHealth` of that listing, so the interest of these 2 days is not considered and the listing will take more time (2days extra). Also its a loss of fees to the pool and manipulation of the listing health. The more gap, the higher the impact. It is easier to block stuff this kind of transaction by bots because they can submit transactions on every block, but on first line, they will check if the latest checkpoint has same timestamp as live timestamp and they can do this whole day on base chain with near to zero gas.

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L564-L567

```solidity
ProtectedListings.sol

531:     function _createCheckpoint(address _collection) internal returns (uint index_) {
532:         // Determine the index that will be created
533:         index_ = collectionCheckpoints[_collection].length;
537: 
538:         // If this is our first checkpoint, then our logic will be different as we won't have
540:         if (index_ == 0) {
---- SNIP ----
546:             collectionCheckpoints[_collection].push(
547:                 Checkpoint({
548:                     compoundedFactor: locker.taxCalculator().calculateCompoundedFactor({
---- SNIP ----
552:                     }),
553:                     timestamp: block.timestamp
554:                 })
555:             );
556: 
557:             return index_;
558:         }

561:         Checkpoint memory checkpoint = _currentCheckpoint(_collection);
562: 
563:         // If no time has passed in our new checkpoint, then we just need to update the
564:         // utilization rate of the existing checkpoint. 
568:         if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
569:             collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
570:    >>>      return index_;
571:         }
572: 
574:         collectionCheckpoints[_collection].push(checkpoint);
575:     }

```
https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L137-L143

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L202

```solidity
94:     function createListings(CreateListing[] calldata _createListings) public nonReentrant lockerNotPaused {
---- SNIP ----
100: 
101:         // Loop over the unique listing structures
102:         for (uint i; i < _createListings.length; ++i) {
---- SNIP ----
111:             checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection));
112:             assembly { checkpointIndex := tload(checkpointKey) }
113:             if (checkpointIndex == 0) {
114:     >>>         checkpointIndex = _createCheckpoint(listing.collection);
115:                 assembly { tstore(checkpointKey, checkpointIndex) }
116:             }
117: 
119:             tokensIdsLength = listing.tokenIds.length;
120:     >>>     tokensReceived = _mapListings(listing, tokensIdsLength, checkpointIndex) * 10 ** locker.collectionToken(listing.collection).denomination();
122: 

---- SNIP ----
134:         }
135:     }


177:     function _mapListings(CreateListing memory _createListing, uint _tokenIds, uint _checkpointIndex) internal returns (uint tokensReceived_) {
178:         // Loop through our tokens
179:         for (uint i; i < _tokenIds; ++i) {
180:             // Update our request with the current checkpoint and store the listing
181:    >>>      _createListing.listing.checkpoint = _checkpointIndex;
182:             _protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;
---- SNIP ----
188:         }
189:     }

611:     function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
---- SNIP ----
616:         unlockPrice_ = locker.taxCalculator().compound({
617:             _principle: listing.tokenTaken,
618:    >>>      _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
619:             _currentCheckpoint: _currentCheckpoint(_collection)
620:         });
621:     }

498:     function getProtectedListingHealth(address _collection, uint _tokenId) public view listingExists(_collection, _tokenId) returns (int) {
501:    >>>     return int(MAX_PROTECTED_TOKEN_AMOUNT) - int(unlockPrice(_collection, _tokenId));
502:     }

```

## Impact
Loss of fees generation to the protocol (hence loss of funds). And the likelihood is very easy.
Users can manipulate the `unlock price` and its `ProtectedListingHealth` by creating listings on the same timestamp of the latest checkpoint update. The more gap between their listing creation and next checkpoint update time, the more manipulation of their listing health they can do.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L137-L143

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L202

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L564-L567

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L497-L500

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L607-L614

## Tool used

Manual Review
## Recommendation
https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L564-L567

```diff
    function _createCheckpoint(address _collection) internal returns (uint index_) {
---- SNIP ----
        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint. 
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
-           return index_;
+           return index_ - 1;
        }

        // Store the new (current) checkpoint
        collectionCheckpoints[_collection].push(checkpoint);
    }
```