Happy Wintergreen Kookaburra

High

# The `collectionShutdown` contract fails to return tokens to users who voted when `cancel` is called due to an increase in token supply causing the tokens to be stuck inside the contract

## Summary
The `collectionShutdown` contract fails to return or mint tokens to users who voted when the `cancel` function is triggered due to an increase in token supply. This results in users not being compensated for their taken tokens, causing a loss of assets as they cannot redeem their NFT without those taken tokens.

## Vulnerability Detail
Users who voted are expected to claim tokens equivalent to what was taken from them. The mapping `shutdownVoters[_collection][msg.sender]` is used to track these claims. But when the `cancel` is called, `_collectionParams[_collection]` is deleted, causing the `reclaimVote` function to revert. 

The `reclaimVote` function relies on `_collectionParams[_collection]` to fetch the collection Token address and send tokens to users using `params.collectionToken.transfer(msg.sender, userVotes)`. If the mapping entry is removed, `reclaimVote` will revert (for trying to use a zero address to send back tokens), preventing users from claiming their tokens using `reclaimVote`.

## Impact
If `_collectionParams[_collection]` is deleted, users will be unable to use `reclaimVote` to claim their tokens, as the function will revert. This could lead to lost claims and tokens being stuck.

## Code Snippet
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L191-L214
<details>
<summary>Snippet 1</summary>

```solidity
    function _vote(address _collection, CollectionShutdownParams memory params) internal returns (CollectionShutdownParams memory) {
  // Take tokens from the user and hold them in this escrow contract
  uint userVotes = params.collectionToken.balanceOf(msg.sender);
  if (userVotes == 0) revert UserHoldsNoTokens();

  // Pull our tokens in from the user
  params.collectionToken.transferFrom(msg.sender, address(this), userVotes);

  // Register the amount of votes sent as a whole, and store them against the user
  params.shutdownVotes += uint96(userVotes);

  // Register the amount of votes for the collection against the user
  unchecked { shutdownVoters[_collection][msg.sender] += userVotes; }

  emit CollectionShutdownVote(_collection, msg.sender, userVotes);

  // If we can execute, then we need to fire another event
  if (!params.canExecute && params.shutdownVotes >= params.quorumVotes) {
    params.canExecute = true;
    emit CollectionShutdownQuorumReached(_collection);
  }

  return params;
}
```

</details>

- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L390-L405
<details>
<summary>Snippet 2</summary>

```solidity
    function cancel(address _collection) public whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Check if the total supply has surpassed an amount of the initial required
        // total supply. This would indicate that a collection has grown since the
        // initial shutdown was triggered and could result in an unsuspected liquidation.
        if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
            revert InsufficientTotalSupplyToCancel();
        }

        // Remove our execution flag
        delete _collectionParams[_collection];
        emit CollectionShutdownCancelled(_collection);
    }
```

</details>

- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L356-L377
<details>
<summary>Snippet 3</summary>

```solidity
    function reclaimVote(address _collection) public whenNotPaused {
        // If the quorum has passed, then we can no longer reclaim as we are pending
        // an execution.
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (params.canExecute) revert ShutdownQuorumHasPassed();

        // Get the amount of votes that the user has cast for this collection
        uint userVotes = shutdownVoters[_collection][msg.sender];

        // If the user has not cast a vote, then we can revert early
        if (userVotes == 0) revert NoVotesPlacedYet();

        // We delete the votes that the user has attributed to the collection
        params.shutdownVotes -= uint96(userVotes);
        delete shutdownVoters[_collection][msg.sender];

        // We can now return their tokens
        params.collectionToken.transfer(msg.sender, userVotes);

        // Notify our stalkers that a vote has been reclaimed
        emit CollectionShutdownVoteReclaim(_collection, msg.sender, userVotes);
    }
```

</details>


## Tool used
Manual Review

## Recommendation
- Ensure users who voted receive tokens equivalent to what they burned by using the `shutdownVoters[_collection][msg.sender]` mapping. This will allow them to claim their 1:1 Collection Token.
- ⚠️ NOTICE: The `cancel` function deletes the `_collectionParams[_collection]` which is used when calling `reclaimVote` to send back the tokens with `params.collectionToken.transfer(msg.sender, userVotes)`
