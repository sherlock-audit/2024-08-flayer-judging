Large Mauve Parrot

High

# `ProtectedListings::adjustPosition()` doesn't adjust the taxes to pay

### Summary

The function [ProtectedListings::adjustPosition()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366) doesn't adjust the taxes to pay. This results in users paying more taxes than they should when increasing their debt and paying less taxes when decreasing their debt.

### Root Cause

The function [ProtectedListings::adjustPosition()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366) doesn't adjust the taxes to pay and simply changes the amount of debt to the new one. The function [ProtectedListings::unlockProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287), used to unlock a position by paying the debt plus taxes back, calculates taxes via [unlockPrice()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L613) which only considers the current debt (ie. `tokenTaken`) when calculating taxes:

```solidity
function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
    // Get the information relating to the protected listing
    ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

    // Calculate the final amount using the compounded factors and principle amount
    unlockPrice_ = locker.taxCalculator().compound({
        _principle: listing.tokenTaken,
        _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
        _currentCheckpoint: _currentCheckpoint(_collection)
    });
}
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Alice creates a protected listing via [ProtectedListings::createListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L117) locking an ERC721 asset to get `0.8e18` collection tokens.
2. After some days she decides to get her ERC721 asset back but she doesn't want to pay taxes.
3. Alice calls [ProtectedListings::adjustPosition()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366) decreasing her debt to `1` single wei of collection tokens.
4. Alice now calls [ProtectedListings::unlockProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287) to get her ERC721 back but she only pay taxes on the `1` wei of debt she left in her position.

### Impact

1. Users can avoid paying taxes on protected listings
2. Users can pay more taxes than they should when taking new debt using [ProtectedListings::adjustPosition()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366)

### PoC

<details>
  <summary>To copy-paste in `ProtectedListings.t.sol`:</summary>
```solidity
function test_AdjustPositionToAvoidFees() public {
    uint _tokenId = 0;
    address payable _owner = users[0];
    erc721a.mint(_owner, _tokenId);
    ICollectionToken token = locker.collectionToken(address(erc721a));

    //Alice starts with 0 collection tokens
    assertEq(token.balanceOf(address(_owner)), 0);

    //Create protected listing while taking `0.8e18` collection tokens of debt
    vm.startPrank(_owner);
    erc721a.approve(address(protectedListings), _tokenId);
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(_tokenId),
            listing: IProtectedListings.ProtectedListing({
                owner: _owner,
                tokenTaken: 0.8 ether,
                checkpoint: 0
            })
        })
    });

    //Alice borrows `0.8e18` collection tokens
    assertEq(token.balanceOf(address(_owner)), 0.8 ether);

    //Two days passes and interest accumulates
    vm.warp(block.timestamp + 2 days);

    //Ajudst the position by removing `0.8e18 - 1` debt, leaving exactly `1` wei of debt
    token.approve(address(protectedListings), type(uint256).max);
    protectedListings.adjustPosition(address(erc721a), _tokenId, int(-1 * (0.8 ether - 1)));

    //Alice has `1` wei left
    assertEq(token.balanceOf(address(_owner)), 1);

    //Unlock the position and avoid paying taxes
    protectedListings.unlockProtectedListing(address(erc721a), _tokenId, true);
    vm.stopPrank();

    //Alice paid no interest for 2 days of borrowing
    assertEq(token.balanceOf(address(_owner)), 0);
}
```

</details>

### Mitigation

Adjusting a position via [ProtectedListings::adjustPosition()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L366) should be treated like an user unlocking and then creating a new listing. This way taxes are properly paid.