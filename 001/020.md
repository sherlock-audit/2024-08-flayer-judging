Lone Coconut Cat

Medium

# Cancelled Sunset Collections Results In Stuck Tokens

## Summary

When an attempt to sunset a collection is cancelled, users who voted to sunset the collection cannot reclaim their tokens due to numeric underflow.

## Vulnerability Detail

When a user votes to sunset a collection, their token balance is sent to the [`CollectionShutdown`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol) contract and their votes are tracked in the `shutdownVoters` mapping:

```solidity
function _vote(address _collection, CollectionShutdownParams memory params) internal returns (CollectionShutdownParams memory) {
    // Take tokens from the user and hold them in this escrow contract
    uint userVotes = params.collectionToken.balanceOf(msg.sender);
    if (userVotes == 0) revert UserHoldsNoTokens();

    // Pull our tokens in from the user
    params.collectionToken.transferFrom(msg.sender, address(this), userVotes); /// @audit user sends entire collectionToken balance

    // Register the amount of votes sent as a whole, and store them against the user
    params.shutdownVotes += uint96(userVotes); /// @audit shutdownVotes is increased

    // Register the amount of votes for the collection against the user
    unchecked { shutdownVoters[_collection][msg.sender] += userVotes; } /// @audit user's specific balance is tracked

    emit CollectionShutdownVote(_collection, msg.sender, userVotes);

    // If we can execute, then we need to fire another event
    if (!params.canExecute && params.shutdownVotes >= params.quorumVotes) {
        params.canExecute = true;
        emit CollectionShutdownQuorumReached(_collection);
    }

    return params;
}
```

**Notice that the total** `shutdownVotes` **is tracked against the** `CollectionShutdownParams` struct:

```solidity
// Register the amount of votes sent as a whole, and store them against the user
params.shutdownVotes += uint96(userVotes);
```

Next, remember any actor may permissionlessly cancel the vote to sunset the collection if the vote has met quorum but the circulating supply of the collection has increased since the vote was started:

```solidity
function cancel(address _collection) public whenNotPaused {
    // Ensure that the vote count has reached quorum
    CollectionShutdownParams memory params = _collectionParams[_collection];
    if (!params.canExecute) revert ShutdownNotReachedQuorum(); /// @audit ensure vote has met quorum

    // Check if the total supply has surpassed an amount of the initial required
    // total supply. This would indicate that a collection has grown since the
    // initial shutdown was triggered and could result in an unsuspected liquidation.
    if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) { /// @audit if the supply has increased, allow cancellation to take place
        revert InsufficientTotalSupplyToCancel();
    }

    // Remove our execution flag
    delete _collectionParams[_collection]; /// @audit delete the collectionParams to reset the vote
    emit CollectionShutdownCancelled(_collection);
}
```

Crucially, we see that **cancelling a collection sunset will** `delete` **the corresponding** `_collectionParams`:

```solidity
// Remove our execution flag
delete _collectionParams[_collection];
```

Invariably, this hinders the ability for owners to reclaim their tokens, since:

```solidity
function reclaimVote(address _collection) public whenNotPaused {
    // If the quorum has passed, then we can no longer reclaim as we are pending
    // an execution.
    CollectionShutdownParams storage params = _collectionParams[_collection];
    if (params.canExecute) revert ShutdownQuorumHasPassed(); /// @audit cancellation deleted `params`, `canExecute` is `false`

    // Get the amount of votes that the user has cast for this collection
    uint userVotes = shutdownVoters[_collection][msg.sender]; /// @audit user has nonzero votes

    // If the user has not cast a vote, then we can revert early
    if (userVotes == 0) revert NoVotesPlacedYet();

    // We delete the votes that the user has attributed to the collection
    params.shutdownVotes -= uint96(userVotes); /// @audit `params` was deleted, so `shutdownVotes` is zero resulting in underflow
    delete shutdownVoters[_collection][msg.sender];

    // We can now return their tokens
    params.collectionToken.transfer(msg.sender, userVotes);

    // Notify our stalkers that a vote has been reclaimed
    emit CollectionShutdownVoteReclaim(_collection, msg.sender, userVotes);
}
```

Cancelling a sunset vote deletes the corresponding `CollectionShutdownParams` and resets the `shutdownVotes`, so an attempt to decrease this post-shutdown will result in numeric underflow.

Consequently, the voter may not withdraw their tokens.

> [!NOTE]
>
> Technically, a user _could_ withdraw their tokens during a second round of voting. If another user were to vote in favour of sunsetting, with a token balance greater than or equal to the user with stuck funds, the user with stuck funds could reclaim their balance.
>
> However, **this would merely misappropriate the funds**, since the new depositor would then be unable to claim their own balance due to underflow.

## Impact

Users who campaigned unsuccessfully to sunset a [`CollectionToken`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/CollectionToken.sol#L109C5-L111C6) are unable to reclaim their tokens.

## Code Snippet

```solidity
function cancel(address _collection) public whenNotPaused {
    // Ensure that the vote count has reached quorum
    CollectionShutdownParams memory params = _collectionParams[_collection];
    if (!params.canExecute) revert ShutdownNotReachedQuorum();

    // Check if the total supply has surpassed an amount of the initial required
    // total supply. This would indicate that a collection has grown since the
    // initial shutdown was triggered and could result in an unsuspected liquidation.
    if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
        revert InsufficientTotalSupplyToCancel();
    }

    // Remove our execution flag
    delete _collectionParams[_collection];
    emit CollectionShutdownCancelled(_collection);
}
```

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L390C5-L405C6

## Tool used

Manual Review

## Recommendation

Do not persist the `shutdownVotes` for the collection on the `CollectionShutdownParams` struct.
