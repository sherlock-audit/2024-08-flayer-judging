Mini Bamboo Orangutan

High

# In ProtectedListings, listing.checkpoint is greater by 1 than it should be in case the _collection has already been checkpointed at least once during the same block.timestamp, leading to incorect health factor and unlockPrice calculations

## Summary
The problem stems from a wrong `index_` being returned in the `_createCheckpoint` function in the circumstances when:
- `index_ = collectionCheckpoints[_collection].length` > 0;
- `checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp`.

### The issue has 3 outcoming impacts.

First onto the logic of the `_createCheckpoint` function:
1. When there're currently no `collectionCheckpoints[_collection]` at the moment, a new checkpoint is constructed and pushed, storing the new checkpoint at the `collectionCheckpoints[_collection][0]`;
2. When there's a request to checkpoint, and the `collectionCheckpoints[_collection][collectionCheckpoints[_collection].length - 1].timestamp` is equal to the current `block.timestamp`, no items are pushed to the `collectionCheckpoints` array, however the `compoundedFactor` is updated accordingly;
3. And, finally, when the `collectionCheckpoints[_collection][collectionCheckpoints[_collection].length - 1].timestamp` is not equal to the current timestamp, we're going to populate the `collectionCheckpoints[_collection]` array by pushing a corresponding new checkpoint item to it. That automatically increases the `collectionCheckpoints[_collection].length`.

## Vulnerability Detail
The problem is that in the `_addCheckpoint`'s case №2, *the `index_` returned from the function is not the index that contains the updated `compoundedFactor` within the array items, or, specifically, it is not the real "index" in terms of array count that was updated,* **but the "real index + 1"**, which is evident from this snippet:
```solidity
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        // Determine the index that will be created
        index_ = collectionCheckpoints[_collection].length;

        // ...CLIP...

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
            return index_; // it should actually be index_ - 1 here
        }

       // ...CLIP...
    }
```

You can see from this snippet that what the `_createCheckpoint` function returns as "index", in the aforementioned case №2 is the `collectionCheckpoints.length`.

---

In the case №1, the `index_` returned is in fact correct because in that case at the moment the `collectionCheckpoints[_collection].length` is `0`:
```solidity
        // Determine the index that will be created
        index_ = collectionCheckpoints[_collection].length;

        // Register the checkpoint that has been created
        emit CheckpointCreated(_collection, index_);

        // If this is our first checkpoint, then our logic will be different as we won't have
        // a previous checkpoint to compare against and we don't want to underflow the index.
        if (index_ == 0) {
            // Calculate the current interest rate based on utilization
            (, uint _utilizationRate) = utilizationRate(_collection);



            // We don't have a previous checkpoint to calculate against, so we initiate our
            // first checkpoint with base data.
            collectionCheckpoints[_collection].push(
                Checkpoint({
                    compoundedFactor: locker.taxCalculator().calculateCompoundedFactor({
                        _previousCompoundedFactor: 1e18,
                        _utilizationRate: _utilizationRate,
                        _timePeriod: 0
                    }),
                    timestamp: block.timestamp
                })
            );



            return index_;
        }
```

---

Likewise, in the case №3, a proper `index_` is returned, because the initial `index_` is based on the current `_collectionCheckpoints[_collection].length`, and as new item is `.push`'ed to that array, the real new `length` increases by 1 at the end, and the `index_` will correspond to the **last real AVAILABLE "index"** of that array, **not the length of that array**.

## Impact
**It is important that `_createCheckpoint` returns the real correct index of the item that was updated, because that `index_` is later used to track the parameter of `unlockPrice` via getting stored as `_createListing.checkpoint` in the `_mapListings` function:**
```solidity
    function _mapListings(CreateListing memory _createListing, uint _tokenIds, uint _checkpointIndex) internal returns (uint tokensReceived_) {
        // Loop through our tokens
        for (uint i; i < _tokenIds; ++i) {
            // Update our request with the current checkpoint and store the listing
            _createListing.listing.checkpoint = _checkpointIndex;
            _protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;

            // Increase the number of tokens received by the amount requested
            tokensReceived_ += _createListing.listing.tokenTaken;

            emit ListingDebtAdjusted(_createListing.collection, _createListing.tokenIds[i], int(uint(_createListing.listing.tokenTaken)));
        }
    }
```

The `_mapListings` function is called from `createListings`:
```solidity
        // Loop over the unique listing structures
        for (uint i; i < _createListings.length; ++i) {
            // Store our listing for cheaper access
            CreateListing calldata listing = _createListings[i];

            // Ensure our listing will be valid
            _validateCreateListing(listing);

            // Update our checkpoint for the collection if it has not been done yet for
            // the listing collection.
            checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection));
            assembly { checkpointIndex := tload(checkpointKey) }
            if (checkpointIndex == 0) {
                checkpointIndex = _createCheckpoint(listing.collection);
                assembly { tstore(checkpointKey, checkpointIndex) }
            }


            // Map our listings
            tokensIdsLength = listing.tokenIds.length;
            tokensReceived = _mapListings(listing, tokensIdsLength, checkpointIndex) * 10 ** locker.collectionToken(listing.collection).denomination();
}
}
```


Please note that transient storage is used for optimizing gas and preserving the `checkpointIndex` for same-collection `tokenIds`, but it is absolutely possible that multiple calls that trigger `createCheckpoint` (there're **MANY OF THEM!**) are put in the same `block`, **ESPECIALLY "FOR FUTURE PROTOCOL INTEGRATIONS"**, which will lead to that the `listing.checkpoint` is the offset by ***+ 1*** `checkpointIndex`.

## Code Snippet
I've already explained above where the wrong `index_` is stored (as the `listing.checkpoint` field).

`unlockPrice` is detrimental for the `unlockProtectedListing` function:
```solidity
   function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
        // Ensure this is a protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Ensure the caller owns the listing
        if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

        // Ensure that the protected listing has run out of collateral
        int collateral = getProtectedListingHealth(_collection, _tokenId);
        if (collateral < 0) revert InsufficientCollateral();

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);
        uint denomination = collectionToken.denomination();
        uint96 tokenTaken = _protectedListings[_collection][_tokenId].tokenTaken;

        // Repay the loaned amount, plus a fee from lock duration
        uint fee = unlockPrice(_collection, _tokenId) * 10 ** denomination; // it is used here
        collectionToken.burnFrom(msg.sender, fee);
       // ...CLIP...
}
```

Unlock price is calculated as:
```solidity
    /**
     * Calculates the amount of tax that would need to be paid against a protected listings. This
     * is returned in terms of the underlying ERC20 token, but with a consistent 18 decimal accuracy.
     *
     * @param _collection The collection address of the listing
     * @param _tokenId The tokenId of the listing
     *
     * @return unlockPrice_ The price required to unlock, in 1e18
     */
    function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
        // Get the information relating to the protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Calculate the final amount using the compounded factors and principle amount
        unlockPrice_ = locker.taxCalculator().compound({
            _principle: listing.tokenTaken,
            _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint], // the REAL listing.checkpoint was listing.checkpoint - 1

            _currentCheckpoint: _currentCheckpoint(_collection)
        });
    }
```

### Here, the real `listing.checkpoint` is in fact `listing.checkpoint - 1`, you can see the discrepancy: `_initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint], // the REAL listing.checkpoint was listing.checkpoint - 1`.

---

*But the `ProtectedListings` contract passes the next checkpoint after the real initial checkpoint.*

---

`compound` is based on the following properties:
```solidity
    function compound(
        uint _principle,
        IProtectedListings.Checkpoint memory _initialCheckpoint,
        IProtectedListings.Checkpoint memory _currentCheckpoint
    ) public pure returns (uint compoundAmount_) {
        // If the initial checkpoint timestamp is >= the current checkpoint then we just
        // return the initial principle value.
        if (_initialCheckpoint.timestamp >= _currentCheckpoint.timestamp) {
            return _principle;
        }

        uint compoundedFactor = _currentCheckpoint.compoundedFactor * 1e18 / _initialCheckpoint.compoundedFactor;
        compoundAmount_ = _principle * compoundedFactor / 1e18;
    }
```

***The "_initialCheckpoint" is not the real "_initialCheckpoint", but "_initialCheckpointIndex + 1".***

#### This, respectively, makes the `_initialCheckpoint.compoundedFactor` and `_initialCheckpoint.timestamp` variables absolutely inaccurate.

---

## Additional impact
If `unlockProtectedListing` is called right in the next block as the first transaction that gets executed in the Flayer protocol related transactions bundle, the `_initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],` line will always throw an "Out of bounds" array revert.

That is because, if, for instance, the `collectionCheckpoints[_collection]` length is 9, and the last time the `_collection` was `createCheckpoint`'ed **(assume the checkpoint logic's case was our problematic №2),** *the `index_` returned from `_createCheckpoint`* and stored as `listing.checkpoint` in `createListings` *was the INCORRECT `9`* (whilst it should've been `8` in that case), then accessing an index of `9` in an array with total length of `9` will of course not pass the EVM overflow checks.

---

So, besides causing problems in future `unlockPrice` calculations **(which happens in any future block no matter of conditions)**, there's also a serious impact of the NEXT BLOCK's `unlockProtectedListings` complete block-wise DoS.

**Please note:** *This block-wise DoS additionally causes losses for the `msg.sender` calling `unlockProtectedListing` function, because the call might be time-sensitive in some cases, as the `block.timestamp` is used for determining the `TaxCalculator.compound(...)` function's `_currentCheckpoint().timestamp`.*

**This additionally affects how much `fee` the `msg.sender` will pay (`collectionToken.burnFrom(msg.sender, fee)`).**

---

#### Moreover, there's even a 3rd impact: `getProtectedListingHealth` also uses the `unlockPrice` which will be invalid in the case of an original "`createListings` --> `_createCheckpoint` -> №2 switch-case" problem:
```solidity
    function getProtectedListingHealth(address _collection, uint _tokenId) public view listingExists(_collection, _tokenId) returns (int) {
        // So we start at a whole token, minus: the keeper fee, the amount of tokens borrowed
        // and the amount of collateral based on the protected tax.
        return int(MAX_PROTECTED_TOKEN_AMOUNT) - int(unlockPrice(_collection, _tokenId));
    }
```

## Tool used
Manual review.

## Recommendation
For the case №2 in `_createCheckpoint`, return `index_ - 1`:
```diff
        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
-           return index_;
+           return index_ - 1;
        }
```

## Additional references to the codebase:
1. https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L562-L567
2. https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L202
3. https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L614
4. https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/TaxCalculator.sol#L113
5. https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/TaxCalculator.sol#L117