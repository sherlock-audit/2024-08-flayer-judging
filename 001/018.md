Modern Metal Butterfly

High

# Even after execute has been called, an attacker can reset canExecute to true and cancle to delete the collection which will DOS users claims.

## Summary
Missing restriction in [```CollectionShutdown::vote```](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L175-L214) function can cause complete DOS of users since it doesn't check that the given collection is already executed. 

## Vulnerability Detail
In ```vote``` function there is a missing check to ensure that the given collection is not yet executed.

Because of this, an attacker can call ```vote``` even after shutdown has been executed, which will set ```params.canExecute = true;``` and can now call cancle to delete the collection's data.
```javascript
        if (!params.canExecute && params.shutdownVotes >= params.quorumVotes) {
            params.canExecute = true;
            emit CollectionShutdownQuorumReached(_collection);
        }
```

```javascript
    function cancel(address _collection) public whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Check if the total supply has surpassed an amount of the initial required
        // total supply. This would indicate that a collection has grown since the
        // initial shutdown was triggered and could result in an unsuspected liquidation.
        if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
            revert InsufficientTotalSupplyToCancel();
        }

        // Remove our execution flag
        delete _collectionParams[_collection];
        emit CollectionShutdownCancelled(_collection);
    }
```

Example:
* Bob is an malicious user, who has just 0.1 of the collection token(0.1 as in e18 decimals, could also be just 1 i.e. dust),
* The other holders vote to shutdown the collection
* quorum is reached and execute is triggered, this ```execute``` function will reset the `params.canExecute` to [`false`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L273). 
* Now cancle cannot be called since it checks [`if (!params.canExecute) revert ShutdownNotReachedQuorum();`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L393)
* But, Bob can easily bypass this by calling `vote` with his 0.1 collection tokens, which will reset `params.canExecute` to [`true`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L208C1-L209C38)
* Since `params.canExecute` is reset to `true` Bob can now call `cancle` which will delete the collection params [`delete _collectionParams[_collection];`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L403)

Now if the users try to call claim, it will revert because they are calling an empty(deleted) collection data;
```javascript
    function claim(address _collection, address payable _claimant) public nonReentrant whenNotPaused {
        // Ensure our user has tokens to claim
        uint claimableVotes = shutdownVoters[_collection][_claimant];
        if (claimableVotes == 0) revert NoTokensAvailableToClaim();

        // Ensure that we have moved token IDs to the pool
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.sweeperPool == address(0)) revert ShutdownNotExecuted();

        // Ensure that all NFTs have sold from our Sudoswap pool
        if (!collectionLiquidationComplete(_collection)) revert NotAllTokensSold();

        // We can now delete our sweeper pool tokenIds
        if (params.sweeperPoolTokenIds.length != 0) {
            delete _collectionParams[_collection].sweeperPoolTokenIds;
        }

        // Burn the tokens from our supply
        params.collectionToken.burn(claimableVotes);

        // Set our available tokens to claim to zero
        delete shutdownVoters[_collection][_claimant];

        // Get the number of votes from the claimant and the total supply and determine from that the percentage
        // of the available funds that they are able to claim.
        uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
        (bool sent,) = _claimant.call{value: amount}('');
        if (!sent) revert FailedToClaim();

        emit CollectionShutdownClaim(_collection, _claimant, claimableVotes, amount);
    }
```


## Impact
Since the collection will be deleted, ```delete _collectionParams[_collection];```, users will not be able to call claim and the eth from selling the nfts will be locked forever in the contract.

## Code Snippet
```javascript
    function vote(address _collection) public nonReentrant whenNotPaused {
        // Ensure that we are within the shutdown window
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.quorumVotes == 0) revert ShutdownProccessNotStarted();

        _collectionParams[_collection] = _vote(_collection, params);
    }
```
```javascript
        params.canExecute = false;
        emit CollectionShutdownExecuted(_collection, pool, _tokenIds);
```

## Tool used
Manual Review

## Recommendation
introduce a new mapping to keep track of executed collections like;
```mapping(address _collection => bool) public isCollectionExecuted;```

And then use this to keep track of executed collections
```diff
function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused { 
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum(); 

        // Ensure we have specified token IDs
        uint _tokenIdsLength = _tokenIds.length; 
        if (_tokenIdsLength == 0) revert NoNFTsSupplied();
        ...
        ...

+       isCollectionExecuted[_collection] = true;
}
```

Now make sure that the given collection is not yet executed in the `vote` function;
```diff
    function vote(address _collection) public nonReentrant whenNotPaused {
        // Ensure that we are within the shutdown window
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.quorumVotes == 0) revert ShutdownProccessNotStarted();

+       if (isCollectionExecuted) revert CollectionAlreadyExecutedUse_voteAndClaim()

        _collectionParams[_collection] = _vote(_collection, params);
    }
```