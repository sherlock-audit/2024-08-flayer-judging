Skinny Pear Crane

High

# Canceling a collection shutdown can cause loss of userVotes of previous users

## Summary
Issue High: Canceling a collection shutdown can cause loss of userVotes of previous users

## Vulnerability Detail

In the contract `CollectionShutdown.sol`, anyone can cancel a collection shutdown by invoking the function `cancel` when a collection shutdown is not reached Quorum. During the cancelation, the param `_collectionParams[_collection]` will be deleted which will set the `_collectionParams[_collection].shutdownVotes` to be 0.

[CollectionShutdown](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L390-L405)

```solidity
    function cancel(address _collection) public whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Check if the total supply has surpassed an amount of the initial required
        // total supply. This would indicate that a collection has grown since the
        // initial shutdown was triggered and could result in an unsuspected liquidation.
        if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
            revert InsufficientTotalSupplyToCancel();
        }

        // Remove our execution flag
        delete _collectionParams[_collection];
        emit CollectionShutdownCancelled(_collection);
    }
```

However, after the cancelation of the collection shutdown, the previous users who voted this shutdown can not reclaim their votes and get collection tokens back since the calculation of `params.shutdownVotes -= uint96(userVotes)` will revert due to underflow. That the param `params.shutdownVotes` has already been set to be 0 in the cancelation. This will cause loss to previous voters, since they can not reclaim their votes and lose the collection tokens.

[CollectionShutdown](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L356-L377)

```solidity
    function reclaimVote(address _collection) public whenNotPaused {
        // If the quorum has passed, then we can no longer reclaim as we are pending
        // an execution.
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (params.canExecute) revert ShutdownQuorumHasPassed();

        // Get the amount of votes that the user has cast for this collection
        uint userVotes = shutdownVoters[_collection][msg.sender];

        // If the user has not cast a vote, then we can revert early
        if (userVotes == 0) revert NoVotesPlacedYet();

        // We delete the votes that the user has attributed to the collection
        params.shutdownVotes -= uint96(userVotes);
        delete shutdownVoters[_collection][msg.sender];

        // We can now return their tokens
        params.collectionToken.transfer(msg.sender, userVotes);

        // Notify our stalkers that a vote has been reclaimed
        emit CollectionShutdownVoteReclaim(_collection, msg.sender, userVotes);
    }
```


## Proof of Concept


1.set MAINNET_RPC_URL with your key in `flayer/.env`

`MAINNET_RPC_URL=https://mainnet.infura.io/v3/{YOUR_KEY_HERE} `

2.add this poc in `flayer/test/utils/CollectionShutdown.t.sol`

```solidity
     function test_CanReclaimVote_bug_poc() public  {
    
        vm.startPrank(address(locker));
        collectionToken.mint(address(1), 1 ether);
        vm.stopPrank();

        // Start our vote from address(1)
        vm.startPrank(address(1));
        collectionToken.approve(address(collectionShutdown), 1 ether);
        collectionShutdown.start(address(erc721b));
        vm.stopPrank();

        vm.startPrank(address(locker));
        collectionToken.mint(address(2), 10 ether);
        vm.stopPrank();

        vm.startPrank(address(2));
        //cancel the shutdown by address(2)
        collectionShutdown.cancel(address(erc721b));
        vm.stopPrank();

        // We can now reclaim our vote
        vm.prank(address(1));
        vm.expectRevert();
        //revert due to underflow 
        collectionShutdown.reclaimVote(address(erc721b));

    }
```

3.run this test with cmd `forge test -vvv --match-test test_CanReclaimVote_bug_poc`


## Impact

This will cause loss to previous voters, since they can not reclaim their votes and lose the collection tokens.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L390-L405

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L356-L377


## Tool used
Manual Review

## Recommendation
Do not simply delete `_collectionParams[_collection]` when canceling a collection shutdown.