Precise Lava Starfish

Medium

# Missing update `_isLiquidation` in relist

## Summary
When we relist one liquidated list, the `_isLiquidation` is not reset.

## Vulnerability Detail
When one protected list is not healthy, this protected list will be liquidated and one normal listing will be created and traders can get this token via trading. When we create the liquidation listing, `_isLiquidation` will be set to true. Because we don't add some tax fee when we create the liquidation listing. It means there is no refund fee to the initial owner when someone buys this liquidation token.
The problem is that when users buy this liquidation token via `relist`, we miss reset `_isLiquidation`.
For example:
1. Alice's protected position is liquidated.
2. Bob relist Alice's listing.
3. Bob can cancel his relist. And Bob gets this ERC721 token, and `_isLiquidation` for this token is still true.
4. Bob try to list this token, bob has to pay some tax when bob creates the list.
5. Bob will not get the expected refund when we fulfill this list, because the system thinks this token is one liquidation list.
```solidity
    function createLiquidationListing(CreateListing calldata _createListing) public nonReentrant lockerNotPaused {
       ...
        // Flag our listing as a liquidation
        _isLiquidation[_createListing.collection][_createListing.tokenIds[0]] = true;
       ...
}
```
```solidity
    function _fillListing(address _collection, address _collectionToken, uint _tokenId) private {
        ...
        // If the `owner` is still a zero-address, then it is a Floor item and should
        // not process any additional listing related functions.
        if (_listings[_collection][_tokenId].owner != address(0)) {
            // Check if there is collateral on the listing, as this we bypass fees and refunds
            if (!_isLiquidation[_collection][_tokenId]) {
                // Find the amount of prepaid tax from current timestamp to prepaid timestamp
                // and refund unused gas to the user.
                // FILL FEE is for the LP. Who should pay for this FILL FEE, the fee is tax fee, the list creator has already paid for this.
                (uint fee, uint refund) = _resolveListingTax(_listings[_collection][_tokenId], _collection, false);
                emit ListingFeeCaptured(_collection, _tokenId, fee);

                assembly {
                    tstore(FILL_FEE, add(tload(FILL_FEE), fee))
                    tstore(FILL_REFUND, add(tload(FILL_REFUND), refund))
                }
            } else {
                delete _isLiquidation[_collection][_tokenId];
            }
}
```
### Poc
```solidity
    function test_Poc_Liquidate() public {
        // Set up some test users
        address payable userA = payable(address(1)); // Initial listing creator
        address payable userB = payable(address(2)); // Keeper / liquidator
        address payable userC = payable(address(3)); // Relisting user

        // Mint the initial token to UserA
        erc721a.mint(userA, 0);

        // Store our {CollectionToken} for quick checks
        ICollectionToken token = locker.collectionToken(address(erc721a));

        // As our {Locker} and {Listings} supply may already be altered, we get their starting
        // balances before further calculation.
        uint lockerBalance = token.balanceOf(address(locker));
        uint listingsBalance = token.balanceOf(address(listings));
        uint protectedListingsBalance = token.balanceOf(address(protectedListings));
        uint uniswapBalance = token.balanceOf(address(uniswapImplementation));

        // Give each of our users a starting balance of 5 tokens so that we can pay
        // taxes and cover costs without additional transfers.
        deal(address(token), userA, 5 ether);
        deal(address(token), userB, 5 ether);
        deal(address(token), userC, 5 ether);

        // Confirm starting balances
        // We start with 15 ERC20 tokens and 1 ERC721 token. This means that we should
        // always hold a consisten 16 tokens.
        // [User A] Create a protected listing that liquididates
        vm.startPrank(userA);
        erc721a.approve(address(protectedListings), 0);
        
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(0),
                listing: IProtectedListings.ProtectedListing({
                    owner: userA,
                    tokenTaken: 0.95 ether,
                    checkpoint: 0
                })
            })
        });
        vm.stopPrank();

        // Skip some time to liquidate
        vm.warp(block.timestamp + 52 weeks);

        // [User B] Liquidate the listing
        vm.prank(userB);
        protectedListings.liquidateProtectedListing(address(erc721a), 0);

        // UserA will not have paid tax on their protected listing as this is paid to unlock the
        // protected asset. They will have, however, also received the protectedTokenTaken
        // amount. UserB will have received their `KEEPER_REWARD` for liquidating the expired
        // protected listing. The {Locker} currently only holds the ERC721.
        vm.startPrank(userC);
        token.approve(address(listings), 100 ether);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(0),
                listing: IListings.Listing({
                    owner: userC,
                    created: uint40(block.timestamp),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: 10_00
                })
            }),
            _payTaxWithEscrow: false
        });
        listings.cancelListings(address(erc721a), _tokenIdToArray(0), true);
        erc721a.approve(address(listings), 0);
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(0),
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 120
            })
        });
        listings.createListings(_listings);
        vm.stopPrank();
    }
```
## Impact
`_isLiquidation` may be true even if this ERC721 token is selled out.  When this ERC721 is listed again, the lister may lose tax refund because the system thinks this is one liquidation list.
 
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672

## Tool used

Manual Review

## Recommendation
When we relist one listing, delete `_isLiquidation`