Massive Emerald Python

High

# When a protected listing is liquidated, the owner of the newly created listing will be the current owner of the protect listing, not the original owner of the NFT listing

### Summary
The protocol allows user to reserve NFTs that they can't fully purchase via the [reserve()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759) function. The users who reserve the NFT has to pay the difference between the NFT listing price and the floor price, plus an initial collateral, as well as an interest rate which accrues over time. If the user fails to pay his interest rate or unlock his NFT on time, the NFT will be liquidated and a Dutch auction will be created for it. The NFT is liquidated via the [liquidateProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L429-L484) function:
```solidity
    function liquidateProtectedListing(address _collection, uint _tokenId) public lockerNotPaused listingExists(_collection, _tokenId) {
        // Ensure that the protected listing has run out of collateral
        int collateral = getProtectedListingHealth(_collection, _tokenId);
        if (collateral >= 0) revert ListingStillHasCollateral();

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);
        uint denomination = collectionToken.denomination();

        // Keeper gets 0.05 as a reward for triggering the liquidation
        collectionToken.transfer(msg.sender, KEEPER_REWARD * 10 ** denomination);

        // Create a base listing
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;

        // Load our {ProtectedListing} for subsequent reads
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Create our liquidation {Listing} belonging to the original owner. Since we
        // have already collected our `KEEPER_REWARD`, we don't need to highlight them
        // in any way against the new listing.
        _listings.createLiquidationListing(
            IListings.CreateListing({
                collection: _collection,
                tokenIds: tokenIds,
                listing: IListings.Listing({
                    owner: listing.owner,
                    created: uint40(block.timestamp),
                    duration: 4 days,
                    floorMultiple: 400
                })
            })
        );
        ...
    }
```
As can be seen from the above code snippet the owner for the listing will be set as the current owner of the protectedListing, instead of the original owner that created the initial listing for the NFT. We can see from the comments in the function the owner of the listing that will be created in the ``Listings.sol`` contract should be the initial owner of the listing, that a user reserved an NFT from. Thus if a protected listing gets liquidated the owner of the newly created listing in the ``Listings.sol`` contract shouldn't be the owner of the protected listing that is being liquidated. Otherwise the person who gets liquidated will become the owner of the NFT. 

### Root Cause
In the [liquidateProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L429-L484) function, the owner of the newly created listing in the ``Listings.sol`` contract will be the owner of the protected listing in the ``ProtectedLisitngs.sol`` contract:
```solidity
        _listings.createLiquidationListing(
            IListings.CreateListing({
                collection: _collection,
                tokenIds: tokenIds,
                listing: IListings.Listing({
                    owner: listing.owner,
                    created: uint40(block.timestamp),
                    duration: 4 days,
                    floorMultiple: 400
                })
            })
        );
```

### Internal pre-conditions
1. Alice creates a listing for her NFT
2. Bob really likes that NFT, but can't afford it right now, so he decides to reserve it via the [reserve()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759) function
3. Bob fails to pay his interest rates and unlock his NFT on time, and thus gets liquidated 


### External pre-conditions

_No response_

### Attack Path
1. Alice creates a listing for her NFT, with a floorMultiplier of **120**, the token denomination for the collection is 1
2. Bob really likes the NFT, but can buy it at the moment, so he pays Alice the difference between the listing price which is **(1e18 * 120) / 100 = 1.2e18** and the floor price which is **1e18** **1.2e18 - 1e18 = 0.2e18**, plus some collateral lets say **0.3e18**, now Bob has a protected listing for the NFT he reserved and has to pay interest rate until he fully unlocks it. 
3. Some time passes and bob hasn't paid any of the interest he owns, and now he can be liquidated. However when the protectedListing of bob gets liquidated, bob will be the owner of the newly created NFT listing in the ``Listings.sol`` contract. This goes against the logic of liquidations, and the comments in the liquidation function.

### Impact
When a user reserves and NFT, and he fails to repay the interest rate he owns, and gets liquidated the newly created NFT listing in the ``Listings.sol`` contract will be owned by the user who got liquidated. Thus if the NFT is sold the liquidated user will receive the amount the NFT was sold for. This goes against the logic of liquidations, and the comments in the liquidate function. If the correct owner of the listing that is created after a user is liquidated, should be the initial creator of the listing, in the case a liquidated NFT is sold, the initial creator of the listing will loose funds. (Who the owner of the newly created listing in the ``Listings.sol`` contract should be is a question for a higher power, it definitely shouldn't be the person who just got liquidated. )

### PoC
[Gist](https://gist.github.com/AtanasDimulski/95fe424bd5c38a08b7d12cc5c3911878)

After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/95fe424bd5c38a08b7d12cc5c3911878), add the following test to the ``AuditorTests.t.sol`` file: 
```solidity
       function test_LiquidationAssignsNFTToWrongAddress() public {
        vm.startPrank(alice);
        collection1.mint(alice, 12);
        collection1.setApprovalForAll(address(listings), true);

        Listings.Listing memory listingAlice = IListings.Listing({
            owner: payable(alice),
            created: uint40(block.timestamp),
            duration: 10 days,
            floorMultiple: 200
        });

        uint256[] memory tokenIdsAlice = new uint256[](1);
        tokenIdsAlice[0] = 12;
        IListings.CreateListing[] memory createLisings = new IListings.CreateListing[](1);       
        IListings.CreateListing memory createListing = IListings.CreateListing({
                collection: address(collection1),
                tokenIds: tokenIdsAlice,
                listing: listingAlice
        });
        createLisings[0] = createListing;
        listings.createListings(createLisings);  
        skip(2);
        vm.stopPrank();

        vm.startPrank(bob);
        collection1.mint(bob, 13);
        collection1.mint(bob, 14);
        collection1.mint(bob, 15);
        collection1.setApprovalForAll(address(locker), true);
        uint256[] memory tokenIdsBob = new uint256[](3);
        tokenIdsBob[0] = 13;
        tokenIdsBob[1] = 14;
        tokenIdsBob[2] = 15;
        locker.deposit(address(collection1), tokenIdsBob);
        collectionTokenA.approve(address(listings), type(uint256).max);
        listings.reserve(address(collection1), 12, 0.3e18);
        vm.stopPrank();

        vm.startPrank(tom);
        collection1.mint(tom, 16);
        collection1.setApprovalForAll(address(locker), true);
        uint256[] memory tokenIdsTom = new uint256[](1);
        tokenIdsTom[0] = 16;
        locker.deposit(address(collection1), tokenIdsTom);
        collectionTokenA.approve(address(protectedListings), type(uint256).max);

        /// @notice skip 3 years so I can simulate the liquidation easier
        skip(3 * 31_536_000);
        protectedListings.liquidateProtectedListing(address(collection1), 12);
        IListings.Listing memory listingAfterLiquidation = listings.listings(address(collection1), 12);

        /// @notice the owner of the newly created lisitng is bob, the person who reserved the NFT but got liquidated
        assertEq(bob, address(listingAfterLiquidation.owner));
        vm.stopPrank();
    }
```


To run the test use: ``forge test -vvv --mt test_LiquidationAssignsNFTToWrongAddress``
### Mitigation

_No response_