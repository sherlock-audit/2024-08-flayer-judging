Precise Lava Starfish

High

# Lack of delete `_listings[_collection][_tokenId]` in reserve

## Summary
We miss to delete `_listings[_collection][_tokenId]` in reserve. This ERC721 token will always on the list.

## Vulnerability Detail
In Listing contract, users can reserve one listing token via reserve(). This listing token will be unlist from the Listing contract, and be added into the protectedListing list.
The problem is that we decrease the `listingCount`, but we miss delete `_listings[_collection][_tokenId]`. This will cause this token will always on the list.
When someone gets this token, deposits into locker, this token will never be redeemed because this token is always on list.
```solidity
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];
        ...
        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
@> Missing delete _listings[_collection][_tokenId]
            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }
        }
        ...
    }

```

```solidity
    function redeem(address _collection, uint[] calldata _tokenIds, address _recipient) public nonReentrant whenNotPaused collectionExists(_collection) {
        ...
        IERC721 collection = IERC721(_collection);

        for (uint i; i < tokenIdsLength; ++i) {
@>            if (isListing(_collection, _tokenIds[i])) revert TokenIsListing(_tokenIds[i]);
            collection.transferFrom(address(this), _recipient, _tokenIds[i]);
        }

        emit TokenRedeem(_collection, _tokenIds, msg.sender, _recipient);
    }
```
### Poc
```solidity
    function test_Poc_reserve() public {
        // Mint one ERC721 token
        address alice = vm.addr(1);
        erc721a.mint(alice, 0);
        // List
        vm.startPrank(alice);
        erc721a.approve(address(listings), 0);
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = 0;
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 120
            })
        });

        listings.createListings(_listings);
        // Reserve
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), alice, 10 ether);
        token.approve(address(listings), 10 ether);

        listings.reserve({
            _collection: address(erc721a),
            _tokenId: 0,
            _collateral: 0.3 ether
        });
        // Unlock the reserve
        token.approve(address(protectedListings), 10 ether);
        protectedListings.unlockProtectedListing(address(erc721a), 0, true);
        // Deposit & redeem

        // Rather than creating a listing, we will deposit it as a floor token
        erc721a.approve(address(locker), 0);
        locker.deposit(address(erc721a), tokenIds);
        // Redeem
        token.approve(address(locker), 10 ether);
        locker.redeem(address(erc721a), tokenIds);
    }
```
The test will be reverted: `[FAIL. Reason: TokenIsListing(0)]`

## Impact
One ERC721 token will always on the list. If someone deposits in the locker, this token cannot be redeemed.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759

## Tool used

Manual Review

## Recommendation
Delete `_listings[_collection][_tokenId]` in reserve() if this token is on the list.