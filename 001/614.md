Shiny Glass Hare

High

# Manipulation of Unlock Price Through Position Adjustment Leading to Incorrect Interest Calculation

## Summary

The unlockPrice function is susceptible to manipulation due to how it calculates the price based on the current tokenTaken amount and initial checpoint. A user can exploit this by adjusting their position, reducing their debt significantly before unlocking to reduce the interest paid. Or another user can adjust to increase his position before unlocking. This leads to incorrect unlock price calculations and allows users to pay less than they should, or alternatively, overpay based on the wrong debt amount.

## Vulnerability Detail

The `unlockPrice` function calculates the final unlock price by considering the tokenTaken and compounding the interest over time. The issue arises when a user calls the `adjustPosition` function to change the tokenTaken value. The unlock price is recalculated based on the adjusted tokenTaken, which could be lower or higher than the initial debt amount.

```solidity
function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
        // Get the information relating to the protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Calculate the final amount using the compounded factors and principle amount
        unlockPrice_ = locker.taxCalculator().compound({
            _principle: listing.tokenTaken,
            _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
            _currentCheckpoint: _currentCheckpoint(_collection)
        });
    }
```

If a user reduces their debt by adjusting their position before unlocking, the tokenTaken is reduced, and the unlockPrice will be calculated based on the new lower debt. This leads to paying significantly less interest than what should be owed based on the original debt.

For example, if a user initially took 0.2 ETH and accrued interest over time, they could adjust their position by repaying a portion of the debt (e.g., 0.1 ETH), reducing their tokenTaken value to 0.1 ETH. The interest is then recalculated based on the remaining 0.1 ETH, which is incorrect because the interest should still be based on the original 0.2 ETH.

```solidity
    function adjustPosition(address _collection, uint _tokenId, int _amount) public lockerNotPaused {
        ...
        if (_amount < 0) {
            ...
            _protectedListings[_collection][_tokenId].tokenTaken -= uint96(absAmount);
        }
```
Conversely, if a user initially borrowed a small amount (e.g., 0.1 ETH) and later increased their position by taking more debt (e.g., 0.9 ETH), the unlock price will be calculated based on the larger debt, despite the initial interest being accrued on the smaller amount. This can cause the user to overpay interest based on the wrong debt value.
Test:

```solidity

    function test_position() public {

        uint _tokenId = 0;
        address payable _owner = users[0];
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), address(_owner), 100 ether);

        erc721a.mint(_owner, _tokenId);

        // Create our listing
        vm.startPrank(_owner);
        erc721a.approve(address(protectedListings), _tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IProtectedListings.ProtectedListing({
                    owner: _owner,
                    tokenTaken: 0.2 ether,
                    checkpoint: 0
                })
            })
        });

        locker.collectionToken(address(erc721a)).approve(address(protectedListings),type(uint).max);
        assertEq(protectedListings.unlockPrice(address(erc721a), _tokenId),0.2 ether);

        // Warp forward to a point
        vm.warp(block.timestamp + 100 days);

        // Price is incresead normally
        uint256 interest = 14684931505728000;
        assertEq(protectedListings.unlockPrice(address(erc721a), _tokenId), 0.2 ether + interest);

        // Half of the  initial debt is payed
        protectedListings.adjustPosition(address(erc721a),_tokenId, -0.1 ether);
        emit log("unlock price after position adjust");

        // Expected price shoold be more than 0.1ether + whole interest
        // But current price is  0.1ether + interest / 2
        assertEq(protectedListings.unlockPrice(address(erc721a), _tokenId), 0.1 ether + interest/2);

        uint256 ownerBalanceBefore= token.balanceOf(_owner);
        // be the amount of loan that we took out.
        protectedListings.unlockProtectedListing(
            address(erc721a),
            _tokenId,
            true
        );
        assertEq(ownerBalanceBefore-0.1 ether -interest/2,  token.balanceOf(_owner));
        vm.stopPrank();
    }
```

## Impact
Users can pay significantly less interest than what is actually owed by reducing their debt before unlocking their position.
 Users who increase their debt may end up overpaying interest based on a higher debt amount, despite only having a smaller debt for the majority of the loan period

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L607

## Tool used

Manual Review

## Recommendation
Implement a new logic to keep record of old position interest before adjusting a position.