Wobbly Neon Hyena

Medium

# `CollectionShutdown::execute` is not resetting `shutdownVotes`, blocking future shutdown processes

### Summary

When voting on a collection shutdown, users pay collection tokens to, when doing so `params.shutdownVotes` is incremented to reflect the total votes on the shutdown process:
```solidity
// Register the amount of votes sent as a whole, and store them against the user
params.shutdownVotes += uint96(userVotes);
```
This parameter is never reset when the shutdown is executed.

This `shutdownVotes` is checked whenever starting a new shutdown process for a collection, in `CollectionShutdown::start`:
```solidity
// Ensure that a shutdown process is not already actioned
CollectionShutdownParams memory params = _collectionParams[_collection];
if (params.shutdownVotes != 0) revert ShutdownProcessAlreadyStarted();
```

So if a collection for an NFT was shutdown and a new collection was created for the same NFT, and for whatever reason the new collection needs to be shutdown, it'll never work, as `shutdownVotes` will hold the state of the previous shut down process.

This blocks the shut down of collections.

### Root Cause

`shutdownVotes` is not being reset after the shutdown execution in `CollectionShutdown::execute`, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231-L275).
Which is used as a check when starting a shutdown process, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L141).

### Impact

Users won't be able to start a shutdown process for some collections, that were shut down before.

### PoC

Add the following test in `flayer/test/utils/CollectionShutdown.t.sol`:

```solidity
interface ILSSVMPairERC721 {
    function swapTokenForSpecificNFTs(
        uint256[] calldata nftIds,
        uint256 maxExpectedTokenInput,
        address nftRecipient,
        bool isRouter,
        address routerCaller
    ) external payable returns (uint256);
}

function test_CantShutdownAfterShutdown() public {
    // Simulate the minting of 4 tokens
    vm.prank(address(locker));
    collectionToken.mint(address(this), 4 ether);

    // Start the shutdown process
    collectionToken.approve(address(collectionShutdown), 4 ether);
    collectionShutdown.start(address(erc721b));

    // Execute the shutdown
    uint[] memory tokenIds = _mintTokensIntoCollection(erc721b, 1);
    collectionShutdown.execute(address(erc721b), tokenIds);

    // Compute the amount of ETH required to buy the NFTs
    ICollectionShutdown.CollectionShutdownParams
        memory params = collectionShutdown.collectionParams(
            address(erc721b)
        );
    (, , , uint256 amount, , ) = ILSSVMPair(params.sweeperPool)
        .getBuyNFTQuote(0, tokenIds.length);
    deal(address(this), amount);

    // Buy NFTs on the Sudoswap pool
    ILSSVMPairERC721(address(params.sweeperPool)).swapTokenForSpecificNFTs{
        value: amount
    }(tokenIds, amount, address(this), false, address(0));

    // Contract does not have any ETH
    assertEq(address(this).balance, 0);

    // Claim the ETH resulting from the shutdown
    collectionShutdown.claim(address(erc721b), payable(address(this)));

    // Contract has 500 ETH
    assertEq(address(this).balance, 500 ether);

    // Create and initialize a new collection for the same ERC721 contract
    locker.createCollection(
        address(erc721b),
        "Test Collection 2",
        "TEST 2",
        0
    );
    locker.setInitialized(address(erc721b), true);
    collectionToken = locker.collectionToken(address(erc721b));

    // Simulate the minting of 4 tokens
    vm.prank(address(locker));
    collectionToken.mint(address(this), 4 ether);

    collectionToken.approve(address(collectionShutdown), 4 ether);

    // Start the shutdown process, reverts
    vm.expectRevert(
        abi.encodeWithSelector(
            ICollectionShutdown.ShutdownProcessAlreadyStarted.selector
        )
    );
    collectionShutdown.start(address(erc721b));
}
```

### Mitigation

Reset `shutdownVotes` when executing the shutdown of a collection, in `CollectionShutdown::execute`:
```diff
function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
    // Ensure that the vote count has reached quorum
    CollectionShutdownParams storage params = _collectionParams[_collection];
    if (!params.canExecute) revert ShutdownNotReachedQuorum();

    // Ensure we have specified token IDs
    uint _tokenIdsLength = _tokenIds.length;
    if (_tokenIdsLength == 0) revert NoNFTsSupplied();

    // Check that no listings currently exist
    if (_hasListings(_collection)) revert ListingsExist();

    // Refresh total supply here to ensure that any assets that were added during
    // the shutdown process can also claim their share.
    uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
    if (params.quorumVotes != newQuorum) {
        params.quorumVotes = uint88(newQuorum);
    }

    // Lockdown the collection to prevent any new interaction
    locker.sunsetCollection(_collection);

    // Iterate over our token IDs and transfer them to this contract
    IERC721 collection = IERC721(_collection);
    for (uint i; i < _tokenIdsLength; ++i) {
        locker.withdrawToken(_collection, _tokenIds[i], address(this));
    }

    // Approve sudoswap pair factory to use our NFTs
    collection.setApprovalForAll(address(pairFactory), true);

    // Map our collection to a newly created pair
    address pool = _createSudoswapPool(collection, _tokenIds);

    // Set the token IDs that have been sent to our sweeper pool
    params.sweeperPoolTokenIds = _tokenIds;
    sweeperPoolCollection[pool] = _collection;

    // Update our collection parameters with the pool
    params.sweeperPool = pool;

    // Prevent the collection from being executed again
    params.canExecute = false;
+   params.shutdownVotes = 0;
    emit CollectionShutdownExecuted(_collection, pool, _tokenIds);
}
```