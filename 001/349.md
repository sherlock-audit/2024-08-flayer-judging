Soft Violet Lion

High

# User lose fund when vote after the shutdown is canceled

## Summary

In CollectoinShutdown.sol, we have the logic below

```solidity
 function vote(address _collection) public nonReentrant whenNotPaused {
        // Ensure that we are within the shutdown window
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (params.quorumVotes == 0) revert ShutdownProccessNotStarted();

        _collectionParams[_collection] = _vote(_collection, params);
    }

    /**
     * Processes the logic for casting a vote.
     *
     * @param _collection The collection address
     * @param params The collection shutdown parameters
     *
     * @return The updated shutdown parameters
     */
    function _vote(address _collection, CollectionShutdownParams memory params) internal returns (CollectionShutdownParams memory) {
        // Take tokens from the user and hold them in this escrow contract
        uint userVotes = params.collectionToken.balanceOf(msg.sender);
        if (userVotes == 0) revert UserHoldsNoTokens();

        // Pull our tokens in from the user
        params.collectionToken.transferFrom(msg.sender, address(this), userVotes);

        // Register the amount of votes sent as a whole, and store them against the user
        params.shutdownVotes += uint96(userVotes);

        // Register the amount of votes for the collection against the user
        unchecked { shutdownVoters[_collection][msg.sender] += userVotes; }

        emit CollectionShutdownVote(_collection, msg.sender, userVotes);

        // If we can execute, then we need to fire another event
        if (!params.canExecute && params.shutdownVotes >= params.quorumVotes) {
            params.canExecute = true;
            emit CollectionShutdownQuorumReached(_collection);
        }

        return params;
    }

```

and [line of code here](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L373)

```solidity
function reclaimVote(address _collection) public whenNotPaused {
        // If the quorum has passed, then we can no longer reclaim as we are pending
        // an execution.
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (params.canExecute) revert ShutdownQuorumHasPassed();

        // Get the amount of votes that the user has cast for this collection
        uint userVotes = shutdownVoters[_collection][msg.sender];

        // If the user has not cast a vote, then we can revert early
        if (userVotes == 0) revert NoVotesPlacedYet();

        // We delete the votes that the user has attributed to the collection
        params.shutdownVotes -= uint96(userVotes);
        delete shutdownVoters[_collection][msg.sender];

        // We can now return their tokens
        params.collectionToken.transfer(msg.sender, userVotes);

        // Notify our stalkers that a vote has been reclaimed
        emit CollectionShutdownVoteReclaim(_collection, msg.sender, userVotes);
    }

    /**
     * If a shutdown flow has not been triggered and the total supply of the token has risen
     * above the threshold, then this function can be called to remove the process and prevent
     * execution.
     *
     * This is done to ensure that collections cannot be marked to shutdown in it's infancy and
     * then as more tokens are added then the shutdown is actioned, rugging people that weren't
     * aware of the action.
     *
     * @param _collection The collection address
     */
    function cancel(address _collection) public whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams memory params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Check if the total supply has surpassed an amount of the initial required
        // total supply. This would indicate that a collection has grown since the
        // initial shutdown was triggered and could result in an unsuspected liquidation.
        if (params.collectionToken.totalSupply() <= MAX_SHUTDOWN_TOKENS * 10 ** locker.collectionToken(_collection).denomination()) {
            revert InsufficientTotalSupplyToCancel();
        }

        // Remove our execution flag
        delete _collectionParams[_collection];
        emit CollectionShutdownCancelled(_collection);
    }

```

the user can vote for a shutdown, they need to lock token

if user change their mind, they can claim the token back and delete his vote.

However, consider the sequence of action beflow.

1. alice use 10000 token to vote for shutdown.
2. bob trigger [cancel function](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L403) and the _collectionParams get deleted.
3. alice cannot reclaim the vote and get her token back.

because after 

```solidity
    // Remove our execution flag
    delete _collectionParams[_collection];
```

calling [reclaimVote](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L373) would not work because params.collectionToken is address(0)

```solidity
        // We can now return their tokens
    params.collectionToken.transfer(msg.sender, userVotes);
```

## Impact

User cannot reclaim the vote and lose fund after the shutdown is canceled.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L191

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L373

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L403

## Recommendation

implement what comments says:

```solidity
 * If a shutdown flow has not been triggered and the total supply of the token has risen
     * above the threshold, then this function can be called to remove the process and prevent
     * execution.
```

instead of delete all _collectionParams data.