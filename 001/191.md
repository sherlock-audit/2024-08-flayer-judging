Tiny Plastic Hyena

High

# Purchases in Listings.sol do not always reset _isLiquidation, leading to lost listing tax refunds for the buyer

### Summary

Neither reserve() nor relist() properly reset _isLiquidation, making the new owner of a liquidation listing lose his or her listing tax refund next time it is listed.

### Root Cause

In the lines of code in reserve() that [handle the case of a liquidation](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L708-L712), there is nothing to reset _isLiquidation back to false. relist() similarly does not have any way to reset a liquidation. In contrast, _fillListing() handles this [in this snippet of code](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L501-L513) by deleting the mapping.

```solidity
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        ...
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
@>          } @audit // there is no subsequent else to toggle liquidation off if it is in fact a liquidation
```

```solidity
    function _fillListing(address _collection, address _collectionToken, uint _tokenId) private {
        ...
            // Check if there is collateral on the listing, as this we bypass fees and refunds
            if (!_isLiquidation[_collection][_tokenId]) {
                ...
                }
            } else {
@>              delete _isLiquidation[_collection][_tokenId]; // @audit here is where it is actually toggled to false
            }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Any user that purchases a liquidation listing via reserve() or relist() will lose their listing tax refund when they decide to sell it. This is because liquidation listings bypass the listing tax refund functionality of the contract, and the NFT will still count as a liquidation listing according to the protocol. The NFT will remain this way until someone buys it via fillListings(). Until that point, every person who lists it is guaranteed to lose the entirety of their listing tax refund even if it is immediately bought.

### PoC

Please copy and past the following into Listings.t.sol:
```solidity
    function test_IsLiquidationNotToggled() public {

        address payable user = payable(makeAddr("user"));
        uint tokenId = 5;

        // Mint a mock erc to the bypasser
        erc721a.mint(user, tokenId);

        // The bypasser creates a protected listing and takes the maximum amount
        vm.startPrank(user);        

        erc721a.approve(address(protectedListings), tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(5),
                listing: IProtectedListings.ProtectedListing({
                    owner: user,
                    tokenTaken: 0.95 ether,
                    checkpoint: 0
                })
            })
        });

        // wait one second, then liquidate
        vm.warp(block.timestamp + 1);
        protectedListings.liquidateProtectedListing(address(erc721a), 5);
        vm.warp(block.timestamp + 1);

        // user2 reserves and then unlocks the asset
        address user2 = makeAddr("user2");
        deal(address(locker.collectionToken(address(erc721a))), user2, 5 ether);

        vm.startPrank(user2);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);
        locker.collectionToken(address(erc721a)).approve(address(protectedListings), type(uint).max);

        listings.reserve(address(erc721a), 5, 0.5 ether);
        vm.warp(block.timestamp + 1);
        protectedListings.unlockProtectedListing(address(erc721a), 5, true);
        vm.warp(block.timestamp + 1);

        // even though it was bought and paid for reserve did not flip _isLiquidation for the NFT
        // now if the user lists it he will not recieve a refund on fees even if it immediately sells

        // check token balance before sale
        uint256 balanceBeforeSale = locker.collectionToken(address(erc721a)).balanceOf(user2);
        // assert escrow is empty
        assertEq(listings.balances(user2, address(locker.collectionToken(address(erc721a)))), 0);

        erc721a.approve(address(listings), 5);
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(5),
            listing: IListings.Listing({
                owner: payable(user2),
                created: uint40(block.timestamp),
                duration: 18 days,
                floorMultiple: 1000
            })
        });
        listings.createListings(_listings);

        // a user immediately buys it
        vm.startPrank(user);
        deal(address(locker.collectionToken(address(erc721a))), user, 10 ether);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = 5;
        
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            })
        );

        // but the listing tax is not refunded because it is still marked as a liquidation!
        vm.startPrank(user2);
        listings.withdraw(address(locker.collectionToken(address(erc721a))), listings.balances(user2, address(locker.collectionToken(address(erc721a)))));
        uint256 balanceAfterSale = locker.collectionToken(address(erc721a)).balanceOf(user2);
        assertEq(balanceAfterSale - balanceBeforeSale, 9074285714285714286);

        // even though the listing immediately sold for 10 fTokens, the user loses the entirety of listing taxes

    }
```

### Mitigation

Consider setting _isLiquidation to false at the end of reserve() and relist()