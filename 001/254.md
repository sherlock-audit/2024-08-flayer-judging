Happy Wintergreen Kookaburra

Medium

# The `reserve` Function does not create a checkpoint when the Listing is shifted to the Protected Listing

## Summary
According to Flayer, the more there are protected Listing, the more the utilization rate will be, but the reserve function does not update the checkpoint like how the `ProtectedListings.sol`  does when sending a listing from a protected listing to an auction listing
## Vulnerability Detail
Checkpoints are used to track the state of listings, including protected ones, which directly impact liquidation calculations. By not creating or updating a checkpoint during this transition, the system fails to recognize the change in the number of protected listings. This leads to inaccurate tracking of liquidation risks. The issue is the `reserve` function does not update the Checkpoint when you send it to the protected listing

## Impact
The rate of how many protected listings there are won't get updated as the more there are protected listings, the more it is easier to get liquidated (High costs), but when you take a listing from reserve to protected listing it does not update the checkpoint, causing the protected listing to use stale rates.

## Code Snippet
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759

<details>

<summary>Snippet</summary>

```solidity
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];

        // Ensure the caller is not the owner of the listing
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();

        // Find the underlying {CollectionToken} attached to our collection
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }
        }

        // Burn the tokens that the user provided as collateral, as we will have it minted
        // from {ProtectedListings}.
        collectionToken.burnFrom(msg.sender, _collateral * 10 ** collectionToken.denomination());

        // We can now pull in the tokens from the Locker
        locker.withdrawToken(_collection, _tokenId, address(this));
        IERC721(_collection).approve(address(protectedListings), _tokenId);

        // Create a protected listing, taking only the tokens
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;
        IProtectedListings.CreateListing[] memory createProtectedListing = new IProtectedListings.CreateListing[](1);
        createProtectedListing[0] = IProtectedListings.CreateListing({
            collection: _collection,
            tokenIds: tokenIds,
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: uint96(1 ether - _collateral),
                checkpoint: 0 // Set in the `createListings` call
            })
        });

        // Create our listing, receiving the ERC20 into this contract
        protectedListings.createListings(createProtectedListing);

        // We should now have received the non-collateral assets, which we will burn in
        // addition to the amount that the user sent us.
        collectionToken.burn((1 ether - _collateral) * 10 ** collectionToken.denomination());

        // We can now transfer ownership of the listing to the user reserving it
        protectedListings.transferOwnership(_collection, _tokenId, payable(msg.sender));
    }
```
</details>

## Tool used

Manual Review

## Recommendation
- Add a Checkpoint at the end of the `reserve` function
```diff
        // We should now have received the non-collateral assets, which we will burn in
        // addition to the amount that the user sent us.
        collectionToken.burn((1 ether - _collateral) * 10 ** collectionToken.denomination());

+       // Create our checkpoint as utilisation rates will change
+       protectedListings.createCheckpoint(collection);

        // We can now transfer ownership of the listing to the user reserving it
        protectedListings.transferOwnership(_collection, _tokenId, payable(msg.sender));
    }
```
