Clean Snowy Mustang

High

# _isLiquidation status is not reset when a liquidation listing is relisted/reserved

## Summary
[_isLiquidation](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L76) status is not reset when a liquidation listing is relisted/reserved, owner who lists the corresponding ERC721 item wont't receive any tax refund.

## Vulnerability Detail

When a liquidation listing is created, its `_isLiquidation` is set to **true**, when the liquidation listing is filled, no tax will be refunded.

[Listings.sol#L501-L513](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L500-L513):
```solidity
@>          // Check if there is collateral on the listing, as this we bypass fees and refunds
@>          if (!_isLiquidation[_collection][_tokenId]) {
                // Find the amount of prepaid tax from current timestamp to prepaid timestamp
                // and refund unused gas to the user.
                (uint fee, uint refund) = _resolveListingTax(_listings[_collection][_tokenId], _collection, false);
                emit ListingFeeCaptured(_collection, _tokenId, fee);

                assembly {
                    tstore(FILL_FEE, add(tload(FILL_FEE), fee))
                    tstore(FILL_REFUND, add(tload(FILL_REFUND), refund))
                }
            } else {
                delete _isLiquidation[_collection][_tokenId];
            }
```

Just like normal listings, a liquidation listing can also be relisted by any users, and user who relists the liquidation listing need to pay tax.

[Listings.sol#L667-L668](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L667-L668):
```solidity
        // Pay our required taxes
        payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);
```

However, the `_isLiquidation` status is not reset when a liquidation listing is relisted, results in the user who relists the listing receive no tax refund when the listing is filled.

Please run the PoC in Listings.t.sol to verify:
```solidity
    function testAudit_LiquidationStatusNotReset() public {
        address bob = makeAddr("Bob");
        erc721a.mint(bob, 888);

        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        // Bob creates a protected listing
        IProtectedListings.CreateListing[] memory _listings = new IProtectedListings.CreateListing[](1);
        _listings[0] = IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(888),
            listing: IProtectedListings.ProtectedListing({
                owner: payable(bob),
                tokenTaken: 0.95 ether,
                checkpoint: 0
            })
        });

        vm.startPrank(bob);
        erc721a.approve(address(protectedListings), 888);
        protectedListings.createListings(_listings);
        vm.stopPrank();

        vm.warp(block.timestamp + 1);
        
        // Bob's protected listing is liquidated
        protectedListings.liquidateProtectedListing(address(erc721a), 888);

        address alice = makeAddr("Alice");
        mintCollectionTokens(address(erc721a), alice, 1, 4);

        assertEq(collectionToken.balanceOf(alice), 4e18);

        // Alice relists Bob's liquidation listing for 5 ether
        vm.startPrank(alice);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(888),
                listing: IListings.Listing({
                    owner: payable(alice),
                    created: uint40(block.timestamp),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: 500
                })
            }),
            _payTaxWithEscrow: false
        });
        vm.stopPrank();

        // Alice pays 0.1225 ether tax
        assertEq(collectionToken.balanceOf(alice), 4 ether - 3 ether - 0.1225 ether);

        address cathy = makeAddr("Cathy");
        mintCollectionTokens(address(erc721a), cathy, 5, 5);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = 888;

        // Cathy fills Alice's listing
        vm.startPrank(cathy);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            })
        );
        vm.stopPrank();

        // Alice receives 4.8775 ether in total, the 0.1225 ether tax is not refunded
        assertEq(collectionToken.balanceOf(alice), 0.8775 ether);
        assertEq(listings.balances(alice, address(collectionToken)), 4 ether);
    }

    function mintCollectionTokens(address collection, address to, uint id, uint count) private {
        ERC721Mock token = ERC721Mock(collection);

        uint[] memory depositTokenIds = new uint[](count);

        for (uint i; i < count; ++i) {
            token.mint(address(this), id);
            depositTokenIds[i] = id;
            ++id;
        }

        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(collection, depositTokenIds, to);
    }
```

Likewise, the `_isLiquidation` status is not reset when the liquidation listing is reserved.

## Impact

No tax is refunded, user will suffer a loss.

## Code Snippet

## Tool used

Manual Review

## Recommendation

`_isLiquidation` status should be reset when a liquidation listing is relisted/reserved.

[Listings.sol#L667-L671](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L667-L671):
```diff
        // Pay our required taxes
        payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);

+       delete _isLiquidation[_collection][_tokenId];

        // Emit events
        emit ListingRelisted(_collection, _tokenId, listing);
```

[Listings.sol#L708-L713](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L708-L713):
```diff
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }
+           else {
+               delete _isLiquidation[_collection][_tokenId];
+           }
```