Happy Wintergreen Kookaburra

High

# Users won't be able to claim their 2nd+ Protected Listing or Liquidate them because of a panic error (array-out-of-bounds)

## Summary
When two different listings of NFTs are created, the user provides a checkpoint for them, but the system overwrites it with a `_checkpointIndex` (Which is correct). But the out-of-bounds error in the `createListings` function occurs because checkpoints are not properly initialized before being accessed as when you create a listing the `createCheckpoint` is called once and not for the other following token ids.

## Vulnerability Detail
Users won't be able to liqudate a specific NFT inside the protected listing smart contract as they can only do that until the checkpoint of the newly added tokens have been created which `createListings` does not do expect if it is the first listing of the Collection.

## Array Out-of-Bounds Error (Code Snippet):
- The Function `createListings` calls The `_mapListings`
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L143
```solidity
tokensReceived = _mapListings(listing, tokensIdsLength, checkpointIndex) * 10 ** locker.collectionToken(listing.collection).denomination();
```
- `_mapListings` Updates the provided checkpoint to the `_checkpointIndex` on each token listing
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L202
```solidity
_createListing.listing.checkpoint = _checkpointIndex;
_protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;
```
- How the `_checkpointIndex` is created inside the `createListings` (Once per first collection listing and never again) (Keep in mind It also calls create Checkpoint once)
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L134
```solidity
            checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection)); //n create a checkpoint for the listing collection
            assembly { checkpointIndex := tload(checkpointKey) } //n If the checkpoint for that particular collection has not been created before, the checkpointIndex will be zero
            if (checkpointIndex == 0) { //n If the checkpointIndex is zero, it means that this is the first time a listing is being created for this collection
                checkpointIndex = _createCheckpoint(listing.collection); //n create a new checkpoint for the collection
                assembly { tstore(checkpointKey, checkpointIndex) } //n Save a word created (checkpointKey) to transient storage
            } //n wont execute the if statement if the collection already has a Checkpoint key from the transient storage
```
- When you call getProtectedListingHealth() for tokenId2 it calls the unlockPrice, 
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L500
```solidity
ProtectedListing memory listing = _protectedListings[_collection][_tokenId];
int health = int(MAX_PROTECTED_TOKEN_AMOUNT) - int(unlockPrice(_collection, _tokenId));
```
- Inside the `unlockPrice()` the function tries to access the checkpoint data (`collectionCheckpoints`) for that token
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L614
```solidity
unlockPrice_ = locker.taxCalculator().compound({
    _principle: listing.tokenTaken,
    _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],  // Accesses checkpoint data
    _currentCheckpoint: _currentCheckpoint(_collection)
});
```
- Here, listing.checkpoint is used to access data in the array `collectionCheckpoints[_collection]`. If the checkpoint data is out-of-bounds for tokenId2, it throws the array out-of-bounds error
- So, when getProtectedListingHealth() tries to access `collectionCheckpoints[_collection][_checkpointIndex]` for tokenId2, it finds invalid data, causing the error(Because the checkpoint was never created for the second Token)
## Actual Problem
- The function `_createCheckpoint` is responsible for generating and updating the `collectionCheckpoints` for a collection (Remember it is called only once when you create a listing)
- If the `listing.checkpoint` refers to an index that exceeds the length of `collectionCheckpoints[_collection]`, it results in an out-of-bounds access
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L530
```solidity
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        // Determine the index that will be created
        index_ = collectionCheckpoints[_collection].length;

        // Register the checkpoint that has been created
        emit CheckpointCreated(_collection, index_);

        // If this is our first checkpoint, then our logic will be different as we won't have
        // a previous checkpoint to compare against and we don't want to underflow the index.
        if (index_ == 0) {
            // Calculate the current interest rate based on utilization
            (, uint _utilizationRate) = utilizationRate(_collection);

            // We don't have a previous checkpoint to calculate against, so we initiate our
            // first checkpoint with base data.
            collectionCheckpoints[_collection].push(
                Checkpoint({
                    compoundedFactor: locker.taxCalculator().calculateCompoundedFactor({
                        _previousCompoundedFactor: 1e18,
                        _utilizationRate: _utilizationRate,
                        _timePeriod: 0
                    }),
                    timestamp: block.timestamp
                })
            );

            return index_;
        }

        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
            return index_;
        }

        // Store the new (current) checkpoint
        collectionCheckpoints[_collection].push(checkpoint);
    }
```

## Impact (2 scenarios)
### First
- A user can List multiple tokens but provide collateral for only one token avoiding liquidation for other tokens. Since liquidation function depend on the `getProtectedListingHealth`, keepers attempting to liquidate other tokens will be blocked by the presence of the first token. This could prevent the liquidation of all associated tokens, leaving them in the userâ€™s possession (Protected Listing) despite insufficient collateral (They can't be sent for auction). If the first Token gets liquidated the other tokens liquidation period will start (meaning they will act like they are newly added listings) since that's when the checkpoint will be called for other tokens

### Second
- Because of the array out-of-bounds issue, A user may face difficulty unlocking their tokens unless they do so in a specific order. If the first token is not withdrawn, other tokens will cause the functions (`unlockProtectedListing`, `adjustPosition`, `liquidateProtectedListing`) to revert, making it impossible to unlock the remaining tokens before the first one.

## PoC
- Put the PoC inside the `ProtectedListings.t.sol`

<details>
<summary>POC</summary>

```solidity
 function test_PoC3() public {
  // create an address for alice
  address payable alice = users[1];
  address payable bob = users[2];

  // The token address (Underlying ERC20 for the collection)
  ICollectionToken token = locker.collectionToken(address(erc721a));

  // Create 2 Token Id's for Alice
  uint _tokenId1 = 444;
  uint _tokenId2 = 555;

  // Mint both tokens to alice
  erc721a.mint(alice, _tokenId1);
  erc721a.mint(alice, _tokenId2);

  // Create the listing for alice
  vm.startPrank(alice);
  erc721a.approve(address(protectedListings), _tokenId1); // approve token 1
  erc721a.approve(address(protectedListings), _tokenId2); // approve token 2
  _createProtectedListing({ // create a listing for token 1
    _listing: IProtectedListings.CreateListing({
    collection: address(erc721a),
    tokenIds: _tokenIdToArray(_tokenId1),
    listing: IProtectedListings.ProtectedListing({
    owner: alice,
    tokenTaken: 0.5 ether,
    checkpoint: 1 // the checkpoint alice set (will be overwritten)
  })
  })
  });
  _createProtectedListing({ // create a listing for token 1
    _listing: IProtectedListings.CreateListing({
    collection: address(erc721a),
    tokenIds: _tokenIdToArray(_tokenId2),
    listing: IProtectedListings.ProtectedListing({
    owner: alice,
    tokenTaken: 0.5 ether,
    checkpoint: 2 // the second checkpoint alice set (will be overwritten)
  })
  })
  });
  vm.stopPrank();

  // Confirm that alice holds the 1 ether (0.5 ether for each Listing)
  assertEq(token.balanceOf(alice), 1 ether);

  // Let us read the object of each token Id (checkpoint)
  IProtectedListings.ProtectedListing memory listing1 = protectedListings.listings(address(erc721a), _tokenId1);
  IProtectedListings.ProtectedListing memory listing2 = protectedListings.listings(address(erc721a), _tokenId2);
  console.log("The _tokenId1 listing:", listing1.checkpoint); // 0
  console.log("The _tokenId2 listing:", listing2.checkpoint); // 1

  // Since bob will be taking the liquidation route let's pass time (both liquidation and unlockListing call getProtectedListingHealth)
  vm.warp(block.timestamp + LIQUIDATION_TIME);

  // The first deposited Token id check will go through
  protectedListings.getProtectedListingHealth(address(erc721a), _tokenId1);

  // The second deposited Token id check will not go through (If the user wants to unlock their second NFT)
  vm.expectRevert();
  protectedListings.getProtectedListingHealth(address(erc721a), _tokenId2);

  vm.prank(bob); // If bob tries to liquidate the second Token Id it won't be successful too
  vm.expectRevert();
  protectedListings.liquidateProtectedListing(address(erc721a), _tokenId2);
}
```
</details>

## Tool used
Manual Review

## Recommendation
To fix the out-of-bounds error, you should call `_createCheckpoint` function before accessing `collectionCheckpoints`. This ensures that a valid checkpoint is created and initialized for each listing before being used in other operations