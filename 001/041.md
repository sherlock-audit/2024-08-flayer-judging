Wobbly Neon Hyena

High

# Wrong checkpoint index used in `ProtectedListings::createListings`, forcing users to pay more unlock price

### Summary

When creating a protected listing using `ProtectedListings::createListings`, a checkpoint index is assigned to the listing that can be later used when calculating the unlock price of the token:
```solidity
function unlockPrice(address _collection, uint _tokenId) public view returns (uint unlockPrice_) {
    // Get the information relating to the protected listing
    ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

    // Calculate the final amount using the compounded factors and principle amount
    unlockPrice_ = locker.taxCalculator().compound({
        _principle: listing.tokenTaken,
        _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
        _currentCheckpoint: _currentCheckpoint(_collection)
    });
}
```
The protocol checks if the stored index is 0, if so, it creates/uses the current index and stores it in the storage, this is implemented to make it gas-efficient so it is cached and quickly accessible. However, it doesn't take into consideration new checkpoints, let's have an example where the cached/saved index is 1 but the real index is 3, when `createListings` is called the index that is going to be used is 1 because the following condition is false:
```solidity
if (checkpointIndex == 0) {
    checkpointIndex = _createCheckpoint(listing.collection);
    assembly { tstore(checkpointKey, checkpointIndex) }
}
```

This forces users to lose funds as they'll be forced to pay more money when unlocking their tokens, as the range between the listing's index and the current index will be greater than what it actually is

### Root Cause

`ProtectedListings::createListings` is not clearing the storage after creation forcing the index only to be 0 or 1, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L117-L156).

### Impact

Users with protected listings will be forced to pay more than required to unlock their tokens.

### PoC

Add the following test in `flayer/test/ProtectedListings.t.sol`:

```solidity
function test_WrongCheckpointIdxOnCreation() public {
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );

    address payable bob = payable(makeAddr("bob"));
    uint256 bobTokenId1 = 0;
    uint256 bobTokenId2 = 1;
    uint256 bobTokenId3 = 2;
    erc721a.mint(bob, bobTokenId1);
    erc721a.mint(bob, bobTokenId2);
    erc721a.mint(bob, bobTokenId3);

    address payable alice = payable(makeAddr("alice"));
    uint256 aliceTokenId1 = 3;
    erc721a.mint(alice, aliceTokenId1);

    vm.startPrank(bob);
    erc721a.setApprovalForAll(address(protectedListings), true);
    erc721a.setApprovalForAll(address(listings), true);

    // Bob creates a normal listing
    _createListing({
        _listing: IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(bobTokenId1),
            listing: IListings.Listing({
                owner: bob,
                created: uint40(block.timestamp),
                duration: 5 days,
                floorMultiple: 200
            })
        })
    });

    // Checkpoint of index 0 was created
    (, uint timestamp) = protectedListings.collectionCheckpoints(
        address(erc721a),
        0
    );
    assertGt(timestamp, 0);

    vm.warp(block.timestamp + 1 days);

    // Bob creates a protected listing
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(bobTokenId2),
            listing: IProtectedListings.ProtectedListing({
                owner: bob,
                tokenTaken: 0.9 ether,
                checkpoint: 0
            })
        })
    });

    // Checkpoint of index 1 was created
    (, timestamp) = protectedListings.collectionCheckpoints(
        address(erc721a),
        1
    );
    assertGt(timestamp, 0);

    vm.warp(block.timestamp + 1 days);

    // Bob creates a normal listing
    _createListing({
        _listing: IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(bobTokenId3),
            listing: IListings.Listing({
                owner: bob,
                created: uint40(block.timestamp),
                duration: 5 days,
                floorMultiple: 200
            })
        })
    });
    vm.stopPrank();

    // Checkpoint of index 2 was created
    (, timestamp) = protectedListings.collectionCheckpoints(
        address(erc721a),
        2
    );
    assertGt(timestamp, 0);

    vm.warp(block.timestamp + 1 days);

    // Alice creates a protected listing
    vm.startPrank(alice);
    erc721a.approve(address(protectedListings), aliceTokenId1);
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(aliceTokenId1),
            listing: IProtectedListings.ProtectedListing({
                owner: bob,
                tokenTaken: 0.9 ether,
                checkpoint: 0
            })
        })
    });
    vm.stopPrank();

    // No checkpoint was created
    vm.expectRevert();
    protectedListings.collectionCheckpoints(address(erc721a), 3);

    // Alice's listing was created with the wrong checkpoint
    assertEq(
        protectedListings
            .listings(address(erc721a), aliceTokenId1)
            .checkpoint,
        1
    );
}
```

### Mitigation

In `ProtectedListings::createListings`, make sure a new checkpoint is created/used whenever needed.