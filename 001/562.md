Genuine Slate Sloth

High

# Users can't claim ETH after the collection has been shut down and fully liquidated due to unsafe casting

## Summary
In the `CollectionShutdown` contract, users cannot claim ETH after the collection has been shut down and fully liquidated due to unsafe casting of `uint88` for `quorumVotes`.

## Vulnerability Detail
In the `CollectionShutdown` contract, the `CollectionShutdownParams` struct contains a field `quorumVotes` of type `uint88`. The maximum value of `uint88` is (2^88 âˆ’ 1) < 10^27. As a result, in the `CollectionShutdown::start` and `CollectionShutdown::execute` functions, when casting the total supply of `CollectionToken` (which can have a denomination equal to 9) to type `uint88`, the value of `quorumVotes` can be significantly smaller than expected.

Unsafe casting in [CollectionShutdown::start](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L150) function:
```Solidity
function start(address _collection) public whenNotPaused {
    ...
    // Get the total number of tokens still in circulation, specifying a maximum number
    // of tokens that can be present in a "dormant" collection.
    params.collectionToken = locker.collectionToken(_collection);
    uint totalSupply = params.collectionToken.totalSupply();
    if (totalSupply > MAX_SHUTDOWN_TOKENS * 10 ** params.collectionToken.denomination()) revert TooManyItems();

    // Set our quorum vote requirement
    // The total supply only needs to equal 10**27 for the error to occur
=>  params.quorumVotes = uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT);
    ...
}
```

Unsafe casting in [CollectionShutdown::execute](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L246-L248) function:
```Solidity
function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
    ...
    // Refresh total supply here to ensure that any assets that were added during
    // the shutdown process can also claim their share.
    uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
    if (params.quorumVotes != newQuorum) {
=>      params.quorumVotes = uint88(newQuorum);
    }
    ...
}
```

Because `quorumVotes` is smaller than expected, many users are unable to claim their ETH after the collection has been shut down and fully liquidated.

[CollectionShutdown::claim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L310) function:

```Solidity
function claim(address _collection, address payable _claimant) public nonReentrant whenNotPaused {
    ...
    // params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT < totalSupply
    uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
    (bool sent,) = _claimant.call{value: amount}('');
    if (!sent) revert FailedToClaim();
    ...
}
```
## Impact
Users can't claim ETH after the collection has been shut down and fully liquidated.

## Proof of Concept
Copy this test case into the `CollectionShutdown.t.sol` file:

```Solidity
function test_unsafeCastingQuorumVotes() external {
    locker.createCollection(address(erc721c), 'Test Collection', 'TEST', 9);
    locker.setInitialized(address(erc721c), true);

    ICollectionToken collectionErc20Token = locker.collectionToken(address(erc721c));
    uint256 erc20Denomination = collectionErc20Token.denomination();

    // Mint erc20 token for users
    address user1 = makeAddr('user1');
    address user2 = makeAddr('user2');
    address user3 = makeAddr('user3');
    uint256 mintAmount = 1 ether * 10 ** erc20Denomination;
    vm.startPrank(address(locker));
    collectionErc20Token.mint(user1, mintAmount);
    collectionErc20Token.mint(user2, mintAmount);
    collectionErc20Token.mint(user3, mintAmount);
    vm.stopPrank();
    
    uint256 totalSupply = collectionErc20Token.totalSupply();
    assertEq(totalSupply, 3 ether * 10** erc20Denomination);
    
    // Initiate the process to shut down the collection
    vm.startPrank(user1);
    collectionErc20Token.approve(address(collectionShutdown), mintAmount);
    collectionShutdown.start(address(erc721c));
    vm.stopPrank();

    // Check shutdown params
    ICollectionShutdown.CollectionShutdownParams memory shutdownParams = collectionShutdown.collectionParams(address(erc721c));
    assertEq(shutdownParams.shutdownVotes, 1 ether * 10** erc20Denomination);
    // Formula for calculating quorumVotes: uint88(totalSupply * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT)
    uint256 expectedQuorumVotes = totalSupply * 50 / 100;
    assertLt(shutdownParams.quorumVotes, expectedQuorumVotes);
    console.log('Total supply:         ', totalSupply);
    console.log('Expected quorumVotes: ', expectedQuorumVotes);
    console.log('Actual quorumVotes:   ', shutdownParams.quorumVotes);
    // Even initial shutdown votes exceed quorum votes
    assertLt(shutdownParams.quorumVotes, shutdownParams.shutdownVotes);

    // More importantly, it will prevent users from claiming ETH after the collection has been shut down and fully liquidated
    uint256 availableToClaim = 5 ether;
    // Formula for calculating the ETH amount to claim: availableClaim * claimableVotes / (quorumVotes * 100 / 50)
    uint256 user1ClaimableVotes = collectionShutdown.shutdownVoters(address(erc721c), user1);
    uint256 user1ClaimAmount = (availableToClaim * user1ClaimableVotes) / (uint256(shutdownParams.quorumVotes) * 100 / 50);
    console.log('Amount of ETH available to claim:   ', availableToClaim);
    console.log('Amount of ETH that User1 can claim: ', user1ClaimAmount);
    assertLt(availableToClaim, user1ClaimAmount);
}
```

Then run `forge test --mt test_unsafeCastingQuorumVotes -vv`, and you will see the following result:

![image](https://github.com/user-attachments/assets/054f3c8d-e40d-4d13-bc1e-bebe79caf8dd)

## Code Snippet
- [CollectionShutdown::start](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L150) function
- [CollectionShutdown::execute](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L246-L248) function
- [CollectionShutdown::claim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L310) function
- [CollectionShutdown::voteAndClaim](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L343-L345) function

## Tool used

Manual Review

## Recommendation
Update the data type of the `quorumVotes` field in the `CollectionShutdownParams` struct, and then update the related code accordingly.

```diff
struct CollectionShutdownParams {
    uint96 shutdownVotes;
    address sweeperPool;
-   uint88 quorumVotes;
+   uint96 quorumVotes;
-   bool canExecute;
    ICollectionToken collectionToken;
+   bool canExecute;
-   uint availableClaim;
+   uint248 availableClaim;
    uint[] sweeperPoolTokenIds;
}
```