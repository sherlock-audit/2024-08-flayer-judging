Flaky Sable Hamster

High

# `withdrawProtectedListing()` can be DoS, user will lost his NFT

## Summary
`withdrawProtectedListing()` can be DoS, user will lost his NFT

## Vulnerability Detail
A user can unlock his protected listing using `protectedListings:unlockProtectedListing()`. While unlocking, a user can decide whether he wants his NFT now or later by indicating `bool _withdraw`.

If bool _withdraw is set to `false` then user can withdraw his NFT later using `withdrawProtectedListing()`.
```solidity
      /**
     * The amount of tokens taken are returned, as well as a fee. The recipient can also
     * opt to leave the asset inside the contract and withdraw at a later date by calling
     * the `withdrawProtectedListing` function.
     *
     * @param _collection The address of the collection to unlock from
     * @param _tokenId The token ID to unlock
     * @param _withdraw If the user wants to receive the NFT now
     */
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
...
        // Delete the listing objects
@>      delete _protectedListings[_collection][_tokenId];

        // Transfer the listing ERC721 back to the user
        if (_withdraw) {
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else {
@>          canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }
...
    }
```

Now the problem is, unlockProtectedListing() `deletes` the `_protectedListings` mapping, whether user wanted to withdraw his NFT now or later. 

This means any user can `withdraw` that NFT(tokenId) willingly or unwillingly from `Locker.sol` because `locker:redeem()` checks the `_protectedListings` mapping to ensure that the NFT is listed or not.
```solidity
function redeem(address _collection, uint[] calldata _tokenIds, address _recipient) public nonReentrant whenNotPaused collectionExists(_collection) {
...
        // Loop through the tokenIds and redeem them
        for (uint i; i < tokenIdsLength; ++i) {
            // Ensure that the token requested is not a listing
@>          if (isListing(_collection, _tokenIds[i])) revert TokenIsListing(_tokenIds[i]);

            // Transfer the collection token to the caller
            collection.transferFrom(address(this), _recipient, _tokenIds[i]);
        }
    }
```
```solidity
function isListing(address _collection, uint _tokenId) public view returns (bool) {
...
        // Check if we have a protected listing
@>      if (_listings.protectedListings().listings(_collection, _tokenId).owner != address(0)) {
            return true;
        }
        return false;
    }
```
```solidity
  function listings(address _collection, uint _tokenId) public view returns (ProtectedListing memory) {
@>      return _protectedListings[_collection][_tokenId];
    }
```
Suppose a user wanted to withdraw his NFT later by setting `_withdraw = false`, which deletes the `_protectedListings` mapping. Another user willingly or unwillingly redeemed that NFT from Locker.sol using `redeem()`.

As result, when user will call `withdrawProtectedListing()` to withdraw his NFT, it will revert because there is no NFT of that tokenId in the Locker.sol
```solidity
 function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
...
        // Transfer the asset to the user
@>      locker.withdrawToken(_collection, _tokenId, msg.sender);
        emit ListingAssetWithdraw(_collection, _tokenId);
    }
```

## Impact
User can lose his NFT, if he decided to withdraw it later using `withdrawProtectedListing()`

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L314C1-L323C1
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L209C4-L231C1
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L447C1-L450C1
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L99C3-L101C6

## Tool used
Manual Review

## Recommendation
There are 2 ways to fix this
1. Don't delete `_protectedListings` mapping in `unlockProtectedListing()` instead delete in `withdrawProtectedListing()`, when user withdraw his NFT
2. Remove the option that allow user to withdraw his NFT later