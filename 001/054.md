Flaky Sable Hamster

High

# `_listings` mapping is not deleted when a tokenId is reserved in `Listing:reserve()`

## Summary
`_listings` mapping is not deleted when a tokenId is reserved in `Listing:reserve()`, which leads to DoS and loss of protocol funds

## Vulnerability Detail
User can reserve a `tokenId` using `listing:reserve()`, while reserving, previousOwner gets `taxRefund` as well as any amount greater than `floorPrice`

Also it creates a protected listing using `protectedListings.createListings()`
```solidity
 function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
...
        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
@>              (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
@>                  collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }
...
        }
...
        // Create a protected listing, taking only the tokens
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;
        IProtectedListings.CreateListing[] memory createProtectedListing = new IProtectedListings.CreateListing[](1);
        createProtectedListing[0] = IProtectedListings.CreateListing({
            collection: _collection,
            tokenIds: tokenIds,
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: uint96(1 ether - _collateral),
                checkpoint: 0 // Set in the `createListings` call
            })
        });

        // Create our listing, receiving the ERC20 into this contract
@>      protectedListings.createListings(createProtectedListing);
...
    }
```
As we can see above code, it doesn't delete the `_listings` mapping which has details of the `previousOwner/ floorMultiple/ duration`

Now the problem is, previousOwner can call `cancelListing()`, `modifyListings()`
1. modifyListings(): He will modify the listings in such a way by combining `floorMultiple` & `duration`(taxRequired is dependent on both) so that `refund` is greater than `taxRequired`(for new config). As result, he will get a refund, which he should not because all refunds were made to him in reserve().
```solidity
function modifyListings(address _collection, ModifyListing[] calldata _modifyListings, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused returns (uint taxRequired_, uint refund_) {
        uint fees;

        for (uint i; i < _modifyListings.length; ++i) {
...
            // Ensure the caller is the owner of the listing
            if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);
...
            // Collect tax on the existing listing
@>          (uint _fees, uint _refund) = _resolveListingTax(listing, _collection, false);
            emit ListingFeeCaptured(_collection, params.tokenId, _fees);

            fees += _fees;
 @>         refund_ += _refund;
...
            // Get the amount of tax required for the newly extended listing
@>          taxRequired_ += getListingTaxRequired(listing, _collection);
        }

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // If our tax refund does not cover the full amount of tax required, then we will need to make an
        // additional tax payment.
        if (taxRequired_ > refund_) {
            unchecked {
                payTaxWithEscrow(address(collectionToken), taxRequired_ - refund_, _payTaxWithEscrow);
            }
            refund_ = 0;
        } else {
            unchecked {
@>              refund_ -= taxRequired_;
            }
        }
...
        // If there is tax to refund after paying the new tax, then allocate it to the user via escrow
        if (refund_ != 0) {
@>          _deposit(msg.sender, address(collectionToken), refund_);
        }
    }
```
2. cancelListing(): If previousOwner cancels the listing by paying requiredAmount then it will `withdraw` that tokenId(NFT) from the `Locker.sol` contract to previousOwner, which means when `reserver` will try to unlock the protected listing with tokenId(which he reserved) in `ProtectedListings.sol` contract, it will revert because there will be no NFT of that tokenId in Locker.sol to withdraw
```solidity
 function cancelListings(address _collection, uint[] memory _tokenIds, bool _payTaxWithEscrow) public lockerNotPaused {
...
        for (uint i; i < _tokenIds.length; ++i) {
...
            // Transfer the listing ERC721 back to the user
@>          locker.withdrawToken(_collection, _tokenId, msg.sender);
        }
...
    }
```
```solidity
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
...
        // Transfer the listing ERC721 back to the user
        if (_withdraw) {
@>          locker.withdrawToken(_collection, _tokenId, msg.sender);
...
    }
```

## Impact
1. previousOwner will steal the tax
2. He will DoS the reserver, ie reserver will lost his NFT even after paying the full price

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690C4-L759C6
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L303C5-L385C1
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L318

## Tool used
Manual Review

## Recommendation
Delete the ` _listings` mapping when a token is reserved in reserve()
```diff
+  delete _listings[_collection][_tokenId];
```
