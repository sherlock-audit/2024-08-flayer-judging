Tall Ultraviolet Turkey

High

# Unable to cancel the listing after relisting with a future creation timestamp

### Summary

The [relist](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625) function in `Listings.sol` permits users to relist a listing with a future creation timestamp. Nonetheless, following the relisting, the relister is unable to cancel the listing due to arithmetic underflow occurring at the [_resolveListingTax](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L918) function.

### Root Cause

- [The comment](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L855) suggests that the protocol permits users to create a listing with a future timestamp.
> // This is an edge case, but protects against potential future logic. If the
> // listing starts in the future, then we can't sell the listing.

- In the following [code snippet](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L933), the relister encounters difficulty in obtaining the accurate refund amount due to the condition where `block.timestamp < _listing.created`, resulting in an arithmetic underflow.
```solidity
        if (block.timestamp < _listing.created + _listing.duration) {
            refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
        }
```

### External pre-conditions

- The lister creates a listing with a listing multiplier exceeding 1.00.
- The relister then uses the `relist` function, setting a future created timestamp.
- Subsequently, the relister aims to cancel the listing and opts not to sell it.

### Impact

The relister is limited to using the `modifyListings` function to adjust the listing created, ensuring the created timestamp is earlier than the current time to successfully cancel the listing. However, this approach diminishes the significance of the `cancelListings` function, as users are unable to cancel listings directly using it. If relisters must consistently modify listings, it allows others to purchase or exchange the NFT before the relister can call `cancelListings`, regardless of whether the relister possesses sufficient tokens to cancel the listing.


### PoC

```solidity
    function test_CannotCancelAfterRelisting(
        address _lister,
        address payable _relister,
        uint _tokenId,
        uint16 _floorMultiple
    ) public {
        // Ensure that we don't get a token ID conflict
        _assumeValidTokenId(_tokenId);

        // Ensure that we don't set a zero address for our lister and relister, and that they aren't the same address
        _assumeValidAddress(_lister);
        _assumeValidAddress(_relister);
        vm.assume(_lister != _relister);

        // Ensure that our listing multiplier is above 1.00
        _assumeRealisticFloorMultiple(_floorMultiple);

        // Provide a token into the core Locker to create a Floor item
        erc721a.mint(_lister, _tokenId);

        vm.startPrank(_lister);
        erc721a.approve(address(locker), _tokenId);

        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;

        // Rather than creating a listing, we will deposit it as a floor token
        locker.deposit(address(erc721a), tokenIds);
        vm.stopPrank();

        // Confirm that our listing user has received the underlying ERC20. From the deposit this will be a straight 1:1 swap.
        ICollectionToken token = locker.collectionToken(address(erc721a));
        assertEq(token.balanceOf(_lister), 1 ether);

        vm.startPrank(_relister);

        // Provide our relister with sufficient, approved ERC20 tokens to make the relist and make sure relister have enough token to cancel the listing
        deal(address(token), _relister, 2 ether);
        token.approve(address(listings), type(uint).max);

        // Relist our floor item into one of various collections
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IListings.Listing({
                    owner: _relister,
                    created: uint40(block.timestamp + 3 days),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: _floorMultiple
                })
            }),
            _payTaxWithEscrow: false
        });

        // Confirm that the listing has been created with the expected details
        IListings.Listing memory _listing = listings.listings(
            address(erc721a),
            _tokenId
        );
        assertEq(_listing.created, block.timestamp + 3 days);

        // Try to cancel the listing
        // FAIL. Reason: panic: arithmetic underflow or overflow (0x11);
        vm.expectRevert(stdError.arithmeticError);
        listings.cancelListings(
            address(erc721a),
            _tokenIdToArray(_tokenId),
            false
        );
        vm.stopPrank();
    }
```

### Mitigation

Implement logic to accurately calculate the refund amount when a future created timestamp is specified.
```solidity
        if (block.timestamp < _listing.created + _listing.duration) {
            if (block.timestamp < _listing.created) {
                ...
            } else {
                refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
            }
        }
```