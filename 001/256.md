Overt Stone Rat

High

# Mismatch of index and array lengths in `ProtectedListings` checkpoint when actions happen at the same timestamp causes an out of bounds array access revert

## Summary
If `ProtectedListings::_createCheckpoint` is called twice in the same block the second call will return an `index_` that is outside of the `collectionCheckpoints[_collection]` array.

This leads to two problems:
- The user is unable to unlock their token until a new value is pushed to `collectionCheckpoints[_collection]` due to attempted out of bounds array access in `unlockPrice`.
- When the new value is pushed to `collectionCheckpoints[_collection]` the details of the `Checkpoint` will be different to what they should have been when user initially created their listing.

## Vulnerability Detail
In the `ProtectedListings::_createCheckpoint` function, if this `_collection` has been checkpointed already in this block the previous index in `collectionCheckpoints[_collection]` is altered rather than pushing a new item to the array. However it then returns `index_` despite this index not being valid:
```solidity
    if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
        collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
        return index_;
    }

```

This is an issue because in `ProtectedListings::createListings` there is the following line:
```solidity
    checkpointIndex = _createCheckpoint(listing.collection);
```
Then in `_mapListings` this `checkpointIndex` is set as part of the `_protectedListings` mapping as shown here:
```solidity
    _createListing.listing.checkpoint = _checkpointIndex;
    _protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;
```

Now if the user attempts to unlock their token they will run into the follwing issue in `unlockPrice`:
```solidity
    ProtectedListing memory listing = _protectedListings[_collection][_tokenId];
    
    unlockPrice_ = locker.taxCalculator().compound({
        _principle: listing.tokenTaken,
        _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint], // The function will revert here because listing.checkpoint attempts to access an out of bounds array index
        _currentCheckpoint: _currentCheckpoint(_collection)
    });
```

Add the following test to `ProtectedListings.t.sol` to highlight this:
```solidity
    function test_Toad_oobArrayAccess() public {
        // Get user A token
        address userA = makeAddr("userA");
        address userB = makeAddr("userB");
        uint256 _tokenIdA = 1199;
        uint256 _tokenIdB = 9911;
        erc721a.mint(userA, _tokenIdA);
        erc721a.mint(userB, _tokenIdB);

        vm.startPrank(userA);
        erc721a.approve(address(listings), _tokenIdA);

        Listings.Listing memory listingA = IListings.Listing({
            owner: payable(userA),
            created: uint40(block.timestamp),
            duration: VALID_LIQUID_DURATION,
            floorMultiple: 120
        });
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenIdA),
                listing: listingA
            })
        });
        vm.stopPrank();

        vm.warp(block.timestamp + 1 hours);

        // Later on a listing for this collection is created in the same block that a protected listing is created
        vm.startPrank(userB);
        erc721a.approve(address(listings), _tokenIdB);

        Listings.Listing memory listingB = IListings.Listing({
            owner: payable(userB),
            created: uint40(block.timestamp),
            duration: VALID_LIQUID_DURATION,
            floorMultiple: 120
        });
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenIdB),
                listing: listingB
            })
        });
        vm.stopPrank();

        uint256 startBalance = 10 ether;
        ICollectionToken token = locker.collectionToken(address(erc721a));
        deal(address(token), userB, startBalance);

        vm.startPrank(userB);
        token.approve(address(listings), startBalance);
        token.approve(address(protectedListings), startBalance);

        uint256 listingCountStart = listings.listingCount(address(erc721a));

        listings.reserve({
            _collection: address(erc721a),
            _tokenId: _tokenIdA,
            _collateral: 0.2 ether
        });


        // User B later tries to unlock his protected listing but failed due to the out of bounds array index
        vm.warp(block.timestamp + 1 days);
        vm.exectRevert();
        protectedListings.unlockProtectedListing(address(erc721a), _tokenIdA, false);
        vm.stopPrank();
    }
```
## Impact
This will mean that the user will be unable to unlock their token until a new value is pushed to `collectionCheckpoints[_collection]` as their `listing.checkpoint` will then no longer be out of bounds. However this creates a second issue in that this new checkpoint will have a different `_utilizationRate` and more importantly a later `block.timestamp` meaning the user will have to pay less tax than if they had the correct checkpoint index.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L564

## Tool used

Manual Review

## Recommendation
When `_createCheckpoint` is called multiple times in a single block the check for this should return `index_ - 1` to ensure a valid array index is returned. An example of this is shown here:
```diff
    if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
        collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
-       return index_;
+       return index_ - 1;
    }
```

