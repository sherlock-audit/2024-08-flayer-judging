Clean Snowy Mustang

Medium

# Incorrect checkpoint index might be returned when snapshots the current checkpoint

## Summary
Incorrect checkpoint index might be returned when snapshots the current checkpoint.

## Vulnerability Detail

When a listing is created, a checkpoint will be snapshotted for the specified collection.

[Listings.sol#L161-L162](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L161-L162):
```solidity
            // Create our checkpoint as utilisation rates will change
            protectedListings.createCheckpoint(listing.collection);
```

When the listing is reserved and a protected listing is created, protocol will update checkpoint for the collection if it has not been done yet for the listing collection.

[ProtectedListings.sol#L132-L139](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L132-L139):
```solidity
            // Update our checkpoint for the collection if it has not been done yet for
            // the listing collection.
            checkpointKey = keccak256(abi.encodePacked('checkpointIndex', listing.collection));
            assembly { checkpointIndex := tload(checkpointKey) }
            if (checkpointIndex == 0) {
@>              checkpointIndex = _createCheckpoint(listing.collection);
                assembly { tstore(checkpointKey, checkpointIndex) }
            }
```

In [_createCheckpoint()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L530), if there are existing checkpoints, then a new checkpoint will be created and pushed after the previous existing checkpoint, and the index of the new checkpoint is the length of all the existing checkpoints.

[ProtectedListings.sol#L532](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L532):
```solidity
        index_ = collectionCheckpoints[_collection].length;
``` 
[ProtectedListings.sol#L559-L560](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L559-L560):
```solidity
        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);
```
[ProtectedListings.sol#L569-L570](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L569-L570):
```solidity
        // Store the new (current) checkpoint
        collectionCheckpoints[_collection].push(checkpoint);
```

The index of the new checkpoint will be assigned to the protected listing.

[ProtectedListings.sol#L141-L143](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L141-L143):
```solidity
            // Map our listings
            tokensIdsLength = listing.tokenIds.length;
            tokensReceived = _mapListings(listing, tokensIdsLength, checkpointIndex) * 10 ** locker.collectionToken(listing.collection).denomination();
```
[ProtectedListings.sol#L201-L203](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L201-L203):
```solidity
            // Update our request with the current checkpoint and store the listing
            _createListing.listing.checkpoint = _checkpointIndex;
            _protectedListings[_createListing.collection][_createListing.tokenIds[i]] = _createListing.listing;
```

At the same time in `_createCheckpoint()`, If no time has passed, then no new checkpoint is created, protocol only updates the utilization rate of the existing checkpoint.

[ProtectedListings.sol#L562-L567](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L562-L567):
```solidity
        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
@>          return index_;
        }
```

However, the returned checkpoint index is still the length of all the existing checkpoints, and the index will be stored in the protected listing. This is problematic and may cause unexpected errors.

Consider the following scenario:
1. Bob creates a Listing, a new checkpoint is created and checkpoint index is 0;
2. Later Alice submits to reserve Bob's Listing, at the same time, Cathy creates another Listing against the same collection;
3. Cathy's transaction gets executed first, a new checkpoint is created and checkpoint index is 1;
4. Alice's transaction get executed at the same block, hence no time has passed, no new checkpoint is created but 2 is returned as the checkpoint index;
5. At the moment, there are only two checkpoints, the max index is supposed to be 1, however, the protected listing is stored with index 2;
6. As a result, when Alice submits to unlock the protected listing, the transaction will revet due to `out-of-bounds` error when the protocol tries to calculate the unlock price.
[ProtectedListings.sol#L611-L616](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L611-L616):
```solidity
        // Calculate the final amount using the compounded factors and principle amount
        unlockPrice_ = locker.taxCalculator().compound({
            _principle: listing.tokenTaken,
 @>         _initialCheckpoint: collectionCheckpoints[_collection][listing.checkpoint],
            _currentCheckpoint: _currentCheckpoint(_collection)
        });
```

Please run the PoC in ProtectedListings.t.sol to verify:
```solidity
    function testAudit_IncorrectCheckpointIndex() public {
        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        address bob = makeAddr("Bob");
        uint256 tokenId = 888;
        erc721a.mint(bob, tokenId);

        Listings.Listing memory listing = IListings.Listing({
            owner: payable(bob),
            created: uint40(block.timestamp),
            duration: 7 days,
            floorMultiple: 200
        });

        // Bob creates a listing
        vm.startPrank(bob);
        erc721a.approve(address(listings), tokenId);
        IListings.CreateListing[] memory createListings = new IListings.CreateListing[](1);
        createListings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(tokenId),
            listing: listing
        });
        listings.createListings(createListings);
        vm.stopPrank();


        vm.warp(block.timestamp + 1 days);


        address cathy = makeAddr("Cathy");
        erc721a.mint(cathy, 111);

        listing = IListings.Listing({
            owner: payable(cathy),
            created: uint40(block.timestamp),
            duration: 7 days,
            floorMultiple: 200
        });

        // Just before Alice reserves Bob's listing, Cathy happens to create a lising of the same collection
        vm.startPrank(cathy);
        erc721a.approve(address(listings), 111);
        createListings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(111),
            listing: listing
        });
        listings.createListings(createListings);
        vm.stopPrank();


        address alice = makeAddr("Alice");
        mintCollectionTokens(address(erc721a), alice, 1, 3);

        // Alice reserves Bob's listing
        vm.startPrank(alice);
        collectionToken.approve(address(listings), type(uint256).max);
        listings.reserve(address(erc721a), tokenId, 0.2 ether);
        vm.stopPrank();

        vm.warp(block.timestamp + 1 days);

        // Alice tris to unlock the protected listing but failed
        vm.startPrank(alice);
        collectionToken.approve(address(protectedListings), type(uint256).max);
        // panic: array out-of-bounds access
        vm.expectRevert(stdError.indexOOBError);
        protectedListings.unlockProtectedListing(address(erc721a), 888, true);
        vm.stopPrank();
    }

    function mintCollectionTokens(address collection, address to, uint id, uint count) private {
        ERC721Mock token = ERC721Mock(collection);

        uint[] memory depositTokenIds = new uint[](count);

        for (uint i; i < count; ++i) {
            token.mint(address(this), id);
            depositTokenIds[i] = id;
            ++id;
        }

        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(collection, depositTokenIds, to);
    }
```

## Impact

User won't be able to unlock their protected listings.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L564-L567

## Tool used

Manual Review

## Recommendation

If no time has passed when creates checkpoint, return `index - 1` instead of `index`.

[ProtectedListings.sol#L564-L567](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L564-L567):
```diff
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
-           return index_;
+           return index_ - 1;
        }
```