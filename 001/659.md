Brilliant Hickory Tiger

Medium

# User is Unable to Reclaim Vote After Collection Shutdown is Canceled

### Summary

The improper vote reclamation logic in `CollectionShutdown.sol` causes the inability to reclaim staked votes for users, as the contract fails to release the votes when a shutdown is canceled. This results in users losing access to their tokens for an indefinite period.

### Root Cause

- In [`CollectionShutdown.sol:369`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L369), the deletion of the user's vote from the collection shutdown votes is not necessary when the collection shutdown is canceled, leading to the inability to reclaim staked votes.
- In [`CollectionShutdown.sol:373`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L373), the collection token address retrieved from `_collectionParams[_collection]` where it has been removed in the cancellation process in [`CollectionShutdown.sol:403`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L403).

### Internal pre-conditions

1. The total supply of the collectionâ€™s tokens is at most the threshold required to initiate a shutdown.
2. A collection token owner must call `CollectionShutdown.start()` to start the shutdown vote and set the `_collectionParams`.
3. The amount of shutdown votes must meet the quorum requirement, putting the shutdown process in the `canExecute` state.
4. After the vote starts, the total supply of collection tokens exceeds the shutdown threshold (e.g., when additional tokens are minted).
5. A user must call `CollectionShutdown.cancel()` to stop the shutdown and delete `_collectionParams`.

### External pre-conditions

No external pre-conditions required.

### Attack Path

1. User A votes to shut down a collection by calling `CollectionShutdown.start()` and staking their tokens. With 50% of the token total supply, the shutdown enters the `canExecute` state.
2. Another user mints additional tokens, increasing the total supply beyond the shutdown threshold, which allows the shutdown to be canceled.
3. User A attempts to reclaim their staked votes by calling `CollectionShutdown.reclaimVote()` after the shutdown is canceled, but the transaction reverts.

### Impact

Users experience an inability to reclaim staked tokens used in the shutdown vote. This leads to the following potential consequences:

- Temporary loss of funds, though they may eventually be reclaimable as ETH value if the collection is fully liquidated. Also, the user can reclaim their tokens if other users vote the same amount in the next shutdown attempt, potentially shifting the issue to others.
- Loss of governance power, as the locked votes are excluded from future governance decisions.
- If the locked votes exceed the quorum requirement, future shutdowns could be blocked, creating a more significant governance issue.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.22;

import {Deployers} from '@uniswap/v4-core/test/utils/Deployers.sol';

import {CollectionShutdown, ICollectionShutdown} from '@flayer/utils/CollectionShutdown.sol';
import {ICollectionToken} from '@flayer-interfaces/ICollectionToken.sol';

import {FlayerTest} from '../lib/FlayerTest.sol';
import {ERC721Mock} from '../mocks/ERC721Mock.sol';


contract CollectionShutdownPoCTest is Deployers, FlayerTest {
    /// Store our {CollectionToken}
    ICollectionToken collectionToken;

    constructor () forkBlock(19_425_694) {
        // Deploy our platform contracts
        _deployPlatform();

        // Define our `_poolKey` by creating a collection. This uses `erc721b`, as `erc721a`
        // is explicitly created in a number of tests.
        locker.createCollection(address(erc721b), 'Test Collection', 'TEST', 0);

        // Initialize our collection, without inflating `totalSupply` of the {CollectionToken}
        locker.setInitialized(address(erc721b), true);

        // Set our collection token for ease for reference in tests
        collectionToken = locker.collectionToken(address(erc721b));

        // Approve our shutdown contract to use test suite's tokens
        collectionToken.approve(address(collectionShutdown), type(uint).max);
    }

    function testPOC_CannotReclaimVoteAfterCollectionShutdownCanceled() public {
        // Mint collection token for address 1 and 2
        vm.startPrank(address(locker));
        collectionToken.mint(address(1), 2 ether);
        collectionToken.mint(address(2), 2 ether);
        vm.stopPrank();

        // Start our vote from address(1)
        vm.startPrank(address(1));
        collectionToken.approve(address(collectionShutdown), 2 ether);
        collectionShutdown.start(address(erc721b));
        vm.stopPrank();

        // Address 2 mint more collection token
        vm.prank(address(locker));
        collectionToken.mint(address(2), 1 ether);

        // Now, collection shutdown can be caneled since the total supply > 4 eth
        vm.prank(address(locker));
        collectionShutdown.cancel(address(erc721b));
        // Checks that shutdownParams has been deleted along with its shutdownVotes
        ICollectionShutdown.CollectionShutdownParams memory shutdownParams = collectionShutdown.collectionParams(address(erc721b));
        assertEq(shutdownParams.shutdownVotes, 0);
        // While address 1 voted token amount in the contract and h
        assertEq(collectionShutdown.shutdownVoters(address(erc721b), address(1)), 2 ether);
        assertEq(collectionToken.balanceOf(address(collectionShutdown)), 2 ether);

        // Address 1 will not be able to reclaim vote
        vm.expectRevert(); // [Revert] panic: arithmetic underflow or overflow (0x11)
        vm.prank(address(1));
        collectionShutdown.reclaimVote(address(erc721b));

        // This vote amount is stale in the contract,
        // If collection shutdown started again, address(1) vote is not accountable in quorum and,
        // if he reclaim his vote (only if another user vote an amount >= address(1) vote) another user will suffer
        // Which generate another issue that can be used to prevent a collection from being shutdown,
        // this can happen only if the stuck amount in collectionShutdown is more than the quorum vote requirement.
    }
}
```

### Mitigation

To resolve the issue, update the `CollectionShutdown.reclaimVote()` function to allow users to retrieve their staked tokens even after a collection shutdown is canceled.

```diff
diff --git a/flayer/src/contracts/utils/CollectionShutdown.sol b/flayer/src/contracts/utils/CollectionShutdown.sol
index 2442e70..ae0e893 100644
--- a/flayer/src/contracts/utils/CollectionShutdown.sol
+++ b/flayer/src/contracts/utils/CollectionShutdown.sol
@@ -366,11 +366,17 @@ contract CollectionShutdown is ICollectionShutdown, Ownable, Pausable, Reentranc
         if (userVotes == 0) revert NoVotesPlacedYet();

         // We delete the votes that the user has attributed to the collection
-        params.shutdownVotes -= uint96(userVotes);
         delete shutdownVoters[_collection][msg.sender];
-
-        // We can now return their tokens
-        params.collectionToken.transfer(msg.sender, userVotes);
+        if (params.shutdownVotes > 0) {
+            params.shutdownVotes -= uint96(userVotes);
+
+            // We can now return their tokens
+            params.collectionToken.transfer(msg.sender, userVotes);
+        } else {
+            // In case collection shutdown is canceled,
+            // we retrieve tokens to user.
+            locker.collectionToken(_collection).transfer(msg.sender, userVotes);
+        }

         // Notify our stalkers that a vote has been reclaimed
         emit CollectionShutdownVoteReclaim(_collection, msg.sender, userVotes);
(END)
```