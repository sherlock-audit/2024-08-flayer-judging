Shiny Mint Lion

High

# The relist() function lacks a check on listing.created, which allows borrowing money from the listing without incurring interest.

## Summary
The relist() function lacks a check on listing.created, which allows borrowing money from the listing without incurring interest.
## Vulnerability Detail
ProtectedListings is designed for users who do not want to sell their NFTs. Through ProtectedListings, users can borrow up to 0.95 ether worth of collectionToken. To retrieve their NFT, they must repay the full principal and interest, and there is also a risk of the NFT being liquidated.

However, due to a vulnerability in the relist() function, it is possible to borrow money from the listing without paying interest and without worrying about the risk of liquidation.
```javascript
function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
        // Load our tokenId
        address _collection = _listing.collection;
        uint _tokenId = _listing.tokenIds[0];

        // Read the existing listing in a single read
        Listing memory oldListing = _listings[_collection][_tokenId];

        // Ensure the caller is not the owner of the listing
        if (oldListing.owner == msg.sender) revert CallerIsAlreadyOwner();

        // Load our new Listing into memory
    @>>    Listing memory listing = _listing.listing;

        // Ensure that the existing listing is available
        (bool isAvailable, uint listingPrice) = getListingPrice(_collection, _tokenId);
        if (!isAvailable) revert ListingNotAvailable();

        // We can process a tax refund for the existing listing
        (uint _fees,) = _resolveListingTax(oldListing, _collection, true);
        if (_fees != 0) {
            emit ListingFeeCaptured(_collection, _tokenId, _fees);
        }

        // Find the underlying {CollectionToken} attached to our collection
        ICollectionToken collectionToken = locker.collectionToken(_collection);

        // If the floor multiple of the original listings is different, then this needs
        // to be paid to the original owner of the listing.
        uint listingFloorPrice = 1 ether * 10 ** collectionToken.denomination();
        if (listingPrice > listingFloorPrice) {
            unchecked {
                collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
            }
        }

        // Validate our new listing
        _validateCreateListing(_listing);

        // Store our listing into our Listing mappings
    @>>    _listings[_collection][_tokenId] = listing;

        // Pay our required taxes
        payTaxWithEscrow(address(collectionToken), getListingTaxRequired(listing, _collection), _payTaxWithEscrow);

        // Emit events
        emit ListingRelisted(_collection, _tokenId, listing);
    }


```
The root cause is that there is no check on listing.created, and the userâ€™s input parameters are directly assigned to _listings[_collection][_tokenId]. As a result, an attacker can set listing.created to a future value, ensuring that the NFT will not be bought by anyone.
```javascript
function getListingPrice(address _collection, uint _tokenId) public view returns (bool isAvailable_, uint price_) {
        // If the token is not currently held in our {Locker}, then the asset is not
        // currently available to be purchased.
        if (IERC721(_collection).ownerOf(_tokenId) != address(locker)) {
            return (isAvailable_, price_);
        }

        // Check if we have a protected listing attributed to this listing
        IProtectedListings.ProtectedListing memory protectedListing = protectedListings.listings(_collection, _tokenId);
        if (protectedListing.owner != address(0)) {
            return (false, price_);
        }

        // Load our collection into memory using a single read
        Listing memory listing = _listings[_collection][_tokenId];

        // Get our collection token's base price, accurate to the token's denomination
        price_ = 1 ether * 10 ** locker.collectionToken(_collection).denomination();

        // If we don't have a listing object against the token ID, then we just consider
        // it to be a Floor level asset.
        if (listing.owner == address(0)) {
            return (true, price_);
        }

        // Determine the listing price based on the floor multiple. If this is a dutch
        // listing then further calculations will be applied later.
        uint totalPrice = (price_ * uint(listing.floorMultiple)) / MIN_FLOOR_MULTIPLE;

        // This is an edge case, but protects against potential future logic. If the
        // listing starts in the future, then we can't sell the listing.
        if (listing.created > block.timestamp) {
@>>            return (isAvailable_, totalPrice);
        }
      //----skip--- 
} 
```
Since the getListingPrice() function checks if listing.created > block.timestamp, it will return isAvailable_ = false, making the NFT unsellable.
#### POC
Assuming the attacker possesses 1 NFT and 2 ether  of collectionToken, the attack steps are as follows:

	1.	Using Account A, the attacker lists the NFT with a price of 3 ether in collectionToken.
According to the createListings() function, Account A will receive 1 ether in collectionToken.

	2.	Transfer all of the collectionToken (3 ether) to Account B.

	3.	Using Account B, the attacker calls relist() for this NFT, setting listing.created to one month in the future and setting the price to 5 ether in collectionToken.
At this point, Account A will receive 3 ether in collectionToken.

	4.	One month later, Account B cancels the listing and repays 1 ether in collectionToken. At this point, block.timestamp = _listing.created, and the refund will equal taxPaid, so no tax will need to be paid.

The steps 1-3 can all be completed within a single block, which means no tax will need to be paid at all.
The attacker effectively borrows 1 ether in collectionToken for free for one month.
```javascript
function _resolveListingTax(Listing memory _listing, address _collection, bool _action) private returns (uint fees_, uint refund_) {
        // If we have been passed a Floor item as the listing, then no tax should be handled
        if (_listing.owner == address(0)) {
            return (fees_, refund_);
        }

        // Get the amount of tax in total that will have been paid for this listing
        uint taxPaid = getListingTaxRequired(_listing, _collection);
        if (taxPaid == 0) {
            return (fees_, refund_);
        }

        // Get the amount of tax to be refunded. If the listing has already ended
        // then no refund will be offered.
        if (block.timestamp < _listing.created + _listing.duration) {
@>>            refund_ = (_listing.duration - (block.timestamp - _listing.created)) * taxPaid / _listing.duration;
        }

        // Send paid tax fees to the {FeeCollector}
        unchecked {
            fees_ = (taxPaid > refund_) ? taxPaid - refund_ : 0;
        }

        if (_action) {
            ICollectionToken collectionToken = locker.collectionToken(_collection);

            if (fees_ != 0) {
                IBaseImplementation implementation = locker.implementation();

                collectionToken.approve(address(implementation), fees_);
                implementation.depositFees(_collection, 0, fees_);
            }

            // If there is tax to refund, then allocate it to the user via escrow
            if (refund_ != 0) {
                _deposit(_listing.owner, address(collectionToken), refund_);
            }
        }
    }
```

## Impact
The attacker can borrow funds from the protocol without paying any interest, leading to losses for the protocol.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L826

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L130C14-L130C28

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L918
## Tool used

Manual Review

## Recommendation
```diff
-                   _listings[_collection][_tokenId] = listing;
+                   _listings[_collection][_tokenId] = Listing({
+                       owner: listing.owner,
+                       created: uint40(block.timestamp),
+                       duration: listing.duration,
+                       floorMultiple: listing.floorMultiple
+                   });
```