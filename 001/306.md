Striped Boysenberry Fox

Medium

# Incorrect checkpoint index is possible during creation of protected listings.

## Summary

The `_createCheckpoint()` function returns incorrect index when the current timestamp is same with the one of the last checkpoint. This may cause an incorrect checkpoint index when creating a protected listing with multiple NFTs in the same timestamp

## Vulnerability Detail

The `_createCheckpoint()` function is intended to return the index of the last checkpoint.

However when the current timestamp is same with the last checkpoint's one, it returns the length of the checkpoints instead of the last index.

```solidity
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        ... ...
        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
@>          return index_;
        }
        ... ...
    }
```

Let's assume the following scenario: 

A user is going to create a protected listing with two NFTs (token Ids: 1, 2) of a single collection.

1. In the first loop of `ProtectedListings::createListings()`, `_protectedListings[_collection][1]` becomes 0 because the collection is new to protected listings.
2. In the second loop, `_protectedListings[_collection][2]` becomes 1 because of the issued return. However, the checkpoint index `1` for the collection is out-of-index because the length of checkpoints is `1`.

### Proof-Of-Concept

For visual effect for testing, the new function to reveal collection checkpoints is added to the `ProtectedListings.sol`:
```solidity
    // @audit-add
    function getCollectionCheckpoints(address _collection) public view returns (IProtectedListings.Checkpoint[] memory) {
        return collectionCheckpoints[_collection];
    }
```

And then new test case is added to `ProtectedListings.t.sol`. The test case is trying to create two protected listings with two NFTs in the same timestamp:

```solidity
    // @audit-poc
    function test_CreateProtectedListingCheckpoints() public {
        address payable _owner = payable(address(0x101));
        address _collection = address(erc721a);

        // Ensure that we don't set a zero address _owner
        _assumeValidAddress(_owner);

        uint256 tokenId1 = 1;
        uint256 tokenId2 = 2;

        erc721a.mint(_owner, tokenId1);
        erc721a.mint(_owner, tokenId2);

        vm.startPrank(_owner);
        erc721a.approve(address(protectedListings), tokenId1);
        erc721a.approve(address(protectedListings), tokenId2);

        IProtectedListings.ProtectedListing memory listing = IProtectedListings.ProtectedListing({
            owner: _owner,
            tokenTaken: 0.4 ether,
            checkpoint: 0
        });

        // Confirm that our expected event it emitted
        vm.expectEmit();
        emit ProtectedListings.ListingsCreated(_collection, _tokenIdToArray(tokenId1), listing, 0.4 ether, _owner);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: _collection,
                tokenIds: _tokenIdToArray(tokenId1),
                listing: listing
            })
        });

        vm.expectEmit();
        emit ProtectedListings.ListingsCreated(_collection, _tokenIdToArray(tokenId2), listing, 0.4 ether, _owner);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: _collection,
                tokenIds: _tokenIdToArray(tokenId2),
                listing: listing
            })
        });
        vm.stopPrank();

        IProtectedListings.ProtectedListing memory listing1 = protectedListings.listings(_collection, tokenId1);
        console.log("Checkpoint Index for NFT 1:", listing1.checkpoint);

        IProtectedListings.ProtectedListing memory listing2 = protectedListings.listings(_collection, tokenId2);
        console.log("Checkpoint Index for NFT 2:", listing2.checkpoint);

        IProtectedListings.Checkpoint[] memory checkpoints = protectedListings.getCollectionCheckpoints(_collection);
        console.log("Length of the checkpoints:", checkpoints.length);

        vm.expectRevert(stdError.indexOOBError);
        protectedListings.getProtectedListingHealth(_collection, tokenId2); // @audit NFT2 is frozen due to OOB revert
    }
```

And here are the logs:
```bash
$ forge test --match-test test_CreateProtectedListingCheckpoints -vv
[⠒] Compiling...
[⠰] Compiling 1 files with Solc 0.8.26
[⠔] Solc 0.8.26 finished in 9.30s
Compiler run successful!

Ran 1 test for test/ProtectedListings.t.sol:ProtectedListingsTest
[PASS] test_CreateProtectedListingCheckpoints() (gas: 769396)
Logs:
  Checkpoint Index for NFT 1: 0
  Checkpoint Index for NFT 2: 1
  Length of the checkpoints: 1

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.36ms (995.10µs CPU time)

Ran 1 test suite in 8.63ms (6.36ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

As can be seen from the logs, the NFT 2 becomes frozen because of out-of-bound revert and it will be available after new checkpoint is added to the collection checkpoint. Even though the NFT 2 becomes free from frozen, it becomes to indicate a wrong checkpoint.

## Impact

Such a second NFT becomes frozen till new checkpoint is added, and even after being released from frozen, the NFT will have miscalculated unlocking price and health factor because of wrong checkpoint index.

## Code Snippet

[ProtectedListings.sol#L566](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L566)

## Tool used

Manual Review

## Recommendation

Should return `index_ - 1` in the issued line:

```diff
    function _createCheckpoint(address _collection) internal returns (uint index_) {
        ... ...
        // Get our new (current) checkpoint
        Checkpoint memory checkpoint = _currentCheckpoint(_collection);

        // If no time has passed in our new checkpoint, then we just need to update the
        // utilization rate of the existing checkpoint.
        if (checkpoint.timestamp == collectionCheckpoints[_collection][index_ - 1].timestamp) {
            collectionCheckpoints[_collection][index_ - 1].compoundedFactor = checkpoint.compoundedFactor;
-           return index_;
+           return index_ - 1;
        }
        ... ...
    }
```
