Overt Stone Rat

High

# Users collection tokens are stuck in `CollectionShutdown` contract if the shutdown is cancelled after they place their vote

## Summary
During the process of sunsetting a collection in `CollectionShutdown` users can vote for a collection to be shutdown using their collection tokens tied to the collection in question. However if the total supply of the token passes `MAX_SHUTDOWN_TOKENS` before the vote reaches quorum, then anyone is able to call `cancel` which ends the process of sunsetting the collection. When this happens the tokens of the users who voted to sunset the collection will be permanently locked inside the `CollectionShutdown` contract.

## Vulnerability Detail
When `cancel` is called in `CollectionShutdown` the `collectionParams[_collection]` mapping is deleted. Therefore when a user who had voted to shutdown the collection attempts to call `reclaimVote` their call will revert when trying to deduct `userVotes` from `params.shutdownVotes` with an underflow error as `params.shutdownVotes` will be zero after the mapping has been deleted.
```solidity
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (params.canExecute) revert ShutdownQuorumHasPassed();

        // Get the amount of votes that the user has cast for this collection
        uint userVotes = shutdownVoters[_collection][msg.sender];

        // If the user has not cast a vote, then we can revert early
        if (userVotes == 0) revert NoVotesPlacedYet();

        // We delete the votes that the user has attributed to the collection
-->     params.shutdownVotes -= uint96(userVotes);
```

Add the following test to `CollectionShutdown.t.sol` for evidence of this:
```solidity
    function test_Toad_votesStuckAfterCancel() public {
        // Mint some tokens to our test users
        address userA = makeAddr("userA");
        address userB = makeAddr("userB");
        _distributeCollectionTokens(collectionToken, userA, 1 ether, 1 ether);
        _distributeCollectionTokens(collectionToken, userB, 1 ether, 1 ether);

        // User A starts collection shutdown and deposits their tokens into the contract
        vm.startPrank(userA);
        collectionToken.approve(address(collectionShutdown), 1 ether);
        collectionShutdown.start(address(erc721b));
        vm.stopPrank();

        assertEq(collectionToken.balanceOf(userA), 0);

        // Collection Token total supply grows (NFTs deposited into protocol)
        _distributeCollectionTokens(collectionToken, userB, 3 ether, 3 ether);
        assert(collectionToken.totalSupply() > 4 ether);

        // Can now cancel the shutdown
        vm.prank(userB);
        collectionShutdown.cancel(address(erc721b));

        // Confirm User A balance still 0 & collectionShutdown balance 1 ether
        assertEq(collectionToken.balanceOf(userA), 0);
        assertEq(collectionToken.balanceOf(address(collectionShutdown)), 1 ether);

        // User A now has no way to withdraw their tokens, underflow revert will occur in CollectionShutdown::reclaimVote
        vm.prank(userA);
        vm.expectRevert();
        collectionShutdown.reclaimVote(address(erc721b));
    }
```

## Impact
Users who vote to shutdown a collection will permanently lose their collection tokens if the shutdown attempt is cancelled before they call `reclaimVote` as there is no way for them to withdraw their vote after it's cancellation.

## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L403

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L369

## Tool used

Manual Review

## Recommendation
The protocol should implement functionality for a user to safely withdraw their tokens in the event that the shutdown vote is cancelled before they can withdraw their votes.