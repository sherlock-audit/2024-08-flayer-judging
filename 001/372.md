Crazy Chiffon Spider

High

# Locker is not setting checkpoints when minting/burning new supply of CT tokens, which can lead to loss of interest rate.

## Summary
Checkpoint creation in `ProtectedListing.sol` is used to accurately adjust the compounding factor based on the utilization rate, its also called in Listings.sol when total supply of `CT` is changed, however `Locker.sol` does not call it, even though it can affect the utilizationRate calculation in the "collection pool", which leads to loss of interest rate.

**Disclaimer**: the required behaviour as mentioned from the comments, is that we should create checkpoints when utilization rate change. As this one from `Listings.sol`
> // Create our checkpoint as utilization rates will change

## Vulnerability Detail

`Locker.sol` allows for NFT holders of specific collection to deposit their NFTs, which would be worth floor price, into the contract, and get `CT` tokens in exchange, which will be minted.

As we can see in `Listings.sol` and `ProtectedListing.sol` such changes are handled with `protectedListings.createCheckpoint();` as they will affect the interest rate, because the compound factor is calculated with the utilization rate:

In [Listings.cancenListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L466-L467) and in [Listings.fillListings()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L602-L603) we have:
```solidity
@>> // Create our checkpoint as utilization rates will change
@>> protectedListings.createCheckpoint(listing.collection);
```

But in Locker.sol in [deposit()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L163) and [redeem()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L209-L230) we lack such a call even though its needed to keep accurate interest rate. 

It does need to be a **liquid**/**dutch**/**lockbox** listing to affect the utilization rate as its also **affected from totalSupply**, thats why we should create checkpoints in `Locker.sol`. - Utilization rate code snipper [here](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L273)
### Coded PoC
Add this to `ProtectedListings.t.sol` and run `forge test --match-test test_LockerAffectsInterestRate  -vvv`
```solidity
    function test_LockerAffectsInterestRate() public {
        // ===--> Setup <--===
        uint startTimeStamp = 1641070800;
        vm.warp(startTimeStamp);
        ICollectionToken token = locker.collectionToken(address(erc721a));

        uint tokenId = 20;
        uint secondTokenId = 21;
        erc721a.mint(address(this), tokenId);
        erc721a.mint(address(this), secondTokenId);
        
        assertEq(erc721a.totalSupply(), 12);

        uint[] memory depositIds = new uint[](1);
        depositIds[0] = secondTokenId;
        erc721a.approve(address(locker), secondTokenId);
        locker.deposit(address(erc721a), depositIds);

        assertEq(token.totalSupply(), 11e18);
        // ===--> Setup <--===

        //Create 1 protected listing so we can track the interest rate
        erc721a.approve(address(protectedListings), tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(tokenId),
                listing: IProtectedListings.ProtectedListing({
                    owner: payable(address(this)),
                    tokenTaken: 0.4 ether,
                    checkpoint: 0
                })
            })
        });
        
        //===================
        //----STEP ONE----
        //===================
        assertEq(protectedListings.unlockPrice(address(erc721a), tokenId), 0.4 ether);
        
        skip(1 minutes);

        //199391171976 compound fee accrued for 1 minute for 11e18 total supply
        uint256 firstInterestRateForMinute = 199391171976; 
        uint256 unlockPriceSave1 = protectedListings.unlockPrice(address(erc721a), tokenId);
        assertEq(unlockPriceSave1, 0.4 ether + firstInterestRateForMinute);     

        skip(1 minutes);

        //Try again to ensure that interest rate is calculated correctly
        uint256 unlockPriceSave2 = protectedListings.unlockPrice(address(erc721a), tokenId);
        assertEq(unlockPriceSave2 - unlockPriceSave1, 199391171976);

        // Now redeem NFT, this will affect the utilization rate as CT will be burned
        token.approve(address(locker), 1e18);
        locker.redeem(address(erc721a), depositIds);

        //Interest rate immediately increases after CT is burned as unlockPrice factor LIVE utilization rate
        //Interest rate is always increased relative to the last checkpoint's timestamp.
        uint256 unlockPriceSave3 = protectedListings.unlockPrice(address(erc721a), tokenId);
        assertEq(unlockPriceSave3, 0.4 ether + 407914764048); 
        // 407914764048 / 2 = 203957382024 New interest rate for per minutes since last checkpoint ( 2 mins ago )

        skip(1 minutes);

        uint256 unlockPriceSave4 = protectedListings.unlockPrice(address(erc721a), tokenId);
        assertEq(unlockPriceSave4 - unlockPriceSave3, 203957382024);

        //===================
        //----STEP TWO----
        //===================
        // Now let's test that again, but by comparing if we were calling createCheckpoint() after each change of supply
        // We can use burn as it would be the same as someone calling redeem()
        // We want to burn a lot so utilization rate becomes high, over the 0.8 kink. 
        // It's not necessary that we have to burn to get more utilization rate, we could also have more listings.
        // For this test we will burn, as it's easier.
        token.burn(9.75e18);

        uint256 interestRateFor1MinuteAfterBurn = 608828006088;
        uint256 unlockPriceSave5WithoutCheckpoint = protectedListings.unlockPrice(address(erc721a), tokenId); 
        assertEq(unlockPriceSave5WithoutCheckpoint, 0.4 ether + interestRateFor1MinuteAfterBurn * 3); // 1826484018264 / 3 = 608828006088 INTEREST RATE ( 3 minutes since last checkpoint )

        // We skip 10 minutes, total interest rates should be interestRateFor1MinuteAfterBurn * 13 as 13 minutes passed since last checkpoint
        skip(10 minutes);

        uint256 saveInterestRateAfter10MinNoCheckpoint = protectedListings.unlockPrice(address(erc721a), tokenId) - 0.4 ether;
        assertEq(protectedListings.unlockPrice(address(erc721a), tokenId), 0.4 ether + interestRateFor1MinuteAfterBurn * 13); 

        //===================
        //----STEP THREE----
        //===================
        //Now lets replay step 2, by rolling by 10 minutes back, so we could compare with checkpoint and without checkpoint
        vm.warp(startTimeStamp + 3 minutes);
        vm.prank(address(listings));
        protectedListings.createCheckpoint(address(erc721a));

        skip(10 minutes);

        uint256 saveTotalInterestRateAfter10MinWithCheckpoint = protectedListings.unlockPrice(address(erc721a), tokenId) - 0.4 ether;
        assertTrue(saveTotalInterestRateAfter10MinWithCheckpoint > saveInterestRateAfter10MinNoCheckpoint);

        //Assert loss for just 10 minutes
        assertEq(saveTotalInterestRateAfter10MinWithCheckpoint - saveInterestRateAfter10MinNoCheckpoint, 27800365);
    }
```
## Impact
Loss of interest rate, which can accrue overtime.

## Tool used

Manual Review

## Recommendation
Adjust the code in `Locker.sol` for `redeem()` and `deposit()` as follows:
```diff
    function redeem(address _collection, uint[] calldata _tokenIds, address _recipient) public nonReentrant whenNotPaused collectionExists(_collection) {
        uint tokenIdsLength = _tokenIds.length;
        if (tokenIdsLength == 0) revert NoTokenIds();

        ICollectionToken collectionToken_ = _collectionToken[_collection];
        collectionToken_.burnFrom(msg.sender, tokenIdsLength * 1 ether * 10 ** collectionToken_.denomination());

+       _listings.protectedListings().createCheckpoint(_collection);
....
```

```diff
    function deposit(address _collection, uint[] calldata _tokenIds, address _recipient) public {
    ...Skipping Code....
        // Mint the tokens to the recipient
        ICollectionToken token = _collectionToken[_collection];
        token.mint(_recipient, tokenIdsLength * 1 ether * 10 ** token.denomination());

+       _listings.protectedListings().createCheckpoint(_collection);

        emit TokenDeposit(_collection, _tokenIds, msg.sender, _recipient);
    }
```

Adjust the code in `ProtectedListings.sol` as follows:
```diff
    function createCheckpoint(address _collection) public returns (uint index_) {
-        if (msg.sender != address(_listings)) revert CallerIsNotListingsContract();
+       if (msg.sender != address(_listings) || msg.sender != address(_locker)) revert CallerIsNotListingsOrLockerContract();
        return _createCheckpoint(_collection);
    }
```    