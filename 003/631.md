Silly Chocolate Vulture

High

# Anyone can call `claimRoyalties` in `InfernalRiftBelow` contract

## Summary
The access control of the `claimRoyalties` function in the `InfernalRiftBelow` contract is invalid.  
Causing anyone to call the `claimRoyalties` function to steal royalties from the contract.  

## Vulnerability Detail
```solidity
    function claimRoyalties(address _collectionAddress, address _recipient, address[] calldata _tokens) public {
        // Ensure that our message is sent from the L1 domain messenger
        if (ICrossDomainMessenger(msg.sender).xDomainMessageSender() != INFERNAL_RIFT_ABOVE) {
            revert CrossChainSenderIsNotRiftAbove();
        }

        // Get our L2 address from the L1
        if (!isDeployedOnL2(_collectionAddress, false)) revert L1CollectionDoesNotExist();

        // Call our ERC721Bridgable contract as the owner to claim royalties to the recipient
        ERC721Bridgable(l2AddressForL1Collection(_collectionAddress, false)).claimRoyalties(_recipient, _tokens);
        emit RoyaltyClaimFinalized(_collectionAddress, _recipient, _tokens);
    }

```
The check of the `claimRoyalties` function is invalid. Any contract that implements `ICrossDomainMessenger:: xDomainMessenger Sender()` and returns the correct `INFERNAL-RIFT-ABOVE` can call this function  

## Impact
This will allow malicious individuals to deploy malicious `ICrossDomainMessengersteal` contract to steal royalties from related collections
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/moongate/src/InfernalRiftBelow.sol#L222C73-L222C92
## Tool used

Manual Review

## Recommendation
Need to check if the caller is `L2_CROSS_DOMAIN_MESSENGER`
```diff
    function claimRoyalties(address _collectionAddress, address _recipient, address[] calldata _tokens) public {
+       // Validate caller is cross-chain
+       if (msg.sender != L2_CROSS_DOMAIN_MESSENGER) {
+           revert NotCrossDomainMessenger();
+       }

        // Ensure that our message is sent from the L1 domain messenger
        if (ICrossDomainMessenger(msg.sender).xDomainMessageSender() != INFERNAL_RIFT_ABOVE) {
            revert CrossChainSenderIsNotRiftAbove();
        }

        // Get our L2 address from the L1
        if (!isDeployedOnL2(_collectionAddress, false)) revert L1CollectionDoesNotExist();

        // Call our ERC721Bridgable contract as the owner to claim royalties to the recipient
        ERC721Bridgable(l2AddressForL1Collection(_collectionAddress, false)).claimRoyalties(_recipient, _tokens);
        emit RoyaltyClaimFinalized(_collectionAddress, _recipient, _tokens);
    }
```