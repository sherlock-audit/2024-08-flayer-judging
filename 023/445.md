Flaky Taupe Platypus

Medium

# Vulnerability in beforeSwap Function Leading to Price Manipulation via sqrtPriceX96 Exploit.

## Summary

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490-L580

there is an issue in [beforeSwap](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L490-L580) and the issue occurs from fetching sqrtPriceX96 value from `(uint160 sqrtPriceX96,,,) = poolManager.getSlot0(poolId);`. 
This makes the protocol vulnerable to price manipulation, potentially causing the protocol to perform trades at unfavorable prices and leading to a loss of funds.

## Vulnerability Detail
The beforeSwap function fetches the sqrtPriceX96 value using `poolManager.getSlot0(poolId)` to determine the current pool price. 
However, since this value reflects the most recent state of the pool, it can be easily manipulated by external actors. 
An attacker can influence this value through techniques like sandwich attack, artificially inflating 
or deflating the price.

POC:
```solidity
function beforeSwap(address sender, PoolKey calldata key, IPoolManager.SwapParams memory params, bytes calldata hookData) public override onlyByPoolManager returns (bytes4 selector_, BeforeSwapDelta beforeSwapDelta_, uint24 swapFee_) {
      PoolId poolId = key.toId();

      // Ensure our dynamic fees are set to the correct amount and mark it with the override flag
      swapFee_ = getFee(poolId, sender) | LPFeeLibrary.OVERRIDE_FEE_FLAG;

      // Load our PoolFees as storage as we will manipulate them later if we trigger
      ClaimableFees storage pendingPoolFees = _poolFees[poolId];
      PoolParams memory poolParams = _poolParams[poolId];

      // We want to check if our token0 is the eth equivalent, or if it has swapped to token1
      bool trigger = poolParams.currencyFlipped ? !params.zeroForOne : params.zeroForOne;
      if (trigger && pendingPoolFees.amount1 != 0) {
          // Set up our internal logic variables
          uint ethIn;
          uint tokenOut;

          // Get the current price for our pool
@>>>      (uint160 sqrtPriceX96,,,) = poolManager.getSlot0(poolId);

          // Since we have a positive amountSpecified, we can determine the maximum
          // amount that we can transact from our pool fees. We do this by taking the
          // max value of either the pool fees or the amount specified to swap for.
          if (params.amountSpecified >= 0) {
              uint amountSpecified = (uint(params.amountSpecified) > pendingPoolFees.amount1) ? pendingPoolFees.amount1 : uint(params.amountSpecified);

              // Capture the amount of desired token required at the current pool state to
              // purchase the amount of token speicified, capped by the pool fees available. We
              // don't apply a fee for this as it benefits the ecosystem and essentially performs
              // a free swap benefitting both parties.
              (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
                  sqrtPriceCurrentX96: sqrtPriceX96,
                  sqrtPriceTargetX96: params.sqrtPriceLimitX96,
                  liquidity: poolManager.getLiquidity(poolId),
                  amountRemaining: int(amountSpecified),
                  feePips: 0
              });

              // Update our hook delta to reduce the upcoming swap amount to show that we have
              // already spent some of the ETH and received some of the underlying ERC20.
              beforeSwapDelta_ = toBeforeSwapDelta(-tokenOut.toInt128(), ethIn.toInt128());
          }
          // As we have a negative amountSpecified, this means that we are spending any amount
          // of token to get a specific amount of undesired token.
          else {
              (, ethIn, tokenOut, ) = SwapMath.computeSwapStep({
                  sqrtPriceCurrentX96: sqrtPriceX96,
                  sqrtPriceTargetX96: params.sqrtPriceLimitX96,
                  liquidity: poolManager.getLiquidity(poolId),
                  amountRemaining: int(pendingPoolFees.amount1),
                  feePips: 0
              });

              // If we cannot fulfill the full amount of the internal orderbook, then we want
              // to avoid using any of it, as implementing proper support for exact input swaps
              // is significantly difficult when we want to restrict them by the output token
              // we have available.
              if (tokenOut <= uint(-params.amountSpecified)) {
                  // Update our hook delta to reduce the upcoming swap amount to show that we have
                  // already spent some of the ETH and received some of the underlying ERC20.
                  // Specified = exact input (ETH)
                  // Unspecified = token1
                  beforeSwapDelta_ = toBeforeSwapDelta(ethIn.toInt128(), -tokenOut.toInt128());
              } else {
                  ethIn = tokenOut = 0;
              }
          }

          // Reduce the amount of fees that have been extracted from the pool and converted
          // into ETH fees.
          if (ethIn != 0 || tokenOut != 0) {
              pendingPoolFees.amount0 += ethIn;
              pendingPoolFees.amount1 -= tokenOut;

              // Transfer the tokens to our PoolManager, which will later swap them to our user
              if (poolParams.currencyFlipped) {
                  poolManager.take(key.currency1, address(this), ethIn);
                  _pushTokens(key.currency0, tokenOut);
              } else {
                  poolManager.take(key.currency0, address(this), ethIn);
                  _pushTokens(key.currency1, tokenOut);
              }

              // Capture the swap cost that we captured from our drip
              emit PoolFeesSwapped(poolParams.collection, params.zeroForOne, ethIn, tokenOut);
          }
      }

      // Set our return selector
      selector_ = IHooks.beforeSwap.selector;
}
```

## Impact
The calculation relies on the value of sqrtPriceX96, which can be manipulated through external transactions. 
An attacker can adjust this price using a sandwich attack, gaining an advantage in the swap process.


## Recommendation

Use TWAP to get the value of sqrtPriceX96.

