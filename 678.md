Massive Emerald Python

High

# The formula for charging interest on reserved assets, charges much more than it should

### Summary

The protocol allows users to reserve NFTs that they can't fully buy at the moment via the [reserve()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Listings.sol#L690-L759) function, users have to provide some collateral, and pay interest rates. The calculations for the interest rates rely on several factors, one of them is the utilization rate calculated in the [utilizationRate()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L261-L276) function:
```solidity
     /// @return utilizationRate_ The utilization rate percentage of the listing type (80% = 0.8 ether) 
    function utilizationRate(address _collection) public view virtual returns (uint listingsOfType_, uint utilizationRate_) {
        // Get the count of active listings of the specified listing type
        listingsOfType_ = listingCount[_collection];

        // If we have listings of this type then we need to calculate the percentage, otherwise
        // we will just return a zero percent value.
        if (listingsOfType_ != 0) {
            ICollectionToken collectionToken = locker.collectionToken(_collection);

            // If we have no totalSupply, then we have a zero percent utilization
            uint totalSupply = collectionToken.totalSupply();
            if (totalSupply != 0) {
                utilizationRate_ = (listingsOfType_ * 1e36 * 10 ** collectionToken.denomination()) / totalSupply;
            }
        }
    }
```
Next we have the [calculateCompoundedFactor()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L80-L91) function, which internally calls the [calculateProtectedInterest()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L59-L71) function: 
```solidity
    function calculateProtectedInterest(uint _utilizationRate) public pure returns (uint interestRate_) {
        // If we haven't reached our kink, then we can just return the base fee
        if (_utilizationRate <= UTILIZATION_KINK) {
            // Calculate percentage increase for input range 0 to 0.8 ether (2% to 8%)
            interestRate_ = 200 + (_utilizationRate * 600) / UTILIZATION_KINK;
        }
        // If we have passed our kink value, then we need to calculate our additional fee
        else {
            // Convert value in the range 0.8 to 1 to the respective percentage between 8% and
            // 100% and make it accurate to 2 decimal places.
            interestRate_ = (((_utilizationRate - UTILIZATION_KINK) * (100 - 8)) / (1 ether - UTILIZATION_KINK) + 8) * 100;
        }
    }
```
As can be seen from the comments in the above code snippet, if the interest rate is **800** this means the interest the user have to pay on his **tokenTaken** amount is **8%**. However this is not the case at all, due to miscalculations if the interest is **0.8e18** that will result in a **80%** interest rate. 
Lets consider the following scenario:
1. The utilization rate calculated by the [utilizationRate()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L261-L276) function is **71428571428571428 ~0.071e18**. When that rate is provided as an argument in the [calculateProtectedInterest()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L59-L71) function, the returned interest rate will be **253**, according to the comments this has to **~2.5%** interest rate for a year. 
2. Now lets say the checkpoints haven't been updated for a year(we are considering this scenario to better illustrate the impact of the issue). If we plug the **253** in the [calculateCompoundedFactor()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L80-L91) function we will get a compounded rate of ~**1.25e18**
3. Now the first compoundedFactor fo the use is about 1e18 if we plug those params in the [compound()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L106-L119) function we will get ~**0.62e18**
4. Remember that based on the comments the user has to pay roughly 2.5% on his 0.5e18 tokeTaken, however this is clearly not the case and the user has to pay close to 25%





### Root Cause

The calculation of the interest rate that a user that reserved an NFT should be charged is wrong, and results in a much bigger interest rate. The problem is most probably in the [calculateCompoundedFactor()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/TaxCalculator.sol#L80-L91) function:
```solidity
    function calculateCompoundedFactor(uint _previousCompoundedFactor, uint _utilizationRate, uint _timePeriod) public view returns (uint compoundedFactor_) {
        // Get our interest rate from our utilization rate
        uint interestRate = this.calculateProtectedInterest(_utilizationRate);

        // Ensure we calculate the compounded factor with correct precision. `interestRate` is
        // in basis points per annum with 1e2 precision and we convert the annual rate to per
        // second rate.
        uint perSecondRate = (interestRate * 1e18) / (365 * 24 * 60 * 60);

        // Calculate new compounded factor
        compoundedFactor_ = _previousCompoundedFactor * (1e18 + (perSecondRate / 1000 * _timePeriod)) / 1e18;
    }
```
The division most probably should be by **10_000** instead of **1_000**

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Due to miscalculations of the interest rate users are charged much more interest for the NFTs they are reserving. For example a user that should be charged **8%** is actually charged **80%** which results in users paying 10 times more for interest rate than they should. 

### PoC

[Gist](https://gist.github.com/AtanasDimulski/95fe424bd5c38a08b7d12cc5c3911878)

After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/95fe424bd5c38a08b7d12cc5c3911878) add the following function to the ``AuditorTests.t.sol`` file:

```solidity
    function test_InterestRateForReservedNFTsIsBiggerThanItShould() public {
        vm.startPrank(alice);
        collection1.mint(alice, 12);
        collection1.setApprovalForAll(address(listings), true);

        Listings.Listing memory listingAlice = IListings.Listing({
            owner: payable(alice),
            created: uint40(block.timestamp),
            duration: 10 days,
            floorMultiple: 200
        });

        uint256[] memory tokenIdsAlice = new uint256[](1);
        tokenIdsAlice[0] = 12;
        IListings.CreateListing[] memory createLisings = new IListings.CreateListing[](1);       
        IListings.CreateListing memory createListing = IListings.CreateListing({
                collection: address(collection1),
                tokenIds: tokenIdsAlice,
                listing: listingAlice
        });
        createLisings[0] = createListing;
        listings.createListings(createLisings);  
        skip(2);
        vm.stopPrank();

        vm.startPrank(bob);
        collection1.mint(bob, 13);
        collection1.mint(bob, 14);
        collection1.mint(bob, 15);
        collection1.setApprovalForAll(address(locker), true);
        uint256[] memory tokenIdsBob = new uint256[](3);
        tokenIdsBob[0] = 13;
        tokenIdsBob[1] = 14;
        tokenIdsBob[2] = 15;
        locker.deposit(address(collection1), tokenIdsBob);
        collectionTokenA.approve(address(listings), type(uint256).max);
        listings.reserve(address(collection1), 12, 0.5e18);
        vm.stopPrank();
        (, uint256 utilizationRateA) = protectedListings.utilizationRate(address(collection1));
        console2.log("the current utilization rate: ", utilizationRateA);
        console2.log("Current total supply of the collectionA token: ", collectionTokenA.totalSupply());
        console2.log("The current price that need to be paid to unlock the price: ", protectedListings.unlockPrice(address(collection1), 12));

        /// @notice we skip one year
        skip(31_536_000);
        (, uint256 utilizationRateB) = protectedListings.utilizationRate(address(collection1));
        console2.log("the current utilization rate: ", utilizationRateB);
        console2.log("The current price that need to be paid to unlock the price: ", protectedListings.unlockPrice(address(collection1), 12));
    }
```

```solidity
Logs:
  the current utilization rate:  71428571428571428
  Current total supply of the collectionA token:  14000000000000000000
  The current price that need to be paid to unlock the price:  500000000000000000
  the current utilization rate:  71428571428571428
  The current price that need to be paid to unlock the price:  626499999985927999
```

To run the test use: ``forge test -vvv --mt test_InterestRateForReservedNFTsIsBiggerThanItShould``

### Mitigation

_No response_