Modern Metal Butterfly

High

# An attacker can frontrun execute call to inflate the newQuorum rate and reduce eth for the voters and lock nfts.

## Summary
When calling [`execute`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231C1-L275C6) for a collection, the function requires the owner to specify the list of nft tokenIds manually. The problem is an when the owner calls `execute` with the current list of tokenIds, an attacker can frontrun by depositing new nfts in the locker contract just to increase the newQuorum.

## Vulnerability Detail
The `execute` function is called by owner with list of the current nft ids in the locker contract;
```javascript
    function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Ensure we have specified token IDs
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength == 0) revert NoNFTsSupplied();
```
we can also see that if there was an increase in the totalSupply of collectionToken i.e. if there was any new deposit of nfts in the locker then we change the `params.quorumVotes`;
```javascript
        // Refresh total supply here to ensure that any assets that were added during
        // the shutdown process can also claim their share.
        uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
        if (params.quorumVotes != newQuorum) {
            params.quorumVotes = uint88(newQuorum);
        }
```

Now, for example if;
* there is a collection with 4 nfts deposited i.e. collectionToken totalSupply is 4e18, and say the nft ids are `10, 11, 12 & 13`,
* now shutdown process was started and quorum votes is reached and owner calls `execute` with the ids `10, 11, 12 & 13`
* Bob frontruns the execute call by depositing nfts into the locker contract and minting new `collectionTokens`, say he deposits 2 more nft then 2e18 collectionTokens are minted to him;
```javascript
    function deposit(address _collection, uint[] calldata _tokenIds, address _recipient) public
        nonReentrant
        whenNotPaused
        collectionExists(_collection)
    {
        uint tokenIdsLength = _tokenIds.length;
        if (tokenIdsLength == 0) revert NoTokenIds();

        // Define our collection token outside the loop
        IERC721 collection = IERC721(_collection);

        // Take the ERC721 tokens from the caller
        for (uint i; i < tokenIdsLength; ++i) {
            // Transfer the collection token from the caller to the locker
            collection.transferFrom(msg.sender, address(this), _tokenIds[i]);
        }

        // Mint the tokens to the recipient
        ICollectionToken token = _collectionToken[_collection];
        token.mint(_recipient, tokenIdsLength * 1 ether * 10 ** token.denomination());

        emit TokenDeposit(_collection, _tokenIds, msg.sender, _recipient);
    }
```
* now the call to `execute` gets executed, and since totalSupply of the collectionToken has changed the `params.quorumVotes` is also changed;
```javascript
        // Refresh total supply here to ensure that any assets that were added during
        // the shutdown process can also claim their share.
        uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
        if (params.quorumVotes != newQuorum) {
            params.quorumVotes = uint88(newQuorum);
        }
```
* previously it was `4e18 * 50 / 100 = 2e18`, now it increases to `6e18 * 50 / 100 = 3e18`,
* now this is a problem because the `execute` call did not contain the newly deposited Bob's 2 nfts ids in its [`_tokenIds`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/utils/CollectionShutdown.sol#L231C42-L231C68) argument, but changes the `quorumVotes` as if they were included.
* hence, the `execute` will withdraw only the given nft ids from the locker contract i.e. `10, 11, 12 & 13`, and ignore the rest 2 nfts deposited by Bob.
```javascript
        // @here _tokenIdsLength only includes the ids 10, 11, 12 & 13
        for (uint i; i < _tokenIdsLength; ++i) {
            locker.withdrawToken(_collection, _tokenIds[i], address(this));
        }

```
* Bob sucessfully reduces the claimable eth for innocent voters without selling his nfts, becuase the amount to claim is calculated using `params.quorumVotes`;
```javascript
        uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
        (bool sent,) = _claimant.call{value: amount}('');
        if (!sent) revert FailedToClaim();
```

* Now, when claiming it will only check that those 4 nfts i.e. `10, 11, 12 & 13` are sold and allow claiming,
* Bob can also claim without selling his nfts, while his nfts are locked in the locker contract.

**Note that this is reported as high because in the example Bob might actully loss instead of gaining from the attack, but he also affect other innocent users. And also the fact that this can happen unintentionally without the need of a malicious actor, in that case it is a loss of nft and eth for all innocent users.**


## Impact
lock of nft, and loss of eth for innocent users.

## Code Snippet
```javascript
    function execute(address _collection, uint[] calldata _tokenIds) public onlyOwner whenNotPaused {
        // Ensure that the vote count has reached quorum
        CollectionShutdownParams storage params = _collectionParams[_collection];
        if (!params.canExecute) revert ShutdownNotReachedQuorum();

        // Ensure we have specified token IDs
        uint _tokenIdsLength = _tokenIds.length;
        if (_tokenIdsLength == 0) revert NoNFTsSupplied();

        // Check that no listings currently exist
        if (_hasListings(_collection)) revert ListingsExist();

        // Refresh total supply here to ensure that any assets that were added during
        // the shutdown process can also claim their share.
        uint newQuorum = params.collectionToken.totalSupply() * SHUTDOWN_QUORUM_PERCENT / ONE_HUNDRED_PERCENT;
        if (params.quorumVotes != newQuorum) {
            params.quorumVotes = uint88(newQuorum);
        }

        // Lockdown the collection to prevent any new interaction
        locker.sunsetCollection(_collection);

        // Iterate over our token IDs and transfer them to this contract
        IERC721 collection = IERC721(_collection);
        for (uint i; i < _tokenIdsLength; ++i) {
            locker.withdrawToken(_collection, _tokenIds[i], address(this));
        }

        // Approve sudoswap pair factory to use our NFTs
        collection.setApprovalForAll(address(pairFactory), true);

        // Map our collection to a newly created pair
        address pool = _createSudoswapPool(collection, _tokenIds);

        // Set the token IDs that have been sent to our sweeper pool
        params.sweeperPoolTokenIds = _tokenIds;
        sweeperPoolCollection[pool] = _collection;

        // Update our collection parameters with the pool
        params.sweeperPool = pool;

        // Prevent the collection from being executed again
        params.canExecute = false;
        emit CollectionShutdownExecuted(_collection, pool, _tokenIds);
    }
```

## Tool used
Manual Review

## Recommendation
Instead of manually passing the list of tokenIds we can automate the process of getting the list in real time.

This can be done by;
* Introducting a new mapping for collection to deposited nft ids 
* and for every deposit, redeem and swap calls update the mapping.
* and query the mapping that contains the real time list when calling execute, and remove the manually added tokenIds.

**Note: make sure to implement the mapping update in the [swap and swapBatch](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/Locker.sol#L241-L287) functions, so that a malicious actor cannot DOS the `execute` call through swapping.**