Lone Coconut Cat

Medium

# Incorrect Implementation Of Fee Exemption Logic

## Summary

Due to incorrect comparisons, the [`UniswapImplementation`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol) fails to determine whether a fee exemption has been applied.

## Vulnerability Detail

When applying a fee exemption to a `_beneficiary`, an owner can call [`setFeeExemption(address,uint24)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L729C14-L729C68).

To mark a `_beneficiary` as fee exempt, the protocol underlying appends internal flags to the original `_flatFee` argument:

```solidity
function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
    // Ensure that our custom fee conforms to Uniswap V4 requirements
    if (!_flatFee.isValid()) {
        revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
    }

    // We need to be able to detect if the zero value is a flat fee being applied to
    // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
    // we essentially get a boolean flag to show this.
    feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF; /// @audit appends `type(uint24).max` to the fee
    emit BeneficiaryFeeSet(_beneficiary, _flatFee);
}
```

Let's assume we assign a `_flatFee` of `LPFeeLibrary.MAX_LP_FEE` (`1000000`) to a beneficiary:

```shell
Welcome to Chisel! Type `!help` to show available commands.
➜ uint24 flatFee = 1000000;
➜ uint48 feeOverride = uint48(flatFee) << 24 | 0xFFFFFF;
➜ feeOverride
Type: uint48
├ Hex: 0x0f4240ffffff
├ Hex (full word): 0x00000000000000000000000000000000000000000000000000000f4240ffffff
└ Decimal: 16777232777215
```

Notice that the bytes `ffffff` are appended to the evaluated fee.

However, this is not compatible the [`UniswapImplementation`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol)'s dependent logic, since consumers of the fee exemption bytes expect a single lastmost truthy bit to determine if the caller is fee exempt.

For example, let's see what happens when the owner attempts to [`removeFeeExemption(address)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L749C14-L749C54) from a beneficiary:

```solidity
function removeFeeExemption(address _beneficiary) public onlyOwner {
    // Check that a beneficiary is currently enabled
    uint24 hasExemption = uint24(feeOverrides[_beneficiary] & 0xFFFFFF);
    if (hasExemption != 1) {
        revert NoBeneficiaryExemption(_beneficiary);
    }

    delete feeOverrides[_beneficiary];
    emit BeneficiaryFeeRemoved(_beneficiary);
}
```

In `chisel`:

```shell
➜ uint24 hasExemption = uint24(feeOverride & 0xFFFFFF)
➜ hasExemption
Type: uint24
├ Hex: 0xffffff
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000000000000ffffff
└ Decimal: 16777215
```

The `hasExemption` variable in this instance returns the full `ffffff` bytes, and not the value `1`, and is therefore interpreted to be non fee-exempt, resulting in `revert`.

Likewise, we see the same error repeated in [`getFee(address,address)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L698C14-L698C53):

```solidity
uint48 swapFeeOverride = feeOverrides[_sender];
if (uint24(swapFeeOverride & 0xFFFFFF) == 1) { /// @audit incorrect comparison
```

## Impact

The `UniswapImplementation` can neither honour a fee exemption nor remove an applied fee exemption.

## Code Snippet

```solidity
function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
    // Ensure that our custom fee conforms to Uniswap V4 requirements
    if (!_flatFee.isValid()) {
        revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
    }

    // We need to be able to detect if the zero value is a flat fee being applied to
    // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
    // we essentially get a boolean flag to show this.
    feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
    emit BeneficiaryFeeSet(_beneficiary, _flatFee);
}
```

https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L721C5-L740C6

## Tool used

Chisel

## Recommendation

It is recommended to the team to adjust the assignment in [`setFeeExemption(address,uint24)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L729C14-L729C68) to toggle only the required fee exemption bit:

```diff
// We need to be able to detect if the zero value is a flat fee being applied to
// the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
// we essentially get a boolean flag to show this.
- feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
+ feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0x000001;
emit BeneficiaryFeeSet(_beneficiary, _flatFee);
```

Again, in `chisel`:

```shell
Welcome to Chisel! Type `!help` to show available commands.
➜ uint24 flatFee = 1000000;
➜ uint48 feeOverride = uint48(flatFee) << 24 | 0x000001;
➜ feeOverride
Type: uint48
├ Hex: 0x0f4240000001
├ Hex (full word): 0x00000000000000000000000000000000000000000000000000000f4240000001
└ Decimal: 16777216000001
➜ uint24 hasExemption = uint24(feeOverride & 0xFFFFFF)
➜ hasExemption
Type: uint24
├ Hex: 0x000001
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000000000000000001
└ Decimal: 1
```