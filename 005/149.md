Rich Indigo Cottonmouth

High

# Incorrect Application of Fee Override Flag Leading to Wrong Swap Fee Calculation

## Summary
The [getFee](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L698) function is designed to calculate and return the applicable fee for a given pool and sender. The function allows for the use of a fee override if one has been set for the user. However, the function checks for a flag in the lower 24 bits of the `feeOverrides[_sender]` variable to determine if an override is active. The issue arises when the flag is set to `0xFFFFFF` instead of `1`, causing the check `if (uint24(swapFeeOverride & 0xFFFFFF) == 1)` to fail. This leads to incorrect fee calculations, where valid fee overrides are ignored, potentially causing users or the pool to be charged incorrect fees.
## Vulnerability Detail
The getFee function relies on the lower 24 bits of the feeOverrides[_sender] value to determine whether a fee override is active. Specifically, it checks whether the flag in the lower 24 bits equals 1:
```solidity
if (uint24(swapFeeOverride & 0xFFFFFF) == 1)
```
However, in the [setFeeExemption](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L738) function, the flag is set to `0xFFFFFF` rather than `1`, resulting in the condition failing because `0xFFFFFF != 1`. This failure causes the function to skip applying the flat fee override, leading to the use of a default or pool-specific fee instead of the intended override.
## Impact
 * Valid fee overrides are ignored, leading to incorrect fee calculations. Users may be charged higher fees than expected, which could lead to user dissatisfaction or financial loss.
 * Users or the pool may incur unexpected costs or losses.
## Code Snippet
```solidity
function getFee(PoolId _poolId, address _sender) public view returns (uint24 fee_) {
        // Our default fee is our first port of call
        fee_ = defaultFee;

        // If we have a specific pool fee then we can overwrite that
        uint24 poolFee = _poolParams[_poolId].poolFee;
        if (poolFee != 0) {
            fee_ = poolFee;
        }

        // If we have a swap fee override, then we want to use that value. We first check
        // our flag to show that we have a valid override.
        uint48 swapFeeOverride = feeOverrides[_sender];
        if (uint24(swapFeeOverride & 0xFFFFFF) == 1) { <--- @audit 
            // We can then extract the original uint24 fee override and apply this, only if
            // it is less that then traditionally calculated base swap fee.
            uint24 baseSwapFeeOverride = uint24(swapFeeOverride >> 24);
            if (baseSwapFeeOverride < fee_) {
                fee_ = baseSwapFeeOverride;
            }
        }
    }
```
```solidity
 function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        // Ensure that our custom fee conforms to Uniswap V4 requirements
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

        // We need to be able to detect if the zero value is a flat fee being applied to
        // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
        // we essentially get a boolean flag to show this.
        feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF; <--- @audit
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```
## PoC
```solidity
function test_CanGetFeeWithExemptFees() public {
        address beneficiary = address(poolSwap);

        assertEq(
            uniswapImplementation.getFee(_poolKey(false).toId(), beneficiary),
            1_0000
        );

        // Exempt a beneficiary with a set fee
        uniswapImplementation.setFeeExemption(beneficiary, 5000);

        assertEq(
            uniswapImplementation.getFee(_poolKey(false).toId(), beneficiary),
            5000
        );

        // Update our exemption
        uniswapImplementation.setFeeExemption(beneficiary, 7500);

        assertEq(
            uniswapImplementation.getFee(_poolKey(false).toId(), beneficiary),
            7500
        );
    }
```
```solidity
[FAIL. Reason: assertion failed: 10000 != 5000]
```

## Recommendation
To fix this issue, ensure the flag in the lower 24 bits of `feeOverrides` is consistently set to 1, as the `getFee` function expects. Update the `setFeeExemption` function as follows:
```solidity
feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 1;
```