Shiny Mint Lion

High

# There is a logical error in the UniswapImplementation::getFee() function.


## Summary
There is a logical error in the UniswapImplementation::getFee() function, causing the fee in feeOverrides[_sender] to never be retrieved.
## Vulnerability Detail
```javascript
 function getFee(PoolId _poolId, address _sender) public view returns (uint24 fee_) {
        // Our default fee is our first port of call
        fee_ = defaultFee;

        // If we have a specific pool fee then we can overwrite that
        uint24 poolFee = _poolParams[_poolId].poolFee;
        if (poolFee != 0) {
            fee_ = poolFee;
        }

        // If we have a swap fee override, then we want to use that value. We first check
        // our flag to show that we have a valid override.
@>>        uint48 swapFeeOverride = feeOverrides[_sender];
@>>        if (uint24(swapFeeOverride & 0xFFFFFF) == 1) {
            // We can then extract the original uint24 fee override and apply this, only if
            // it is less that then traditionally calculated base swap fee.
            uint24 baseSwapFeeOverride = uint24(swapFeeOverride >> 24);
            if (baseSwapFeeOverride < fee_) {
                fee_ = baseSwapFeeOverride;
            }
        }
    }
```
Through setFeeExemption(), we know that the lower 24 bits of feeOverrides[_beneficiary] are set to 0xFFFFFF. Therefore, the expression uint24(feeOverrides[_beneficiary] & 0xFFFFFF) = 0xFFFFFF & 0xFFFFFF = 0xFFFFFF will never be 1. As a result, the code will not enter the if condition, and baseSwapFeeOverride will never be assigned to fee_.
```javascript
function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        // Ensure that our custom fee conforms to Uniswap V4 requirements
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

        // We need to be able to detect if the zero value is a flat fee being applied to
        // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
        // we essentially get a boolean flag to show this.
@>>        feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```
## Impact
The configured baseSwapFeeOverride will never be applied, causing the userâ€™s actual fees to be inconsistent with the set value.
## Code Snippet
https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L698

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L729
## Tool used

Manual Review

## Recommendation
```diff
 function getFee(PoolId _poolId, address _sender) public view returns (uint24 fee_) {
        // Our default fee is our first port of call
        fee_ = defaultFee;

        // If we have a specific pool fee then we can overwrite that
        uint24 poolFee = _poolParams[_poolId].poolFee;
        if (poolFee != 0) {
            fee_ = poolFee;
        }

        // If we have a swap fee override, then we want to use that value. We first check
        // our flag to show that we have a valid override.
        uint48 swapFeeOverride = feeOverrides[_sender];
-        if (uint24(swapFeeOverride & 0xFFFFFF) == 1) {
+        if (uint24(swapFeeOverride & 0xFFFFFF) == 0xFFFFFF) {
            // We can then extract the original uint24 fee override and apply this, only if
            // it is less that then traditionally calculated base swap fee.
            uint24 baseSwapFeeOverride = uint24(swapFeeOverride >> 24);
            if (baseSwapFeeOverride < fee_) {
                fee_ = baseSwapFeeOverride;
            }
        }
    }
```