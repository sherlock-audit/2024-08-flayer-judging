Winning Juniper Ram

High

# `UniswapImplementation::setFeeExemption` functionality broken

### Summary

There is a logical error in the fee override mechanism within the `UniswapImplementation` contract. Specifically, the way fee overrides are stored in the `feeOverrides` mapping and the way they are checked in the `getFee` function are inconsistent.

The `UniswapImplementation::setFeeExemption` function is supposed to pack the value `1` in the latter `uint24` of the `_flatFee` according to the natspec inside the function. What the function does instead is to store the value `0xFFFFFF` instead of the expected `1`. [Link to code](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L738).

This inconsistency causes the fee override functionality to be worthless because [this assertion](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L711) inside the `getFee` function will always be false `if (uint24(swapFeeOverride & 0xFFFFFF) == 1)` and therefore the custom fees that were set for the `beneficiary` inside the `feeOverrides` mapping will never be used. The `UniswapImplementation::getFee` function expects the value of `1` in the latter `uint24` but what it will actually read is `0xFFFFFF` because that's what the `setFeeExemption` function set.

### Root Cause

The `setFeeExemption` function sets the lower 24 bits of the `uint48(_flatFee)` to `0xFFFFFF`, instead of `1` but the `getFee` function expects the value of `1` in the lower 24 bits in order to return true. Because of this, even if a user was exempted from fees, they will still pay them because this `if` statement in the `getFee` function will always be false `if (uint24(swapFeeOverride & 0xFFFFFF) == 1)`.

### Internal pre-conditions

1. Call the `UniswapImplementation::setFeeExemption` for any `_beneficiary` address.

### External pre-conditions

None

### Attack Path

1. Call `UniswapImplementation::setFeeExemption` with a valid `_flatFee` value for a `_beneficiary` address. 

### Impact

- The fee overrides mechanism is broken. Users who are supposed to receive fee exemptions or reduced fees will not benefit from them. Even if a user was supposed to be exempted from fees, they will still pay them.

### PoC

Put the following test in `UniswapImplementation.t.sol` test file and run it.

```solidity
    function test_FeeExemptionDoesntWork(address _beneficiary, uint24 _validFee) public {
        // Ensure that we have a valid fee
        vm.assume(_validFee.isValid());
        console.log("Valid fee for this run is", _validFee);

        // Confirm that the position does not yet exist
        (uint24 storedFee, bool enabled) = _decodeEnabledFee(uniswapImplementation.feeOverrides(_beneficiary));
        assertEq(storedFee, 0);
        assertEq(enabled, false);

        //set fee exemption for beneficiary
        uniswapImplementation.setFeeExemption(_beneficiary, _validFee);

        // Get stored fee override
        uint48 feeOverride = uniswapImplementation.feeOverrides(_beneficiary);
        console.log("FeeOverride:", feeOverride);

        // Confirm the fee override is NOT what we expect it to be. We do assertNotEq
        assertNotEq(feeOverride, _encodeEnabledFee(_validFee, true));

        // Confirm that storedFee amount is correct, but appears as NOT enabled. We do assertNotEq(enabled, true);
        (storedFee, enabled) = _decodeEnabledFee(feeOverride);
        assertEq(storedFee, _validFee);
        assertNotEq(enabled, true);
    }
```

Test output

```javascript
    ├─ [615] 0x57D88D547641a626eC40242196F69754b25D2FCC::feeOverrides(0x0000000000000000000000000000000000000556) [staticcall]
    │   └─ ← [Return] 201058156543 [2.01e11]
    ├─ [0] console::log("FeeOverride:", 201058156543 [2.01e11]) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] VM::assertNotEq(201058156543 [2.01e11], 201041379329 [2.01e11]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertEq(11983 [1.198e4], 11983 [1.198e4]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertNotEq(false, true) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Stop] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 31.16ms (26.85ms CPU time)
```

The PoC proves that the `feeOverride` is NOT what we expect it to be and it also shows that the fee exemption appears as if it is not enabled although we did enable it with this call `uniswapImplementation.setFeeExemption(_beneficiary, _validFee);`.

### Mitigation

Correct the `UniswapImplementation::setFeeExemption` function and replace `0xFFFFFF` with `1` to align it with the natspec and its expected behavior. After this small change is done, this other test (that's already in the file) should pass `UniswapImplementation.t.sol::test_CanSetFeeExemption`. 

```diff
    function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        // Ensure that our custom fee conforms to Uniswap V4 requirements
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

        // We need to be able to detect if the zero value is a flat fee being applied to
        // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
        // we essentially get a boolean flag to show this.
-       feeOverrides[_beneficiary] = (uint48(_flatFee) << 24) | 0xFFFFFF;
+       feeOverrides[_beneficiary] = (uint48(_flatFee) << 24) | 1;
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```