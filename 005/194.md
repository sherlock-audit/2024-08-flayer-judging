Clean Snowy Mustang

Medium

# Fee Exemption cannot be applied or removed

## Summary
Fee Exemption cannot be applied or removed.

## Vulnerability Detail
UniswapImplementation owner can call [setFeeExemption()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L729) to set user's flat fee rate across all pools. The fee rate is stored in `feeOverrides` with the lower 24 bits are all **1**s in binary.

[UniswapImplementation.sol#L729-L740](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L729-L740):
```solidity
    function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        // Ensure that our custom fee conforms to Uniswap V4 requirements
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

        // We need to be able to detect if the zero value is a flat fee being applied to
        // the user, or it just hasn't been set. By packing the `1` in the latter `uint24`
        // we essentially get a boolean flag to show this.
@>      feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```
If a beneficiary is set, then the fee processed during a swap will be overwritten if this fee exemption value is lower than the otherwise determined fee, as we can see in [getFee()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L698).

[UniswapImplementation.sol#L698-L719](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L698-L719):
```solidity
    function getFee(PoolId _poolId, address _sender) public view returns (uint24 fee_) {
        // Our default fee is our first port of call
        fee_ = defaultFee;

        // If we have a specific pool fee then we can overwrite that
        uint24 poolFee = _poolParams[_poolId].poolFee;
        if (poolFee != 0) {
            fee_ = poolFee;
        }

        // If we have a swap fee override, then we want to use that value. We first check
        // our flag to show that we have a valid override.
        uint48 swapFeeOverride = feeOverrides[_sender];
@>      if (uint24(swapFeeOverride & 0xFFFFFF) == 1) {
            // We can then extract the original uint24 fee override and apply this, only if
            // it is less that then traditionally calculated base swap fee.
            uint24 baseSwapFeeOverride = uint24(swapFeeOverride >> 24);
            if (baseSwapFeeOverride < fee_) {
                fee_ = baseSwapFeeOverride;
            }
        }
    }
```
Before applying the fee exemption, it checks out flag to show if the sender has a valid override. As we can see from above, it retrieves `swapFeeOverride` value from `feeOverrides`, and performs bitwise AND (&) with `0xFFFFFF`, to see if the result is `1`. 

The check is essentially wrong, assume user is set fee exemption value to $999$, then `swapFeeOverride` is $1111100111111111111111111111111111$ (0x3E7FFFFFF) in binary, and the bitwise AND value is $111111111111111111111111$ (0xFFFFFF) instead of `1`.

Because the check does not pass, the fee exemption won't be applied.

Likewise, when owner calls [removeFeeExemption()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L749), the same check will be used to see if a beneficiary is currently enabled, and the transaction will revert due to the same reason.

[UniswapImplementation.sol#L749-L758](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L749-L758):
```solidity
    function removeFeeExemption(address _beneficiary) public onlyOwner {
        // Check that a beneficiary is currently enabled
@>      uint24 hasExemption = uint24(feeOverrides[_beneficiary] & 0xFFFFFF);
@>      if (hasExemption != 1) {
@>          revert NoBeneficiaryExemption(_beneficiary);
@>      }

        delete feeOverrides[_beneficiary];
        emit BeneficiaryFeeRemoved(_beneficiary);
    }
```

Please run the PoC in UniswapImplementation.t.sol to verify:
```solidity
    function testAudit_SetAndRemoveFeeExemption() public {
        address alice = makeAddr("Alice");

        // Set Fee Exemption
        uniswapImplementation.setFeeExemption(alice, uint24(999)); // 0.0999%

        // Check Fee Exemption
        bytes memory poolKeyBytes = uniswapImplementation.getCollectionPoolKey(address(flippedErc));
        PoolKey memory poolKey = abi.decode(poolKeyBytes, (PoolKey));
        PoolId poolId = poolKey.toId();

        // The applied fee is not 999 but 10_000
        uint24 fee = uniswapImplementation.getFee(poolId, alice);
        assertEq(fee, 10_000);

        // Fee Exemption cannot be removed
        vm.expectRevert(abi.encodeWithSelector(
            UniswapImplementation.NoBeneficiaryExemption.selector, alice
        ));
        uniswapImplementation.removeFeeExemption(alice);
    }
```

## Impact

Fee Exemption cannot be applied or removed, user won't enjoy a fee discount when they do swaps.

## Code Snippet

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L711

https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/implementation/UniswapImplementation.sol#L751

## Tool used

Manual Review

## Recommendation

The correct check should be:
```solidity
        if (uint24(swapFeeOverride & 0xFFFFFF) == 0xFFFFFF)
```