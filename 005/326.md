Crazy Chiffon Spider

Medium

# Fee exemption logic in UniswapImplementation will never work due to wrong bit-packing.

## Summary

FeeExempt functionality will never work due to wrong bit packing. So admins won't be able to set fee discounts for specific beneficiaries.

**Disclaimer**: This should be **valid issue** by Sherlock's **rules**, as this functionality will **NEVER** work, for **ANY** exempt value set by an **ADMIN**.

## Vulnerability Detail

In the `UniswapImplementation.setFeeExemption()`, the fee override is packed as follows, with 0xFFFFFF.
```solidity
feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
```
This means the **lower 24 bits** of the uint48 are always set to 0xFFFFFF. This is intended to act as a flag.

But in the [getFee()](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/implementation/UniswapImplementation.sol#L698-L719) function which factors exempts we perform this check:

```solidity
    function getFee(....)  {
        fee_ = defaultFee;
        uint24 poolFee = _poolParams[_poolId].poolFee;
        if (poolFee != 0) {
            fee_ = poolFee;
        }

        uint48 swapFeeOverride = feeOverrides[_sender];
@>>     if (uint24(swapFeeOverride & 0xFFFFFF) == 1) {
            uint24 baseSwapFeeOverride = uint24(swapFeeOverride >> 24);
            if (baseSwapFeeOverride < fee_) {
                fee_ = baseSwapFeeOverride;
            }
        }
    }
```

The code is trying to check if the **lower 24 bits** of swapFeeOverride are equal to 1. However, because you packed 0xFFFFFF (16777215) into those bits, the result of swapFeeOverride & 0xFFFFFF will always be 0xFFFFFF, which will **never be equal to 1**.

Currently there are 4 tests failing due to this problem:
- `test_CanSwapWithZeroFeeExemption`
- `test_CanSetFeeExemption`
- `test_CanRemoveFeeExemption`
- `test_CanGetFeeWithExemptFees`



The implied fix, described in the "Recommendation" section will fix them.

## Impact
Two use-cases will never work:
1. Admin tries to add promotion for a "partner" by setting fixed fee exempt for him using `setFeeExemption()`
2. Admin tries to remove a set exempt for a "partner" using `removeFeeExemption()`

So the protocol won't be able to provide special fee charges for partners, which is an intended functionality which can help with the protocol's future development.

## Code Snippet

The wrong mask is here:
```solidity
    function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

@>>     feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```    


## Tool used

Manual Review

## Recommendation

Use correct mask when setting feeOverride for a specific beneficiary/partner.
```diff
    function setFeeExemption(address _beneficiary, uint24 _flatFee) public onlyOwner {
        if (!_flatFee.isValid()) {
            revert FeeExemptionInvalid(_flatFee, LPFeeLibrary.MAX_LP_FEE);
        }

-       feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 0xFFFFFF;      
+       feeOverrides[_beneficiary] = uint48(_flatFee) << 24 | 1;
        emit BeneficiaryFeeSet(_beneficiary, _flatFee);
    }
```    