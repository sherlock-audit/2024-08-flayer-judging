Lone Coconut Cat

Medium

# Attackers Can Prevent Claiming Funds Clearing In SudoSwap

## Summary

Holders of a sunset [`CollectionToken`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/CollectionToken.sol) may only reclaim the market value of their auctioned tokens on SudoSwap after all tokens have cleared, however an attacker can prevent this by maliciously donating a sweeper pool token back to the sweeper pool.

## Vulnerability Detail

When a [`CollectionToken`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/CollectionToken.sol) is sunset, the floor tokens held by the protocol are sent for dutch auction on SudoSwap.

This enables the protocol to liquidate all remaining assets in exchange for ETH at the fair market price, which can then be claimed by [`CollectionToken`](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/CollectionToken.sol) holders via a call to either [`claim(address,address)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L285C14-L285C67) or [`voteAndClaim(address)`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L323C14-L323C47).

For both claim functions, the protocol enforces that the claimant must wait until all tokens sold onto SudoSwap auction must be liquidated before they can redeem their due ETH:

```solidity
// Ensure that all NFTs have sold from our Sudoswap pool
if (!collectionLiquidationComplete(_collection)) revert NotAllTokensSold();
```

This is done so that when claimants make a withdrawal, they withdraw their fair share of the full value for all sold `sweeperPoolTokenIds` tokens, referred to as the `availableClaim`:

```solidity
// Get the number of votes from the claimant and the total supply and determine from that the percentage
// of the available funds that they are able to claim.
uint amount = params.availableClaim * claimableVotes / (params.quorumVotes * ONE_HUNDRED_PERCENT / SHUTDOWN_QUORUM_PERCENT);
```

However, an attacker can prevent the liquidation from being reached.

Diving into the [`collectionLiquidationComplete`](https://github.com/sherlock-audit/2024-08-flayer/blob/0ec252cf9ef0f3470191dcf8318f6835f5ef688c/flayer/src/contracts/utils/CollectionShutdown.sol#L445C14-L445C64), we see that the condition that for asserting whether claimants can access their token value is if the `sweeperPool` is no longer in possession of the `sweeperPoolTokenIds`:

```solidity
function collectionLiquidationComplete(address _collection) public view returns (bool) {
    CollectionShutdownParams memory params = _collectionParams[_collection];
    uint sweeperPoolTokenIdsLength = params.sweeperPoolTokenIds.length;

    // If we have no registered tokens, then there is nothing to check
    if (sweeperPoolTokenIdsLength == 0) {
        return true;
    }

    // Store our loop iteration variables
    address sweeperPool = params.sweeperPool;
    IERC721 collection = IERC721(_collection);

    // Check that all token IDs have been bought from the pool
    for (uint i; i < sweeperPoolTokenIdsLength; ++i) {
        // If the pool still owns the NFT, then we have to revert as not all tokens have been sold
        /// @audit liquidiation is not complete until the sweeperPool is not in possession of any sweeperPoolTokenIds
        if (collection.ownerOf(params.sweeperPoolTokenIds[i]) == sweeperPool) {
            return false;
        }
    }

    return true;
}
```

Specifically, the vulnerability lies in the following sequence: 

```solidity
if (collection.ownerOf(params.sweeperPoolTokenIds[i]) == sweeperPool) {
    return false;
}
```

This is to say that if an attacker purchased one of the sweeper pool tokens from SudoSwap, and then transferred it back to the `sweeperPool`, the sunsetting process would never reach the liquidation state since the `sweeperPool` would continue to be marked as the owner.

Although normally, the token could be rescued from the `sweeperPool` by the `sweeperPool`'s owner, however in this instance the owner is the `CollectionShutdown` itself, which exposes no such functionality to facilitate token rescue.

Consequently, users cannot redeem their share of the `availableClaim`.

## Impact

An attacker can prevent funds from being liquidated by preventing the liquidation state condition from being reached.

## Code Snippet

```solidity
function collectionLiquidationComplete(address _collection) public view returns (bool) {
    CollectionShutdownParams memory params = _collectionParams[_collection];
    uint sweeperPoolTokenIdsLength = params.sweeperPoolTokenIds.length;

    // If we have no registered tokens, then there is nothing to check
    if (sweeperPoolTokenIdsLength == 0) {
        return true;
    }

    // Store our loop iteration variables
    address sweeperPool = params.sweeperPool;
    IERC721 collection = IERC721(_collection);

    // Check that all token IDs have been bought from the pool
    for (uint i; i < sweeperPoolTokenIdsLength; ++i) {
        // If the pool still owns the NFT, then we have to revert as not all tokens have been sold
        if (collection.ownerOf(params.sweeperPoolTokenIds[i]) == sweeperPool) {
            return false;
        }
    }

    return true;
}
```

## Tool used

Manual Review

## Recommendation

Since a call to `createPairERC721ETH` [configures](https://etherscan.io/address/0xa020d57ab0448ef74115c112d18a9c231cc86000#code) the `CollectionShutdown` contract as the `owner` of the newly created SudoSwap pool, expose functionality to allow the Flayer team to `withdrawERC721` from the pool upon the `CollectionShutdown`'s behalf:

```solidity
/// @notice Allows the team to rescue tokens.
function withdrawERC721(address _collection, uint256[] calldata _tokenIds, address _recipient) external nonReentrant onlyOwner {
    CollectionShutdownParams memory params = _collectionParams[_collection];
    address sweeperPool = params.sweeperPool;
    require(sweeperPool != address(0));
    LLSVMPairERC721ETH(sweeperPool).withdrawERC721(_collection, _tokenIds);
    for (uint256 i = 0; i < _tokenIds.length; i++)
        IERC721(_collection).transferFrom(address(this), _receipient, _tokenIds[i]);
}
```

This will also require `CollectionShutdown` to implement `IERC721Receiver` since the `LLSVMPairERC721ETH` [relies upon](https://etherscan.io/address/0x6b75fb66EB8e34CF0e2c39DFD91A97d5a37d819b#code) `safeTransferFrom` back to the `owner`.